#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
    Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыНакопления.ПартииЗатратНаВыпуск.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("c8d63f6d-f58d-457d-8dff-53250c7896f3");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ПартииЗатратНаВыпуск.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "Справочник.ВидыЗапасов,"
		+ "РегистрНакопления.ПартииТоваровОрганизаций,"
		+ "Справочник.КлючиАналитикиУчетаНоменклатуры,"
		+ "РегистрНакопления.ПартииЗатратНаВыпуск"
	;
	Обработчик.ИзменяемыеОбъекты = ""
		+ "РегистрНакопления.ПартииЗатратНаВыпуск"
	;
	Обработчик.БлокируемыеОбъекты = "";
	Обработчик.Комментарий = НСтр("ru='Очищает измерения ""Вид запасов"" и ""Кор. вид запасов"" с типом запасов ""Услуга"" записей регистра накопления ""Партии затрат на выпуск"".
                                   |Заполняет новый реквизит ""Назначение"" в справочнике ""Ключи аналитики учета номенклатуры"", и обновляет движения по регистру.
                                   |Данные в регистре накопления ""Партии затрат на выпуск"" не рекомендуется использовать до момента завершения обработки. Данные будут некорректны.'
                                   |;uk='Очищає вимір ""Вид запасів"" та ""Кор. вид запасів"" з типом запасів ""Послуга"" записів регістру накопичення ""Партії витрат на випуск"".
                                   |Заповнює новий реквізит ""Призначення"" у довіднику ""Ключі аналітики обліку номенклатури"" та оновлює рухи по регістру.
                                   |Дані в регістрі накопичення ""Партії витрат на випуск"" не рекомендується використовувати до завершення обробки. Дані будуть некоректні.'");

	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	


	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПартииТоваровОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
    
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.КлючиАналитикиУчетаНоменклатуры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьКлючиАналитикиНоменклатуры(
		Обработчик.ПриоритетыВыполнения,
		"После",
		Исключения
	);	                        

КонецПроцедуры

// Обработчик обновления УТ 3.5.4
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
    
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрНакопления.ПартииЗатратНаВыпуск";
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ПартииЗатратНаВыпуск КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	ИЛИ ДанныеРегистра.КорВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"), ДополнительныеПараметры);
    
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Ссылка КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДанныеРегистра.Регистратор КАК Ссылка
	|	ИЗ
	|		РегистрНакопления.ПартииЗатратНаВыпуск КАК ДанныеРегистра
	|ГДЕ
	|	(ДанныеРегистра.АналитикаУчетаНоменклатуры.Назначение <> ДанныеРегистра.ВидЗапасов.УстарелоНазначение
	|		И ДанныеРегистра.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И ДанныеРегистра.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
	|	ИЛИ (ДанныеРегистра.КорАналитикаУчетаПродукции.Назначение <> ДанныеРегистра.КорВидЗапасов.УстарелоНазначение
	|		И ДанныеРегистра.КорВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И ДанныеРегистра.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// заполнение назначения в аналитике учета продукции по данным регистра
	//	Партии товаров организаций.
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДанныеРегистра.Регистратор КАК Ссылка
	|	ИЗ
	|		РегистрНакопления.ПартииЗатратНаВыпуск КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиПродукции
	|	ПО КлючиПродукции.Ссылка = ДанныеРегистра.АналитикаУчетаПродукции
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК ПартииТоваров
	|	ПО ПартииТоваров.Регистратор = ДанныеРегистра.Регистратор
	|		И ПартииТоваров.ДокументПоступления = ДанныеРегистра.ДокументВыпуска
	|		И ПартииТоваров.Организация = ДанныеРегистра.Организация
	|		И КлючиПродукции.Номенклатура = ПартииТоваров.АналитикаУчетаНоменклатуры.Номенклатура
	|		И КлючиПродукции.Характеристика = ПартииТоваров.АналитикаУчетаНоменклатуры.Характеристика
	|		И КлючиПродукции.Серия = ПартииТоваров.АналитикаУчетаНоменклатуры.Серия
	|		И КлючиПродукции.МестоХранения = ПартииТоваров.АналитикаУчетаНоменклатуры.МестоХранения
	//++ НЕ УТ
	|		И КлючиПродукции.СтатьяКалькуляции = ПартииТоваров.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
	//-- НЕ УТ
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК ПартииТоваровПроверка
	|	ПО ПартииТоваровПроверка.Регистратор = ДанныеРегистра.Регистратор
	|		И ПартииТоваровПроверка.АналитикаУчетаНоменклатуры = ДанныеРегистра.АналитикаУчетаПродукции
	|		И ПартииТоваровПроверка.ДокументПоступления = ДанныеРегистра.ДокументВыпуска
	|		И ПартииТоваровПроверка.Организация = ДанныеРегистра.Организация
	|ГДЕ
	|	КлючиПродукции.Назначение <> ПартииТоваров.АналитикаУчетаНоменклатуры.Назначение
	|	И ПартииТоваров.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И КлючиПродукции.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ПартииТоваровПроверка.Регистратор ЕСТЬ NULL
	|	
	|	) КАК ДанныеРегистра
	|
	|");
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления УТ 3.5.4
// Очищаются виды запасов с типом запасов Услуга.   
// Заполняется новый реквизит "Назначение" в справочнике "Ключи аналитики учета номенклатуры",
// и обновляются движения по регистру.
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
    
	ПолноеИмяРегистра = "РегистрНакопления.ПартииЗатратНаВыпуск";
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ПартииЗатратНаВыпуск;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(
        Параметры.Очередь, 
        Неопределено, 
        ПолноеИмяРегистра
    );
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Движения.Регистратор                   КАК Регистратор,
	|	Движения.Период                        КАК Период,
	|	Движения.ВидДвижения                   КАК ВидДвижения,
	|	Движения.Организация                   КАК Организация,
	|	ВЫБОР КОГДА НЕ ПартииТоваров.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|		ТОГДА ПартииТоваров.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ Движения.АналитикаУчетаПродукции
	|	КОНЕЦ                            КАК АналитикаУчетаПродукции,
	|	ВЫБОР КОГДА Движения.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		И Движения.АналитикаУчетаНоменклатуры.Назначение <> Движения.ВидЗапасов.УстарелоНазначение
	|		И Движения.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Движения.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И НЕ Аналитика.КлючАналитики ЕСТЬ NULL
	|		ТОГДА Аналитика.КлючАналитики
	|		ИНАЧЕ Движения.АналитикаУчетаНоменклатуры
	|	КОНЕЦ                                  КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР КОГДА Движения.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		ИНАЧЕ Движения.ВидЗапасов
	|	КОНЕЦ                                  КАК ВидЗапасов,
	|	Движения.ДокументВыпуска               КАК ДокументВыпуска,
	|	Движения.ДокументПоступления           КАК ДокументПоступления,
	|	Движения.АналитикаУчетаПартий          КАК АналитикаУчетаПартий, 
	|
	|	СУММА(Движения.Количество)			   КАК Количество,
	|	СУММА(Движения.Стоимость)			   КАК Стоимость,
	|	СУММА(Движения.СтоимостьБезНДС)        КАК СтоимостьБезНДС,
	|	СУММА(Движения.СтоимостьРегл)          КАК СтоимостьРегл,
	|	СУММА(Движения.НДСРегл)                КАК НДСРегл,
	|	СУММА(Движения.ПостояннаяРазница)      КАК ПостояннаяРазница,
	|	СУММА(Движения.ВременнаяРазница)       КАК ВременнаяРазница,
	|
	|	Движения.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
    |	Движения.НалоговоеНазначение           КАК НалоговоеНазначение,
    |	Движения.НалоговоеНазначениеПродукции  КАК НалоговоеНазначениеПродукции,
    |	Движения.НалоговоеНазначениеПартии     КАК НалоговоеНазначениеПартии,
	|	Движения.СтатьяРасходовСписания        КАК СтатьяРасходовСписания,
	|	Движения.АналитикаРасходов             КАК АналитикаРасходов,
	|	Движения.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|	Движения.ПодразделениеРасходов         КАК ПодразделениеРасходов,
	|	ВЫБОР КОГДА Движения.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		И Движения.КорАналитикаУчетаПродукции.Назначение <> Движения.КорВидЗапасов.УстарелоНазначение
	|		И Движения.КорВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Движения.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И НЕ КорАналитикаПродукции.КлючАналитики ЕСТЬ NULL
	|		ТОГДА КорАналитикаПродукции.КлючАналитики
	|		ИНАЧЕ Движения.КорАналитикаУчетаПродукции
	|	КОНЕЦ                                  КАК КорАналитикаУчетаПродукции,
	|	Движения.КорВидЗапасов                 КАК КорВидЗапасов,
	|	Движения.Знаменатель                   КАК Знаменатель,
	|	Движения.ДокументИсточник              КАК ДокументИсточник,
	|
	|	ВЫБОР КОГДА (Аналитика.КлючАналитики ЕСТЬ NULL
	|			И Движения.АналитикаУчетаНоменклатуры.Назначение <> Движения.ВидЗапасов.УстарелоНазначение
	|			И Движения.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И Движения.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И Движения.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))
	|		ИЛИ (КорАналитикаПродукции.КлючАналитики ЕСТЬ NULL
	|			И Движения.КорАналитикаУчетаПродукции.Назначение <> Движения.КорВидЗапасов.УстарелоНазначение
	|			И Движения.КорВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И Движения.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
	|		ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КлючиИнициализированы
	|ИЗ
	|	РегистрНакопления.ПартииЗатратНаВыпуск КАК Движения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|	ПО Ключи.Ссылка = Движения.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО Ключи.Номенклатура = Аналитика.Номенклатура
	|		И Ключи.Характеристика = Аналитика.Характеристика
	|		И Ключи.Серия = Аналитика.Серия
	|		И Ключи.МестоХранения = Аналитика.МестоХранения
	|		И Движения.ВидЗапасов.УстарелоНазначение = Аналитика.Назначение
	|		И Ключи.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КорКлючи
	|	ПО КорКлючи.Ссылка = Движения.КорАналитикаУчетаПродукции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КорАналитикаПродукции
	|	ПО КорКлючи.Номенклатура = КорАналитикаПродукции.Номенклатура
	|		И КорКлючи.Характеристика = КорАналитикаПродукции.Характеристика
	|		И КорКлючи.Серия = КорАналитикаПродукции.Серия
	|		И КорКлючи.МестоХранения = КорАналитикаПродукции.МестоХранения
	|		И Движения.КорВидЗапасов.УстарелоНазначение = КорАналитикаПродукции.Назначение
	|		И КорКлючи.СтатьяКалькуляции = КорАналитикаПродукции.СтатьяКалькуляции
	|
	// заполнение назначения в аналитике учета продукции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиПродукции
	|	ПО КлючиПродукции.Ссылка = Движения.АналитикаУчетаПродукции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК ПартииТоваров
	|	ПО ПартииТоваров.Регистратор = Движения.Регистратор
	|		И ПартииТоваров.ДокументПоступления = Движения.ДокументВыпуска
	|		И ПартииТоваров.Организация = Движения.Организация
	|		И КлючиПродукции.Номенклатура = ПартииТоваров.АналитикаУчетаНоменклатуры.Номенклатура
	|		И КлючиПродукции.Характеристика = ПартииТоваров.АналитикаУчетаНоменклатуры.Характеристика
	|		И КлючиПродукции.Серия = ПартииТоваров.АналитикаУчетаНоменклатуры.Серия
	|		И КлючиПродукции.МестоХранения = ПартииТоваров.АналитикаУчетаНоменклатуры.МестоХранения
	//++ НЕ УТ
	|		И КлючиПродукции.СтатьяКалькуляции = ПартииТоваров.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
	//-- НЕ УТ
	|		И ПартииТоваров.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И КлючиПродукции.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|
	|ГДЕ
	|	Движения.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	Движения.Регистратор,
	|	Движения.Период,
	|	Движения.ВидДвижения,
	|	Движения.Организация,
	|	ВЫБОР КОГДА НЕ ПартииТоваров.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|		ТОГДА ПартииТоваров.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ Движения.АналитикаУчетаПродукции
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Движения.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		И Движения.АналитикаУчетаНоменклатуры.Назначение <> Движения.ВидЗапасов.УстарелоНазначение
	|		И Движения.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Движения.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И НЕ Аналитика.КлючАналитики ЕСТЬ NULL
	|		ТОГДА Аналитика.КлючАналитики
	|		ИНАЧЕ Движения.АналитикаУчетаНоменклатуры
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Движения.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		ИНАЧЕ Движения.ВидЗапасов
	|	КОНЕЦ,
	|	Движения.ДокументВыпуска,
	|	Движения.ДокументПоступления,
	|	Движения.АналитикаУчетаПартий,
	|	Движения.ХозяйственнаяОперация,
    |	Движения.НалоговоеНазначение,
    |	Движения.НалоговоеНазначениеПродукции,
    |	Движения.НалоговоеНазначениеПартии,
	|	Движения.СтатьяРасходовСписания,
	|	Движения.АналитикаРасходов,       
	|	Движения.АналитикаАктивовПассивов,
	|	Движения.ПодразделениеРасходов,
	|	ВЫБОР КОГДА Движения.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		И Движения.КорАналитикаУчетаПродукции.Назначение <> Движения.КорВидЗапасов.УстарелоНазначение
	|		И Движения.КорВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Движения.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И НЕ КорАналитикаПродукции.КлючАналитики ЕСТЬ NULL
	|		ТОГДА КорАналитикаПродукции.КлючАналитики
	|		ИНАЧЕ Движения.КорАналитикаУчетаПродукции
	|	КОНЕЦ,
	|	Движения.КорВидЗапасов,
	|	Движения.Знаменатель,
	|	Движения.ДокументИсточник,
	|	ВЫБОР КОГДА (Аналитика.КлючАналитики ЕСТЬ NULL
	|			И Движения.АналитикаУчетаНоменклатуры.Назначение <> Движения.ВидЗапасов.УстарелоНазначение
	|			И Движения.ВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И Движения.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И Движения.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))
	|		ИЛИ (КорАналитикаПродукции.КлючАналитики ЕСТЬ NULL
	|			И Движения.КорАналитикаУчетаПродукции.Назначение <> Движения.КорВидЗапасов.УстарелоНазначение
	|			И Движения.КорВидЗапасов.УстарелоНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И Движения.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
	|		ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|УПОРЯДОЧИТЬ ПО
	|	КлючиИнициализированы
	|";
	
	Пока Выборка.Следующий() Цикл
		
		Регистратор = Выборка.Регистратор;
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

			Блокировка.Заблокировать();
			
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Регистратор", Регистратор);
			
			Набор = РегистрыНакопления.ПартииЗатратНаВыпуск.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Регистратор);
			
			Результат = Запрос.Выполнить().Выгрузить();
			Если Результат.Количество() = 0 Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Регистратор, ДополнительныеПараметры);
				ЗафиксироватьТранзакцию();
				Продолжить;
			ИначеЕсли Результат[0].КлючиИнициализированы = 0 Тогда
				ТекстСообщения = НСтр("ru='есть необновленные ключи. Необходимо перепровести документ вручную.';uk='існують неоновлені ключі. Необхідно перепровести документ вручну.'");
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			
			Набор.Загрузить(Результат);
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Не удалось обработать документ: %Регистратор% по причине: %Причина%';uk='Не вдалося обробити документ: %Регистратор% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Регистратор%", Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Регистратор.Метаданные(), 
				ТекстСообщения
			);
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
    
КонецПроцедуры

#КонецОбласти

#КонецЕсли