#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу, чтобы при записи получить
	// изменение нового набора относительно текущего.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Назначение     КАК Назначение,
	|	Таблица.Номенклатура   КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Подразделение  КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.КОбеспечению
	|		ИНАЧЕ -Таблица.КОбеспечению
	|	КОНЕЦ                  КАК КОбеспечениюПередЗаписью
	|ПОМЕСТИТЬ ОбеспечениеЗаказовРаботамиПередЗаписью
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказовРаботами КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ Таблица.Назначение.КонтролироватьТолькоНаличие
	|	И НЕ &ЭтоНовый");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Назначение                   КАК Назначение,
	|	ТаблицаИзменений.Номенклатура                 КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика               КАК Характеристика,
	|	ТаблицаИзменений.Подразделение                КАК Подразделение,
	|	СУММА(ТаблицаИзменений.КОбеспечениюИзменение) КАК КОбеспечениюИзменение
	|ПОМЕСТИТЬ ОбеспечениеЗаказовРаботамиИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Назначение               КАК Назначение,
	|		Таблица.Номенклатура             КАК Номенклатура,
	|		Таблица.Характеристика           КАК Характеристика,
	|		Таблица.Подразделение            КАК Подразделение,
	|		Таблица.КОбеспечениюПередЗаписью КАК КОбеспечениюИзменение
	|	ИЗ
	|		ОбеспечениеЗаказовРаботамиПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Назначение              КАК Назначение,
	|		Таблица.Номенклатура            КАК Номенклатура,
	|		Таблица.Характеристика          КАК Характеристика,
	|		Таблица.Подразделение           КАК Подразделение,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КОбеспечению
	|			ИНАЧЕ Таблица.КОбеспечению
	|		КОНЕЦ                           КАК КОбеспечениюИзменение
	|	ИЗ
	|		РегистрНакопления.ОбеспечениеЗаказовРаботами КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|		И НЕ Таблица.Назначение.КонтролироватьТолькоНаличие) КАК ТаблицаИзменений
	|ГДЕ
	|	НЕ (ТаблицаИзменений.Назначение.Заказ <> НЕОПРЕДЕЛЕНО И &ОбособлениеСверхПотребности)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Подразделение
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.КОбеспечениюИзменение) > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОбеспечениеЗаказовРаботамиПередЗаписью");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ОбособлениеСверхПотребности",
		ПолучитьФункциональнуюОпцию("РазрешитьОбособлениеТоваровСверхПотребности"));
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ОбеспечениеЗаказовРаботамиИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли