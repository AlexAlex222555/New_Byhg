#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.Период КАК Период,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.ВидПоставки КАК ВидПоставки,
	|	Таблица.Валюта КАК Валюта,
	|	Таблица.СпособЗаполнения КАК СпособЗаполнения,
	|	Таблица.СтавкаНДС КАК СтавкаНДС,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Упаковка КАК Упаковка,
	|	Таблица.ЦенаНН КАК ЦенаНН,
	|	Таблица.ДокументПоставки КАК ДокументПоставки,
	|	Таблица.ДокументПоставкиДляВозвратов КАК ДокументПоставкиДляВозвратов,
	|	Таблица.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Таблица.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ НДСНоменклатурныйСоставДляНалоговыхНакладныхПередЗаписью
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор");
	
	Запрос.УстановитьПараметр("Регистратор",              Отбор.Регистратор.Значение);
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ) КАК День,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.Регистратор КАК Документ,
	|	Таблица.ДокументПоставки КАК ДокументПоставки
	|ПОМЕСТИТЬ ТаблицаИзмененийНДСНоменклатурныйСоставЗадания
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Регистратор КАК Регистратор,
	|		Таблица.Период КАК Период,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Таблица.ВидПоставки КАК ВидПоставки,
	|		Таблица.Валюта КАК Валюта,
	|		Таблица.СпособЗаполнения КАК СпособЗаполнения,
	|		Таблица.СтавкаНДС КАК СтавкаНДС,
	|		Таблица.НомерГТД КАК НомерГТД,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Упаковка КАК Упаковка,
	|		Таблица.ЦенаНН КАК ЦенаНН,
	|		Таблица.ДокументПоставки КАК ДокументПоставки,
	|		Таблица.ДокументПоставкиДляВозвратов КАК ДокументПоставкиДляВозвратов,
	|		Таблица.КоличествоУпаковок КАК КоличествоУпаковок,
	|		Таблица.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов
	|	ИЗ
	|		НДСНоменклатурныйСоставДляНалоговыхНакладныхПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Регистратор,
	|		Таблица.Период,
	|		Таблица.АналитикаУчетаПоПартнерам,
	|		Таблица.ОбъектРасчетов,
	|		Таблица.ВидПоставки,
	|		Таблица.Валюта,
	|		Таблица.СпособЗаполнения,
	|		Таблица.СтавкаНДС,
	|		Таблица.НомерГТД,
	|		Таблица.Номенклатура,
	|		Таблица.Характеристика,
	|		Таблица.Упаковка,
	|		Таблица.ЦенаНН,
	|		Таблица.ДокументПоставки,
	|		Таблица.ДокументПоставкиДляВозвратов,
	|		-Таблица.КоличествоУпаковок,
	|		-Таблица.СуммаВзаиморасчетов
	|	ИЗ
	|		РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов,
	|	Таблица.ВидПоставки,
	|	Таблица.Валюта,
	|	Таблица.СпособЗаполнения,
	|	Таблица.СтавкаНДС,
	|	Таблица.НомерГТД,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.ЦенаНН,
	|	Таблица.ДокументПоставкиДляВозвратов,
	|	Таблица.ДокументПоставки
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаВзаиморасчетов) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НДСНоменклатурныйСоставДляНалоговыхНакладныхПередЗаписью");
	
	Запрос.УстановитьПараметр("Регистратор",              Отбор.Регистратор.Значение);
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
