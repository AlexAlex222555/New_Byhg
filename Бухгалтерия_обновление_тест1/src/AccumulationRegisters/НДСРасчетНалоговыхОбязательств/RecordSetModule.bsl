#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.Период КАК Период,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.ВидПоставки КАК ВидПоставки,
	|	Таблица.Валюта КАК Валюта,
	|	Таблица.ДокументПоставки КАК ДокументПоставки,
	|	Таблица.МоментОпределенияБазыНДС КАК МоментОпределенияБазыНДС,
	|	Таблица.СуммаПоставкиТребующаяРегистрацииНН КАК СуммаПоставкиТребующаяРегистрацииНН,
	|	Таблица.СуммаПоставкиНеТребующаяРегистрацииНН КАК СуммаПоставкиНеТребующаяРегистрацииНН
	|ПОМЕСТИТЬ НДСРасчетНалоговыхОбязательствПередЗаписью
	|ИЗ
	|	РегистрНакопления.НДСРасчетНалоговыхОбязательств КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И Таблица.СуммаПоставкиТребующаяРегистрацииНН <> 0");
	
	Запрос.УстановитьПараметр("Регистратор",              Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ) КАК День,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.Регистратор КАК Документ
	|ПОМЕСТИТЬ ТаблицаИзмененийНДСРасчетНалоговыхОбязательствЗадания
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Регистратор КАК Регистратор,
	|		Таблица.Период КАК Период,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Таблица.ВидПоставки КАК ВидПоставки,
	|		Таблица.Валюта КАК Валюта,
	|		Таблица.ДокументПоставки КАК ДокументПоставки,
	|		Таблица.МоментОпределенияБазыНДС КАК МоментОпределенияБазыНДС,
	|		Таблица.СуммаПоставкиТребующаяРегистрацииНН КАК СуммаПоставкиТребующаяРегистрацииНН,
	|		Таблица.СуммаПоставкиНеТребующаяРегистрацииНН КАК СуммаПоставкиНеТребующаяРегистрацииНН
	|	ИЗ
	|		НДСРасчетНалоговыхОбязательствПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Регистратор,
	|		Таблица.Период,
	|		Таблица.АналитикаУчетаПоПартнерам,
	|		Таблица.ОбъектРасчетов,
	|		Таблица.ВидПоставки,
	|		Таблица.Валюта,
	|		Таблица.ДокументПоставки,
	|		Таблица.МоментОпределенияБазыНДС,
	|		-Таблица.СуммаПоставкиТребующаяРегистрацииНН,
	|		-Таблица.СуммаПоставкиНеТребующаяРегистрацииНН
	|	ИЗ
	|		РегистрНакопления.НДСРасчетНалоговыхОбязательств КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|		И Таблица.СуммаПоставкиТребующаяРегистрацииНН <> 0) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов,
	|	Таблица.ВидПоставки,
	|	Таблица.Валюта,
	|	Таблица.ДокументПоставки,
	|	Таблица.МоментОпределенияБазыНДС
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаПоставкиТребующаяРегистрацииНН) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НДСРасчетНалоговыхОбязательствПередЗаписью");
	
	Запрос.УстановитьПараметр("Регистратор",              Отбор.Регистратор.Значение);
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
