#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
	ДополнительныеСвойства.Вставить("ВалютаРегл", Константы.ВалютаРегламентированногоУчета.Получить());
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Записи.Период                     КАК Период,
	|	Записи.Регистратор                КАК Регистратор,
	|	Записи.Организация                КАК Организация,
	|	Записи.Касса                      КАК Касса,
	|
	|	Записи.Сумма       КАК Сумма,
	|	Записи.СуммаУпр    КАК СуммаУпр,
	|	Записи.СуммаРегл   КАК СуммаРегл,
	|
	|	Записи.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	Записи.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Записи.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|	Записи.ОбъектРасчетов                КАК ОбъектРасчетов
	|ПОМЕСТИТЬ ДенежныеСредстваНаличныеПередЗаписью
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК Записи
	|ГДЕ
	|	Записи.Регистратор = &Регистратор
	|	И (ТипЗначения(Записи.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|		ИЛИ Записи.Касса.ВалютаДенежныхСредств <> &ВалютаУпр
	|		ИЛИ Записи.Касса.ВалютаДенежныхСредств <> &ВалютаРегл
	|	)
	|");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ВалютаУпр",   ДополнительныеСвойства.ВалютаУпр);
	Запрос.УстановитьПараметр("ВалютаРегл",  ДополнительныеСвойства.ВалютаРегл);
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	Если ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		ДополнительныеСвойства.Вставить("ДатыРасчетаГрафикаПлатежей", ДатыРасчетаГрафикаПлатежей());
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Таблица.Период                        КАК Период,
	|	Таблица.Регистратор                   КАК Регистратор,
	|	Таблица.Организация                   КАК Организация,
	|	Таблица.Касса                         КАК Касса,
	|	Таблица.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	Таблица.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Таблица.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов                КАК ОбъектРасчетов,
	|	СУММА(Таблица.Сумма)                  КАК СуммаИзменение,
	|	СУММА(Таблица.СуммаУпр)               КАК СуммаУпрИзменение,
	|	СУММА(Таблица.СуммаРегл)              КАК СуммаРеглИзменение
	|ПОМЕСТИТЬ ТаблицаИзмененийДенежныеСредстваНаличные
	|ИЗ
	|	(ВЫБРАТЬ
	|		Записи.Период                     КАК Период,
	|		Записи.Регистратор                КАК Регистратор,
	|		Записи.Организация                КАК Организация,
	|		Записи.Касса                      КАК Касса,
	|
	|		Записи.Сумма       КАК Сумма,
	|		Записи.СуммаУпр    КАК СуммаУпр,
	|		Записи.СуммаРегл   КАК СуммаРегл,
	|
	|		Записи.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		Записи.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		Записи.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Записи.ОбъектРасчетов                КАК ОбъектРасчетов
	|	ИЗ
	|		ДенежныеСредстваНаличныеПередЗаписью КАК Записи
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Записи.Период                     КАК Период,
	|		Записи.Регистратор                КАК Регистратор,
	|		Записи.Организация                КАК Организация,
	|		Записи.Касса                      КАК Касса,
	|
	|		-Записи.Сумма       КАК Сумма,
	|		-Записи.СуммаУпр    КАК СуммаУпр,
	|		-Записи.СуммаРегл   КАК СуммаРегл,
	|
	|		Записи.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		Записи.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		Записи.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Записи.ОбъектРасчетов                КАК ОбъектРасчетов
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваНаличные КАК Записи
	|	ГДЕ
	|		Записи.Регистратор = &Регистратор
	|		И (ТипЗначения(Записи.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|			ИЛИ Записи.Касса.ВалютаДенежныхСредств <> &ВалютаУпр
	|			ИЛИ Записи.Касса.ВалютаДенежныхСредств <> &ВалютаРегл)
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.Касса,
	|	Таблица.ХозяйственнаяОперация,
	|	Таблица.СтатьяДвиженияДенежныхСредств,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Сумма) <> 0
	|	ИЛИ СУММА(Таблица.СуммаУпр) <> 0
	|	ИЛИ СУММА(Таблица.СуммаРегл) <> 0
	|");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ВалютаУпр", ДополнительныеСвойства.ВалютаУпр);
	Запрос.УстановитьПараметр("ВалютаРегл", ДополнительныеСвойства.ВалютаРегл);
	
	Запрос.Выполнить();
	
	Если ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		РегистрыСведений.ГрафикПлатежей.РассчитатьГрафикПлатежейПоОжидаемымПоступлениямДенежныхСредств(
			ДополнительныеСвойства.ДатыРасчетаГрафикаПлатежей);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует массив дат, в которые изменяются движения регистра.
//
Функция ДатыРасчетаГрафикаПлатежей()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ) КАК ДатаРасчета
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|";
	
	ДатыРасчета = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДатаРасчета");
	
	НовыеДатыРасчета = ВыгрузитьКолонку("Период");
	Для каждого НоваяДатаРасчета Из НовыеДатыРасчета Цикл
		НоваяДатаРасчетаНачалоДня = НачалоДня(НоваяДатаРасчета);
		Если ДатыРасчета.Найти(НоваяДатаРасчетаНачалоДня) = Неопределено Тогда
			ДатыРасчета.Добавить(НоваяДатаРасчетаНачалоДня);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДатыРасчета;
	
КонецФункции

#КонецОбласти

#КонецЕсли