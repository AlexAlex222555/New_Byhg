#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция ТекстЗапросаОстатков предназначена для получения остатков регистра
// в разрезе номенклатуры, характеристики и склада.
//
// Параметры:
//  ИспользоватьКорректировку - Булево - признак необходимости скорректировать движения регистра перед получением остатков.
//  Разделы - Массив - массив в который будет добавлена информация о временных таблицах создаваемых при выполнении запроса.
//
// Возвращаемое значение:
//  Строка - Текст запроса свободного остатка регистра в разрезе номенклатуры, характеристики и склада.
//
Функция ТекстЗапросаОстатков(ИспользоватьКорректировку, Разделы = Неопределено) Экспорт

	Если Не ИспользоватьКорректировку Тогда
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	Т.Номенклатура           КАК Номенклатура,
			|	Т.Характеристика         КАК Характеристика,
			|	Т.Склад                  КАК Склад,
			|
			|	Т.ВНаличииОстаток
			|		- Т.ВРезервеСоСкладаОстаток
			|		- Т.ВРезервеПодЗаказОстаток КАК Количество
			|
			|ПОМЕСТИТЬ ВтОстаткиСклада
			|ИЗ
			|	РегистрНакопления.СвободныеОстатки.Остатки(,
			|		(Номенклатура, Характеристика, Склад) В(
			|			ВЫБРАТЬ
			|				Ключи.Номенклатура   КАК Номенклатура,
			|				Ключи.Характеристика КАК Характеристика,
			|				Ключи.Склад          КАК Склад
			|			ИЗ
			|				ВтТовары КАК Ключи
			|		)) КАК Т
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад
			|;
			|
			|/////////////////////////////////////////////////////////////
			|";
	Иначе
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	НаборДанных.Номенклатура           КАК Номенклатура,
			|	НаборДанных.Характеристика         КАК Характеристика,
			|	НаборДанных.Склад                  КАК Склад,
			|
			|	СУММА(НаборДанных.Количество)      КАК Количество
			|
			|ПОМЕСТИТЬ ВтОстаткиСклада
			|ИЗ (
			|	ВЫБРАТЬ
			|		Т.Номенклатура           КАК Номенклатура,
			|		Т.Характеристика         КАК Характеристика,
			|		Т.Склад                  КАК Склад,
			|
			|		Т.ВНаличииОстаток
			|			- Т.ВРезервеСоСкладаОстаток
			|			- Т.ВРезервеПодЗаказОстаток КАК Количество
			|
			|	ИЗ
			|		РегистрНакопления.СвободныеОстатки.Остатки(,
			|			(Номенклатура, Характеристика, Склад) В(
			|				ВЫБРАТЬ
			|					Ключи.Номенклатура   КАК Номенклатура,
			|					Ключи.Характеристика КАК Характеристика,
			|					Ключи.Склад          КАК Склад
			|				ИЗ
			|					ВтТовары КАК Ключи
			|			)) КАК Т
			|
			|	ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		Т.Номенклатура            КАК Номенклатура,
			|		Т.Характеристика          КАК Характеристика,
			|		Т.Склад                   КАК Склад,
			|
			|		- Т.ВРезерве - Т.КОтгрузке КАК Количество
			|
			|	ИЗ
			|		ВтТоварыКОтгрузкеКорректировка КАК Т
			|	ГДЕ
			|		Т.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|
			|	) КАК НаборДанных
			|
			|СГРУППИРОВАТЬ ПО
			|	НаборДанных.Номенклатура, НаборДанных.Характеристика, НаборДанных.Склад
			|ИМЕЮЩИЕ
			|	СУММА(НаборДанных.Количество) <> 0
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад
			|;
			|
			|/////////////////////////////////////////////////////////////
			|";
	КонецЕсли;

	Если Разделы <> Неопределено Тогда
		Разделы.Добавить("ТаблицаОстаткиСклада");
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

// Получает текст запроса для сторнирования движений документа.
//
// Возвращаемое значение:
//  Строка - Текст запроса.
//
Функция ТекстЗапросаСторноЗаписейЗаказаВРезервеИКОтгрузке() Экспорт

	Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура     КАК Номенклатура,
		|	Таблица.Характеристика   КАК Характеристика,
		|	Таблица.Склад            КАК Склад,
		|
		|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
		|				- Таблица.ВНаличии
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК КОтгрузке,
		|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|				- Таблица.ВРезервеСоСклада
		|			ИНАЧЕ
		|				Таблица.ВРезервеСоСклада
		|		КОНЕЦ КАК ВРезерве
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки КАК Таблица
		|
		|ГДЕ
		|	Таблица.Активность
		|	И Таблица.Регистратор В(&Ссылка)
		|	И (Таблица.ВНаличии <> 0 ИЛИ Таблица.ВРезервеСоСклада <> 0)
		|	И &Отбор
		|;
		|
		|//////////////////////////////////////////////////
		|";

	Возврат Текст;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Склад)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "РегистрыНакопления.СвободныеОстатки.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("c5fc6a62-ea23-45da-b6e9-fdfaf593175e");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.СвободныеОстатки.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "Документ.КорректировкаПоОрдеруНаТовары,"
		+ "РегистрНакопления.СвободныеОстатки";
	Обработчик.ИзменяемыеОбъекты = "РегистрНакопления.СвободныеОстатки";
	Обработчик.БлокируемыеОбъекты = "";
	Обработчик.Комментарий = НСтр("ru='Обновляет движения документов ""Корректировка по ордеру на товары"" по регистру накопления ""Свободные остатки"".';uk='Оновлює рухи документів ""Коригування за ордером на товари"" по регістру накопичення ""Вільні залишки"".'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизаций.СгенерироватьДокументыДляПереброскиОстатковСПустогоНазначенияПоДавальческойСхеме";
	НоваяСтрока.Порядок = "Любой";
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ИмяРегистра = "СвободныеОстатки";
	ПолноеИмяРегистра = "РегистрНакопления.СвободныеОстатки";
	
	Запрос = Новый Запрос;
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		СвободныеОстатки.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрНакопления.СвободныеОстатки КАК СвободныеОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПоОрдеруНаТовары.Товары КАК КорректировкаПоОрдеруНаТоварыТовары
	|			ПО СвободныеОстатки.Регистратор = КорректировкаПоОрдеруНаТоварыТовары.Ссылка
	|				И СвободныеОстатки.Номенклатура = КорректировкаПоОрдеруНаТоварыТовары.Номенклатура
	|				И СвободныеОстатки.Характеристика = КорректировкаПоОрдеруНаТоварыТовары.Характеристика
	|				И (КорректировкаПоОрдеруНаТоварыТовары.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|				И (КорректировкаПоОрдеруНаТоварыТовары.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишек), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)))
	|	ГДЕ
	|		СвободныеОстатки.ВРезервеПодЗаказ = 0
	|	) КАК Регистраторы
	|";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	Запрос.Текст = ТекстЗапроса;
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы, 
		ПолноеИмяРегистра
	);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.СвободныеОстатки";
	
	ОбработкаЗавершена = Ложь;
	
	Регистраторы = Новый Массив;
	
	Регистраторы.Добавить("Документ.КорректировкаПоОрдеруНаТовары");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы, 
		ПолноеИмяРегистра, 
		Параметры.Очередь
	);
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли