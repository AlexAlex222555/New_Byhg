#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Взаиморасчеты

// Создает и заполняет/удаляет элементы справочника "Объекты расчетов" перед записью связанного объекта.
//
// Параметры:
// 	Объект - СправочникОбъект, ДокументОбъект - Записываемый объект.
// 	МассивСтруктур - Массив из см. ВзаиморасчетыСервер.ПараметрыМеханизма - Массив структур параметров взаиморасчетов.
// 	РежимЗаписи - РежимЗаписиДокумента - Режим записи, если метод вызывается из события ПередЗаписью.
//
Процедура ПроверитьОбъектыРасчетовПередЗаписью(Объект, МассивСтруктур, РежимЗаписи, Отказ) Экспорт
	
	// Очистка объекта от ссылок на объекты расчетов.
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ОчиститьОбъектыРасчетов(Объект,МассивСтруктур);
	КонецЕсли;
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("СправочникСсылка.Организации"));
	СписокТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	ОписаниеТипаПоляКонтрагент = Новый ОписаниеТипов(СписокТипов);
	
	// Удаление лишних элементов справочника.
	ТаблицаСторон = Новый ТаблицаЗначений;
	ТаблицаСторон.Колонки.Добавить("Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаСторон.Колонки.Добавить("Контрагент", ОписаниеТипаПоляКонтрагент);
	ТаблицаСторон.Колонки.Добавить("Партнер",Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	ТаблицаСторон.Колонки.Добавить("ТипРасчетов",Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРасчетовСПартнерами"));
	
	МассивОбъектовРасшифровки = Новый Массив();
	
	Ссылка = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.Ссылка");
	
	Для Каждого СтруктураПараметров Из МассивСтруктур Цикл
		
		Если ЗначениеЗаполнено(СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа) Тогда
			Расшифровка = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОбъектовРасшифровки, Расшифровка.Выгрузить().ВыгрузитьКолонку("ОбъектРасчетов"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтруктураПараметров.ОбъектРасчетов) И НЕ СтруктураПараметров.ЭтоДоговор Тогда
			Продолжить;
		КонецЕсли;
		
		Если СсылкаЯвляетсяОбъектомРасчетов(Объект, СтруктураПараметров) Тогда
			НовСтр = ТаблицаСторон.Добавить();
			НовСтр.Организация = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация);
			НовСтр.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент);
			НовСтр.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер);
			НовСтр.ТипРасчетов = СтруктураПараметров.ТипРасчетов;
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("СсылкаНового") Тогда
			Ссылка = СтруктураПараметров.СсылкаНового;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаСторон.Организация КАК Организация,
	|	ТаблицаСторон.Контрагент  КАК Контрагент,
	|	ТаблицаСторон.Партнер     КАК Партнер,
	|	ТаблицаСторон.ТипРасчетов КАК ТипРасчетов
	|ПОМЕСТИТЬ ТаблицаСторон
	|ИЗ &ТаблицаСторон КАК ТаблицаСторон
	|;
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСторон КАК ТаблицаСторон
	|			ПО ТаблицаСторон.Организация = ОбъектыРасчетов.Организация
	|				И ТаблицаСторон.ТипРасчетов = ОбъектыРасчетов.ТипРасчетов
	|				И ТаблицаСторон.Контрагент = ОбъектыРасчетов.Контрагент
	|				И ТаблицаСторон.Партнер = ОбъектыРасчетов.Партнер
	|ГДЕ
	|	ОбъектыРасчетов.Объект = &Ссылка
	|	И ТаблицаСторон.ТипРасчетов ЕСТЬ NULL
	|	И НЕ ОбъектыРасчетов.ТолькоОстатки
	|	И ОбъектыРасчетов.Ссылка НЕ В (&МассивОбъектовРасшифровки)");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ТаблицаСторон", ТаблицаСторон);
	
	Запрос.УстановитьПараметр("МассивОбъектовРасшифровки", МассивОбъектовРасшифровки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ОбъектыРасчетов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		Блокировка.Заблокировать();
		
		ПроверитьУдалитьОбъектРасчетов(Выборка.Ссылка, Отказ);
	КонецЦикла;
	
	//Создание элементов справочника 
	Для Каждого СтруктураПараметров Из МассивСтруктур Цикл
		Если СсылкаЯвляетсяОбъектомРасчетов(Объект, СтруктураПараметров) Тогда
			ПроверитьЗаполнитьОбъектРасчетовПоСтруктуре(Объект, СтруктураПараметров,,РежимЗаписи);
		ИначеЕсли ЗначениеЗаполнено(СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа) Тогда
			
			РасшифровкаПлатежа      = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа);
			ТребуетсяОбъектРасчетов = Ложь;
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	Расшифровка.ОбъектРасчетов
			|ПОМЕСТИТЬ Расшифровка
			|ИЗ &Расшифровка КАК Расшифровка
			|;
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	Расшифровка
			|ГДЕ
			|	Расшифровка.ОбъектРасчетов.Объект = &Ссылка";
			Запрос.УстановитьПараметр("Расшифровка", РасшифровкаПлатежа);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			ТребуетсяОбъектРасчетов = Выборка.Следующий();
			
			Если ТребуетсяОбъектРасчетов Тогда
				ПроверитьЗаполнитьОбъектРасчетовПоСтруктуре(Объект, СтруктураПараметров, Истина);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	//Заполнение
	ЗаполнитьОбъектРасчетов(Объект, МассивСтруктур,, РежимЗаписи);
	
КонецПроцедуры

// Создает, перезаполняет при необходимости и возвращает ссылку на объект расчетов по параметрам механизма взаиморасчетов.
// Если в рамках одной структуры было создано несколько элементов объектов расчетов то возвращает первый.
// 
// Параметры:
// 	Объект - ДокументОбъект, СправочникОбъект - Объект, по ссылке на который проверяется объект расчетов.
// 	СтруктураПараметров - Структура - Текущий набор параметров. см. ВзаиморасчетыСервер.ПараметрыМеханизма().
// 	ВернутьВсеОбъекты - Булево - Вернуть один объект расчетов или таблицу всех объектов расчетов.
// 	РежимЗаписи - РежимЗаписиДокумента - Режим записи, если метод вызывается из события ПередЗаписью.
// 	
// Возвращаемое значение:
// 	СправочникСсылка.ОбъектыРасчетов - Ссылка на объект расчетов.
//
Функция ПроверитьЗаполнитьОбъектРасчетовПоСтруктуре(Объект, СтруктураПараметров, ВернутьВсеОбъекты = Ложь, РежимЗаписи = Неопределено) Экспорт
	
	Если СтруктураПараметров.Свойство("СсылкаНового") Тогда
		Ссылка = СтруктураПараметров.СсылкаНового;
	Иначе
		Ссылка = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Ссылка);
	КонецЕсли;
	
	ОрганизацияВСтроках = СтруктураПараметров.ОрганизацияВСтроках И ТипЗнч(СтруктураПараметров.Организация) = Тип("Строка");
	КонтрагентВСтроках  = СтруктураПараметров.КонтрагентВСтроках И ТипЗнч(СтруктураПараметров.Контрагент) = Тип("Строка");
	ПартнерВСтроках     = СтруктураПараметров.ПартнерВСтроках И ТипЗнч(СтруктураПараметров.Партнер) = Тип("Строка");
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить("СправочникСсылка.Организации");
	СписокТипов.Добавить("СправочникСсылка.Контрагенты");
	ОписаниеТипаПоляКонтрагент = Новый ОписаниеТипов(СписокТипов);
	
	ТаблицаСторон = Новый ТаблицаЗначений;
	ТаблицаСторон.Колонки.Добавить("Контрагент", Новый ОписаниеТипов(ОписаниеТипаПоляКонтрагент));
	ТаблицаСторон.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаСторон.Колонки.Добавить("Партнер", Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	ТаблицаСторон.Колонки.Добавить("ОбъектРасчетов", Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	
	ВалютаВзаиморасчетов = Неопределено;
	СуммаВзаиморасчетов = 0;
	
	Если ОрганизацияВСтроках ИЛИ КонтрагентВСтроках ИЛИ ПартнерВСтроках Тогда
		Если ОрганизацияВСтроках И КонтрагентВСтроках
			И СтрРазделить(СтруктураПараметров.Организация, ".")[1] <> СтрРазделить(СтруктураПараметров.Контрагент, ".")[1] Тогда
			ВызватьИсключение(НСтр("ru='Ошибка встраивания. Отличаются табличные части содержащие контрагента и организацию.';uk='Помилка вбудовування. Відрізняються табличні частини, що містять контрагента і організацію.'"));
		КонецЕсли;
		
		Если ОрганизацияВСтроках Тогда
			ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация); // ТабличнаяЧасть
		ИначеЕсли КонтрагентВСтроках Тогда
			ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент); // ТабличнаяЧасть
		Иначе
			ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер); // ТабличнаяЧасть
		КонецЕсли;
		
		Для Каждого Стр Из ТЧ Цикл
			
			Если ЗначениеЗаполнено(Стр.ОбъектРасчетов)
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Стр.ОбъектРасчетов,"Объект") <> Ссылка Тогда
				Продолжить;
			КонецЕсли;
			
			НовСтр = ТаблицаСторон.Добавить();
			
			Если ТипЗнч(Объект) = Тип("ФормаКлиентскогоПриложения") Тогда
				Идентификатор = Стр.ПолучитьИдентификатор();
			Иначе
				Идентификатор = Стр.НомерСтроки;
			КонецЕсли;
			
			Если ОрганизацияВСтроках Тогда
				НовСтр.Организация = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация, Идентификатор);
			Иначе
				НовСтр.Организация = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация);
			КонецЕсли;
			
			Если КонтрагентВСтроках Тогда
				НовСтр.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент, Идентификатор);
			Иначе
				НовСтр.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент);
			КонецЕсли;
			
			Если ПартнерВСтроках Тогда
				НовСтр.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер, Идентификатор);
			Иначе
				НовСтр.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер);
			КонецЕсли;
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Стр, "ВалютаВзаиморасчетов") Тогда
				ВалютаВзаиморасчетов = Стр.ВАлютаВзаиморасчетов;
			КонецЕсли;
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Стр, "СуммаВзаиморасчетов") Тогда
				СуммаВзаиморасчетов = СуммаВзаиморасчетов + Стр.СуммаВзаиморасчетов;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		НовСтр = ТаблицаСторон.Добавить();
		НовСтр.Организация = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация);
		НовСтр.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент);
		НовСтр.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер);
	КонецЕсли;
	
	Для Каждого Стр Из ТаблицаСторон Цикл
		
		ПараметрыОбъектаРасчетов = РеквизитыОбъектаРасчетовПоСтруктуре(Объект, Ссылка, СтруктураПараметров, РежимЗаписи);
		
		//Ключевые реквизиты.
		ПараметрыОбъектаРасчетов.Организация = Стр.Организация;
		ПараметрыОбъектаРасчетов.Контрагент = Стр.Контрагент;
		ПараметрыОбъектаРасчетов.Партнер =  Стр.Партнер;
		
		Если ЗначениеЗаполнено(ВалютаВзаиморасчетов) Тогда
			ПараметрыОбъектаРасчетов.ВалютаВзаиморасчетов = ВалютаВзаиморасчетов;
		КонецЕсли;
		Если СуммаВзаиморасчетов > 0 Тогда
			ПараметрыОбъектаРасчетов.СуммаВзаиморасчетов = СуммаВзаиморасчетов;
		КонецЕсли;
		
		ДополнительныеРеквизиты = ?(Объект.Метаданные().ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено,
									Объект.ДополнительныеРеквизиты.Выгрузить(),
									Неопределено);
		
		Стр.ОбъектРасчетов = ПроверитьСоздатьОбъектРасчетов(ПараметрыОбъектаРасчетов, ДополнительныеРеквизиты);
	КонецЦикла;
	
	Если ВернутьВсеОбъекты Тогда
		Если ОрганизацияВСтроках И КонтрагентВСтроках Тогда
			ТаблицаСторон.Индексы.Добавить("Организация, Контрагент");
		ИначеЕсли ОрганизацияВСтроках Тогда
			ТаблицаСторон.Индексы.Добавить("Организация");
		Иначе
			ТаблицаСторон.Индексы.Добавить("Контрагент");
		КонецЕсли;
		
		Возврат ТаблицаСторон;
	ИначеЕсли ТаблицаСторон.Количество() > 0 Тогда
		Возврат ТаблицаСторон[0].ОбъектРасчетов;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Создает, перезаполняет при необходимости и возвращает ссылку на объект расчетов по параметрам механизма взаиморасчетов.
// Если в рамках одной структуры было создано несколько элементов объектов расчетов то возвращает первый.
// 
// Параметры:
// 	Объект - ДокументОбъект, СправочникОбъект - Объект, по ссылке на который проверяется объект расчетов.
// 	Ссылка - ДокументСсылка, СправочникСсылка - Ссылка на объект.
// 	СтруктураПараметров - Структура - Текущий набор параметров. см. ВзаиморасчетыСервер.ПараметрыМеханизма().
// 	РежимЗаписи - РежимЗаписиДокумента - Режим записи, если метод вызывается из события ПередЗаписью.
// 	
// Возвращаемое значение:
// 	Структура - Структура параметров.
//
Функция РеквизитыОбъектаРасчетовПоСтруктуре(Объект, Ссылка, СтруктураПараметров, РежимЗаписи) Экспорт
	
	ЭтоДокумент = Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Ссылка));
	
	ПараметрыОбъектаРасчетов = Новый Структура;
	
	//Ключевые реквизиты.
	Если СтруктураПараметров.Свойство("СсылкаНового") Тогда
		ПараметрыОбъектаРасчетов.Вставить("Объект", СтруктураПараметров.СсылкаНового);
	Иначе
		ПараметрыОбъектаРасчетов.Вставить("Объект", Ссылка);
	КонецЕсли;
	ПараметрыОбъектаРасчетов.Вставить("ТипРасчетов", СтруктураПараметров.ТипРасчетов);
	ПараметрыОбъектаРасчетов.Вставить("Организация", ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация, ,Справочники.Организации.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("Контрагент",  ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент, ,Справочники.Контрагенты.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("Партнер",     ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер, ,Справочники.Партнеры.ПустаяСсылка()));
	
	//Служебные реквизиты.
	ПараметрыОбъектаРасчетов.Вставить("Наименование", Строка(Объект));
	ПараметрыОбъектаРасчетов.Вставить("ТипСсылки", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Ссылка)));
	ПометкаУдаления = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,"Объект.ПометкаУдаления");
	ПараметрыОбъектаРасчетов.Вставить("ПометкаУдаления", ПометкаУдаления);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Состояние = 1;
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Состояние = ?(ПараметрыОбъектаРасчетов.ПометкаУдаления,2,0);
	Иначе
		Если ЭтоДокумент Тогда
			Состояние = ?(ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,"Объект.Проведен"),1,?(ПараметрыОбъектаРасчетов.ПометкаУдаления,2,0));
		Иначе
			Состояние = ?(ПараметрыОбъектаРасчетов.ПометкаУдаления,2,0);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОбъектаРасчетов.Вставить("Состояние", Состояние);
	
	Если СтруктураПараметров.ЭтоДоговор Тогда
		ТипОбъектаРасчетов = Перечисления.ТипыОбъектовРасчетов.Договор;
	ИначеЕсли СтруктураПараметров.ЭтоЗаказ Тогда
		ТипОбъектаРасчетов = Перечисления.ТипыОбъектовРасчетов.Заказ;
	ИначеЕсли СтруктураПараметров.ЭтоПродажаЗакупка Тогда
		ТипОбъектаРасчетов = Перечисления.ТипыОбъектовРасчетов.Накладная;
	Иначе
		ТипОбъектаРасчетов = Перечисления.ТипыОбъектовРасчетов.ПлатежВозврат;
	КонецЕсли;
	ПараметрыОбъектаРасчетов.Вставить("ТипОбъектаРасчетов", ТипОбъектаРасчетов);
	
	ВалютаВзаиморасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ВалютаВзаиморасчетов,,Справочники.Валюты.ПустаяСсылка());
	
	Если ЗначениеЗаполнено(СтруктураПараметров.ПутьКДаннымТЧ) Тогда
		ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПутьКДаннымТЧ);
		СуммаДокумента = ТЧ.Итог(СтруктураПараметров.ИмяРеквизитаТЧСуммаСНДС);
		Если ТЧ.Количество() > 0 И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТЧ[0],"СуммаВзаиморасчетов" ) Тогда
			СуммаВзаиморасчетов = ТЧ.Итог("СуммаВзаиморасчетов");
		Иначе
			СуммаВзаиморасчетов = СуммаДокумента;
		КонецЕсли;
	Иначе
		СуммаДокумента = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.СуммаДокумента,,0);
		Если ЗначениеЗаполнено(СтруктураПараметров.СуммаВзаиморасчетов) Тогда
			СуммаВзаиморасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.СуммаВзаиморасчетов,,0);
		Иначе
			СуммаВзаиморасчетов = СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОбъектаРасчетов.Вставить("ГруппаФинансовогоУчета", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ГруппаФинансовогоУчета,,Справочники.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("Менеджер",
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,СтруктураПараметров.Менеджер,, Справочники.Пользователи.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("Подразделение", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,СтруктураПараметров.Подразделение,,Справочники.СтруктураПредприятия.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("НомерВходящегоДокумента",
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,СтруктураПараметров.НомерВходящегоДокумента,,""));
	ПараметрыОбъектаРасчетов.Вставить("ДатаВходящегоДокумента",
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,СтруктураПараметров.ДатаВходящегоДокумента,,Дата(1,1,1)));
	ПараметрыОбъектаРасчетов.Вставить("Сумма",
		СуммаДокумента);
	ПараметрыОбъектаРасчетов.Вставить("Валюта", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ВалютаДокумента,,Справочники.Валюты.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("ВалютаВзаиморасчетов", 
		?(ЗначениеЗаполнено(ВалютаВзаиморасчетов),ВалютаВзаиморасчетов,ПараметрыОбъектаРасчетов.Валюта));
	ПараметрыОбъектаРасчетов.Вставить("СуммаВзаиморасчетов", 
		СуммаВзаиморасчетов);
	ПараметрыОбъектаРасчетов.Вставить("Договор", 
		?(СтруктураПараметров.ЭтоДоговор, Ссылка, ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Договор,, Неопределено)));
	ПараметрыОбъектаРасчетов.Вставить("НаправлениеДеятельности", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.НаправлениеДеятельности,, Справочники.НаправленияДеятельности.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("ОплатаВВалюте", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ОплатаВВалюте,, Ложь));
	ПараметрыОбъектаРасчетов.Вставить("Номер", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.Номер", ,""));
	ПараметрыОбъектаРасчетов.Вставить("Дата", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.Дата", ,Дата(1,1,1)));
	ПараметрыОбъектаРасчетов.Вставить("Комментарий", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.Комментарий", ,""));
	ПараметрыОбъектаРасчетов.Вставить("БанковскийСчетОрганизации", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.БанковскийСчетОрганизации,,Справочники.БанковскиеСчетаОрганизаций));
	ПараметрыОбъектаРасчетов.Вставить("БанковскийСчетКонтрагента", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.БанковскийСчетКонтрагента,,Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("Касса", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Касса,,Справочники.Кассы.ПустаяСсылка()));
	ПараметрыОбъектаРасчетов.Вставить("ФормаОплаты", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ФормаОплаты,, Перечисления.ФормыОплаты.ПустаяСсылка()));
		
	ПустоеСоглашение = ?(СтруктураПараметров.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом, 
		Справочники.СоглашенияСКлиентами.ПустаяСсылка(),
		Справочники.СоглашенияСПоставщиками.ПустаяСсылка());
	ПараметрыОбъектаРасчетов.Вставить("Соглашение", 
		ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Соглашение,,ПустоеСоглашение));
		
	ПараметрыОбъектаРасчетов.Вставить("ТолькоОстатки", СтруктураПараметров.ТолькоОстатки);
		
	Возврат ПараметрыОбъектаРасчетов;
	
КонецФункции

#КонецОбласти

#Область Общий

// Возвращает ссылку на уже имеющийся объект расчетов.
// Платежи с разными партнерами и контрагентами не поддерживаются.
// 
// Параметры: 
// 	Ссылка - ДокументСсылка, СправочникСсылка - Ссылка на исходный объект.
// 	Организация - СправочникСсылка.Организации - Организация.
// 	ТипРасчетов - ПеречислениеСсылка.ТипыРасчетовСПартнерами - Тип расчетов объекта расчетов.
// 	ДополнительныеКритерииПоиска - Структура см. ОбъектыРасчетовСервер.ДополнительныеКритерииПоиска - дополнительные отборы.
// 
// Возвращаемое значение: 
// 	СправочникСсылка.ОбъектыРасчетов - Ссылка на объект расчетов.
//
Функция ПолучитьОбъектРасчетовПоСсылке(Ссылка, Организация = Неопределено, ТипРасчетов = Неопределено, ДополнительныеКритерииПоиска = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Справочники.ОбъектыРасчетов.ПустаяСсылка();
	КонецЕсли;
	
	Массив = Новый Массив;
	Массив.Добавить(Ссылка);
	Возврат ПолучитьОбъектыРасчетовПоСсылкам(Массив, Организация, ТипРасчетов, ДополнительныеКритерииПоиска)[Ссылка];
	
КонецФункции

// Возвращает соответствие ссылок имеющимся объектам расчетов.
// 
// Параметры: 
// 	МассивСсылок - Массив из ДокументСсылка, СправочникСсылка - Массив ссылок на исходные объекты.
// 	Организация - СправочникСсылка.Организации - Кем является организация.
// 	ТипРасчетов - ПеречислениеСсылка.ТипыРасчетовСПартнерами - Тип расчетов объектов расчетов.
// 	ДополнительныеКритерииПоиска - Структура см. ОбъектыРасчетовСервер.ДополнительныеКритерииПоиска - дополнительные отборы.
// 
// Возвращаемое значение: 
// 	Соответствие - Ключ - исходная ссылка, Значение - ссылка на объект расчетов.
//
Функция ПолучитьОбъектыРасчетовПоСсылкам(МассивСсылок, Организация = Неопределено, ТипРасчетов = Неопределено, ДополнительныеКритерииПоиска = Неопределено) Экспорт
	
	Если ДополнительныеКритерииПоиска = Неопределено Тогда
		ДополнительныеКритерииПоиска = ДополнительныеКритерииПоиска();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Объект КАК Объект,
	|	ОбъектыРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	НЕ &ТолькоРеглУчет
	|	И ОбъектыРасчетов.Объект В (&МассивСсылок)
	|	И (ОбъектыРасчетов.Организация = &Организация ИЛИ &ЛюбаяОрганизация)
	|	И (ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов ИЛИ &ЛюбойТип)
	|	И (ОбъектыРасчетов.Партнер = &Партнер ИЛИ &ЛюбойПартнер)
	|	И (ОбъектыРасчетов.Контрагент = &Контрагент ИЛИ &ЛюбойКонтрагент)
	|	И &УсловиеТолькоОстатки
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Объект КАК Объект,
	|	ОбъектыРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	&ТолькоРеглУчет
	|	И (ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.РеализацияТоваровУслуг)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ПоступлениеТоваровНаСклад)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ЗаказКлиента)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ЗаказПоставщику)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Справочник.ДоговорыКонтрагентов))
	|	И ОбъектыРасчетов.Объект В (&МассивСсылок)
	|	И (ОбъектыРасчетов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация))
	|	И (ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов ИЛИ &ЛюбойТип)
	|	И (ОбъектыРасчетов.Партнер = &Партнер ИЛИ &ЛюбойПартнер)
	|	И (ОбъектыРасчетов.Контрагент = &Контрагент ИЛИ &ЛюбойКонтрагент)
	|	
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Объект КАК Объект,
	|	ОбъектыРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	&ТолькоРеглУчет
	|	И НЕ (ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.РеализацияТоваровУслуг)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ПоступлениеТоваровНаСклад)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ЗаказКлиента)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Документ.ЗаказПоставщику)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(ОбъектыРасчетов.Объект) = ТИП(Справочник.ДоговорыКонтрагентов))
	|	И ОбъектыРасчетов.Объект В (&МассивСсылок)
	|	И (ОбъектыРасчетов.Организация = &Организация ИЛИ &ЛюбаяОрганизация)
	|	И (ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов ИЛИ &ЛюбойТип)
	|	И (ОбъектыРасчетов.Партнер = &Партнер ИЛИ &ЛюбойПартнер)
	|	И (ОбъектыРасчетов.Контрагент = &Контрагент ИЛИ &ЛюбойКонтрагент)
	|	";
	
	Запрос.УстановитьПараметр("МассивСсылок",     МассивСсылок);
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("ТипРасчетов",      ТипРасчетов);
	Запрос.УстановитьПараметр("Партнер",          ДополнительныеКритерииПоиска.Партнер);
	Запрос.УстановитьПараметр("Контрагент",       ДополнительныеКритерииПоиска.Контрагент);
	Запрос.УстановитьПараметр("ОбновлениеИБ",     ДополнительныеКритерииПоиска.ОбновлениеИБ);
	Запрос.УстановитьПараметр("ТолькоРеглУчет",   ДополнительныеКритерииПоиска.ТолькоРеглУчет);
	Запрос.УстановитьПараметр("ЛюбаяОрганизация", ?(Организация = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ЛюбойТип",         ?(ТипРасчетов = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ЛюбойПартнер",     ?(ДополнительныеКритерииПоиска.Партнер     = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ЛюбойКонтрагент",  ?(ДополнительныеКритерииПоиска.Контрагент  = Неопределено, Истина, Ложь));
	
	Если ДополнительныеКритерииПоиска.Свойство("ТолькоОстатки") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&УсловиеТолькоОстатки", "ОбъектыРасчетов.ТолькоОстатки = &ТолькоОстатки");
		Запрос.УстановитьПараметр("ТолькоОстатки", ДополнительныеКритерииПоиска.ТолькоОстатки);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&УсловиеТолькоОстатки", "ИСТИНА");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ОбъектыРасчетов = Запрос.Выполнить().Выгрузить();
	ОбъектыРасчетов.Индексы.Добавить("Объект");
	
	Результат = Новый Соответствие;
	Для Каждого Ссылка Из МассивСсылок Цикл
		СтрокиОбъектов = ОбъектыРасчетов.НайтиСтроки(Новый Структура("Объект", Ссылка));
		Если СтрокиОбъектов.Количество() > 1 Тогда
			Если ДополнительныеКритерииПоиска.ВернутьПервый Тогда
				Результат.Вставить(Ссылка, СтрокиОбъектов[0].Ссылка);
			Иначе
				ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='По %1 найдено несколько объектов расчетов.';uk='За %1 знайдено кілька об''єктів розрахунків.'"),
					Ссылка));
			КонецЕсли;
		ИначеЕсли СтрокиОбъектов.Количество() = 1 Тогда
			Результат.Вставить(Ссылка, СтрокиОбъектов[0].Ссылка);
		Иначе
			Результат.Вставить(Ссылка, Справочники.ОбъектыРасчетов.ПустаяСсылка());
		КонецЕсли;
	КонецЦикла;        
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру дополнительных параметров по для поиска объектов расчета по умолчанию
// 
// Возвращаемое значение:
// 	Структура - Описание:
// 		* ОбновлениеИБ - Булево - Признак, указывающий что следует производить поиск и среди некорректных объектов расчетов
// 		* ВернутьПервый - Булево - Признак, указывающий что следует вернуть первый найденный объект расчетов
// 		* Партнер - Неопределено, СправочникСсылка.Партнеры - Значение поиска по полю Партнер
// 		* Контрагент - Неопределено, СправочникСсылка.Контрагенты - Значение поиска по полю Контрагент 
Функция ДополнительныеКритерииПоиска() Экспорт
	
	КритерииПоискаОбъектаРасчетов = Новый Структура();
	КритерииПоискаОбъектаРасчетов.Вставить("Контрагент", Неопределено);
	КритерииПоискаОбъектаРасчетов.Вставить("Партнер", Неопределено);
	КритерииПоискаОбъектаРасчетов.Вставить("ТолькоРеглУчет", Ложь);
	КритерииПоискаОбъектаРасчетов.Вставить("ОбновлениеИБ", Ложь);
	КритерииПоискаОбъектаРасчетов.Вставить("ВернутьПервый", Ложь);
	
	Возврат КритерииПоискаОбъектаРасчетов;
	
КонецФункции

// Создает, перезаполняет при необходимости и возвращает ссылку на объект расчетов по переданной структуре реквизитов.
// 
// Параметры:
// 	РеквизитыОбъекта - Структура - Текущий набор реквизитов объекта расчетов.
// 	ДопРеквизитыИсточника - ТаблицаЗначений - Таблица дополнительных свойств и их значений объекта-источника.
// 
// Возвращаемое значение:
// 	СправочникСсылка.ОбъектыРасчетов - Ссылка на объект расчетов.
//
Функция ПроверитьСоздатьОбъектРасчетов(РеквизитыОбъекта, ДопРеквизитыИсточника = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(РеквизитыОбъекта.Объект) Тогда
		ВызватьИсключение(НСтр("ru='Невозможно создать объект расчетов. Не указаны обязательные параметры.';uk='Неможливо створити об''єкт розрахунків. Не зазначені обов''язкові параметри.'"))
	КонецЕсли;
	
	Если ТипЗнч(РеквизитыОбъекта.Контрагент) = Тип("СправочникСсылка.Организации")
		И (РеквизитыОбъекта.Договор = Неопределено
			ИЛИ ТипЗнч(РеквизитыОбъекта.Договор) <> Тип("СправочникСсылка.ДоговорыМеждуОрганизациями")) Тогда
		РеквизитыОбъекта.Договор = Справочники.ДоговорыМеждуОрганизациями.ПустаяСсылка();
	ИначеЕсли ТипЗнч(РеквизитыОбъекта.Контрагент) <> Тип("СправочникСсылка.Организации")
		И (РеквизитыОбъекта.Договор = Неопределено
			ИЛИ ТипЗнч(РеквизитыОбъекта.Договор) <> Тип("СправочникСсылка.ДоговорыКонтрагентов")) Тогда
		РеквизитыОбъекта.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	РеквизитыСправочника = РеквизитыСправочникаОбъектыРасчетов();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ *
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	ОбъектыРасчетов.Объект = &Объект
	|	И ОбъектыРасчетов.Организация = &Организация
	|	И ОбъектыРасчетов.Контрагент = &Контрагент
	|	И ОбъектыРасчетов.Партнер = &Партнер
	|	И ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов";
	Запрос.УстановитьПараметр("Объект", РеквизитыОбъекта.Объект);
	Запрос.УстановитьПараметр("Организация", РеквизитыОбъекта.Организация);
	Запрос.УстановитьПараметр("Контрагент", РеквизитыОбъекта.Контрагент);
	Запрос.УстановитьПараметр("Партнер", РеквизитыОбъекта.Партнер);
	Запрос.УстановитьПараметр("ТипРасчетов", РеквизитыОбъекта.ТипРасчетов);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ТребуетсяПерезаполнение = Ложь;
		
		Для Каждого Реквизит Из РеквизитыСправочника Цикл
			Если РеквизитыОбъекта.Свойство(Реквизит) И Выборка[Реквизит] <> РеквизитыОбъекта[Реквизит] Тогда
				ТребуетсяПерезаполнение = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ДопРеквизитыИсточника <> Неопределено Тогда
			ДопРеквизитыОбъектаРасчетов = Выборка.ДополнительныеРеквизиты.Выгрузить(); // ТаблицаЗначений -
			ДопРеквизитыОбъектаРасчетов.Индексы.Добавить("Свойство");
			Для Каждого Реквизит Из ДопРеквизитыИсточника Цикл
				Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(Выборка.Ссылка,Реквизит.Свойство) Тогда
					СтрокаДопРеквизита = ДопРеквизитыОбъектаРасчетов.Найти(Реквизит.Свойство);
					Если СтрокаДопРеквизита = Неопределено
						ИЛИ СтрокаДопРеквизита.Значение <> Реквизит.Значение Тогда
						ТребуетсяПерезаполнение = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Выборка.ПометкаУдаления <> РеквизитыОбъекта.ПометкаУдаления Тогда
			ТребуетсяПерезаполнение = Истина;
		КонецЕсли;
		
		Если ТребуетсяПерезаполнение Тогда
			УстановитьПривилегированныйРежим(Истина);
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект(); // СправочникОбъект -
			ЗаполнитьЗначенияСвойств(СправочникОбъект, РеквизитыОбъекта);
			
			Если ДопРеквизитыИсточника <> Неопределено Тогда
				Для Каждого Реквизит Из ДопРеквизитыИсточника Цикл
					Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(Выборка.Ссылка,Реквизит.Свойство) Тогда
						СтрокаДопРеквизита = ДопРеквизитыОбъектаРасчетов.Найти(Реквизит.Свойство);
						Если СтрокаДопРеквизита = Неопределено Тогда
							СтрокаДопРеквизита = ДопРеквизитыОбъектаРасчетов.Добавить();
						КонецЕсли;
						СтрокаДопРеквизита.Свойство = Реквизит.Свойство;
						СтрокаДопРеквизита.Значение = Реквизит.Значение;
						СтрокаДопРеквизита.ТекстоваяСтрока = Реквизит.ТекстоваяСтрока;
					КонецЕсли;
				КонецЦикла;
				СправочникОбъект.ДополнительныеРеквизиты.Загрузить(ДопРеквизитыОбъектаРасчетов);
			КонецЕсли;
			
			СправочникОбъект.Записать();
			УстановитьПривилегированныйРежим(Ложь);
			Возврат СправочникОбъект.Ссылка;
		КонецЕсли;
		
		Возврат Выборка.Ссылка;
		
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		СправочникОбъект = Справочники.ОбъектыРасчетов.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(СправочникОбъект, РеквизитыОбъекта);
		СправочникОбъект.УникальныйИдентификатор = Новый УникальныйИдентификатор();
		
		Если ДопРеквизитыИсточника <> Неопределено Тогда
			Для Каждого Реквизит Из ДопРеквизитыИсточника Цикл
				Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(СправочникОбъект.Ссылка,Реквизит.Свойство) Тогда
					СтрокаДопРеквизита = ДопРеквизитыОбъектаРасчетов.Добавить();
					СтрокаДопРеквизита.Свойство = Реквизит.Свойство;
					СтрокаДопРеквизита.Значение = Реквизит.Значение;
					СтрокаДопРеквизита.ТекстоваяСтрока = Реквизит.ТекстоваяСтрока;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		СправочникОбъект.Записать();
		УстановитьПривилегированныйРежим(Ложь);
		Возврат СправочникОбъект.Ссылка;
	КонецЕсли;
	
КонецФункции

// Проверяет наличие ссылок в объектах данных на переданный объект расчетов. Если ссылок нет, то удаляет объект непосредственно.
//
// Параметры:
// 	ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - Ссылка на объект расчетов.
//
Процедура ПроверитьУдалитьОбъектРасчетов(ОбъектРасчетов, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ОбъектРасчетов)  Тогда
		ВызватьИсключение(НСтр("ru='Невозможно создать объект расчетов. Не указаны обязательные параметры.';uk='Неможливо створити об''єкт розрахунків. Не зазначені обов''язкові параметри.'"))
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОбъектРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	КритерийОтбора.ОбъектРасчетов(&ОбъектРасчетов) КАК ОбъектРасчетов
	|ГДЕ
	|	ОбъектРасчетов.Ссылка <> &Ссылка";
	Запрос.УстановитьПараметр("ОбъектРасчетов", ОбъектРасчетов);
	Запрос.УстановитьПараметр("Ссылка", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектРасчетов, "Объект"));

	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбъектРасчетовОбъект = ОбъектРасчетов.ПолучитьОбъект();
		ОбъектРасчетовОбъект.Удалить();
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Объект расчетов %1 используется в других документах, изменение запрещено.';uk='Об''єкт розрахунків %1 використовується в інших документах, зміну заборонено.'"),
				ОбъектРасчетов);
		Отказ = Истина;
		Сообщить(Сообщение);
	КонецЕсли;
	
КонецПроцедуры

// Создает, перезаполняет при необходимости и возвращает ссылку на объект расчетов по переданной структуре реквизитов.
// 
// Параметры:
// 	РеквизитыОбъекта - Структура - Текущий набор реквизитов объекта расчетов.
// 
// Возвращаемое значение:
// 	СправочникСсылка.ОбъектыРасчетов - Ссылка на объект расчетов.
//
Функция НайтиНовыйОбъектРасчетов(РеквизитыОбъекта) Экспорт
	
	Если Не ЗначениеЗаполнено(РеквизитыОбъекта.Объект) Тогда
		Возврат Справочники.ОбъектыРасчетов.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	1 КАК Приоритет,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	ОбъектыРасчетов.Объект = &Объект
	|	И ОбъектыРасчетов.Организация = &Организация
	|	И ОбъектыРасчетов.Контрагент = &Контрагент
	|	И ОбъектыРасчетов.Партнер = &Партнер
	|	И ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	2 КАК Приоритет,
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	ОбъектыРасчетов.Объект = &Объект
	|	И ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет
	|
	|";
	
	Запрос.УстановитьПараметр("Объект", 		РеквизитыОбъекта.Объект);
	Запрос.УстановитьПараметр("Организация", 	РеквизитыОбъекта.Организация);
	Запрос.УстановитьПараметр("Контрагент", 	РеквизитыОбъекта.Контрагент);
	Запрос.УстановитьПараметр("Партнер", 		РеквизитыОбъекта.Партнер);
	Запрос.УстановитьПараметр("ТипРасчетов", 	РеквизитыОбъекта.ТипРасчетов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда     
		Возврат Выборка.ОбъектРасчетов;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает структуру с обязательными данными для генерации/поиска объекта расчетов
// 
// Возвращаемое значение:
// 	Структура - Описание:
// 		* Объект - ОпределяемыйТип.ОбъектРасчетов - Ссылка на источник объекта расчетов.
// 		* ТипРасчетов - ПеречислениеСсылка.ТипыРасчетовСПартнерами - В каких расчетах отражается – с клиентами или с поставщиками.
// 		* Организация - СправочникСсылка.Организации - Организация объекта расчетов.
// 		* Контрагент - Неопределено - Контрагент объекта расчетов.
// 		* Партнер - СправочникСсылка.Партнеры - Партнер объекта расчетов.
// 		* Договор - Неопределено - Договор объекта расчетов.
// 		* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - Направление деятельности, по которому отражается объект расчетов.
Функция ПолучитьПараметрыОбъектаРасчетов() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Объект");
	СтруктураПараметров.Вставить("ТипРасчетов");
	СтруктураПараметров.Вставить("Организация",             Справочники.Организации.ПустаяСсылка());
	СтруктураПараметров.Вставить("Валюта",                  Справочники.Валюты.ПустаяСсылка());
	СтруктураПараметров.Вставить("Партнер",                 Справочники.Партнеры.ПустаяСсылка());
	СтруктураПараметров.Вставить("Контрагент",              Неопределено);
	СтруктураПараметров.Вставить("Договор",                 Неопределено);
	СтруктураПараметров.Вставить("НаправлениеДеятельности", Справочники.НаправленияДеятельности.ПустаяСсылка());
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Возвращает пустые значения типов, которые могли быть указаны в качестве объекта расчетов
// 
// Возвращаемое значение:
// 	Массив - Массив из ОпределяемыйТип.ОбъектРасчетов, Неопределено
Функция ПустыеЗначенияОбъектРасчетов() Экспорт
	
	ТипыОбъектовРасчетов = Метаданные.ОпределяемыеТипы.ОбъектРасчетов.Тип.Типы();
	ПустыеЗначенияОбъектРасчетов = Новый Массив();
	ПустыеЗначенияОбъектРасчетов.Добавить(Неопределено);
	
	Для Каждого ТипОбъектаРасчетов Из ТипыОбъектовРасчетов Цикл
		 ПустыеЗначенияОбъектРасчетов.Добавить(Новый(ТипОбъектаРасчетов));
	КонецЦикла;
	
	Возврат ПустыеЗначенияОбъектРасчетов;
	
КонецФункции

// Дополняет массив ссылок ссылками на связанные объекты расчетов.
// 
// Параметры:
// 	МассивСсылок - Массив из ДокументСсылка, СправочникСсылка - Массив ссылок на документы и справочники.
//
Процедура ДополнитьСсылкамиНаОбъектыРасчетов(МассивСсылок) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	ОбъектыРасчетов.Объект В (&МассивСсылок)";
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	МассивОбъектовРасчетов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСсылок,МассивОбъектовРасчетов);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеДляГенерацииОбъектовРасчетаСтарая(ЭкземплярОбъекта, Параметры) Экспорт
	
	ПолноеИмяОбъекта = ЭкземплярОбъекта.Метаданные().ПолноеИмя();
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПолноеИмяОбъекта; 
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();

	Запрос = Новый Запрос();
	Запрос.Текст =	"
	|//Документы, являющиеся объектом расчетов
	|ВЫБРАТЬ
	|	ИсточникДанных.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ТолькоДляОстатков,
	|	1 КАК Приоритет
	|ИЗ
	|	&ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ГДЕ
	|			ОбъектыРасчетов.Объект = ИсточникДанных.Ссылка)
	|	И &УсловиеГенерации
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Документы, не являющиеся объектом расчетов, но были выбраны в ТЧ Расшифровка платежа
	|ВЫБРАТЬ
	|	ИсточникДанных.Ссылка,
	|	ИСТИНА,
	|	2
	|ИЗ
	|	&ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ГДЕ
	|			ОбъектыРасчетов.Объект = ИсточникДанных.Ссылка)
	|	И НЕ (&УсловиеГенерации)
	|	И (&УсловиеИспользованиеВРасшифровкеПлатежа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Документы, не являющиеся объектом расчетов, но участвуют в движениях регистров накопления
	|ВЫБРАТЬ
	|	ИсточникДанных.Ссылка,
	|	ИСТИНА,
	|	3
	|ИЗ
	|	&ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ГДЕ
	|			ОбъектыРасчетов.Объект = ИсточникДанных.Ссылка)
	|	И НЕ (&УсловиеГенерации)
	|	И (&УсловиеИспользованиеВРН)
	|
	|";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеГенерации", УсловиеГенерацииОбъектаРасчетов(ЭкземплярОбъекта));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеИспользованиеВРасшифровкеПлатежа", ТекстПоискаПоТЧРасшифровкаПлатежа());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеИспользованиеВРН", ТекстПоискаВРегистрахНакопления());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИсточникДанных", ПолноеИмяОбъекта);
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ПустыеЗначенияОбъектРасчетов());
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Функция ЭкземплярыТиповОбъектовРасчетов()
	
	ЭкземплярыОбъектов = Новый Массив();
	ЭкземплярыОбъектов.Добавить(Документы.АвансовыйОтчет.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.АктВыполненныхРабот.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ВозвратТоваровМеждуОрганизациями.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ВозвратТоваровОтКлиента.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ВозвратТоваровПоставщику.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ВыкупВозвратнойТарыКлиентом.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ВыкупВозвратнойТарыУПоставщика.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ВыкупПринятыхНаХранениеТоваров.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ВыкупТоваровХранителем.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ЗаказКлиента.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ЗаказПоставщику.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОперацияПоПлатежнойКарте.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОтчетКомиссионера.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОтчетКомиссионераОСписании.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОтчетКомитенту.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОтчетКомитентуОСписании.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОтчетПоКомиссииМеждуОрганизациями.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОтчетПоКомиссииМеждуОрганизациямиОСписании.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ПервичныйДокумент.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ПоступлениеБезналичныхДенежныхСредств.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ПриобретениеТоваровУслуг.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ПриобретениеУслугПрочихАктивов.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ПриходныйКассовыйОрдер.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.РасходныйКассовыйОрдер.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.РеализацияТоваровУслуг.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.РеализацияУслугПрочихАктивов.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.СписаниеБезналичныхДенежныхСредств.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.СписаниеПринятыхНаХранениеТоваров.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ТаможеннаяДекларацияИмпорт.ПустаяСсылка());
	//++ НЕ УТ
	//++ Локализация
	ЭкземплярыОбъектов.Добавить(Документы.ВыбытиеДенежныхДокументов.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ПоступлениеДенежныхДокументов.ПустаяСсылка());
	//-- Локализация
	//-- НЕ УТ
	//++ НЕ УТ
	ЭкземплярыОбъектов.Добавить(Документы.ЗаказПереработчику.ПустаяСсылка());
	ЭкземплярыОбъектов.Добавить(Документы.ОтчетПереработчика.ПустаяСсылка());
	//-- НЕ УТ
	
	Возврат ЭкземплярыОбъектов;
	
КонецФункции

Функция ПолныеИменаТиповОбъектовРасчетов()
	
	ЭкземплярыТиповОбъектовРасчетов = ЭкземплярыТиповОбъектовРасчетов();
	ПолныеИмена = Новый Массив();
	
	Для Каждого ТипОбъектаРасчетов Из ЭкземплярыТиповОбъектовРасчетов Цикл
		ПолныеИмена.Добавить(ТипОбъектаРасчетов.Метаданные().ПолноеИмя());
	КонецЦикла;
	
	Возврат СтрСоединить(ПолныеИмена, ",");
	
КонецФункции

// Обработчик обновления
// 
// Параметры:
// 	ЭкземплярОбъекта - ДокументСсылка - Пустая ссылка на документ, по которому требуется сгенерировать объект расчетов
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеДляГенерацииОбъектовРасчета(ЭкземплярОбъекта, Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПолныеИменаТиповОбъектовРасчетов();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Дата УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	ПолноеИмяОбъекта = ЭкземплярОбъекта.Метаданные().ПолноеИмя();

	Запрос = Новый Запрос();
	Запрос.Текст =	"
	|//Документы, являющиеся объектом расчетов
	|ВЫБРАТЬ
	|	ИсточникДанных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТДокументыОбъектыРасчетов
	|ИЗ
	|	&ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	&УсловиеГенерации
	|ИНДЕКСИРОВАТЬ ПО Ссылка
	|;
	|
	|//Документы, являющиеся объектом расчетов, выборка для регистрации
	|ВЫБРАТЬ
	|	ИсточникДанных.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ТолькоДляОстатков,
	|	1 КАК Приоритет
	|ИЗ
	|	ВТДокументыОбъектыРасчетов КАК ИсточникДанных
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ГДЕ
	|			ОбъектыРасчетов.Объект = ИсточникДанных.Ссылка
	|			И НЕ ОбъектыРасчетов.ТолькоОстатки)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Документы, не являющиеся объектом расчетов, но были выбраны в ТЧ Расшифровка платежа
	|ВЫБРАТЬ
	|	ИсточникДанных.Ссылка,
	|	ИСТИНА,
	|	2
	|ИЗ
	|	&ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ВТДокументыОбъектыРасчетов КАК ДокОбъектРасчетов
	|		ГДЕ
	|			ДокОбъектРасчетов.Ссылка = ИсточникДанных.Ссылка)
	|	И НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ГДЕ
	|			ОбъектыРасчетов.Объект = ИсточникДанных.Ссылка
	|			И ОбъектыРасчетов.ТолькоОстатки)
	|	И (&УсловиеИспользованиеВРасшифровкеПлатежа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Документы, не являющиеся объектом расчетов, но участвуют в движениях регистров накопления
	|ВЫБРАТЬ
	|	ИсточникДанных.Ссылка,
	|	ИСТИНА,
	|	3
	|ИЗ
	|	&ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ВТДокументыОбъектыРасчетов КАК ДокОбъектРасчетов
	|		ГДЕ
	|			ДокОбъектРасчетов.Ссылка = ИсточникДанных.Ссылка)
	|	И НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ГДЕ
	|			ОбъектыРасчетов.Объект = ИсточникДанных.Ссылка
	|			И ОбъектыРасчетов.ТолькоОстатки)
	|	И (&УсловиеИспользованиеВРН)
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеГенерации", УсловиеГенерацииОбъектаРасчетов(ЭкземплярОбъекта));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеИспользованиеВРасшифровкеПлатежа", ТекстПоискаПоТЧРасшифровкаПлатежа());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеИспользованиеВРН", ТекстПоискаВРегистрахНакопления());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИсточникДанных", ПолноеИмяОбъекта);
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ПустыеЗначенияОбъектРасчетов());
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура СгенерироватьОбъектыРасчетов(ЭкземплярОбъекта, Параметры) Экспорт
	
	ПолноеИмяОбъекта = ЭкземплярОбъекта.Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если Не ОбновляемыеДанные.Количество() Тогда
		Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДанных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТДляОбработки
	|ИЗ
	|	&ОбновляемыеДанные КАК ТаблицаДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДляОбработки.Ссылка КАК Ссылка,
	|	ИсточникДанных.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА НЕ &УсловиеГенерацииОбъектаРасчетов
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоОстатки
	|ИЗ
	|	ВТДляОбработки КАК ДанныеДляОбработки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ИсточникДанных КАК ИсточникДанных
	|		ПО ИсточникДанных.Ссылка = ДанныеДляОбработки.Ссылка
	|";
	
	ТекстЗапроса = ТекстЗапроса + ТекстПроверкаИспользованияВРасчетныхРегистрах(Истина);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ПустыеЗначенияОбъектРасчетов());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеГенерацииОбъектаРасчетов", УсловиеГенерацииОбъектаРасчетов(ЭкземплярОбъекта));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИсточникДанных", ПолноеИмяОбъекта);
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[1].Выбрать();
	
	ИспользованиеВРасчетныхРегистрах = Результат[2].Выгрузить();
	ИспользованиеВРасчетныхРегистрах.Индексы.Добавить("ОбъектРасчетов, Организация, ТипРасчетов");
	
	Пока Выборка.Следующий() Цикл
		СсылкаНаЭлемент = Выборка.Ссылка;
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СсылкаНаЭлемент);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
			Объект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(СсылкаНаЭлемент, Выборка.ВерсияДанных, Параметры.Очередь);
			Если Объект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
 				Продолжить;
			КонецЕсли;
			
			ПараметрыГенерации = ПараметрыВзаиморасчетовОбъектаРасчетов(Объект);
			
			Если Не ТипЗнч(ПараметрыГенерации) = Тип("Массив") Тогда
				ПараметрыГенерации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыГенерации);
			КонецЕсли;
			
			ВзаиморасчетыСервер.ДополненныеПараметрыМеханизма(Объект, ПараметрыГенерации);
			
			Если (ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияТоваровУслуг")
				И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
				Или (ТипЗнч(Объект) = Тип("ДокументОбъект.ПриобретениеТоваровУслуг")
				И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет) Тогда
					ПараметрыГенерацииПоУпрОрганизации = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыГенерации[0]);
					ПараметрыГенерацииПоУпрОрганизации.Организация = Справочники.Организации.УправленческаяОрганизация;
					ПараметрыГенерацииПоУпрОрганизации.Договор = "";
					ПараметрыГенерации.Добавить(ПараметрыГенерацииПоУпрОрганизации);
			КонецЕсли;
			
			//Для некорректных объектов расчетов дополняем параметры генерации позициями,
			//которые уже участвуют в движениях расчетных регистров, но отсутствуют в поставляемых документом
			//параметрах взаиморасчетов
			ДополнитьПараметрыГенерацииИсключениями(ПараметрыГенерации, ИспользованиеВРасчетныхРегистрах, Объект);
			
			Для Каждого ПараметрГенерации Из ПараметрыГенерации Цикл
				//Генерация объекта расчетов, объектом которого является сам документ, а так же
				//объекта, который не должен быть объектом расчетов по параметрам взаиморасчетов, 
				//но был ранее указан в расшифровке платежа в других документах.
				Если Не ПараметрГенерации.ТолькоОстатки Тогда
					ПараметрГенерации.ТолькоОстатки = Выборка.ТолькоОстатки;
				КонецЕсли;
				
				ОрганизацияВСтроках = ПараметрГенерации.ОрганизацияВСтроках И ТипЗнч(ПараметрГенерации.Организация) = Тип("Строка");
				КонтрагентВСтроках  = ПараметрГенерации.КонтрагентВСтроках И ТипЗнч(ПараметрГенерации.Контрагент) = Тип("Строка");
				ПартнерВСтроках     = ПараметрГенерации.ПартнерВСтроках И ТипЗнч(ПараметрГенерации.Партнер) = Тип("Строка");
				
				ТребуетсяГенерацияОР = СсылкаЯвляетсяОбъектомРасчетов(Объект, ПараметрГенерации, Истина) Или ПараметрГенерации.ТолькоОстатки;
				
				ГенерацияПоТЧ = ОрганизацияВСтроках Или КонтрагентВСтроках Или ПартнерВСтроках;
				Если ГенерацияПоТЧ Тогда
					Если ОрганизацияВСтроках Тогда
						ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Организация);
					ИначеЕсли КонтрагентВСтроках Тогда
						ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Контрагент);
					Иначе
						ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Партнер);
					КонецЕсли;
					Если Не ТЧ.Количество() Тогда
						ТребуетсяГенерацияОР = Ложь;
					Иначе
						ТребуетсяГенерацияОР = Ложь;
						ИмяТЧРасшифровкаПлатежа = СтрЗаменить(ПараметрГенерации.ПутьКДаннымТЧРасшифровкаПлатежа, "Объект.", "");
						УстаревшееПолеОР = Объект.Метаданные().ТабличныеЧасти[ИмяТЧРасшифровкаПлатежа].Реквизиты.Найти("УдалитьЗаказ");
						
						Для Каждого СтрокаТЧ Из ТЧ Цикл
							Если СтрокаТЧ.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка() 
								И УстаревшееПолеОР <> Неопределено 
								И (Не ЗначениеЗаполнено(СтрокаТЧ.УдалитьЗаказ) 
									Или СтрокаТЧ.УдалитьЗаказ = Объект.Ссылка) Тогда
									ТребуетсяГенерацияОР = Истина;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				
				Если ТребуетсяГенерацияОР Тогда
					НовыйОбъектРасчетов = ПроверитьЗаполнитьОбъектРасчетовПоСтруктуре(Объект, ПараметрГенерации);
					Если Не ЗначениеЗаполнено(НовыйОбъектРасчетов) Тогда
						Шаблон = НСтр("ru='Не удалось создать объект расчетов для документа: ""%1""';uk='Не вдалося створити об''єкт розрахунків для документа: ""%1""'");
							ТекстСообщения = 
							СтрШаблон(Шаблон,
								Объект.Ссылка);
						ВызватьИсключение ТекстСообщения;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СсылкаНаЭлемент);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), СсылкаНаЭлемент);
			Продолжить;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

// Генерирует новый элемент в справочнике ОбъектыРасчетов по комбинации полей АналитикаУчетаРасчетовПоПартнеру 
// и возвращает ее
//
// Параметры:
//  Параметры - Структура - Коллекция Параметров:
//  	АналитикаУчетаПоПартнерам
//  
// Возвращаемое значение:
// 	СправочникСсылка.ОбъектыРасчетов - Сгенерированный объект расчетов
//
Функция СоздатьОбъектРасчетовПоАналитикеУчетаПоПартнерам(Параметры) Экспорт

	ОбъектРасчетов = Справочники.ОбъектыРасчетов.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(ОбъектРасчетов, Параметры);
	ОбъектРасчетов.ТолькоОстатки = Истина;
	ОбъектРасчетов.УникальныйИдентификатор = Новый УникальныйИдентификатор();
	ОбъектРасчетов.Записать();
	
	Возврат ОбъектРасчетов.Ссылка;
	
КонецФункции

Функция НайтиОбъектРасчетовПоАналитикеУчетаПоПартнерам(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыРасчетов.Ссылка
		|ИЗ
		|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|ГДЕ
		|	ОбъектыРасчетов.Организация = &Организация
		|	И ОбъектыРасчетов.Партнер = &Партнер
		|	И ОбъектыРасчетов.Контрагент = &Контрагент
		|	И ОбъектыРасчетов.Договор = &Договор
		|	И ОбъектыРасчетов.НаправлениеДеятельности = &НаправлениеДеятельности
		|	И ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов
		|	И ОбъектыРасчетов.Объект = НЕОПРЕДЕЛЕНО";
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Запрос.Параметры, Параметры);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает соответствия ссылок элементам справочника объекты расчетов
// 
// Параметры:
// 	ТаблицаПоиска - ТаблицаЗначений - таблица ссылок для поиска:
// 	 * НомерСтроки - Число - номер строки поиска.
// Возвращаемое значение:
// 	Соответствие - Описание
Функция НайтиОбъектыРасчетовПоАналитикеУчетаПоПартнерам(ТаблицаПоиска) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеПоиска.НомерСтроки,
		|	ДанныеПоиска.Организация,
		|	ДанныеПоиска.Партнер,
		|	ДанныеПоиска.Контрагент,
		|	ДанныеПоиска.Договор,
		|	ДанныеПоиска.НаправлениеДеятельности,
		|	ДанныеПоиска.ТипРасчетов,
		|	ДанныеПоиска.Валюта
		|ПОМЕСТИТЬ ВТДанныеПоиска
		|ИЗ 
		|	&ДанныеПоиска КАК ДанныеПоиска
		|;
		|
		|ВЫБРАТЬ
		|	ДанныеПоиска.НомерСтроки КАК НомерСтроки,
		|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
		|ИЗ
		|	ВТДанныеПоиска КАК ДанныеПоиска
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ПО ОбъектыРасчетов.Организация = ДанныеПоиска.Организация
		|		И ОбъектыРасчетов.Партнер = ДанныеПоиска.Партнер
		|		И ОбъектыРасчетов.Контрагент = ДанныеПоиска.Контрагент
		|		И ОбъектыРасчетов.Договор = ДанныеПоиска.Договор
		|		И ОбъектыРасчетов.НаправлениеДеятельности = ДанныеПоиска.НаправлениеДеятельности
		|		И ОбъектыРасчетов.ТипРасчетов = ДанныеПоиска.ТипРасчетов
		|		И ОбъектыРасчетов.Валюта = ДанныеПоиска.Валюта
		|		И ОбъектыРасчетов.Объект = НЕОПРЕДЕЛЕНО";
	
	Запрос.УстановитьПараметр("ДанныеПоиска", ТаблицаПоиска);
	ОбъектыРасчетов = Запрос.Выполнить().Выгрузить();

	Результат = Новый Соответствие;
	Для Каждого СтрокаАналитики Из ТаблицаПоиска Цикл
		СтрокиОбъектов = ОбъектыРасчетов.НайтиСтроки(Новый Структура("НомерСтроки", СтрокаАналитики.НомерСтроки));
		Если СтрокиОбъектов.Количество() > 1 Тогда
			ВызватьИсключение(НСтр("ru='Ошибка заполнения объекта расчетов. Найдено несколько объектов расчетов.';uk='Помилка заповнення об''єкта розрахунків. Знайдено кілька об''єктів розрахунків.'"));
		ИначеЕсли СтрокиОбъектов.Количество() = 1 Тогда
			Результат.Вставить(СтрокаАналитики.НомерСтроки, СтрокиОбъектов[0].ОбъектРасчетов);
		Иначе
			Результат.Вставить(СтрокаАналитики.НомерСтроки, Справочники.ОбъектыРасчетов.ПустаяСсылка());
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыПоискаПустогоОбъектаРасчетов() Экспорт
	
	ПараметрыПоиска = Новый ТаблицаЗначений();
	ПараметрыПоиска.Колонки.Добавить("НомерСтроки",             Новый ОписаниеТипов("Число"));
	ПараметрыПоиска.Колонки.Добавить("Организация",             Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	СписокТиповКонтрагенты = Новый Массив();
	СписокТиповКонтрагенты.Добавить(Тип("СправочникСсылка.Организации"));
	СписокТиповКонтрагенты.Добавить(Тип("СправочникСсылка.Контрагенты"));
	ПараметрыПоиска.Колонки.Добавить("Контрагент",              Новый ОписаниеТипов(СписокТиповКонтрагенты));
	ПараметрыПоиска.Колонки.Добавить("Партнер",                 Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	
	СписокТиповДоговора = Новый Массив();
	СписокТиповДоговора.Добавить(Тип("СправочникСсылка.ДоговорыКонтрагентов"));
	СписокТиповДоговора.Добавить(Тип("СправочникСсылка.ДоговорыМеждуОрганизациями"));
	ПараметрыПоиска.Колонки.Добавить("Договор",                 Новый ОписаниеТипов(СписокТиповДоговора));
	ПараметрыПоиска.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));
	ПараметрыПоиска.Колонки.Добавить("ТипРасчетов",             Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРасчетовСПартнерами"));
	ПараметрыПоиска.Колонки.Добавить("Валюта",                  Новый ОписаниеТипов("СправочникСсылка.Валюты"));

	Возврат ПараметрыПоиска;
	
КонецФункции

Процедура ЗаполнитьОбъектыРасчетов(Объект, ОбъектИзменен = Неопределено, ТабЧастиВидыЗапасов = Неопределено) Экспорт
	
	ПараметрыГенерации = ПараметрыВзаиморасчетовОбъектаРасчетов(Объект);
	
	ТребуетсяЗаполнениеОбъектовРасчетовВШапкеИлиТЧ = Ложь;
	ТребуетсяЗаполнениеОбъектовРасчетовВРасшифровкеПлатежа = Ложь;

	Если Не ТипЗнч(ПараметрыГенерации) = Тип("Массив") Тогда
		ПараметрыГенерации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыГенерации);
	КонецЕсли;

	ДополненныеПараметрыМеханизма = ВзаиморасчетыСервер.ДополненныеПараметрыМеханизма(Объект, ПараметрыГенерации);
	
	Для Каждого СтруктураПараметровГенерации Из ПараметрыГенерации Цикл
		
		ПутьКОбъектамРасчетов = СтрРазделить(СтруктураПараметровГенерации.ОбъектРасчетов, ".", Ложь);
		ОбъектыРасчетовВТЧ = ПутьКОбъектамРасчетов.Количество() = 3;
		
		Если Не ОбъектыРасчетовВТЧ И Не ЗначениеЗаполнено(ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(
			Объект,
			СтруктураПараметровГенерации.ОбъектРасчетов)) Тогда
			ТребуетсяЗаполнениеОбъектовРасчетовВШапкеИлиТЧ = Истина;
		ИначеЕсли ОбъектыРасчетовВТЧ Тогда
			Для Каждого СтрокаТЧ Из Объект[ПутьКОбъектамРасчетов[1]] Цикл
				Если СтрокаТЧ.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка() Тогда
					ТребуетсяЗаполнениеОбъектовРасчетовВШапкеИлиТЧ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураПараметровГенерации.ПутьКДаннымТЧРасшифровкаПлатежа)
			И Не ТребуетсяЗаполнениеОбъектовРасчетовВРасшифровкеПлатежа Тогда
			ДанныеТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметровГенерации.ПутьКДаннымТЧРасшифровкаПлатежа);
			Для Каждого СтрокаТЧ Из ДанныеТЧ Цикл
				Если СтрокаТЧ.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка() Тогда
					ТребуетсяЗаполнениеОбъектовРасчетовВРасшифровкеПлатежа = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если ТабЧастиВидыЗапасов <> Неопределено Тогда
		Для Каждого ТабЧастьВидыЗапасов Из ТабЧастиВидыЗапасов Цикл
			Для Каждого СтрокаВидовЗапасов Из Объект[ТабЧастьВидыЗапасов.Ключ] Цикл
				Если СтрокаВидовЗапасов.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка() Тогда
					ТребуетсяЗаполнениеОбъектовРасчетовВШапкеИлиТЧ = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если ТребуетсяЗаполнениеОбъектовРасчетовВШапкеИлиТЧ Тогда
		ЗаполнитьОбъектРасчетов(Объект, ДополненныеПараметрыМеханизма.МассивПараметров, Истина, РежимЗаписиДокумента.Запись, ТабЧастиВидыЗапасов);
		ОбъектИзменен = Истина;
	КонецЕсли;
	
	Если ТребуетсяЗаполнениеОбъектовРасчетовВРасшифровкеПлатежа Тогда
		Для Каждого СтруктураПараметровГенерации Из ДополненныеПараметрыМеханизма.МассивПараметров Цикл
			Если ЗначениеЗаполнено(СтруктураПараметровГенерации.ПутьКДаннымТЧРасшифровкаПлатежа) Тогда
				ЗаполнитьОбъектРасчетовВРасшифровкеПлатежа(Объект, СтруктураПараметровГенерации);
				ОбъектИзменен = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстПроверкаИспользованияВРасчетныхРегистрах(УниверсальнаяГенерация = Ложь) Экспорт
	
	ТекстЗапроса = "
	|;
	|         
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщиками.УдалитьЗаказПоставщику КАК ОбъектРасчетов,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыСПоставщиками.Валюта КАК ВалютаВзаиморасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|ГДЕ РасчетыСПоставщиками.УдалитьЗаказПоставщику В (&ОбновляемыеДанные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентами.УдалитьЗаказКлиента КАК ОбъектРасчетов,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыСКлиентами.Валюта КАК ВалютаВзаиморасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|ГДЕ РасчетыСКлиентами.УдалитьЗаказКлиента В (&ОбновляемыеДанные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщикамиПоДокументам.УдалитьЗаказПоставщику КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыСПоставщикамиПоДокументам.Валюта КАК ВалютаВзаиморасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщикамиПоДокументам
	|ГДЕ РасчетыСПоставщикамиПоДокументам.УдалитьЗаказПоставщику В (&ОбновляемыеДанные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентамиПоДокументам.УдалитьЗаказКлиента КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыСКлиентамиПоДокументам.Валюта КАК ВалютаВзаиморасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСКлиентамиПоДокументам
	|ГДЕ РасчетыСКлиентамиПоДокументам.УдалитьЗаказКлиента В (&ОбновляемыеДанные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщикамиПоСрокам.УдалитьОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыСПоставщикамиПоСрокам.Валюта КАК ВалютаВзаиморасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|ГДЕ РасчетыСПоставщикамиПоСрокам.УдалитьОбъектРасчетов В (&ОбновляемыеДанные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентамиПоСрокам.УдалитьОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыСКлиентамиПоСрокам.Валюта КАК ВалютаВзаиморасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|ГДЕ РасчетыСКлиентамиПоСрокам.УдалитьОбъектРасчетов В (&ОбновляемыеДанные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.УдалитьОбъектРасчетов КАК ОбъектРасчетов,
	|	НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Неопределено КАК ВалютаВзаиморасчетов,                                       
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов КАК НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов
	|ГДЕ НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.УдалитьОбъектРасчетов В (&ОбновляемыеДанные)
	|    И ТИПЗНАЧЕНИЯ(НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов.УдалитьОбъектРасчетов) = ТИП(Документ.АвансовыйОтчет)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСРасчетНалоговогоКредита.УдалитьОбъектРасчетов КАК ОбъектРасчетов,
	|	НДСРасчетНалоговогоКредита.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НДСРасчетНалоговогоКредита.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	НДСРасчетНалоговогоКредита.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	НДСРасчетНалоговогоКредита.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	НДСРасчетНалоговогоКредита.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Неопределено КАК ВалютаВзаиморасчетов,                                       
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов
	|ИЗ
	|	РегистрНакопления.НДСРасчетНалоговогоКредита КАК НДСРасчетНалоговогоКредита
	|ГДЕ НДСРасчетНалоговогоКредита.УдалитьОбъектРасчетов В (&ОбновляемыеДанные)
	|    И ТИПЗНАЧЕНИЯ(НДСРасчетНалоговогоКредита.УдалитьОбъектРасчетов) = ТИП(Документ.АвансовыйОтчет)
	|
	|
	|";
		
	Если УниверсальнаяГенерация Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбновляемыеДанные", "ВЫБРАТЬ ОбновляемыеДанные.Ссылка ИЗ ВТДляОбработки КАК ОбновляемыеДанные");
	КонецЕсли;
			
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ВсеОбъектыРасчетовСгенерированы(Очередь) Экспорт
	
		ОбработчикиГенерации = Новый Массив();
		ОбработчикиГенерации.Добавить("Документ.АктВыполненныхРабот");
		ОбработчикиГенерации.Добавить("Документ.ВозвратТоваровМеждуОрганизациями");
		ОбработчикиГенерации.Добавить("Документ.ВозвратТоваровОтКлиента");
		ОбработчикиГенерации.Добавить("Документ.ВозвратТоваровПоставщику");
		ОбработчикиГенерации.Добавить("Документ.ВыкупВозвратнойТарыКлиентом");
		ОбработчикиГенерации.Добавить("Документ.ВыкупВозвратнойТарыУПоставщика");
		ОбработчикиГенерации.Добавить("Документ.ЗаказКлиента");
		ОбработчикиГенерации.Добавить("Документ.ЗаказПоставщику");
		ОбработчикиГенерации.Добавить("Документ.ЗаявкаНаВозвратТоваровОтКлиента");
		ОбработчикиГенерации.Добавить("Документ.ОперацияПоПлатежнойКарте");
		ОбработчикиГенерации.Добавить("Документ.ОтчетКомиссионера");
		ОбработчикиГенерации.Добавить("Документ.ОтчетКомиссионераОСписании");
		ОбработчикиГенерации.Добавить("Документ.ОтчетКомитенту");
		ОбработчикиГенерации.Добавить("Документ.ОтчетКомитентуОСписании");
		ОбработчикиГенерации.Добавить("Документ.ОтчетПоКомиссииМеждуОрганизациями");
		ОбработчикиГенерации.Добавить("Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании");
		ОбработчикиГенерации.Добавить("Документ.ПервичныйДокумент");
		ОбработчикиГенерации.Добавить("Документ.ПередачаТоваровМеждуОрганизациями");
		ОбработчикиГенерации.Добавить("Документ.ПоступлениеБезналичныхДенежныхСредств");
		ОбработчикиГенерации.Добавить("Документ.ПриобретениеТоваровУслуг");
		ОбработчикиГенерации.Добавить("Документ.ПриобретениеУслугПрочихАктивов");
		ОбработчикиГенерации.Добавить("Документ.ПриходныйКассовыйОрдер");
		ОбработчикиГенерации.Добавить("Документ.РасходныйКассовыйОрдер");
		ОбработчикиГенерации.Добавить("Документ.РеализацияТоваровУслуг");
		ОбработчикиГенерации.Добавить("Документ.РеализацияУслугПрочихАктивов");
		ОбработчикиГенерации.Добавить("Документ.СписаниеБезналичныхДенежныхСредств");
		ОбработчикиГенерации.Добавить("Документ.ТаможеннаяДекларацияИмпорт");
		ОбработчикиГенерации.Добавить("Справочник.ДоговорыКонтрагентов");
		ОбработчикиГенерации.Добавить("Справочник.ДоговорыМеждуОрганизациями");
		ОбработчикиГенерации.Добавить("Справочник.Контрагенты");
		//++ НЕ УТ
		//++ Локализация
		ОбработчикиГенерации.Добавить("Документ.ВыбытиеДенежныхДокументов");
		ОбработчикиГенерации.Добавить("Документ.ПоступлениеДенежныхДокументов");
		//-- Локализация
		ОбработчикиГенерации.Добавить("Документ.ЗаказПереработчику");
		ОбработчикиГенерации.Добавить("Документ.ОтчетПереработчика");
		//-- НЕ УТ
	
		ОбработкаЗавершена = Истина;
		
		Для Каждого ПолноеИмяОбъекта Из ОбработчикиГенерации Цикл 
			
			ИмяТипа = СокрЛП(СтрРазделить(ПолноеИмяОбъекта, ".", Ложь)[0]);
			Если ВРЕГ(ИмяТипа) = ВРЕГ("Документ") Тогда
				ИмяТипа = "Документы";
			ИначеЕсли ВРЕГ(ИмяТипа) = ВРЕГ("Справочник") Тогда
				ИмяТипа = "Справочники";					
			КонецЕсли;
			ИмяОбъекта = СокрЛП(СтрРазделить(ПолноеИмяОбъекта, ".", Ложь)[1]);
			ИмяОбработчикаГенерации = ИмяТипа + "." + ИмяОбъекта + ".СгенерироватьОбъектыРасчетов"; 
			ОчередьОбработчикаГенерации = ОбновлениеИнформационнойБазы.ОчередьОтложенногоОбработчикаОбновления(ИмяОбработчикаГенерации);
			Если ОчередьОбработчикаГенерации = Неопределено Тогда
				Продолжить;
				//ВызватьИсключение (
				//	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				//		НСтр("ru='ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы - не обнаружен обработчик СгенерироватьОбъектыРасчетов для объекта: %1.'"),
				//		ПолноеИмяОбъекта
				//	)
				//);
			КонецЕсли;
			
			Если ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(ОчередьОбработчикаГенерации + 1, ПолноеИмяОбъекта) Тогда
				ОбработкаЗавершена = Ложь;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат ОбработкаЗавершена;
	
КонецФункции

Процедура ДополнитьПараметрыГенерацииИсключениями(ПараметрыГенерации, ИспользованиеВРасчетныхРегистрах, Объект) Экспорт
	
	СсылкаНаЭлемент = Объект.Ссылка;
	ПараметрыГенерацииБезИсключений = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыГенерации);
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить("СправочникСсылка.Организации");
	СписокТипов.Добавить("СправочникСсылка.Контрагенты");
	ОписаниеТипаПоляКонтрагент = Новый ОписаниеТипов(СписокТипов);
	
	ОбъектыРасчетовКорректные = Новый ТаблицаЗначений();
	ОбъектыРасчетовКорректные.Колонки.Добавить("ОбъектРасчетов");
	ОбъектыРасчетовКорректные.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ОбъектыРасчетовКорректные.Колонки.Добавить("Контрагент", Новый ОписаниеТипов(ОписаниеТипаПоляКонтрагент));
	ОбъектыРасчетовКорректные.Колонки.Добавить("Партнер", Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	ОбъектыРасчетовКорректные.Колонки.Добавить("ТипРасчетов", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРасчетовСПартнерами"));
	ОбъектыРасчетовКорректные.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ОбъектыРасчетовКорректные.Колонки.Добавить("ВалютаВзаиморасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ОбъектыРасчетовКорректные.Колонки.Добавить("ТолькоОстатки", Новый ОписаниеТипов("Булево"));
	
	Для Каждого ПараметрГенерации Из ПараметрыГенерацииБезИсключений Цикл
		
		ОрганизацияВСтроках = ПараметрГенерации.ОрганизацияВСтроках;
		КонтрагентВСтроках  = ПараметрГенерации.КонтрагентВСтроках;
		ПартнерВСтроках     = ПараметрГенерации.ПартнерВСтроках;
		ТолькоОстатки       = Не СсылкаЯвляетсяОбъектомРасчетов(Объект, ПараметрГенерации);

		Если ОрганизацияВСтроках Или КонтрагентВСтроках Или ПартнерВСтроках Тогда
			
			Если ОрганизацияВСтроках Тогда
				ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Организация); // ТабличнаяЧасть -
			ИначеЕсли КонтрагентВСтроках Тогда
				ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Контрагент); // ТабличнаяЧасть -
			Иначе
				ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Партнер); // ТабличнаяЧасть -
			КонецЕсли;
		
			Для Каждого Стр Из ТЧ Цикл
				
				Идентификатор = Стр.НомерСтроки;
				
				НовыйКорректныйОбъектРасчетов                 = ОбъектыРасчетовКорректные.Добавить();
				НовыйКорректныйОбъектРасчетов.ОбъектРасчетов  = СсылкаНаЭлемент;
				НовыйКорректныйОбъектРасчетов.ТипРасчетов     = ПараметрГенерации.ТипРасчетов;
				НовыйКорректныйОбъектРасчетов.ТолькоОстатки   = ТолькоОстатки;
				НовыйКорректныйОбъектРасчетов.Валюта = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.ВалютаДокумента);
				НовыйКорректныйОбъектРасчетов.ВалютаВзаиморасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,
					?(ЗначениеЗаполнено(ПараметрГенерации.ВалютаВзаиморасчетов),ПараметрГенерации.ВалютаВзаиморасчетов,ПараметрГенерации.ВалютаДокумента));
				
				Если ОрганизацияВСтроках Тогда
					НовыйКорректныйОбъектРасчетов.Организация = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Организация, Идентификатор);
				Иначе
					НовыйКорректныйОбъектРасчетов.Организация = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Организация);
				КонецЕсли;
				
				Если КонтрагентВСтроках Тогда
					НовыйКорректныйОбъектРасчетов.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Контрагент, Идентификатор);
				Иначе
					НовыйКорректныйОбъектРасчетов.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Контрагент);
				КонецЕсли;
				
				Если ПартнерВСтроках Тогда
					НовыйКорректныйОбъектРасчетов.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Партнер, Идентификатор);
				Иначе
					НовыйКорректныйОбъектРасчетов.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Партнер);
				КонецЕсли;
				
			КонецЦикла;
				
		Иначе
			
			НовыйКорректныйОбъектРасчетов                 = ОбъектыРасчетовКорректные.Добавить();
			НовыйКорректныйОбъектРасчетов.ТипРасчетов     = ПараметрГенерации.ТипРасчетов;
			НовыйКорректныйОбъектРасчетов.ОбъектРасчетов  = СсылкаНаЭлемент;
			НовыйКорректныйОбъектРасчетов.ТолькоОстатки   = ТолькоОстатки;
			НовыйКорректныйОбъектРасчетов.Организация     = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Организация);
			НовыйКорректныйОбъектРасчетов.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Партнер);
			НовыйКорректныйОбъектРасчетов.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.Контрагент);
			НовыйКорректныйОбъектРасчетов.Валюта = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, ПараметрГенерации.ВалютаДокумента);
			НовыйКорректныйОбъектРасчетов.ВалютаВзаиморасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,
				?(ЗначениеЗаполнено(ПараметрГенерации.ВалютаВзаиморасчетов),ПараметрГенерации.ВалютаВзаиморасчетов,ПараметрГенерации.ВалютаДокумента));
			
		КонецЕсли;
	КонецЦикла;
	
	ДанныеИзРасчетныхРегистров = ИспользованиеВРасчетныхРегистрах.НайтиСтроки(Новый Структура("ОбъектРасчетов", СсылкаНаЭлемент));
	
	Для Каждого КлючРегистра Из ДанныеИзРасчетныхРегистров Цикл
		
		КлючПоиска = Новый Структура("ОбъектРасчетов, Организация, ТипРасчетов, Контрагент, Партнер, ТолькоОстатки, ВалютаВзаиморасчетов");
		ЗаполнитьЗначенияСвойств(КлючПоиска, КлючРегистра);
		КлючПоиска.ТолькоОстатки = Ложь;
		Если Не ОбъектыРасчетовКорректные.НайтиСтроки(КлючПоиска).Количество() Тогда
			Если ПараметрыГенерацииБезИсключений.Количество() = 1 Тогда
				ПараметрыИсточник = ПараметрыГенерацииБезИсключений[0];
			ИначеЕсли ПараметрыГенерацииБезИсключений.Количество() = 2 Тогда
				ПараметрыИсточник = ?(ПараметрыГенерацииБезИсключений[0].ТипРасчетов = КлючПоиска.ТипРасчетов, 
					ПараметрыГенерацииБезИсключений[0],
					ПараметрыГенерацииБезИсключений[1]);
			Иначе
				НайденныйПараметрИсточник = ОбъектыРасчетовКорректные.НайтиСтроки(Новый Структура("Организация, ТипРасчетов", 
					КлючРегистра.Организация,
					КлючРегистра.ТипРасчетов));
				Если НайденныйПараметрИсточник.Количество() = 1 Тогда
					ПараметрыИсточник = ПараметрыГенерацииБезИсключений[ОбъектыРасчетовКорректные.Индекс(НайденныйПараметрИсточник[0])];
				Иначе
					ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Не удалось сгенерировать объект расчетов для элемента: %1.';uk='Не вдалося згенерувати об''єкт розрахунків для елемента: %1.'"),
									СсылкаНаЭлемент));
				КонецЕсли;
			КонецЕсли;
			
			ПараметрыГенерацииПоТолькоОстатки = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыИсточник);
			ЗаполнитьЗначенияСвойств(ПараметрыГенерацииПоТолькоОстатки, КлючРегистра);
			ПараметрыГенерацииПоТолькоОстатки.ТолькоОстатки = Истина;
			ПараметрыГенерации.Добавить(ПараметрыГенерацииПоТолькоОстатки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Описание
// 
// Параметры:
// 	ЭкземплярОбъекта - ДокументСсылка
// 	ПолеКонтрагент
// Возвращаемое значение:
// 	Строка - Описание
Функция ШаблонГенерацияПустогоОбъектаРасчетов(ЭкземплярОбъекта, ПолеКонтрагент) Экспорт
	
	ПараметрыВзаиморасчетов = Документы[ЭкземплярОбъекта.Метаданные().Имя].ПараметрыВзаиморасчеты();
	
	Если ТипЗнч(ПараметрыВзаиморасчетов) = Тип("Массив") Тогда
		ПараметрыВзаиморасчетов = ПараметрыВзаиморасчетов[0];
	КонецЕсли;
	
	Шаблон = "
		|ВЫБРАТЬ
		|	&ПолеКонтрагента
		|ИЗ
		|	&ИсточникДанных КАК ИсточникДанных
		|ГДЕ
		|(
		|&УсловиеИзменяетРасчеты
		|)
		|И
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			&ИсточникДанных.&РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
		|		ГДЕ
		|			ТЧРасшифровкаПлатежа.Ссылка = ИсточникДанных.Ссылка
		|			И ТЧРасшифровкаПлатежа.Ссылка.Проведен
		|			И ТЧРасшифровкаПлатежа.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов)
		|			И ТЧРасшифровкаПлатежа.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка))";
		
	Шаблон = СтрЗаменить(Шаблон, "&ПолеКонтрагента",        ПолеКонтрагент);
	Шаблон = СтрЗаменить(Шаблон, "&ИсточникДанных",         ЭкземплярОбъекта.Метаданные().ПолноеИмя());
	Шаблон = СтрЗаменить(Шаблон, "&РасшифровкаПлатежа",     СтрЗаменить(ПараметрыВзаиморасчетов.ПутьКДаннымТЧРасшифровкаПлатежа, "Объект.", ""));
	Шаблон = СтрЗаменить(Шаблон, "&УсловиеИзменяетРасчеты", ПараметрыВзаиморасчетов.ИзменяетРасчетыСтрокой);
	
	Возврат Шаблон;
	
КонецФункции

// Возвращает пустые значения типов, которые могли быть указаны в поле Контрагнет ключа аналитики учета по партнерам
// 
// Возвращаемое значение:
// 	Массив - Массив из ОпределяемыйТип.ОбъектРасчетов, Неопределено
Функция ПустыеЗначенияКонтрагент() Экспорт
	
	ПустыеЗначенияКонтрагент = Новый Массив();
	ПустыеЗначенияКонтрагент.Добавить(Неопределено);
	ПустыеЗначенияКонтрагент.Добавить(Справочники.Контрагенты.ПустаяСсылка());
	ПустыеЗначенияКонтрагент.Добавить(Справочники.Организации.ПустаяСсылка());
	
	Возврат ПустыеЗначенияКонтрагент;
	
КонецФункции

// Возвращает массив порций для обработки данных обработчика обновления заполнения объектов расчетов.
// 
// Параметры:
// 	ОбновляемыеДанные - см. ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике
//
// Возвращаемое значение:
// 	Массив из см. ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике.
Функция ПорцииДанныхДляОбработки(ОбновляемыеДанные) Экспорт 
	
	РазмерПорции = 9;
	Счетчик = 0;
	
	Порции = Новый Массив();
	НоваяПрорция = Новый ТаблицаЗначений();
	НоваяПрорция.Колонки.Добавить("Регистратор");
	Для Каждого СтрокаТЧ Из ОбновляемыеДанные Цикл
		Если Счетчик > РазмерПорции Тогда
			Порции.Добавить(НоваяПрорция.Скопировать());
			НоваяПрорция.Очистить();
			Счетчик = 0;
		КонецЕсли;
		СтрокаТЗ = НоваяПрорция.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, СтрокаТЧ);
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Порции.Добавить(НоваяПрорция);
	
	Возврат Порции;
	
КонецФункции

// Возвращает условие для текста запроса, которое отфильструет только те документы, у которых есть
//	движения по регистрам онлайн взаиморасчетов. Таблица документа должна иметь псевдоним Документ
Функция ТесктПроверкиДвиженийПоРасчетнымРегистрамОнлайн() Экспорт
	
	Возврат " (ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСКлиентамиПланОплат КАК РасчетыСКлиентамиПланОплат
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСКлиентамиПланОплат.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСКлиентамиПланОтгрузок КАК РасчетыСКлиентамиПланОтгрузок
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСКлиентамиПланОтгрузок.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСКлиентамиПоСрокам.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСПоставщикамиПланОплат КАК РасчетыСПоставщикамиПланОплат
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСПоставщикамиПланОплат.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСПоставщикамиПланПоставок КАК РасчетыСПоставщикамиПланПоставок
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСПоставщикамиПланПоставок.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСПоставщикамиПоСрокам.Регистратор))" 
	
КонецФункции

// Возвращает условие для текста запроса, которое отфильструет только те документы, у которых есть
//	движения регистрам офлайн взаиморасчетов. Таблица документа должна иметь псевдоним Документ
Функция ТесктПроверкиДвиженийПоРасчетнымРегистрамОфлайн() Экспорт
	
	Возврат " (ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСКлиентамиПоДокументам
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСКлиентамиПоДокументам.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСКлиентами.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщикамиПоДокументам
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСПоставщикамиПоДокументам.Регистратор)
	|		ИЛИ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|			ГДЕ
	|				Документ.Ссылка = РасчетыСПоставщиками.Регистратор))" 
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьОбъектРасчетов(Объект, МассивСтруктур, ОбновлениеДанных = Ложь, РежимЗаписи = Неопределено, ТабЧастиВидыЗапасов = Неопределено)
	
	Для Каждого СтруктураПараметров Из МассивСтруктур Цикл
		
		РеквизитОбъектРасчетов     = ОбщегоНазначенияУТКлиентСервер.ДанныеДляПрисвоения(Объект, СтруктураПараметров.ОбъектРасчетов);
		ЭтоДоговор                 = СтруктураПараметров.ЭтоДоговор;
		
		Если РеквизитОбъектРасчетов = Неопределено И НЕ ЭтоДоговор Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтоЗаказ                   = СтруктураПараметров.ЭтоЗаказ;
		ЭтоПродажаЗакупка          = СтруктураПараметров.ЭтоПродажаЗакупка;
		ЭтоПлатежИлиПрочийДокумент = СтруктураПараметров.ЭтоПлатежИлиПрочийДокумент;
		ТипРасчетов                = СтруктураПараметров.ТипРасчетов;
		
		ИмяРеквизитаТЧЗаказ        = СтруктураПараметров.ИмяРеквизитаТЧЗаказ;
		Организация                = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация);
		ЗаказОснование             = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ЗаказОснование);
		ТЧ                         = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПутьКДаннымТЧ);
		ТЧЭтапыОплаты              = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПутьКДаннымТЧЭтапыОплаты);
		
		Договор               = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Договор);
		НакладнаяПоЗаказам    = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.НакладнаяПоЗаказам, , Ложь);
		Если СтруктураПараметров.Свойство("СсылкаНового") Тогда
			Ссылка = СтруктураПараметров.СсылкаНового;
		Иначе
			Ссылка = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Ссылка);
		КонецЕсли;
		
		ОбъектРасчетовНеНужен      = Ложь;
		
		ПорядокРасчетов            = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПорядокРасчетов);
		Если ПорядокРасчетов = Неопределено И ЗначениеЗаполнено(Договор) Тогда
			ПорядокРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ПорядокРасчетов");
		КонецЕсли;
		
		ДополнительныеКритерииПоиска = ДополнительныеКритерииПоиска();
		ДополнительныеКритерииПоиска.Контрагент = ?(Не СтруктураПараметров.КонтрагентВСтроках,
			ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент),
			Неопределено);
		ДополнительныеКритерииПоиска.Партнер = ?(Не СтруктураПараметров.ПартнерВСтроках,
			ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер),
			Неопределено);
			
		//Определение.
		Если СсылкаЯвляетсяОбъектомРасчетов(Объект, СтруктураПараметров) Тогда
			Если ОбновлениеДанных Тогда

				ДополнительныеКритерииПоиска.ОбновлениеИБ = Истина;
				ДополнительныеКритерииПоиска.ВернутьПервый = Истина;
				
				ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(
					Ссылка,
					ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация),
					ТипРасчетов,
					ДополнительныеКритерииПоиска);
			Иначе
				ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(Ссылка, Организация, ТипРасчетов, ДополнительныеКритерииПоиска);
			КонецЕсли;
		ИначеЕсли (ЭтоЗаказ ИЛИ ЭтоПродажаЗакупка ИЛИ ЭтоПлатежИлиПрочийДокумент И ЗначениеЗаполнено(СтруктураПараметров.ОбъектРасчетов))
			И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов Тогда
				
			Если НЕ ЗначениеЗаполнено(Договор) И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Не заполнено поле ""Договор"".';uk='Не заповнене поле ""Договір"".'"),
									Договор));
			КонецЕсли;
				
			ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(Договор, Организация, ТипРасчетов, ДополнительныеКритерииПоиска);
			
			Если НЕ ЗначениеЗаполнено(ОбъектРасчетов) И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Не удалось найти объект расчетов для договора %1.';uk='Не вдалося знайти об''єкт розрахунків для договору %1.'"),
									Договор));
			КонецЕсли;
			
		ИначеЕсли ЭтоПродажаЗакупка И НакладнаяПоЗаказам И НЕ ЗначениеЗаполнено(ИмяРеквизитаТЧЗаказ) И ЗначениеЗаполнено(ЗаказОснование) 
			ИЛИ ЭтоПлатежИлиПрочийДокумент И ЗначениеЗаполнено(ЗаказОснование) И ПорядокРасчетов <> Перечисления.ПорядокРасчетов.ПоНакладным Тогда
			ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(ЗаказОснование, Организация, ТипРасчетов);
			
			Если НЕ ЗначениеЗаполнено(ОбъектРасчетов) И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Не удалось найти объект расчетов для %1. Проверьте соответствие ключевых реквизитов заказу.';uk='Не вдалося знайти об''єкт розрахунків для %1. Перевірте відповідність ключових реквізитів замовлення.'"),
									ЗаказОснование));
			КонецЕсли;
			
		ИначеЕсли ЭтоПродажаЗакупка И НакладнаяПоЗаказам И ЗначениеЗаполнено(ИмяРеквизитаТЧЗаказ) 
			И ПорядокРасчетов <> Перечисления.ПорядокРасчетов.ПоНакладным Тогда
			
			Заказы = ТЧ.ВыгрузитьКолонку(ИмяРеквизитаТЧЗаказ);
			Если Заказы.Количество() > 0 Тогда
				ОбъектРасчетов = ПолучитьОбъектыРасчетовПоСсылкам(Заказы, Организация, ТипРасчетов, ДополнительныеКритерииПоиска);
			Иначе
				Возврат;
			КонецЕсли;
			
			Для Каждого Элемент Из ОбъектРасчетов Цикл
				Если НЕ ЗначениеЗаполнено(Элемент.Значение) И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
					ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru='Не удалось найти объект расчетов для %1. Проверьте соответствие ключевых реквизитов заказу.';uk='Не вдалося знайти об''єкт розрахунків для %1. Перевірте відповідність ключових реквізитів замовлення.'"),
										Элемент.Ключ));
				КонецЕсли;
			КонецЦикла;
			
		// Корректировки реализации и приобретения.
		ИначеЕсли ЭтоПродажаЗакупка И ЗначениеЗаполнено(СтруктураПараметров.Ссылка) И СтруктураПараметров.Ссылка <> "Объект.Ссылка" 
			И (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным
				ИЛИ НЕ НакладнаяПоЗаказам И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным) Тогда
			ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(
					ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Ссылка), Организация, , ДополнительныеКритерииПоиска);
			
			Если НЕ ЗначениеЗаполнено(ОбъектРасчетов) И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Не удалось найти объект расчетов для %1.';uk='Не вдалося знайти об''єкт розрахунків для %1.'"),
									Объект));
			КонецЕсли;
		Иначе
			ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
			ОбъектРасчетовНеНужен = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ОбъектРасчетов) И Не ОбъектРасчетовНеНужен 
			И (ЭтоДоговор ИЛИ ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.Проведен")) Тогда
			Если ОбновлениеДанных И Не СтруктураПараметров.КонтрагентВСтроках 
				И Не СтруктураПараметров.ПартнерВСтроках Тогда
					
					ПараметрыПоиска = ПолучитьПараметрыОбъектаРасчетов();
					ПараметрыПоиска.Партнер                 = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(
						Объект, СтруктураПараметров.Партнер);
					ПараметрыПоиска.Контрагент              = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(
						Объект, СтруктураПараметров.Контрагент);
					ПараметрыПоиска.НаправлениеДеятельности = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(
						Объект, СтруктураПараметров.НаправлениеДеятельности);
					ПараметрыПоиска.Организация             = Организация;
					ПараметрыПоиска.Договор                 = Договор;
					ПараметрыПоиска.ТипРасчетов             = ТипРасчетов;
					
					ОбъектРасчетов = НайтиОбъектРасчетовПоАналитикеУчетаПоПартнерам(ПараметрыПоиска);
					
					Если Не ЗначениеЗаполнено(ОбъектРасчетов) И ЕстьДвиженияПоРасчетнымРегистрам(Объект) Тогда
					//Для возврата по заявке способ компенсации должен совпадать
						Если Не (ТипЗнч(Объект) = Тип("ДокументОбъект.ВозвратТоваровОтКлиента") И ЗначениеЗаполнено(Объект.ЗаявкаНаВозвратТоваровОтКлиента)
							И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаявкаНаВозвратТоваровОтКлиента, "СпособКомпенсации") <> Объект.СпособКомпенсации) Тогда
								ВызватьИсключение (НСтр("ru='Не удалось найти объект расчетов.';uk='Не вдалося знайти об''єкт розрахунків.'"))
						КонецЕсли;
					КонецЕсли;
			Иначе
				ВызватьИсключение (НСтр("ru='Не удалось найти объект расчетов.';uk='Не вдалося знайти об''єкт розрахунків.'"))
			КонецЕсли;
		КонецЕсли;
		
		ЭтоДокумент = Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Ссылка));
		
		Если НЕ ЭтоДокумент
			ИЛИ (РежимЗаписи = РежимЗаписиДокумента.Проведение
				ИЛИ РежимЗаписи = РежимЗаписиДокумента.Запись И ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект,"Объект.Проведен")) Тогда
			//Присвоение.
			Если РеквизитОбъектРасчетов <> Неопределено Тогда
				Если ТипЗнч(ОбъектРасчетов) = Тип("Соответствие") Тогда
					Для Каждого СтрокаТЧ Из ТЧ Цикл
						СтрокаТЧ[РеквизитОбъектРасчетов.Имя] = ОбъектРасчетов[СтрокаТЧ[ИмяРеквизитаТЧЗаказ]];
					КонецЦикла;
					Если ТЧЭтапыОплаты <> Неопределено И ТЧЭтапыОплаты.Количество() > 0
						И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТЧЭтапыОплаты[0], "Заказ") Тогда
						Для Каждого СтрокаТЧ Из ТЧЭтапыОплаты Цикл
							СтрокаТЧ.ОбъектРасчетов = ОбъектРасчетов[СтрокаТЧ.Заказ];
						КонецЦикла;
					КонецЕсли;
					Если ОбновлениеДанных И ТабЧастиВидыЗапасов <> Неопределено Тогда
						Для Каждого ТЧВидыЗапасов Из ТабЧастиВидыЗапасов Цикл
							Для Каждого СтрокаТЧ Из Объект[ТЧВидыЗапасов.Ключ] Цикл
								Если Не ЗначениеЗаполнено(СтрокаТЧ.ОбъектРасчетов) Тогда
									СтрокаТЧ.ОбъектРасчетов = ОбъектРасчетов[СтрокаТЧ[ТЧВидыЗапасов.Значение]];
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
				Иначе
					Если СтрРазделить(СтруктураПараметров.ОбъектРасчетов,".").Количество() = 3 Тогда
						Для Каждого СтрокаТЧ Из РеквизитОбъектРасчетов.Данные Цикл
							СтрокаТЧ[РеквизитОбъектРасчетов.Имя] = ОбъектРасчетов;
						КонецЦикла;
					Иначе
						РеквизитОбъектРасчетов.Данные[РеквизитОбъектРасчетов.Имя] = ОбъектРасчетов;
					КонецЕсли;
					Если ТЧЭтапыОплаты <> Неопределено И ТЧЭтапыОплаты.Количество() > 0 
						И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТЧЭтапыОплаты[0],"ОбъектРасчетов") Тогда
						Для Каждого СтрокаТЧ Из ТЧЭтапыОплаты Цикл
							СтрокаТЧ.ОбъектРасчетов = ОбъектРасчетов;
						КонецЦикла;
					КонецЕсли;
					Если ОбновлениеДанных И ТабЧастиВидыЗапасов <> Неопределено Тогда
						Для Каждого ТЧВидыЗапасов Из ТабЧастиВидыЗапасов Цикл
							Для Каждого СтрокаТЧ Из Объект[ТЧВидыЗапасов.Ключ] Цикл
								Если Не ЗначениеЗаполнено(СтрокаТЧ.ОбъектРасчетов) Тогда
									СтрокаТЧ.ОбъектРасчетов = ОбъектРасчетов;
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьОбъектыРасчетов(Объект, МассивСтруктур)
	
	Для Каждого СтруктураПараметров Из МассивСтруктур Цикл
		
		РеквизитОбъектРасчетов     = ОбщегоНазначенияУТКлиентСервер.ДанныеДляПрисвоения(Объект, СтруктураПараметров.ОбъектРасчетов);
		ЭтоДоговор                 = СтруктураПараметров.ЭтоДоговор;
		
		Если РеквизитОбъектРасчетов = Неопределено И НЕ ЭтоДоговор Тогда
			Возврат;
		КонецЕсли;
		
		ТЧЭтапыОплаты              = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПутьКДаннымТЧЭтапыОплаты);
		
		Если РеквизитОбъектРасчетов <> Неопределено Тогда
			Если СтрРазделить(СтруктураПараметров.ОбъектРасчетов,".").Количество() = 3 Тогда
				Для Каждого СтрокаТЧ Из РеквизитОбъектРасчетов.Данные Цикл
					СтрокаТЧ[РеквизитОбъектРасчетов.Имя] = Справочники.ОбъектыРасчетов.ПустаяСсылка();
				КонецЦикла;
			Иначе
				РеквизитОбъектРасчетов.Данные[РеквизитОбъектРасчетов.Имя] = Справочники.ОбъектыРасчетов.ПустаяСсылка();
			КонецЕсли;
			Если ТЧЭтапыОплаты <> Неопределено И ТЧЭтапыОплаты.Количество() > 0 
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТЧЭтапыОплаты[0],"ОбъектРасчетов") Тогда
				Для Каждого СтрокаТЧ Из ТЧЭтапыОплаты Цикл
					СтрокаТЧ.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Проверяет, что ссылка указанного объекта может содержаться в объекте расчетов
// 
// Параметры:
// 	Объект - СправочникОбъект, ДокументОбъект - проверяемый объект.
// 	СтруктураПараметров - Структура - Текущий набор параметров. см. ВзаиморасчетыСервер.ПараметрыМеханизма.
// 
// Возвращаемое значение:
// 	Булево - Истина, если является объектом расчетов
//
Функция СсылкаЯвляетсяОбъектомРасчетов(Объект, СтруктураПараметров, ОбновлениеИБ = Ложь) Экспорт
	
	ИзменяетРасчеты            = СтруктураПараметров.ИзменяетРасчеты;
	ЭтоДоговор                 = СтруктураПараметров.ЭтоДоговор;
	ЭтоЗаказ                   = СтруктураПараметров.ЭтоЗаказ;
	ЭтоПродажаЗакупка          = СтруктураПараметров.ЭтоПродажаЗакупка;
	ЭтоПлатежИлиПрочийДокумент = СтруктураПараметров.ЭтоПлатежИлиПрочийДокумент;
	ПорядокРасчетов            = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПорядокРасчетов, , Перечисления.ПорядокРасчетов.ПоНакладным);
	НакладнаяПоЗаказам         = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.НакладнаяПоЗаказам, ,Ложь);
	ЗаказОснование             = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ЗаказОснование);
	ИспользоватьРасширенные    = СтруктураПараметров.ДокументРасчетовСКлиентами И ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента")
								ИЛИ СтруктураПараметров.ДокументРасчетовСПоставщиками;
	
	Возврат ЭтоДоговор И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
			ИЛИ ЭтоЗаказ 
				И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным 
				И (ИспользоватьРасширенные ИЛИ ОбновлениеИБ)
			ИЛИ ЭтоПродажаЗакупка И ИзменяетРасчеты 
				И (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным 
					ИЛИ ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным 
						И ((НЕ ИспользоватьРасширенные ИЛИ НЕ НакладнаяПоЗаказам) 
							ИЛИ ОбновлениеИБ
						)
					)
					// Корректировки реализаций и приобретений.
					И СтруктураПараметров.Ссылка = "Объект.Ссылка"
			ИЛИ ЭтоПлатежИлиПрочийДокумент И ИзменяетРасчеты
				// Возвраты товаров не по договорам.
				И ЗначениеЗаполнено(СтруктураПараметров.ОбъектРасчетов) И ПорядокРасчетов <> Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
				// Возвраты товаров клиентов не по заявке.
				И НЕ (ЗначениеЗаполнено(ЗаказОснование) И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным);
	
КонецФункции

Функция ЭтоПлатежныйДокумент(Объект)
	Возврат ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств")
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств")
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.ПриходныйКассовыйОрдер")
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.РасходныйКассовыйОрдер")
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.ЗаявкаНаРасходованиеДенежныхСредств")
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.ОперацияПоПлатежнойКарте")
				//++ Локализация
				//-- Локализация
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.АвансовыйОтчет");
КонецФункции

Процедура ЗаполнитьОбъектРасчетовВРасшифровкеПлатежа(Объект, СтруктураПараметров) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа) Тогда
		Возврат;
	КонецЕсли;
	
	РасшифровкаПлатежа    = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа); // ТабличнаяЧасть
	Ссылка                = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.Ссылка");
	Организация           = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация);
	ТипРасчетов           = СтруктураПараметров.ТипРасчетов;
	
	КонтрагентВСтроках  = СтруктураПараметров.КонтрагентВСтроках;
	ПартнерВСтроках     = СтруктураПараметров.ПартнерВСтроках;
	
	ЗаказыИзРасшифровкиПлатежа = РасшифровкаПлатежа.ВыгрузитьКолонку("УдалитьЗаказ");
	УникальныеЗаказы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ЗаказыИзРасшифровкиПлатежа);
	Для Каждого ПустоеЗначение Из ПустыеЗначенияОбъектРасчетов() Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(УникальныеЗаказы, ПустоеЗначение);
	КонецЦикла;
	
	Если ЭтоПлатежныйДокумент(Объект) Тогда
		ОбъектыРасчетовПоЗаказамПлатежныйДокумент = ПолучитьОбъектыРасчетовПоФилиалу(УникальныеЗаказы, Организация, ТипРасчетов);
	Иначе
		ДополнительныеКритерииПоиска = ДополнительныеКритерииПоиска();
		ДополнительныеКритерииПоиска.ОбновлениеИБ = Истина;
		ДополнительныеКритерииПоиска.ТолькоРеглУчет = (ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияТоваровУслуг") 
			И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
			ИЛИ (ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеТоваровНаСклад")
			И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет);
			
			Если Не ПартнерВСтроках Тогда
				ДополнительныеКритерииПоиска.Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Партнер);
			КонецЕсли;
			
			Если Не КонтрагентВСтроках Тогда
				ДополнительныеКритерииПоиска.Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Контрагент);
			КонецЕсли;
		
		
		ОбъектыРасчетовПоЗаказам = ПолучитьОбъектыРасчетовПоСсылкам(УникальныеЗаказы, Организация, ТипРасчетов, ДополнительныеКритерииПоиска);
	КонецЕсли;
	
	Для Каждого СтрокаРасшифровки Из РасшифровкаПлатежа Цикл
		
		Партнер = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(
			Объект,
			СтруктураПараметров.Партнер,
			?(ПартнерВСтроках, СтрокаРасшифровки.НомерСтроки, Неопределено));
		
		Контрагент = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(
			Объект,
			СтруктураПараметров.Контрагент,
			?(КонтрагентВСтроках, СтрокаРасшифровки.НомерСтроки, Неопределено));
			
		ДополнительныеКритерииПоиска = ДополнительныеКритерииПоиска();
		ДополнительныеКритерииПоиска.Партнер = Партнер;
		ДополнительныеКритерииПоиска.Контрагент = Контрагент;
			
		Если ЗначениеЗаполнено(СтрокаРасшифровки.УдалитьЗаказ) Тогда
			Если ЭтоПлатежныйДокумент(Объект) Тогда
				
				Ключ = Новый Структура();
				Ключ.Вставить("Объект", СтрокаРасшифровки.УдалитьЗаказ);
				Если ЗначениеЗаполнено(Партнер) Тогда
					Ключ.Вставить("Партнер", Партнер);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Контрагент) Тогда
					Ключ.Вставить("Контрагент", Контрагент);
				КонецЕсли;
				
				НайденныеОбъектыРасчетов = ОбъектыРасчетовПоЗаказамПлатежныйДокумент.НайтиСтроки(Ключ);
				
				Если НайденныеОбъектыРасчетов.Количество() Тогда
					Строка = НайденныеОбъектыРасчетов[0]; //
					СтрокаРасшифровки.ОбъектРасчетов = Строка.Ссылка;
				КонецЕсли;
			Иначе
				СтрокаРасшифровки.ОбъектРасчетов = ОбъектыРасчетовПоЗаказам[СтрокаРасшифровки.УдалитьЗаказ];
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаРасшифровки.ОбъектРасчетов) И Объект.Проведен Тогда
				
				//Обработка исключений
				Если Не (ТипЗнч(Объект) = Тип("ДокументОбъект.ЗаявкаНаРасходованиеДенежныхСредств")
					И Объект.Статус = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.Отклонена)
					И ЕстьДвиженияПоРасчетнымРегистрам(Объект) Тогда
						ВызватьИсключение (СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru='Не удалось заполнить объект расчетов в документе: %1, в табличной части %2, по заказу %3';uk='Не вдалося заповнити об''єкт розрахунків в документі: %1, в табличній частині %2, по замовленню %3'"),
											Ссылка,
											СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа,
											СтрокаРасшифровки.УдалитьЗаказ));
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если СтруктураПараметров.ЭтоПродажаЗакупка
				И Не ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияТоваровУслуг")
				И Не ТипЗнч(Объект) = Тип("ДокументОбъект.ПриобретениеТоваровУслуг") 
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияТоваровУслуг")
					И ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.ХозяйственнаяОперация")
						<> Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.ПриобретениеТоваровУслуг")
					И ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, "Объект.ХозяйственнаяОперация")
						<> Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет Тогда
						Если Не ЗначениеЗаполнено(СтрокаРасшифровки.УдалитьЗаказ) Тогда

							СтрокаРасшифровки.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();  
							
						КонецЕсли;
						
			ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияТоваровУслуг")
				ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.ПриобретениеТоваровУслуг") Тогда
					
					ПараметрыСоздания = ПолучитьПараметрыОбъектаРасчетов();
					ПараметрыСоздания.Партнер                 = Партнер;
					ПараметрыСоздания.Контрагент              = Контрагент;
					ПараметрыСоздания.НаправлениеДеятельности = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.НаправлениеДеятельности);
					ПараметрыСоздания.Организация             = Справочники.Организации.УправленческаяОрганизация;
					ПараметрыСоздания.Договор                 = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
					ПараметрыСоздания.ТипРасчетов             = ТипРасчетов;
					
					СтрокаРасшифровки.ОбъектРасчетов = НайтиОбъектРасчетовПоАналитикеУчетаПоПартнерам(ПараметрыСоздания);
					
			ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
				Если Объект.СпособКомпенсации = Перечисления.СпособыКомпенсацииВозвратовТоваров.ВернутьДенежныеСредства Тогда
					ОбъектРасчетов = Ссылка;
				ИначеЕсли Объект.ДокументПоступления.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
					ИЛИ Объект.ДокументПоступления = Документы.ПриобретениеТоваровУслуг.ПустаяСсылка() 
						И Объект.Договор.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов Тогда
					ОбъектРасчетов = Объект.Договор;
				Иначе
					ОбъектРасчетов = Ссылка;
				КонецЕсли;
				СтрокаРасшифровки.ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(ОбъектРасчетов,
					Организация,
					ТипРасчетов,
					ДополнительныеКритерииПоиска);
			
			ИначеЕсли  ТипЗнч(Объект) = Тип("ДокументОбъект.ВозвратТоваровОтКлиента") Тогда
				Если Объект.СпособКомпенсации = Перечисления.СпособыКомпенсацииВозвратовТоваров.ВернутьДенежныеСредства Тогда
					ОбъектРасчетов = Ссылка;
				ИначеЕсли Объект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов Тогда
					ОбъектРасчетов = Объект.Договор;
				ИначеЕсли Объект.ЗаявкаНаВозвратТоваровОтКлиента <> Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка()
					И Объект.ПорядокРасчетов <> Перечисления.ПорядокРасчетов.ПоНакладным Тогда
						ОбъектРасчетов = Объект.ЗаявкаНаВозвратТоваровОтКлиента;
				Иначе
					ОбъектРасчетов = Ссылка;
				КонецЕсли;
				
				СтрокаРасшифровки.ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(ОбъектРасчетов,
					Организация,
					ТипРасчетов,
					ДополнительныеКритерииПоиска);
			
			ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ВозвратТоваровМеждуОрганизациями") Тогда
				Если Объект.РасчетыЧерезОтдельногоКонтрагента Тогда
					ОбъектРасчетов = ?(ЗначениеЗаполнено(Объект.ДоговорПродажи),
						Объект.ДоговорПродажи,
						Ссылка);
				ИначеЕсли ЗначениеЗаполнено(Объект.ДоговорПродажи)
					И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорПродажи, "ПорядокРасчетов")
						= Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов Тогда
					ОбъектРасчетов = Объект.ДоговорПродажи;
				Иначе
					ОбъектРасчетов = Ссылка;
				КонецЕсли;
				
				СтрокаРасшифровки.ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(ОбъектРасчетов,
					Организация,
					ТипРасчетов,
					ДополнительныеКритерииПоиска);
			//++ Локализация
			//++ НЕ УТ
			ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ВыбытиеДенежныхДокументов") Тогда
				
				Если Объект.СпособКомпенсации = Перечисления.СпособыКомпенсацииВозвратовТоваров.ВернутьДенежныеСредства Тогда
					ОбъектРасчетов = Ссылка;
				ИначеЕсли Объект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
					Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ПорядокРасчетов")
						= Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов Тогда
						ОбъектРасчетов = Объект.Договор;
				Иначе
					ОбъектРасчетов = Ссылка;
				КонецЕсли;
				
				СтрокаРасшифровки.ОбъектРасчетов = ПолучитьОбъектРасчетовПоСсылке(ОбъектРасчетов,
					Организация,
					ТипРасчетов,
					ДополнительныеКритерииПоиска);
			//-- НЕ УТ
			//-- Локализация

			ИначеЕсли СтруктураПараметров.ЭтоПлатежИлиПрочийДокумент Тогда
				
				ПараметрыСоздания = ПолучитьПараметрыОбъектаРасчетов();
				
				ПараметрыСоздания.Партнер = Партнер;
				ПараметрыСоздания.Контрагент = Контрагент;
				ПараметрыСоздания.НаправлениеДеятельности = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.НаправлениеДеятельности);
				ПараметрыСоздания.Организация             = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Организация);
				ПараметрыСоздания.Договор                 = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Объект, СтруктураПараметров.Договор);
				ПараметрыСоздания.ТипРасчетов             = ТипРасчетов;
				
				СтрокаРасшифровки.ОбъектРасчетов = НайтиОбъектРасчетовПоАналитикеУчетаПоПартнерам(ПараметрыСоздания);
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПараметрыВзаиморасчетовОбъектаРасчетов(Объект) Экспорт

	
	ИмяДокумента = Объект.Ссылка.Метаданные().Имя;
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка);
	
	ДокументыБезДопПараметров = Новый Массив();
	ДокументыБезДопПараметров.Добавить("АвансовыйОтчет");
	ДокументыБезДопПараметров.Добавить("АктВыполненныхРабот");
	ДокументыБезДопПараметров.Добавить("ВыкупВозвратнойТарыКлиентом");
	ДокументыБезДопПараметров.Добавить("ВыкупТоваровХранителем");
	ДокументыБезДопПараметров.Добавить("ЗаданиеТорговомуПредставителю");
	//++ НЕ УТ
	ДокументыБезДопПараметров.Добавить("ОтчетПереработчика");
	ДокументыБезДопПараметров.Добавить("ЗаказПереработчику");
	//-- НЕ УТ
	ДокументыБезДопПараметров.Добавить("КорректировкаПриобретения");
	ДокументыБезДопПараметров.Добавить("КорректировкаРеализации");
	ДокументыБезДопПараметров.Добавить("ОтчетКомиссионераОСписании");
	ДокументыБезДопПараметров.Добавить("ОтчетКомитентуОСписании");
	//++ Локализация
	//++ НЕ УТ
	ДокументыБезДопПараметров.Добавить("ПриобретениеУслугПоЛизингу");
	ДокументыБезДопПараметров.Добавить("ПоступлениеПредметовЛизинга");
	//-- НЕ УТ
	//-- Локализация
	ДокументыБезДопПараметров.Добавить("РеализацияУслугПрочихАктивов"); 
	ДокументыБезДопПараметров.Добавить("ТаможеннаяДекларацияИмпорт");

	ДокументыСДопПараметрами = Новый Соответствие();
	
	ДокументыСДопПараметрами.Вставить("ВозвратТоваровПоставщику", "");
	//++ Локализация
	//++ НЕ УТ
	ДокументыСДопПараметрами.Вставить("ВыбытиеДенежныхДокументов", "");
	ДокументыСДопПараметрами.Вставить("ПоступлениеДенежныхДокументов", "");
	//-- НЕ УТ
	//-- Локализация
	ДокументыСДопПараметрами.Вставить("ЗаказКлиента", "");
	ДокументыСДопПараметрами.Вставить("ЗаказПоставщику", "");
	ДокументыСДопПараметрами.Вставить("ЗаявкаНаВозвратТоваровОтКлиента", "");
	ДокументыСДопПараметрами.Вставить("ЗаявкаНаРасходованиеДенежныхСредств", "");
	ДокументыСДопПараметрами.Вставить("ОперацияПоПлатежнойКарте", "");
	ДокументыСДопПараметрами.Вставить("ПриобретениеУслугПрочихАктивов", "");
	ДокументыСДопПараметрами.Вставить("ПриобретениеТоваровУслуг", "");
	ДокументыСДопПараметрами.Вставить("РеализацияТоваровУслуг", "");
	ДокументыСДопПараметрами.Вставить("ПриходныйКассовыйОрдер", "");
	ДокументыСДопПараметрами.Вставить("РасходныйКассовыйОрдер", "");
	
	ДокументыСДопПараметрами.Вставить("ОтчетКомиссионера", 							"УдержатьВознаграждение");
	ДокументыСДопПараметрами.Вставить("ОтчетКомитенту", 							"УдержатьВознаграждение");
	ДокументыСДопПараметрами.Вставить("ОтчетПоКомиссииМеждуОрганизациями", 			"УдержатьВознаграждение,РасчетыЧерезОтдельногоКонтрагента");
	ДокументыСДопПараметрами.Вставить("ВыкупПринятыхНаХранениеТоваров",				"Договор");
	
	ДокументыСДопПараметрами.Вставить("ВыкупВозвратнойТарыУПоставщика",				"ПредусмотренЗалогЗаТару");
	ДокументыСДопПараметрами.Вставить("ОтчетПоКомиссииМеждуОрганизациямиОСписании", "РасчетыЧерезОтдельногоКонтрагента");
	
	ДокументыСДопПараметрами.Вставить("ВозвратТоваровПоставщику",					"ХозяйственнаяОперация,СпособКомпенсации");
	ДокументыСДопПараметрами.Вставить("ВозвратТоваровОтКлиента",					"ХозяйственнаяОперация,СпособКомпенсации");
	ДокументыСДопПараметрами.Вставить("ПоступлениеБезналичныхДенежныхСредств", 		"ХозяйственнаяОперация,ПроведеноБанком");
	ДокументыСДопПараметрами.Вставить("СписаниеБезналичныхДенежныхСредств", 		"ХозяйственнаяОперация,ПроведеноБанком");
	ДокументыСДопПараметрами.Вставить("ВозвратТоваровМеждуОрганизациями", 			"ХозяйственнаяОперация,РасчетыЧерезОтдельногоКонтрагента");
	ДокументыСДопПараметрами.Вставить("ПередачаТоваровМеждуОрганизациями", 			"ХозяйственнаяОперация,РасчетыЧерезОтдельногоКонтрагента");
	ДокументыСДопПараметрами.Вставить("СписаниеПринятыхНаХранениеТоваров", 			"ХозяйственнаяОперация,Договор");
	ДокументыСДопПараметрами.Вставить("ДоговорыКонтрагентов", 						"ТипДоговора,Сумма");
	ДокументыСДопПараметрами.Вставить("ДоговорыМеждуОрганизациями", 				"ТипДоговора,Сумма");
	
	ДопПараметрыСтрокой = ДокументыСДопПараметрами.Получить(ИмяДокумента);
	
	Если ДокументыБезДопПараметров.Найти(ИмяДокумента) <> Неопределено Тогда
		Возврат Менеджер.ПараметрыВзаиморасчеты();
	ИначеЕсли ДопПараметрыСтрокой <> Неопределено Тогда
 		ДопПараметрыСтрокой  = ?(ДопПараметрыСтрокой = "", "ХозяйственнаяОперация", ДопПараметрыСтрокой);
		ДопПараметры = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДопПараметрыСтрокой);
		
		Если ДопПараметры.Количество() = 2 Тогда
			Возврат Менеджер.ПараметрыВзаиморасчеты(			
				Объект[ДопПараметры[0]],
				Объект[ДопПараметры[1]]);
				
		Иначе
			Возврат Менеджер.ПараметрыВзаиморасчеты(Объект[ДопПараметры[0]]);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция РеквизитыСправочникаОбъектыРасчетов()
	
	СлужебныеРеквизиты = Новый Массив;
	СлужебныеРеквизиты.Добавить("Ссылка");
	СлужебныеРеквизиты.Добавить("ПометкаУдаления");
	СлужебныеРеквизиты.Добавить("Объект");
	СлужебныеРеквизиты.Добавить("Организация");
	СлужебныеРеквизиты.Добавить("ТипРасчетов");
	СлужебныеРеквизиты.Добавить("ТипСсылки");
	СлужебныеРеквизиты.Добавить("УникальныйИдентификатор");
	
	РеквизитыСправочника = Метаданные.Справочники.ОбъектыРасчетов.Реквизиты;
	
	Реквизиты = Новый Массив();
	
	Для Каждого Реквизит Из РеквизитыСправочника Цикл
		Если СлужебныеРеквизиты.Найти(Реквизит.Имя) = Неопределено Тогда
			Реквизиты.Добавить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Реквизиты.Добавить("Наименование");
	
	Возврат Реквизиты;
	
КонецФункции

//	Параметры:
//	
// Возвращаемое значение:
// 	ТаблицаЗначений - 
// 		* Ссылка - СправочникСсылка.ОбъектыРасчетов - Ссылка.
//
Функция ПолучитьОбъектыРасчетовПоФилиалу(МассивСсылок, Организация, ТипРасчетов, ДополнительныеКритерииПоиска = Неопределено)
	
	Если ДополнительныеКритерииПоиска = Неопределено Тогда
		ДополнительныеКритерииПоиска = ДополнительныеКритерииПоиска();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ОбъектыРасчетов.Объект КАК Объект,
		|	ОбъектыРасчетов.Контрагент КАК Контрагент,
		|	ОбъектыРасчетов.Партнер КАК Партнер,
		|	ОбъектыРасчетов.Ссылка КАК Ссылка,
		|	СУММА(1) КАК Количество
		|ИЗ
		|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|ГДЕ
		|	(ОбъектыРасчетов.Организация = &Организация 
		|		ИЛИ ОбъектыРасчетов.Организация.ГоловнаяОрганизация = &Организация)
		|	И ОбъектыРасчетов.Объект В(&МассивСсылок)
		|	И (&ЛюбойТип
		|			ИЛИ ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов)
		|	И (&ЛюбойКонтрагент
		|			ИЛИ ОбъектыРасчетов.Контрагент = &Контрагент)
		|	И (&ЛюбойПартнер
		|			ИЛИ ОбъектыРасчетов.Партнер = &Партнер)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъектыРасчетов.Объект,
		|	ОбъектыРасчетов.Контрагент,
		|	ОбъектыРасчетов.Партнер,
		|	ОбъектыРасчетов.Ссылка";
	
	Запрос.УстановитьПараметр("МассивСсылок",     МассивСсылок);
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("ТипРасчетов",      ТипРасчетов);
	Запрос.УстановитьПараметр("Партнер",          ДополнительныеКритерииПоиска.Партнер);
	Запрос.УстановитьПараметр("Контрагент",       ДополнительныеКритерииПоиска.Контрагент);
	Запрос.УстановитьПараметр("ЛюбойТип",         ?(ТипРасчетов = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ЛюбойПартнер",     ?(ДополнительныеКритерииПоиска.Партнер     = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ЛюбойКонтрагент",  ?(ДополнительныеКритерииПоиска.Контрагент  = Неопределено, Истина, Ложь));
	
	ОбъектыРасчетов = Запрос.Выполнить().Выгрузить();
	ОбъектыРасчетов.Индексы.Добавить("Объект");
	
	Для Каждого Ссылка Из МассивСсылок Цикл
		СтрокиОбъектов = ОбъектыРасчетов.НайтиСтроки(Новый Структура("Объект", Ссылка));
		Если Не СтрокиОбъектов.Количество() Тогда
			НоваяСтрока = ОбъектыРасчетов.Добавить();
			НоваяСтрока.Объект = Ссылка;
		Иначе
			Для Каждого СтрокаОбъектаРасчетов Из СтрокиОбъектов Цикл
				Если СтрокаОбъектаРасчетов.Количество > 1 Тогда
					ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='По ключу ""%1, %2, %3, %4"", найдено несколько объектов расчетов.';uk='За ключем ""%1, %2, %3, %4"", знайдено кілька об''єктів розрахунків.'"),
						СтрокаОбъектаРасчетов.ТипРасчетов, Ссылка, Организация, СтрокаОбъектаРасчетов.Партнер, СтрокаОбъектаРасчетов.Контрагент));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбъектыРасчетов;
	
КонецФункции

Процедура ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, ИмяРегистра, ИмяИзмеренияРеквизита);
	
	Если ТаблицаРегистров.Колонки.Количество() = 0 Тогда
		ТаблицаРегистров.Колонки.Добавить("ИмяРегистра");
		ТаблицаРегистров.Колонки.Добавить("ИмяИзмеренияРеквизита")
	КонецЕсли;
	
	НоваяСтрокаТЗ = ТаблицаРегистров.Добавить();
	НоваяСтрокаТЗ.ИмяРегистра = ИмяРегистра;
	НоваяСтрокаТЗ.ИмяИзмеренияРеквизита = ИмяИзмеренияРеквизита;
	
КонецПроцедуры

Функция ТекстПоискаПоТЧРасшифровкаПлатежа()
	
	ШаблонУсловияВхожденияВТЧ = " ИСТИНА В
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ИСТИНА
	|	ИЗ
	|		Документ.[ИмяДокумента].[ИмяТЧ] КАК [ИмяДокумента][ИмяТЧ]
	|	ГДЕ
	|		[ИмяДокумента][ИмяТЧ].УдалитьЗаказ = ИсточникДанных.Ссылка
	|		И [ИмяДокумента][ИмяТЧ].Ссылка.Проведен)
	|";
	
	УсловияВхождения = Новый Массив;
	Для Каждого Документ Из Метаданные.Документы Цикл
		Для Каждого ТЧ Из Документ.ТабличныеЧасти Цикл
			Если Лев(ТЧ.Имя, 18) = "РасшифровкаПлатежа" 
				Или ТЧ.Имя = "ОплатаПоставщикам" Тогда
				Если ТЧ.Реквизиты.Найти("УдалитьЗаказ") <> Неопределено Тогда
					Условие = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонУсловияВхожденияВТЧ,
						Новый Структура("ИмяДокумента, ИмяТЧ", Документ.Имя, ТЧ.Имя));
					УсловияВхождения.Добавить(Условие);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СтрСоединить(УсловияВхождения, "ИЛИ");
	
КонецФункции

Функция ТекстПоискаВРегистрахНакопления()
	
	ШаблонУсловияИспользованиеВРН = " ИСТИНА В
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ИСТИНА
	|	ИЗ
	|		РегистрНакопления.[ИмяРегистра] КАК [ИмяРегистра]
	|	ГДЕ
	|		[ИмяРегистра].[ИмяИзмеренияРеквизита] = ИсточникДанных.Ссылка)
	|
	|";
	
	УсловияВхождения = Новый Массив();
	Для Каждого РегистрНакопления Из Метаданные.РегистрыНакопления Цикл
		Для Каждого Измерение Из РегистрНакопления.Измерения Цикл
			Если Измерение.Тип = Метаданные.ОпределяемыеТипы.ОбъектРасчетов.Тип
				Или Измерение.Тип = Метаданные.ОпределяемыеТипы.ОбъектРасчетовСКлиентами.Тип 
				Или Измерение.Тип = Метаданные.ОпределяемыеТипы.ОбъектРасчетовСПоставщиками.Тип Тогда
					Условие = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
						ШаблонУсловияИспользованиеВРН,
						Новый Структура("ИмяРегистра, ИмяИзмеренияРеквизита", РегистрНакопления.Имя, Измерение.Имя));
				УсловияВхождения.Добавить(Условие);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Реквизит Из РегистрНакопления.Реквизиты Цикл
			Если Реквизит.Тип = Метаданные.ОпределяемыеТипы.ОбъектРасчетов.Тип
				Или Реквизит.Тип = Метаданные.ОпределяемыеТипы.ОбъектРасчетовСКлиентами.Тип 
				Или Реквизит.Тип = Метаданные.ОпределяемыеТипы.ОбъектРасчетовСПоставщиками.Тип Тогда
					Условие = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
						ШаблонУсловияИспользованиеВРН,
						Новый Структура("ИмяРегистра, ИмяИзмеренияРеквизита", РегистрНакопления.Имя, Реквизит.Имя));
				УсловияВхождения.Добавить(Условие);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаРегистров = Новый ТаблицаЗначений();
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "ДвиженияКонтрагентДоходыРасходы", "УдалитьОбъектРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "ДвиженияКонтрагентДоходыРасходы", "УдалитьИсточникГФУРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "ВыручкаИСебестоимостьПродаж", "УдалитьИсточникГФУРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "ДенежныеСредстваБезналичные", "УдалитьЗаказ");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "ДенежныеСредстваНаличные", "УдалитьЗаказ");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "Закупки", "УдалитьИсточникГФУРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "КнигаДоходовРасходовПоЕдиномуНалогу", "УдалитьОбъектРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "НДСНоменклатурныйСоставДляНалоговыхНакладных", "УдалитьОбъектРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "НДСРасчетНалоговогоКредита", "УдалитьОбъектРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "НДСРасчетНалоговыхОбязательств", "УдалитьОбъектРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "НДСРеестрВыданныхНалоговыхДокументов", "УдалитьОбъектРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "НДСРеестрПолученныхНалоговыхДокументов", "УдалитьОбъектРасчетов");
	ДополнитьТаблицуИспользованияОбъектовРасчетов(ТаблицаРегистров, "НДССоставПоставкиДляРегистрацииВходящихНалоговыхДокументов", "УдалитьОбъектРасчетов");
	
	Для Каждого Регистр Из ТаблицаРегистров Цикл
		Условие = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
						ШаблонУсловияИспользованиеВРН,
						Новый Структура("ИмяРегистра, ИмяИзмеренияРеквизита", Регистр.ИмяРегистра, Регистр.ИмяИзмеренияРеквизита));
				УсловияВхождения.Добавить(Условие);
	КонецЦикла;
	
	Возврат СтрСоединить(УсловияВхождения, "ИЛИ");
	
КонецФункции

// Возвращает условие генерации объекта расчетов
// 
// Параметры:
// 	ЭкземплярОбъекта - ДокументОбъект - документ для которого определяется условие.
// Возвращаемое значение:
// 	Строка - Описание - часть запроса условия.
Функция УсловиеГенерацииОбъектаРасчетов(ЭкземплярОбъекта)
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ЭкземплярОбъекта);
	СтруктураПараметрыВзаиморасчетов = Менеджер.ПараметрыВзаиморасчеты();
	
	Если ТипЗнч(СтруктураПараметрыВзаиморасчетов) = Тип("Массив")
		И СтруктураПараметрыВзаиморасчетов.Количество() Тогда
		ПараметрыВзаиморасчетов = СтруктураПараметрыВзаиморасчетов[0];
	Иначе
		ПараметрыВзаиморасчетов = СтруктураПараметрыВзаиморасчетов;
	КонецЕсли;
	
	УсловиеГенерации = "
	|(ЛОЖЬ ИЛИ
	|	// Условие генерации для заказа //
	|	// Условие генерации для продажи/закупки //
	|	// Условие генерации для платежного или прочего документа //
	|)
	|";

	Если ПараметрыВзаиморасчетов.НакладнаяПоЗаказам = ЛОЖЬ
		ИЛИ Не ЗначениеЗаполнено(ПараметрыВзаиморасчетов.НакладнаяПоЗаказам)Тогда
		НакладнаяПоЗаказам = "ЛОЖЬ";
	ИначеЕсли ПараметрыВзаиморасчетов.НакладнаяПоЗаказам = ИСТИНА Тогда
		НакладнаяПоЗаказам = "ИСТИНА";
	Иначе
		НакладнаяПоЗаказам = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"ИсточникДанных.%1",
			СтрЗаменить(ПараметрыВзаиморасчетов.НакладнаяПоЗаказам, "Объект.", ""));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыВзаиморасчетов.УсловныйПорядокРасчетов) Тогда
		ПорядокРасчетов = ПараметрыВзаиморасчетов.УсловныйПорядокРасчетов;
	Иначе
		Если ПараметрыВзаиморасчетов.ПорядокРасчетов = "" Тогда
			ПорядокРасчетов = "НЕОПРЕДЕЛЕНО";
		ИначеЕсли ТипЗнч(ПараметрыВзаиморасчетов.ПорядокРасчетов) = Тип("Строка") Тогда
			Если ПараметрыВзаиморасчетов.ПорядокРасчетов = "Объект.Договор.ПорядокРасчетов" Тогда
				ПорядокРасчетов = "
				|ВЫБОР
				|	КОГДА
				|		ИсточникДанных.Договор В (ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
				|									ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка),
				|									НЕОПРЕДЕЛЕНО) ТОГДА
				|			НЕОПРЕДЕЛЕНО
				|	ИНАЧЕ
				|		ИсточникДанных.Договор.ПорядокРасчетов
				|КОНЕЦ
				|";
			Иначе
				ПорядокРасчетов = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ИсточникДанных.%1",
					СтрЗаменить(ПараметрыВзаиморасчетов.ПорядокРасчетов, "Объект.", ""));
			КонецЕсли;
		Иначе
			ПорядокРасчетов = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.%1)",
				XMLСтрока(ПараметрыВзаиморасчетов.ПорядокРасчетов));
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыВзаиморасчетов.ЭтоЗаказ Тогда
		УсловиеДляЗаказов = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным) И %2",
			ПорядокРасчетов,
			ПараметрыВзаиморасчетов.ИзменяетРасчетыСтрокой);
		УсловиеГенерации = СтрЗаменить(УсловиеГенерации, "// Условие генерации для заказа //", УсловиеДляЗаказов);
		
	ИначеЕсли ПараметрыВзаиморасчетов.ЭтоПродажаЗакупка Тогда
		УсловиеДляПродажиЗакупки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 И (%2 = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
			|	ИЛИ %2 = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))",
			ПараметрыВзаиморасчетов.ИзменяетРасчетыСтрокой,
			ПорядокРасчетов,
			НакладнаяПоЗаказам);
		УсловиеГенерации = СтрЗаменить(УсловиеГенерации, "// Условие генерации для продажи/закупки //", УсловиеДляПродажиЗакупки);
		
	Иначе
		Если ЗначениеЗаполнено(ПараметрыВзаиморасчетов.ЗаказОснование)
			И ТипЗнч(ЭкземплярОбъекта) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
			УсловиеВозвратТоваровКлиентаНеПоЗаявке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				" НЕ (ИсточникДанных.ЗаявкаНаВозвратТоваровОтКлиента <> ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
				|	И %1 = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))", 
				ПорядокРасчетов);
		Иначе
			УсловиеВозвратТоваровКлиентаНеПоЗаявке = "ИСТИНА";
		КонецЕсли;
		
		ПозицияНачалоУсловияВозврата = СтрНайти(ПараметрыВзаиморасчетов.ИзменяетРасчетыСтрокой, "//Условие возврата начало");
		Если ПозицияНачалоУсловияВозврата Тогда
			ПозицияНачалоУсловияВозврата = ПозицияНачалоУсловияВозврата + СтрДлина("//Условие возврата начало");
			ДлинаУсловияВозврата = СтрНайти(ПараметрыВзаиморасчетов.ИзменяетРасчетыСтрокой, "//Условие возврата окончание")
				- ПозицияНачалоУсловияВозврата;
			УсловиеВозврата = Сред(ПараметрыВзаиморасчетов.ИзменяетРасчетыСтрокой, ПозицияНачалоУсловияВозврата, ДлинаУсловияВозврата);
		КонецЕсли;
		
		//Для пустых значений поля заказ в ТЧ Расшифровка платежа, кроме документов возврата,
		//требуется сгенерировать объекты расчетов
		Если Не ТипЗнч(СтруктураПараметрыВзаиморасчетов) = Тип("Массив") Тогда
			МассивПараметровГенерации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтруктураПараметрыВзаиморасчетов);
		Иначе
			МассивПараметровГенерации = СтруктураПараметрыВзаиморасчетов;
		КонецЕсли;
		
		ЭтоДокументВозврата = ЭкземплярОбъекта = Документы.ВозвратТоваровОтКлиента.ПустаяСсылка()
			Или ЭкземплярОбъекта = Документы.ВозвратТоваровПоставщику.ПустаяСсылка()
			Или ЭкземплярОбъекта = Документы.ВозвратТоваровМеждуОрганизациями.ПустаяСсылка()
			//++ НЕ УТ
			//++ Локализация
			Или ЭкземплярОбъекта = Документы.ВыбытиеДенежныхДокументов.ПустаяСсылка()
			//-- Локализация
			//-- НЕ УТ
			Или ЭкземплярОбъекта = Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка();
		
		УсловиеСпособаКомпенсации = "ЛОЖЬ";
			
		Если Не ЭтоДокументВозврата Тогда
			Для Каждого ПараметрГенерации Из МассивПараметровГенерации Цикл
				УсловияГенерацияПоТЧРасшифровкаПлатежа = Новый Массив();
				Если ЗначениеЗаполнено(ПараметрГенерации.ПутьКДаннымТЧРасшифровкаПлатежа) Тогда
					Шаблон = "
					|	ИСТИНА В
					|		(ВЫБРАТЬ ПЕРВЫЕ 1
					|			ИСТИНА
					|		ИЗ
					|			&ИсточникДанных.%1 КАК ТЧРасшифровкаПлатежа
					|		ГДЕ
					|			ТЧРасшифровкаПлатежа.Ссылка = ИсточникДанных.Ссылка
					|			И ТЧРасшифровкаПлатежа.Ссылка.Проведен
					|			И ТЧРасшифровкаПлатежа.УдалитьЗаказ В (&ПустыеЗначенияОбъектРасчетов))";
					
					ЗаполненныйШаблон = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						Шаблон,
						СтрЗаменить(ПараметрГенерации.ПутьКДаннымТЧРасшифровкаПлатежа, "Объект.", ""));
					УсловияГенерацияПоТЧРасшифровкаПлатежа.Добавить(ЗаполненныйШаблон);
				КонецЕсли;
				
				УсловиеНаличияПустыхЗаказовВТЧРасшифровкаПлатежа = СтрСоединить(УсловияГенерацияПоТЧРасшифровкаПлатежа, " ИЛИ ");
			КонецЦикла;
			
		//Для документов возврата, кроме возврата товаров между организациями,
		//со способом компенсации Вернуть денежные средства требуется генерация объекта расчетов
		Иначе
			Если Не ЭкземплярОбъекта = Документы.ВозвратТоваровМеждуОрганизациями.ПустаяСсылка() Тогда
				УсловиеСпособаКомпенсации = "ИсточникДанных.СпособКомпенсации = ЗНАЧЕНИЕ(Перечисление.СпособыКомпенсацииВозвратовТоваров.ВернутьДенежныеСредства)";
			КонецЕсли;
		КонецЕсли;
		
		УсловиеДляПлатежногоИПрочегоДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 И %2 <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
			|И %3
			|И (%4)
			|ИЛИ (%5)
			|ИЛИ (%6)
			|",
			ПараметрыВзаиморасчетов.ИзменяетРасчетыСтрокой,
			ПорядокРасчетов,
			?(ЗначениеЗаполнено(УсловиеВозврата), УсловиеВозврата,
				?(ЗначениеЗаполнено(ПараметрыВзаиморасчетов.ОбъектРасчетов), "ИСТИНА", "ЛОЖЬ")),
			УсловиеВозвратТоваровКлиентаНеПоЗаявке,
			УсловиеСпособаКомпенсации,
			?(Не ЭтоДокументВозврата И УсловияГенерацияПоТЧРасшифровкаПлатежа.Количество(), УсловиеНаличияПустыхЗаказовВТЧРасшифровкаПлатежа, "ЛОЖЬ"));
		
		УсловиеГенерации = СтрЗаменить(УсловиеГенерации,
								"// Условие генерации для платежного или прочего документа //",
								УсловиеДляПлатежногоИПрочегоДокумента);
	КонецЕсли;
	
	Возврат УсловиеГенерации;
	
КонецФункции

Функция ЕстьДвиженияПоРасчетнымРегистрам(Объект)
	
	ИмяТипДокумента = Объект.Метаданные().Имя;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.[ТипДокумента] КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|
	| И (" + ТесктПроверкиДвиженийПоРасчетнымРегистрамОнлайн() + " ИЛИ " + ТесктПроверкиДвиженийПоРасчетнымРегистрамОфлайн() + ")"; 
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ТекстЗапроса, Новый Структура("ТипДокумента", ИмяТипДокумента));
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли
