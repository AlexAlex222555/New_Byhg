#Область ПрограммныйИнтерфейс

// Заполняет служебные реквизиты в табличной части формы зависящие от локализации
//
// Параметры:
//   Форма               - ФормаКлиентскогоПриложения - заполняемая форма
//   ПараметрыЗаполнения - Произвольный     - сценарий и параметры заполнения
//
Процедура ЗаполнитьСлужебныеРеквизиты(Форма, ПараметрыЗаполнения = Неопределено) Экспорт
	
	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	//++ Локализация
	
	ЗаполнитьПризнакиКатегорииЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	ЗаполнитьПартиюТМЦВЭксплуатации(ТекущаяСтрока, СтруктураДействий);
	
    
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ЗаполнитьНалоговоеНазначениеВозвратнойТарыВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ЗаполнитьСтатьюДекларацииПоАкцизномуНалогу(ТекущаяСтрока, СтруктураДействий);
	ПересчитатьСуммуНДСиАкцизногоНалогаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ПересчитатьСуммуНДСПропорциональноВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
    //++ НЕ УТ
    ЗаполнитьСодержаниеПереработки(ТекущаяСтрока, СтруктураДействий);
    //-- НЕ УТ
	ЗаполнитьНоменклатуруГТД(ТекущаяСтрока, СтруктураДействий);
    
	//-- Локализация
	
КонецПроцедуры
 
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, СтруктураДействийЗаполнения) Экспорт
	
	//++ Локализация
	Перем СтруктураПараметровДействия;
	
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПодакцизныйТовар", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакПодакцизныйТовар", СтруктураПараметровДействия);
	КонецЕсли;        
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакУказыватьШтрихкодАкцизнойМаркиПриПечатиЧека", СтруктураПараметровДействия)
	  	И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
	  	
	  	СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакУказыватьШтрихкодАкцизнойМаркиПриПечатиЧека", СтруктураПараметровДействия);
	КонецЕсли;

	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьПоддерживаемыеДействияЗаполненияТЧ(Действия) Экспорт
	
	//++ Локализация
	Действия.Добавить("ЗаполнитьПризнакПодакцизныйТовар");
	Действия.Добавить("ЗаполнитьПризнакУказыватьШтрихкодАкцизнойМаркиПриПечатиЧека");
	//-- Локализация
	
КонецПроцедуры

Процедура ПриОпределенииШаблонаПоляВыборкиПоКлючуДействия(КлючДействия, Шаблон) Экспорт
	
	//++ Локализация
	
	
	Если КлючДействия = "ЗаполнитьПризнакПодакцизныйТовар" Тогда
		Шаблон = ",
		|	втТаблицаНоменклатуры.%Ключ%.ПодакцизныйТовар КАК %Значение%";
	КонецЕсли;

	Если КлючДействия = "ЗаполнитьПризнакУказыватьШтрихкодАкцизнойМаркиПриПечатиЧека" Тогда
		Шаблон = ",
		|	втТаблицаНоменклатуры.%Ключ%.УказыватьШтрихкодАкцизнойМаркиПриПечатиЧека КАК %Значение%";
	КонецЕсли;

	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

Процедура ЗаполнитьПартиюТМЦВЭксплуатации(ТекущаяСтрока, СтруктураДействий)
	
	Перем ПараметрыДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПартиюТМЦВЭксплуатации", ПараметрыДействия)
		И (Не ПараметрыДействия.Свойство("ХозяйственнаяОперация")
			Или ПараметрыДействия.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратИзЭксплуатации) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТМЦВЭксплуатацииОбороты.Партия
			|ИЗ
			|	РегистрНакопления.ТМЦВЭксплуатации.Обороты(
			|			,
			|			&Дата,
			|			,
			|			Партия = &Партия
			|				И Организация = &Организация
			|				И Подразделение = &Подразделение
			|				И ФизическоеЛицо = &ФизическоеЛицо
			|				И Номенклатура = &Номенклатура
			|				И Характеристика = &Характеристика) КАК ТМЦВЭксплуатацииОбороты
			|ГДЕ
			|	ТМЦВЭксплуатацииОбороты.КоличествоОборот > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТМЦВЭксплуатацииОбороты.Партия КАК ПартияТМЦВЭксплуатации,
            |	РАЗНОСТЬДАТ(ТМЦВЭксплуатацииОбороты.Партия.ДатаЗавершенияЭксплуатации, &Дата, ДЕНЬ) КАК РазностьДат
			|ИЗ
			|	РегистрНакопления.ТМЦВЭксплуатации.Обороты(
			|			,
			|			&Дата,
			|			,
			|			Организация = &Организация
			|				И Подразделение = &Подразделение
			|				И ФизическоеЛицо = &ФизическоеЛицо
			|				И Номенклатура = &Номенклатура
			|				И Характеристика = &Характеристика) КАК ТМЦВЭксплуатацииОбороты
			|ГДЕ
			|	ТМЦВЭксплуатацииОбороты.КоличествоОборот > 0
			|
			|УПОРЯДОЧИТЬ ПО
			|	РазностьДат");
		
		Запрос.УстановитьПараметр("Партия", ТекущаяСтрока.ПартияТМЦВЭксплуатации);
		Запрос.УстановитьПараметр("Дата", ПараметрыДействия.Дата);
		Запрос.УстановитьПараметр("Организация", ПараметрыДействия.Организация);
		Запрос.УстановитьПараметр("Подразделение", ПараметрыДействия.Подразделение);
		Запрос.УстановитьПараметр("Номенклатура", ТекущаяСтрока.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика", ТекущаяСтрока.Характеристика);
		Запрос.УстановитьПараметр("ФизическоеЛицо", ТекущаяСтрока.ФизическоеЛицо);
		
		Результат = Запрос.ВыполнитьПакет();
		
		Если Результат[0].Пустой() Тогда
			Если Результат[1].Пустой() Тогда
				ПустыеЗначения = Новый Структура("ПартияТМЦВЭксплуатации, ИнвентарныйУчет", Справочники.ПартииТМЦВЭксплуатации.ПустаяСсылка(), Ложь);
				ПустыеЗначения = Новый Структура("ПартияТМЦВЭксплуатации", Справочники.ПартииТМЦВЭксплуатации.ПустаяСсылка(), Ложь);
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ПустыеЗначения);
			Иначе
				Выборка = Результат[1].Выбрать();
				Выборка.Следующий();
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Выборка);
				
				СтруктураКоличества = Новый Структура("Количество, КоличествоУпаковок, Упаковка", 0, 0, Неопределено);
				ЗаполнитьЗначенияСвойств(СтруктураКоличества, ТекущаяСтрока);
				Если СтруктураКоличества.Количество = 0 
				 Или СтруктураКоличества.КоличествоУпаковок = 0 Тогда
					СтруктураКоличества.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
				КонецЕсли;
				Если СтруктураКоличества.Количество = 0 Тогда
					СтруктураКоличества.Количество = 1;
				КонецЕсли;
				Если СтруктураКоличества.КоличествоУпаковок = 0 Тогда
					СтруктураКоличества.КоличествоУпаковок = 1;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураКоличества);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПризнакиКатегорииЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Если НЕ СтруктураДействий.Свойство("ЗаполнитьПризнакиКатегорииЭксплуатации") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПризнаков = КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Получить(
		ТекущаяСтрока.КатегорияЭксплуатации);
		
	Если СтруктураПризнаков = Неопределено Тогда
		
		СтруктураПризнаков = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ТекущаяСтрока.КатегорияЭксплуатации,
            "СрокЭксплуатации, ВидТМЦ");
			
		КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Вставить(
			ТекущаяСтрока.КатегорияЭксплуатации,
			СтруктураПризнаков);
			
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураПризнаков);
	
КонецПроцедуры








Процедура ПересчитатьСуммуНДСиАкцизногоНалогаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) 
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДСиАкцизногоНалога", СтруктураПараметровДействия) Тогда
        
		СтавкаНДС = Неопределено;
		Если НЕ (СтруктураПараметровДействия <> Неопределено И СтруктураПараметровДействия.Свойство("СтавкаНДС", СтавкаНДС)) Тогда
			СтавкаНДС = ТекущаяСтрока.СтавкаНДС;
		КонецЕсли;
		
		Если КэшированныеЗначения.Свойство("ПроцентыСтавокНДС") Тогда
			ТекПроцентНДС = КэшированныеЗначения.ПроцентыСтавокНДС[СтавкаНДС];
		Иначе
			КэшированныеЗначения.Вставить("ПроцентыСтавокНДС", Новый Соответствие);
			ТекПроцентНДС = Неопределено;
        КонецЕсли;
        
		Если ТекПроцентНДС = Неопределено Тогда
			
			ТекПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтавкаНДС);
			КэшированныеЗначения.ПроцентыСтавокНДС.Вставить(СтавкаНДС, ТекПроцентНДС);
			
		КонецЕсли;
		
		Если УчетАкцизногоНалога.ДействуетАкцизныйНалог(СтруктураПараметровДействия.Дата) Тогда
			ТекущаяСтрока.СуммаАкцизногоНалога = УчетАкцизногоНалога.РассчитатьСуммуАкцизногоНалога(
			    ТекущаяСтрока.Сумма, 
				СтруктураПараметровДействия.ЦенаВключаетНДС, 
				ТекущаяСтрока.СтатьяДекларацииПоАкцизномуНалогу, 
				ТекущаяСтрока.ПодакцизныеТоварыДляКоммерческогоИспользования
            );
		Иначе
			ТекущаяСтрока.СуммаАкцизногоНалога = 0;
		КонецЕсли; 
		
		ТекущаяСтрока.СуммаНДС = УчетНДСУПКлиентСервер.РассчитатьСуммуНДС(
            ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаАкцизногоНалога,
			ТекПроцентНДС,
			СтруктураПараметровДействия.ЦенаВключаетНДС
        );
	
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ
Процедура ЗаполнитьСодержаниеПереработки(ТекущаяСтрока, СтруктураДействий) 
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСодержаниеПереработки", СтруктураПараметровДействия) Тогда
		
		СодержаниеУслуги = "";
		СтруктураПараметровДействия.Свойство("СодержаниеУслуги", СодержаниеУслуги);
		
        Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
            
            СодержаниеПродукции = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
                ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Номенклатура,   "НаименованиеПолное"),
                ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Характеристика, "НаименованиеПолное")
            );
            
			ТекущаяСтрока.Содержание = СодержаниеУслуги + " " + СодержаниеПродукции;
            
		Иначе
			ТекущаяСтрока.Содержание = "";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
//-- НЕ УТ

Процедура ЗаполнитьНоменклатуруГТД(ТекущаяСтрока, СтруктураДействий) 

	Перем СтруктураПараметровДействия;
	
	Если Не СтруктураДействий.Свойство("ЗаполнитьНоменклатуруГТД", СтруктураПараметровДействия) Тогда
		Возврат;
	КонецЕсли;

	ТекущаяСтрока.НомерГТД = Неопределено;
    
    Номенклатура = Неопределено;
    Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Номенклатура") Тогда
        Номенклатура = ТекущаяСтрока.Номенклатура;
    КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат;
    КонецЕсли;
    
    РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
        Номенклатура, 
		"ТипНоменклатуры, ВестиУчетПоГТД, НоменклатураГТД"
    );
    
	Если РеквизитыНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
		Возврат;
	КонецЕсли;
		
	// По умолчанию заполняем только для товаров с установленном флагом ВестиУчетПоГТД
	// В некоторых документах (напр. РТУ, НН для печати) значение по умолчанию заполняется
	// не зависимо от того ведется учет или нет
	ЗаполнятьДляТоваровБезУчетаПоГТД = Ложь; 
	Если СтруктураПараметровДействия <> Неопределено Тогда
		Если СтруктураПараметровДействия.Свойство("ЗаполнятьДляТоваровБезУчетаПоГТД") Тогда 
			ЗаполнятьДляТоваровБезУчетаПоГТД = СтруктураПараметровДействия.ЗаполнятьДляВсех;
		КонецЕсли;
	КонецЕсли;
	
	Если Не РеквизитыНоменклатуры.ВестиУчетПоГТД И Не ЗаполнятьДляТоваровБезУчетаПоГТД Тогда
		Возврат;
	КонецЕсли;
		
	ТекущаяСтрока.НомерГТД = РеквизитыНоменклатуры.НоменклатураГТД;

КонецПроцедуры

Процедура ЗаполнитьСтатьюДекларацииПоАкцизномуНалогу(ТекущаяСтрока, СтруктураДействий) 

	Перем СтруктураПараметровДействия;
	
	Если Не СтруктураДействий.Свойство("ЗаполнитьСтатьюДекларацииПоАкцизномуНалогу", СтруктураПараметровДействия) Тогда
		Возврат;
	КонецЕсли;

	ТекущаяСтрока.СтатьяДекларацииПоАкцизномуНалогу = Неопределено;
    
    Номенклатура = Неопределено;
    Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Номенклатура") Тогда
        Номенклатура = ТекущаяСтрока.Номенклатура;
    КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат;
    КонецЕсли;
    
    РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
        Номенклатура, 
        "ПодакцизныйТовар, СтатьяДекларацииПоАкцизномуНалогу"
    );
    
	Если НЕ РеквизитыНоменклатуры.ПодакцизныйТовар Тогда
		Возврат;
	КонецЕсли;
		
	ТекущаяСтрока.СтатьяДекларацииПоАкцизномуНалогу = РеквизитыНоменклатуры.СтатьяДекларацииПоАкцизномуНалогу;

КонецПроцедуры


//-- Локализация

#КонецОбласти
