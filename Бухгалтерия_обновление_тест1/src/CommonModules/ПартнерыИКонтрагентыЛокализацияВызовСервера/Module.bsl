
#Область ПрограммныйИнтерфейс

// Устанавливает подчиненному контрагенту его головного контрагенту
//
// Параметры
//  Контрагент  - СправочникСсылка.Контрагенты - контрагент, которому задается головной
//  ГоловнойКонтрагент  - СправочникСсылка.Контрагенты - устанавливаемое значение головного контрагента
//  ОписаниеОшибки  - Строка - параметр, в который записываются возникшие ошибки.
//
Процедура ИзменитьГоловногоКонтрагента(Контрагент, ГоловнойКонтрагент, ОписаниеОшибки) Экспорт
	//++ Локализация
    ИННПлательщикаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГоловнойКонтрагент, "ИННПлательщикаНДС");
	
    Если ЗначениеЗаполнено(ИННПлательщикаНДС) Тогда    
		
        КодФилиала = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "КодФилиала");
		
		ВыборкаКонтрагент = ПартнерыИКонтрагентыЛокализация.ИННПлательщикаНДСКодФилиалаУжеИспользуетсяВИнформационнойБазе(ИННПлательщикаНДС, КодФилиала, Истина, Контрагент);
		Если ВыборкаКонтрагент <> Неопределено Тогда
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось изменить головного контрагента для %1.
|Данные ИНН плательщика НДС и код филиала уже указаны для контрагента с кодом %2, ответственный - %3.
|Попробуйте указать правильный код филиала, и лишь затем выбрать головного контрагента.'
|;uk='Не вдалося змінити головного контрагента для %1.
|Дані ІПН платника ПДВ та код філії вже вказані для контрагента з кодом %2, відповідальний - %3.
|Спробуйте ввести правильний код філії, і лише потім вибрати головного контрагента.'"),
				Контрагент,
				ВыборкаКонтрагент.Код,
				ВыборкаКонтрагент.ОсновнойМенеджер);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
	КонтрагентОбъект = Контрагент.ПолучитьОбъект();
	
	Попытка
		КонтрагентОбъект.Заблокировать();
	Исключение
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось изменить головного контрагента для %1.
|Возможно, контрагент в настоящий момент редактируется.'
|;uk='Не вдалося змінити головного контрагента для %1.
|Можливо, контрагент зараз редагується.'"),
			Контрагент);
		Возврат;
	КонецПопытки;
	
	КонтрагентОбъект.ГоловнойКонтрагент = ГоловнойКонтрагент;
	//++ Локализация
    КонтрагентОбъект.ИННПлательщикаНДС = ИННПлательщикаНДС;
	//-- Локализация
	КонтрагентОбъект.Записать();
	
	КонтрагентОбъект.Разблокировать();
	
КонецПроцедуры

// Определяет возможных головных контрагентов по ИНН плательщика НДС.
// 
// Параметры:
//  ИНН  - Строка - ИНН плательщика НДС, по которому необходимо получить список контрагентов.
//  Контрагент - Справочник.Контрагенты - ссылка на контрагента, исключаемого из поиска.
//
// Возвращаемое значение:
//  Число   - количество контрагентов
//
Функция КоличествоВозможныхГоловныхКонтрагентов(ИНН, Контрагент = Неопределено) Экспорт
	Результат = 0;
	//++ Локализация
	Результат = ПартнерыИКонтрагентыЛокализация.ВозможныеГоловныеКонтрагенты(ИНН, Контрагент).Количество();
	//-- Локализация
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает значение реквизита элемента справочник партнеры
//   см. описание ОбщегоНазначения.ЗначениеРеквизитаОбъекта
//
Функция ЗначениеРеквизитаПартнеры(Ссылка, ИмяРеквизита) Экспорт
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	Возврат Результат
КонецФункции

Функция КонтрагентыДанныеВыбора(Параметры) Экспорт
	//++ Локализация
	ДанныеВыбора = Новый СписокЗначений;
	Партнер = Неопределено;
	
	Если Параметры.Отбор.Свойство("Партнер") Тогда
		Партнер = ?(ЗначениеЗаполнено(Параметры.Отбор.Партнер),Параметры.Отбор.Партнер,Неопределено);
		Параметры.Отбор.Удалить("Партнер");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	МассивУстановленныхОтборов = ПартнерыИКонтрагентыВызовСервера.МассивУстановленныхПараметровОтбораПоТипуПартнера(Параметры);
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	
	Если МассивУстановленныхОтборов.Количество() > 0 И ИспользоватьПартнеровКакКонтрагентов Тогда
		
		УстанавливатьОтборПоТипуПартнераКакИЛИ = Ложь;
		
		Если Параметры.Свойство("УстанавливатьОтборПоТипуПартнераКакИЛИ") И Параметры.УстанавливатьОтборПоТипуПартнераКакИЛИ Тогда
			УстанавливатьОтборПоТипуПартнераКакИЛИ = Истина;
		КонецЕсли;
		
		СтрокаОтбора = ПартнерыИКонтрагентыВызовСервера.СтрокаОтбораЗапросаПоТипуПартнера(Параметры, МассивУстановленныхОтборов);
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК Представление,
		|	Партнеры.Наименование КАК ПартнерНаименование,
		|	Контрагенты.Наименование,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоНаименованию КАК НайденоПо,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода 
		|	И Партнеры.Ссылка = &УсловиеОтбора
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	Партнер.Наименование,
		|	Контрагенты.Наименование,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоИНН,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
        |	Контрагенты.ИННПлательщикаНДС ПОДОБНО &СтрокаВвода
		|	И Партнеры.Ссылка = &УсловиеОтбора
		|
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	Партнер.Наименование,
		|	Контрагенты.Наименование,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоКодПоЕДРПОУ,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
        |	Контрагенты.КодПоЕДРПОУ ПОДОБНО &СтрокаВвода
		|	И Партнеры.Ссылка = &УсловиеОтбора
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.ПометкаУдаления";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И Партнеры.Ссылка = &УсловиеОтбора" , СтрокаОтбора);
		
	ИначеЕсли Параметры.Свойство("ВыборКонтрагентовИОрганизаций") И ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 41
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК Представление,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ КАК ПартнерНаименование,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК ПорядокВывода,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоНаименованию КАК НайденоПо,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 41
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоИНН,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
        |	Контрагенты.ИННПлательщикаНДС ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 41
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоКодПоЕДРПОУ,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
        |	Контрагенты.КодПоЕДРПОУ ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	&НашеПредприятие,
		|	Контрагенты.Наименование,
		|	 -1,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Неопределено КАК ИННПлательщикаНДС,
		|	&НайденоПоНаименованию,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Организации КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода
		|
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	&НашеПредприятие,
		|	Контрагенты.Наименование,
		|	 -1,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Неопределено КАК ИННПлательщикаНДС,        
		|	&НайденоПоКодПоЕДРПОУ,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Организации КАК Контрагенты
		|ГДЕ
        |	Контрагенты.КодПоЕДРПОУ ПОДОБНО &СтрокаВвода
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.ПометкаУдаления,
		|	ПорядокВывода УБЫВ";
		
	Иначе
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК Представление,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ КАК ПартнерНаименование,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК ПорядокВывода,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоНаименованию КАК НайденоПо,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоИНН,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
        |	Контрагенты.ИННПлательщикаНДС ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ,
        |	Контрагенты.КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС,
		|	&НайденоПоКодПоЕДРПОУ,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
        |	Контрагенты.КодПоЕДРПОУ ПОДОБНО &СтрокаВвода
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.ПометкаУдаления,
		|	ПорядокВывода УБЫВ";
		
	КонецЕсли;
	СтрокаПоиска = ?(Параметры.СтрокаПоиска = Неопределено, "", Параметры.СтрокаПоиска);
	
	Запрос.УстановитьПараметр("СтрокаВвода", СтрокаПоиска + "%" );
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("НайденоПоНаименованию", "ПоНаименованию");
	Запрос.УстановитьПараметр("НайденоПоИНН", "ПоИНН");
    Запрос.УстановитьПараметр("НайденоПоКодПоЕДРПОУ", "ПоКодПоЕДРПОУ");
	Запрос.УстановитьПараметр("НашеПредприятие", НСтр("ru='Наше предприятие';uk='Наше підприємство'"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Параметры.Свойство("ИсключаяКонтрагента") 
			И Выборка.Ссылка = Параметры.ИсключаяКонтрагента Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если ДанныеВыбора.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
			
			Если Выборка.НайденоПо = "ПоНаименованию"  Тогда
				Представление = Выборка.Наименование;
			ИначеЕсли Выборка.НайденоПо = "ПоКодПоЕДРПОУ" Тогда
				Представление = Выборка.КодПоЕДРПОУ + " (" +Выборка.Наименование + ")";
            Иначе
                Представление = Выборка.ИННПлательщикаНДС + " (" +Выборка.Наименование + ")";
			КонецЕсли;
			
			Если Не ИспользоватьПартнеровКакКонтрагентов Тогда
				Представление = Представление + " (" + Выборка.ПартнерНаименование + ")";
			КонецЕсли;
			
			Если Выборка.ПометкаУдаления Тогда
				СтруктураЗначение = Новый Структура("Значение,ПометкаУдаления", Выборка.Ссылка, Выборка.ПометкаУдаления);
				ДанныеВыбора.Добавить(СтруктураЗначение,Представление,,БиблиотекаКартинок.ПомеченныйНаУдалениеЭлемент);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Ссылка,Представление);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	//-- Локализация
	Возврат Неопределено
	
КонецФункции

Функция ПартнерыДанныеВыбора(Параметры) Экспорт
	//++ Локализация
	МассивУстановленныхОтборов = ПартнерыИКонтрагентыВызовСервера.МассивУстановленныхПараметровОтбораПоТипуПартнера(Параметры);
	
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	
	Если МассивУстановленныхОтборов.Количество() > 0 ИЛИ ИспользоватьПартнеровКакКонтрагентов Тогда
		
		Запрос = Новый Запрос;
	
		Если Параметры.Свойство("УстанавливатьОтборПоТипуПартнераКакИЛИ") И Параметры.УстанавливатьОтборПоТипуПартнераКакИЛИ Тогда
			УстанавливатьОтборПоТипуПартнераКакИЛИ = Истина;
		КонецЕсли;
		
		СтрокаОтбора = ПартнерыИКонтрагентыВызовСервера.СтрокаОтбораЗапросаПоТипуПартнера(Параметры, МассивУстановленныхОтборов);
		
		Если Параметры.Отбор.Свойство("БизнесРегион") Тогда
			СтрокаОтбора = " И Партнеры.БизнесРегион = &БизнесРегион";
			Запрос.УстановитьПараметр("БизнесРегион", Параметры.Отбор.БизнесРегион);
		КонецЕсли;
		
		Если ИспользоватьПартнеровКакКонтрагентов Тогда
			СтрокаОтбора = СтрокаОтбора + " И (НЕ Партнеры.Ссылка = Значение(Справочник.Партнеры.НеизвестныйПартнер)) ";
		КонецЕсли;
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
			СтрокаОтбора = СтрокаОтбора + " И (НЕ Партнеры.Ссылка = Значение(Справочник.Партнеры.НашеПредприятие)) ";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СтрокаВвода",Параметры.СтрокаПоиска + "%" );
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 51
		|	Партнеры.Ссылка КАК Партнер,
		|	ПРЕДСТАВЛЕНИЕ(Партнеры.Ссылка) КАК Представление,
		|	Партнеры.ПометкаУдаления КАК ПометкаУдаления,
        |	"""" КАК КодПоЕДРПОУ,
        |	"""" КАК ИННПлательщикаНДС,
		|	""НайденоПоНаименованию"" КАК НайденоПо
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|ГДЕ
		|	Партнеры.Наименование ПОДОБНО &СтрокаВвода " + СтрокаОтбора + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Партнеры.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Партнеры.Ссылка),
		|	Партнеры.ПометкаУдаления,
		|	Контрагенты.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС КАК ИННПлательщикаНДС,
		|	""НайденоПоИННПлательщикаНДС""
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
        |	Контрагенты.ИННПлательщикаНДС ПОДОБНО &СтрокаВвода " + СтрокаОтбора + "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Партнеры.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Партнеры.Ссылка),
		|	Партнеры.ПометкаУдаления,
		|	Контрагенты.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
        |	Контрагенты.ИННПлательщикаНДС КАК ИННПлательщикаНДС,
		|	""НайденоПоКодуЕДРПОУ""
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
        |	Контрагенты.КодПоЕДРПОУ ПОДОБНО &СтрокаВвода " + СтрокаОтбора;
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.НайденоПо = "НайденоПоНаименованию" Тогда
				ТекстПредставление = Выборка.Представление;
	        ИначеЕсли Выборка.НайденоПо = "НайденоПоКодуЕДРПОУ" Тогда
                ТекстПредставление = Выборка.КодПоЕДРПОУ + "(" +Выборка.Представление + ")";
            Иначе
                ТекстПредставление = Выборка.ИННПлательщикаНДС + "(" +Выборка.Представление + ")";
			КонецЕсли;
			
			Если Выборка.ПометкаУдаления Тогда
				СтруктураЗначение = Новый Структура("Значение,ПометкаУдаления", Выборка.Партнер, Выборка.ПометкаУдаления);
				ДанныеВыбора.Добавить(СтруктураЗначение,ТекстПредставление,, БиблиотекаКартинок.ПомеченныйНаУдалениеЭлемент);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Партнер, ТекстПредставление);
			КонецЕсли;
		КонецЦикла;
		
		Возврат ДанныеВыбора;
	
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	//-- Локализация
КонецФункции

//++ Локализация

// Проверка использования ИННПлательщикаНДС/Кода филиала
// 
// Параметры:
// 	ИННПлательщикаНДС - Строка - строка ИНН плательщика НДС
// 	КодФилиалаГоловногоКонтрагента - Строка - строка Код филиала головного контрагента
// Возвращаемое значение:
// 	
Функция ИННПлательщикаНДСКодФилиалаГоловногоКонтрагентаИспользуютсяВИнформационнойБазе(ИННПлательщикаНДС, КодФилиалаГоловногоКонтрагента) Экспорт    
    Возврат ПартнерыИКонтрагентыЛокализация.ИННПлательщикаНДСКодФилиалаГоловногоКонтрагентаИспользуютсяВИнформационнойБазе(ИННПлательщикаНДС, КодФилиалаГоловногоКонтрагента);
КонецФункции

//-- Локализация
#КонецОбласти