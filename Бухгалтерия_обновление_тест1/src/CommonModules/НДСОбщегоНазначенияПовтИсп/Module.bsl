////////////////////////////////////////////////////////////////////////////////
// НАЛОГООБЛОЖЕНИЕ НДС

Функция СтавкаНДСПоУмолчанию() Экспорт
    Возврат НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС20);
КонецФункции // СтавкаНДСПоУмолчанию()

Функция ОпределитьНалоговоеНазначениеПоНалогообложениюНДС(НалогообложениеНДС) Экспорт

	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС Тогда 
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
	Иначе
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
	КонецЕсли; 
	
КонецФункции // ОпределитьНалоговоеНазначениеПоНалогообложениюНДС

Функция НалогообложениеНДСНалоговогоНазначения(Знач НалоговоеНазначение) Экспорт
	
	ВидДеятельностиНДС = НалоговоеНазначение.ВидДеятельностиНДС;
	Если ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.Облагаемая Тогда
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	ИначеЕсли ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.Необлагаемая Тогда 
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
	ИначеЕсли ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая Тогда 	
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	КонецЕсли; 
	
КонецФункции // НалогообложениеНДСНалоговогоНазначения

Функция ОпределитьНалоговоеНазначениеПродажиПоСтавкеНДС(Знач Организация, Знач Дата, Знач СтавкаНДС) Экспорт

	ОрганизацияПлательщикНДС = НДСОбщегоНазначенияПовтИсп.ОрганизацияПлательщикНДС(Организация, Дата);
    
    ПеречислениеСтавкаНДС = НДСКлиентСерверПовтИсп.ПеречислениеСтавкаНДСПоСтавкеНДС(СтавкаНДС);
    
	Если НЕ ОрганизацияПлательщикНДС Тогда
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
	ИначеЕсли ПеречислениеСтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда 	
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
	ИначеЕсли ПеречислениеСтавкаНДС = Перечисления.СтавкиНДС.НеНДС Тогда 		
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
	Иначе
		Возврат Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
	КонецЕсли; 
	
КонецФункции // ОпределитьНалоговоеНазначениеПродажиПоСтавкеНДС


// Возвращает - является ли организация плательщиком НДС
//
// Параметры:
// Организация - СправочникСсылка.Организации
//
// Возвращаемое значение:
// 	Дата - Дата - дата получения сведений
//
// Возвращаемое значение:
// 	Булево -  Истина - организация плательщик НДС, Ложь - организация неплательщик НДС
//
Функция ОрганизацияПлательщикНДС(Организация, Дата) Экспорт
	Возврат Справочники.Организации.ПлательщикНДС(Организация, Дата);
КонецФункции // ОрганизацияПлательщикНДС()

Функция ДатаВступленияВСилуПриказа1307() Экспорт
	Возврат '2016-04-01';
КонецФункции // ДатаВступленияВСилуПриказа1307()

Функция ВключатьНДСВСебестоимостьПриУсловнойПродаже() Экспорт
	Возврат Истина;
КонецФункции // ВключатьНДСВСебестоимостьПриУсловнойПродаже()
 
Функция ИсключатьНДСИзСебестоимостиПриВосстановленииНДС() Экспорт
	Возврат Истина;
КонецФункции // ИсключатьНДСИзСебестоимостиПриВосстановленииНДС()

Функция ПолучитьКоэффициентПропорциональногоНДСКредит(Организация, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураОтбора = Новый Структура("Организация, СпецРежимНалогообложения", Организация, 0);
	
	ТаблицаКоэффициентов = РегистрыСведений.КоэффициентыПропорциональногоОтнесенияНДСНаКредит.СрезПоследних(Период, СтруктураОтбора);
	
	Если ТаблицаКоэффициентов.Количество() = 0 Тогда
		
		Возврат 0;		
		
	Иначе
		
		Возврат ТаблицаКоэффициентов[0].Коэффициент;		
		
	КонецЕсли;
	
КонецФункции	

Функция ПолучитьКоэффициентПропорциональногоНДСОбязательтсва(Организация, Период) Экспорт
	Возврат (1 - ПолучитьКоэффициентПропорциональногоНДСКредит(Организация, Период));
КонецФункции	

Функция СтавкаНДССоответствуетНалогообложению(СтавкаНДС, НалогообложениеНДС) Экспорт
	
	Результат = Истина;
	
	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС И СтавкаНДС <> Справочники.СтавкиНДС.НеНДС Тогда
		Результат = Ложь;
	ИначеЕсли НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОсвобожденаОтНДС И СтавкаНДС <> Справочники.СтавкиНДС.БезНДС Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
