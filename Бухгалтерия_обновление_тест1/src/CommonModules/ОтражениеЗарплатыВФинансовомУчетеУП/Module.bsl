
////////////////////////////////////////////////////////////////////////////////
// Модуль "ОтражениеЗарплатыВФинансовомУчетеУП" содержит процедуры и функции для 
// обслуживания механизмов отражения зарплаты в финансовом учете.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ФормированиеДвижений

// Процедура формирует дополнительные движения для документов блока расчета зарплаты.
//
// Параметры:
//	Движения - КоллекцияДвижений - Коллекция наборов записей движений документа.
//	Отказ - Булево - Признак отказа от проведения документа.
//	Организация - СправочникСсылка.Организации - Организация, по которой вводится документ.
//	ПериодРегистрации - Период - Отчетный период.
//  ДанныеДляПроведения - Структура, ключ - имя регистра, значение - таблица значений с движениями документа
//						это источник данных, если Неопределено, тогда источник данных - движения регистров.
//	СтрокаСписокТаблиц - Строка - Список таблиц для отражения в финансовом учете.
//
Процедура СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения, СтрокаСписокТаблиц = "") Экспорт

	Если Движения.Найти("ВыплатаОтпусковЗаСчетРезерва") <> Неопределено Тогда
		
		ДанныеДляОтражения = ОтражениеЗарплатыВБухучете.ДанныеДляОтраженияЗарплатыВБухучете(ПериодРегистрации, Организация, СтрокаСписокТаблиц, ДанныеДляПроведения);
		
		ДокументСсылка = Движения.Найти("ВыплатаОтпусковЗаСчетРезерва").Отбор.Регистратор.Значение;  
		Движения.ВыплатаОтпусковЗаСчетРезерва.Очистить();
		Движения.ВыплатаОтпусковЗаСчетРезерва.Записать();
		
		МодульРезервОтпусков = ОбщегоНазначения.ОбщийМодуль("РезервОтпусков");
		НастройкиРезервовОтпусков = МодульРезервОтпусков.НастройкиРезервовОтпусков(Организация, ПериодРегистрации);
		
		Если НастройкиРезервовОтпусков.ФормироватьРезервОтпусковБУ Тогда
			
			КолонкиСуммирования = Неопределено;
			ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетНачисленнаяЗарплатаИВзносы(КолонкиСуммирования);
			НачисленныеОтпуска = ОтражениеЗарплатыВБухучете.НоваяТаблицаНачисленныеОтпуска();
			МодульРезервОтпусков.СписатьРасходыПоОплатеОтпускаЗаСчетОценочныхОбязательств(Организация, ПериодРегистрации, ДокументСсылка, ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы, НачисленныеОтпуска, КолонкиСуммирования);
			МодульРезервОтпусков.СформироватьДвиженияВыплатаОтпусковЗаСчетРезерва(Движения, Отказ, Организация, ПериодРегистрации, НачисленныеОтпуска);
			ДвиженияВыплатаОтпусковЗаСчетРезерва = Движения.ВыплатаОтпусковЗаСчетРезерва.Выгрузить();
			МодульРезервОтпусков.СформироватьДвиженияСписаниеРезерваОтпусков (Движения, Отказ, Организация, ПериодРегистрации, ДвиженияВыплатаОтпусковЗаСчетРезерва);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Функция РаспределитьНачисленияПоЭтапамПроизводства(Параметры) Экспорт
	
	ЗапросВыработка = Новый Запрос;
	ЗапросВыработка.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗапросВыработка.Текст = 
		БазаРаспределенияТрудозатрат() +
		Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов();
	
	ЗапросВыработка.УстановитьПараметр("Ссылка",                          Параметры.Ссылка);
	ЗапросВыработка.УстановитьПараметр("Период",                          Параметры.Период);
	ЗапросВыработка.УстановитьПараметр("Организация",                     Параметры.Организация);
	ЗапросВыработка.УстановитьПараметр("ДатаНачала",                      Параметры.ДатаНачала);
	ЗапросВыработка.УстановитьПараметр("ДатаОкончания",                   Параметры.ДатаОкончания);
	ЗапросВыработка.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",  Параметры.КоэффициентПересчетаВВалютуУПР);
	ЗапросВыработка.УстановитьПараметр("Сводно",                          Параметры.Сводно);
	ЗапросВыработка.УстановитьПараметр("ПроводкиПоРаботникам",            Параметры.ПроводкиПоРаботникам);
	ЗапросВыработка.УстановитьПараметр("ВалютаРегл",                      Параметры.ВалютаРегл);
	
	КоэффициентПересчетаВВалютуУПР = Параметры.КоэффициентПересчетаВВалютуУПР;
	
	МассивРезультатов = ЗапросВыработка.ВыполнитьПакет();
	
	ТаблицаВыработкаСотрудников   = НоваяТаблицаВыработкаСотрудников();
	ВидыВзносов                   = ВидыСуммВзносов();
	БазаРаспределения             = МассивРезультатов[3].Выгрузить();
	СтрокиКРаспределению          = МассивРезультатов[4].Выгрузить();
	ПараметрыСпособовОтражения    = МассивРезультатов[5].Выбрать();
	
	БазаРаспределения.Индексы.Добавить("Сотрудник, Организация, Подразделение, СпособОтражения");
	
	СтруктураОтбора = Новый Структура("Сотрудник, Организация, Подразделение, СпособОтражения");
	
	Для Каждого Строка Из СтрокиКРаспределению Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, Строка);
		
		СтрокиБазы = БазаРаспределения.НайтиСтроки(СтруктураОтбора);
		
		МассивКоэффициентов = Новый Массив;
		
		Для Каждого СтрокаБазы Из СтрокиБазы Цикл
			МассивКоэффициентов.Добавить(СтрокаБазы.Коэффициент);
		КонецЦикла;
		
		Для Каждого ВидВзноса Из ВидыВзносов Цикл
			
			МассивРаспределения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка[ВидВзноса.Представление], МассивКоэффициентов);
			
			Если МассивРаспределения = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для Индекс = 0 По СтрокиБазы.Количество()-1 Цикл
				
				СтрокаБазы = СтрокиБазы[Индекс];
				
				ПараметрыСтроки = ПараметрыРаспределенияСтроки(ВидВзноса, СтрокаБазы, ПараметрыСпособовОтражения);
				
				НоваяСтрока = ТаблицаВыработкаСотрудников.Добавить();
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБазы); // производственная аналитика
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыСтроки); // аналитика расходов в соответствии со способами отражения
				
				НоваяСтрока.ВидОперацииПоЗарплате = Строка.ВидОперации;
				Если ЗначениеЗаполнено(НоваяСтрока.ТипНалога) Тогда
					НоваяСтрока.Налог				  = Строка.Налог;
				КонецЕсли;
				
				НоваяСтрока.Сумма = 0;
				НоваяСтрока.СуммаРегл = 0;
				
				Если Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства
					ИЛИ Строка.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства Тогда
					// Отражаем суммы только в НУ
				ИначеЕсли МассивРаспределения <> Неопределено Тогда
					НоваяСтрока.Сумма = Окр(МассивРаспределения[Индекс]*КоэффициентПересчетаВВалютуУПР, 2);
					НоваяСтрока.СуммаРегл = МассивРаспределения[Индекс];
				КонецЕсли;
				
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	КолонкиГруппировок = "";
	КолонкиСуммирования = "Сумма, СуммаРегл";
	
	Для Каждого Колонка Из ТаблицаВыработкаСотрудников.Колонки Цикл
		Если СтрНайти(КолонкиСуммирования, Колонка.Имя) = 0 Тогда
			КолонкиГруппировок = КолонкиГруппировок + Колонка.Имя + ",";
		КонецЕсли;
	КонецЦикла;
	КолонкиГруппировок = Лев(КолонкиГруппировок, СтрДлина(КолонкиГруппировок) - 1);
	ТаблицаВыработкаСотрудников.Свернуть(КолонкиГруппировок, КолонкиСуммирования);

	
	Возврат ТаблицаВыработкаСотрудников;
	
КонецФункции


// Процедура выполняет проверку заполнения расшифровки платежа в документах выдачи займа сотруднику.
// 
// Параметры:
//   ДокументОбъект - ДокументОбъект.РасходныйКассовыйОрдер,
//                    ДокументОбъект.ЗаявкаНаРасходованиеДенежныхСредств,
//                    ДокументОбъект.СписаниеБезналичныхДенежныхСредств - проверяемый документ
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ПроверитьЗаполнениеДокументаВыдачиЗайма(ДокументОбъект, Отказ) Экспорт
	
	Организация       = ДокументОбъект.Организация;
	Дата              = ДокументОбъект.Дата;
	ФизическоеЛицо    = ДокументОбъект.ПодотчетноеЛицо;
	Ссылка            = ДокументОбъект.Ссылка;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Организация 				= Организация;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода				= Дата;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода			= Дата;
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ 	= Неопределено;
	
	КадровыйУчет.ПроверитьРаботающихФизическихЛиц(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо),
		ПараметрыПолученияСотрудниковОрганизаций,
		Отказ,
		Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "ФизическоеЛицо", "Объект"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Договора.ДоговорЗаймаСотруднику КАК ДоговорЗайма,
	|	Договора.Сумма
	|ПОМЕСТИТЬ ВТДоговора
	|ИЗ
	|	&Договора КАК Договора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДоговора.ДоговорЗайма КАК ДоговорЗайма,
	|	СУММА(ВТДоговора.Сумма) КАК Сумма
	|ИЗ
	|	ВТДоговора КАК ВТДоговора
	|ГДЕ
	|	ВТДоговора.ДоговорЗайма <> ЗНАЧЕНИЕ(Документ.ДоговорЗаймаСотруднику.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДоговора.ДоговорЗайма";
	Запрос.УстановитьПараметр("Договора", ДокументОбъект.РасшифровкаПлатежа.Выгрузить(,"ДоговорЗаймаСотруднику, Сумма"));
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДоговорЗайма = Выборка.ДоговорЗайма;
		Сумма = Выборка.Сумма;
		
		ДатаПредоставления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорЗайма, "ДатаПредоставления");
		ЗаймыСотрудникам.ПроверитьДатуВыдачиЗайма(Дата, ДатаПредоставления, "Объект.Дата", Отказ);
		
		НевыданныйОстаток = ЗаймыСотрудникам.ОстатокНевыданныхСумм(ДоговорЗайма, Дата, Ссылка);
		
		Если НевыданныйОстаток < Сумма Тогда 
			ТекстСообщения = НСтр("ru='На %1 по договору займа не выдано %2 грн. Сумма выдачи не может превышать невыданный остаток.';uk='На %1 за договором позики не видано %2 грн. Сума видачі не може перевищувати невиданий залишок.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Формат(Дата, "ДЛФ=Д"), НевыданныйОстаток);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Сумма", "Объект", Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает условное оформление операций по зарплате.
//
// Параметры:
//    УсловноеОформление - ЭлементУсловногоОформления - Коллекция элементов условного оформления формы;
//    ПутьКДанным        - Строка - Полное название объекта требующего условного оформления;
//    ЭлементФормы       - ЭлементФормы - элемент формы, в котором отображаются данные.
//
Процедура УсловноеОформлениеОперацийПоЗарплате(УсловноеОформление, ПутьКДанным, ЭлементФормы) Экспорт
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Отпуск за счет обязательств';uk='Відпустка за рахунок зобов''язань'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Отпуск за счет резервов';uk='Відпустка за рахунок резервів'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Отпуск за счет обязательств и резервов';uk='Відпустка за рахунок зобов''язань і резервів'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Компенсация отпуска';uk='Компенсація відпустки'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Компенсация отпуска за счет обязательств';uk='Компенсація відпустки за рахунок зобов''язань'"));
	
КонецПроцедуры

#Область ДополнительныеПроцедурыРасчетаРезервовОтпусков

// Добавляет в таблицу резервов отпусков колонку "Подразделение" и заполняет ее по
// кадровым данным сотрудников.
//
// Параметры:
//   ТаблицаРезервов   - ТаблицаЗначений - Таблица значений резервов отпусков;
//   ПериодРегистрации - Дата - Дата, на которую необходимо получить подразделение сотрудника.
//
Процедура ЗаполнитьПодразделениеПредприятиеПоМестамСотрудников(ТаблицаРезервов, ПериодРегистрации) Экспорт
	
	ТаблицаРезервов.Колонки.Добавить("ПодразделениеПредприятия",   Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	
	// заполнение подразделений по кадровым данным сотрудников
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
		Ложь,
		ТаблицаРезервов.ВыгрузитьКолонку("Сотрудник"),
		"МестоВСтруктуреПредприятия",
		КонецМесяца(ПериодРегистрации));
	
	ПодразделенияСотрудников = Новый Соответствие;
	Для Каждого Строка Из КадровыеДанные Цикл
		ПодразделенияСотрудников.Вставить(Строка.Сотрудник, Строка.МестоВСтруктуреПредприятия);
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаРезервов Цикл
		Строка.ПодразделениеПредприятия = ПодразделенияСотрудников[Строка.Сотрудник];
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Отключает учет по статьям финансирования, если используется.
// В ERP учет по статьям финансирования не предусмотрен.
Процедура НастроитьУчетПоСтатьямФинансирования() Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Константы.ИспользоватьСтатьиФинансированияЗарплата.Установить(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает заголовок "Способ отражения" элементу формы.
// Параметры:
//   Форма - Форма - Форма, элементу которой необходимо установить заголовок;
//   ИмяЭлемента - Строка - Имя элемента строкой, которому необходимо установить заголовок.
//
Процедура УстановитьЗаголовокСпособаОтражения(Форма, ИмяЭлемента) Экспорт
	
	ЭлементФормы = Форма.Элементы.Найти(ИмяЭлемента);
	
	Если ЭлементФормы <> Неопределено Тогда
		ЭлементФормы.Заголовок = НСтр("ru='Способ отражения';uk='Спосіб відображення'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РаспределитьПоПодразделениямВыработки(Организация, ПериодРегистрации, ТаблицаРезервов)
	
	СпособыОтражения = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаРезервов, "СпособОтраженияЗарплатыВБухучете", Истина);
	КоличествоЭлементов = СпособыОтражения.Количество();
	Для ОбратныйИндекс = 1 По КоличествоЭлементов Цикл
		Индекс = КоличествоЭлементов - ОбратныйИндекс;
		Если НЕ ЗначениеЗаполнено(СпособыОтражения[Индекс]) Тогда
			СпособыОтражения.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла;
	
	ОплатаСдельныхРабот = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СпособыОтражения, "ОплатаСдельныхРабот");
	
	СписокСотрудников = Новый Массив;
	СтрокиКРаспределению = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ТаблицаРезервов Цикл
		
		Если ОплатаСдельныхРабот[СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете] = Истина Тогда
			СписокСотрудников.Добавить(СтрокаТаблицы.Сотрудник);
			СтрокиКРаспределению.Добавить(СтрокаТаблицы);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокиКРаспределению.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода",      КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ФизическоеЛицо
	|ПОМЕСТИТЬ ВТСписокФизическихЛиц
	|ИЗ Справочник.Сотрудники КАК Сотрудники
	|ГДЕ Ссылка В (&СписокСотрудников)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТрудозатратыНЗП.Сотрудник КАК ФизическоеЛицо,
	|	ТрудозатратыНЗП.Подразделение КАК Подразделение,
	|	СУММА(ТрудозатратыНЗП.НормативнаяСтоимость) КАК Коэффициент
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНЗП
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизическихЛиц КАК СписокФизическихЛиц
	|		ПО ТрудозатратыНЗП.Сотрудник = СписокФизическихЛиц.ФизическоеЛицо
	|
	|ГДЕ
	|	ТрудозатратыНЗП.Регистратор ССЫЛКА Документ.ВыработкаСотрудников
	|	И ТрудозатратыНЗП.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТрудозатратыНЗП.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ТрудозатратыНЗП.Подразделение,
	|	ТрудозатратыНЗП.Сотрудник
	|ИТОГИ ПО
	|	ФизическоеЛицо";
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеВыработки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	РаспределяемыеКолонки = "СуммаРезерва,СуммаРезерваСтраховыхВзносов,СуммаРезерваФССНесчастныеСлучаи,
							|СуммаРезерваНУ,СуммаРезерваСтраховыхВзносовНУ,СуммаРезерваФССНесчастныеСлучаиНУ";
	
	ЗначенияРаспределения = Новый Структура(РаспределяемыеКолонки);
	РезультатыРаспределения = Новый Структура(РаспределяемыеКолонки);
	
	ФизическиеЛица = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокСотрудников, "ФизическоеЛицо");
	
	Для Каждого СтрокаТаблицы Из СтрокиКРаспределению Цикл
		
		ДанныеВыработки.Сбросить();
		Если НЕ ДанныеВыработки.НайтиСледующий(ФизическиеЛица[СтрокаТаблицы.Сотрудник], "ФизическоеЛицо") Тогда
			Продолжить;
		КонецЕсли;
		
		МассивПодразделений = Новый Массив;
		МассивКоэффициентов = Новый Массив;
		
		ПодразделенияВыработки = ДанныеВыработки.Выбрать();
		Пока ПодразделенияВыработки.Следующий() Цикл
			МассивПодразделений.Добавить(ПодразделенияВыработки.Подразделение);
			МассивКоэффициентов.Добавить(ПодразделенияВыработки.Коэффициент);
		КонецЦикла;
		
		Если МассивПодразделений.Количество() = 0 Тогда
			Продолжить;
		ИначеЕсли МассивПодразделений.Количество() = 1 Тогда
			СтрокаТаблицы.МестоВСтруктуреПредприятия = МассивПодразделений[0];
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРаспределения, СтрокаТаблицы);
		Для Каждого Колонка Из ЗначенияРаспределения Цикл
			РезультатыРаспределения[Колонка.Ключ] = 
				ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Колонка.Значение, МассивКоэффициентов);
		КонецЦикла;
		
		ИндексСтроки = ТаблицаРезервов.Индекс(СтрокаТаблицы);
		Для Индекс = 0 По МассивПодразделений.ВГраница() Цикл
			
			НоваяСтрока = ТаблицаРезервов.Вставить(ИндексСтроки);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.МестоВСтруктуреПредприятия = МассивПодразделений[Индекс];
			
			Для Каждого Колонка Из РезультатыРаспределения Цикл
				Если ЗначениеЗаполнено(ЗначенияРаспределения[Колонка.Ключ]) Тогда
					НоваяСтрока[Колонка.Ключ] = Колонка.Значение[Индекс];
				Иначе
					НоваяСтрока[Колонка.Ключ] = 0;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		ТаблицаРезервов.Удалить(СтрокаТаблицы);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьСтрокуТаблицыОценочныхОбязательств(СтрокаТаблицы, ТаблицаДокумента)
	
	Если СтрокаТаблицы.СуммаРезерва = 0
		И СтрокаТаблицы.СуммаРезерваНУ = 0
		И (СтрокаТаблицы.СуммаРезерва - СтрокаТаблицы.СуммаРезерваНУ) = 0
		И СтрокаТаблицы.СуммаРезерваСтраховыхВзносов = 0
		И СтрокаТаблицы.СуммаРезерваСтраховыхВзносовНУ = 0
		И (СтрокаТаблицы.СуммаРезерваСтраховыхВзносов - СтрокаТаблицы.СуммаРезерваСтраховыхВзносовНУ) = 0 
		И СтрокаТаблицы.СуммаРезерваФССНесчастныеСлучаи = 0
		И СтрокаТаблицы.СуммаРезерваФССНесчастныеСлучаиНУ = 0
		И (СтрокаТаблицы.СуммаРезерваФССНесчастныеСлучаи - СтрокаТаблицы.СуммаРезерваФССНесчастныеСлучаиНУ) = 0
		Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТаблицаДокумента.Добавить(), СтрокаТаблицы);
	
КонецПроцедуры

Функция БазаРаспределенияТрудозатрат()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.ПодразделениеПредприятия         КАК Подразделение,
	|	ДД.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	ДД.ФизическоеЛицо                   КАК Сотрудник,
	|	&Организация                        КАК Организация
	|ПОМЕСТИТЬ ВТСотрудникиСдельная
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК ДД
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И ДД.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И НЕ &Сводно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.ПодразделениеПредприятия,
	|	ДД.СпособОтраженияЗарплатыВБухучете,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	&Организация
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК ДД
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И ДД.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И &Сводно
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СпособОтражения,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковСотрудники.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	0 КАК КодСтрокиПродукция,
	|	НЕОПРЕДЕЛЕНО КАК Этап,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.ВидыРаботСотрудников.ПустаяСсылка) КАК ВидРабот,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
	|	ВТСотрудникиСдельная.СпособОтражения КАК СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты КАК СпособОтраженияПрочихРабот,
	|	СУММА(ВыработкаСотрудниковСотрудники.Сумма * ВыработкаСотрудниковВидыРабот.Сумма / ВыработкаСотрудников.СуммаДокумента
	|		* &КоэффициентПересчетаВВалютуУПР) КАК НормативнаяСтоимость,
	|	&Организация КАК Организация,
	|	""ПрочиеРасходыАктивыПассивы"" КАК ИдентификаторНабора,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
	|	0 КАК Количество,
	|	ВЫБОР ИСТИНА
	|		КОГДА ИСТИНА
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОбъектЭксплуатации,
	|	ЛОЖЬ КАК ДоступноПереопределениеСтатьиКалькуляции
	|ПОМЕСТИТЬ ВТТрудозатраты
	|ИЗ
	|	Документ.ВыработкаСотрудников КАК ВыработкаСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников.ВидыРабот КАК ВыработкаСотрудниковВидыРабот
	|			ПО ВыработкаСотрудников.Ссылка = ВыработкаСотрудниковВидыРабот.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников.Работники КАК ВыработкаСотрудниковСотрудники
	|			ПО ВыработкаСотрудниковВидыРабот.Ссылка = ВыработкаСотрудниковСотрудники.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСдельная КАК ВТСотрудникиСдельная
	|			ПО (&Сводно ИЛИ ВыработкаСотрудниковСотрудники.Работник = ВТСотрудникиСдельная.Сотрудник)
	|			И ВыработкаСотрудников.Подразделение = ВТСотрудникиСдельная.Подразделение
	|			И ВыработкаСотрудников.Организация = ВТСотрудникиСдельная.Организация
	|ГДЕ
	|	ВыработкаСотрудников.ВидНаряда В (ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.ПрочиеРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт))
	|	И ВыработкаСотрудников.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВыработкаСотрудников.Проведен
	|	И ВыработкаСотрудников.Организация = &Организация
	|	И ВыработкаСотрудников.Автораспределение
	|	И НЕ ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты.ОплатаСдельныхРабот
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковСотрудники.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВыработкаСотрудников.Ссылка,
	|	ВыработкаСотрудников.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковВидыРабот.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	0 КАК КодСтрокиПродукция,
	|	НЕОПРЕДЕЛЕНО КАК Этап,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.ВидыРаботСотрудников.ПустаяСсылка) КАК ВидРабот,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
	|	ВТСотрудникиСдельная.СпособОтражения КАК СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты КАК СпособОтраженияПрочихРабот,
	|	СУММА(ВыработкаСотрудниковВидыРабот.Сумма
	|		* &КоэффициентПересчетаВВалютуУПР) КАК НормативнаяСтоимость,
	|	&Организация КАК Организация,
	|	""ПрочиеРасходыАктивыПассивы"" КАК ИдентификаторНабора,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
	|	0 КАК Количество,
	|	ВЫБОР ИСТИНА
	|		КОГДА ИСТИНА
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОбъектЭксплуатации,
	|	ЛОЖЬ КАК ДоступноПереопределениеСтатьиКалькуляции
	|ИЗ
	|	Документ.ВыработкаСотрудников КАК ВыработкаСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыработкаСотрудников.ВидыРабот КАК ВыработкаСотрудниковВидыРабот
	|			ПО ВыработкаСотрудников.Ссылка = ВыработкаСотрудниковВидыРабот.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСдельная КАК ВТСотрудникиСдельная
	|		ПО (&Сводно ИЛИ ВыработкаСотрудниковВидыРабот.Работник = ВТСотрудникиСдельная.Сотрудник)
	|			И ВыработкаСотрудников.Подразделение = ВТСотрудникиСдельная.Подразделение
	|			И ВыработкаСотрудников.Организация = ВТСотрудникиСдельная.Организация
	|ГДЕ
	|	ВыработкаСотрудников.ВидНаряда В (ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.ПрочиеРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт))
	|	И ВыработкаСотрудников.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВыработкаСотрудников.Проведен
	|	И ВыработкаСотрудников.Организация = &Организация
	|	И НЕ ВыработкаСотрудников.Автораспределение
	|	И НЕ ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты.ОплатаСдельныхРабот
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВыработкаСотрудниковВидыРабот.СпособОтраженияЗарплаты,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ВыработкаСотрудниковВидыРабот.Работник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВыработкаСотрудников.Ссылка,
	|	ВыработкаСотрудников.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТрудозатратыНЗП.Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ТрудозатратыНЗП.Сотрудник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТрудозатратыНЗП.ПартияПроизводства,
	|	ТрудозатратыНЗП.ЗаказНаПроизводство,
	|	ВЫБОР
	|		КОГДА ИСТИНА ТОГДА
	|			ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|				ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТрудозатратыНЗП.КодСтрокиПродукция,
	|	ТрудозатратыНЗП.Этап,
	|	ТрудозатратыНЗП.СтатьяКалькуляции,
	|	ТрудозатратыНЗП.ВидРабот,
	|	ТрудозатратыНЗП.ГруппаПродукции,
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	СУММА(ТрудозатратыНЗП.НормативнаяСтоимость),
	|	ТрудозатратыНЗП.Организация,
	|	""ТрудозатратыНезавершенногоПроизводства"",
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка),
	|	ТрудозатратыНЗП.Количество,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА НЕ ТрудозатратыНЗП.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоступноПереопределениеСтатьиКалькуляции
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНЗП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСдельная КАК ВТСотрудникиСдельная
	|		ПО (&Сводно ИЛИ ТрудозатратыНЗП.Сотрудник = ВТСотрудникиСдельная.Сотрудник)
	|			И ТрудозатратыНЗП.Организация = ВТСотрудникиСдельная.Организация
	|			И ТрудозатратыНЗП.Подразделение = ВТСотрудникиСдельная.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ТрудозатратыНЗП.ПартияПроизводства
	|
	|ГДЕ
	|	ТрудозатратыНЗП.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ТрудозатратыНЗП.Организация В
	|			(ВЫБРАТЬ
	|				Т.Организация
	|			ИЗ
	|				ВТСотрудникиСдельная КАК Т)
	|	И ТрудозатратыНЗП.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ТрудозатратыНЗП.НормативнаяСтоимость > 0
	|	И ТрудозатратыНЗП.Регистратор Ссылка Документ.ВыработкаСотрудников
	|
	|СГРУППИРОВАТЬ ПО
	|	ТрудозатратыНЗП.Организация,
	|	ТрудозатратыНЗП.Подразделение,
	|	ВТСотрудникиСдельная.СпособОтражения,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|			ТОГДА ТрудозатратыНЗП.Сотрудник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТрудозатратыНЗП.ПартияПроизводства,
	|	ТрудозатратыНЗП.ЗаказНаПроизводство,
	|	ТрудозатратыНЗП.Этап,
	|	ТрудозатратыНЗП.СтатьяКалькуляции,
	|	ТрудозатратыНЗП.ВидРабот,
	|	ТрудозатратыНЗП.ГруппаПродукции,
	|	ТрудозатратыНЗП.КодСтрокиПродукция,
	|	ВЫБОР
	|		КОГДА ИСТИНА ТОГДА
	|			ЕСТЬNULL(СпрПартииПроизводства.НаправлениеДеятельности,
	|				ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ,
	|	ТрудозатратыНЗП.Количество,
	|	ВЫБОР
	|		КОГДА НЕ ТрудозатратыНЗП.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТрудозатраты.СпособОтражения КАК СпособОтражения
	|ПОМЕСТИТЬ ВтСписокСпособовОтражения
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТрудозатраты.СпособОтраженияПрочихРабот КАК СпособОтражения
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Реквизиты.БазаРаспределенияПоСдельнымРаботам = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
	|			ТОГДА ВТТрудозатраты.НормативнаяСтоимость
	|		КОГДА ВидыРабот.КратностьТрудоемкости > 0
	|			ТОГДА ВЫРАЗИТЬ(ВТТрудозатраты.Количество * ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости КАК ЧИСЛО(15,3))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Коэффициент,
	|	ВТТрудозатраты.Подразделение КАК Подразделение,
	|	ВТТрудозатраты.Сотрудник КАК Сотрудник,
	|	ВТТрудозатраты.ПартияПроизводства КАК ПартияПроизводства,
	|	ВТТрудозатраты.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ВТТрудозатраты.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТТрудозатраты.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	ВТТрудозатраты.Этап КАК Этап,
	|	ВТТрудозатраты.ВидРабот КАК ВидРабот,
	|	ВТТрудозатраты.ГруппаПродукции,
	|	ВТТрудозатраты.Организация КАК Организация,
	|	ВТТрудозатраты.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
	|	ВТТрудозатраты.ИдентификаторНабора КАК ИдентификаторНабора,
	|	ВТТрудозатраты.Количество,
	|	ВТТрудозатраты.СпособОтражения,
	|	ВТТрудозатраты.СпособОтраженияПрочихРабот,
	|	ВТТрудозатраты.ОбъектЭксплуатации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОтдельнаяСтатьяКалькуляции И ВТТрудозатраты.ДоступноПереопределениеСтатьиКалькуляции
	|			ТОГДА Реквизиты.СтатьяКалькуляции
	|		ИНАЧЕ ВТТрудозатраты.СтатьяКалькуляции
	|	КОНЕЦ КАК СтатьяКалькуляции,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОтдельнаяСтатьяКалькуляцииВзносов И ВТТрудозатраты.ДоступноПереопределениеСтатьиКалькуляции
	|			ТОГДА Реквизиты.СтатьяКалькуляцииВзносов
	|		ИНАЧЕ ВТТрудозатраты.СтатьяКалькуляции
	|	КОНЕЦ КАК СтатьяКалькуляцииВзносов
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Реквизиты
	|			ПО ВТТрудозатраты.СпособОтражения = Реквизиты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК ВидыРабот
	|			ПО ВТТрудозатраты.ВидРабот = ВидыРабот.Ссылка
	|
	|ГДЕ
	|	ВЫБОР
	|		КОГДА Реквизиты.БазаРаспределенияПоСдельнымРаботам = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
	|			ТОГДА ВТТрудозатраты.НормативнаяСтоимость
	|		КОГДА ВидыРабот.КратностьТрудоемкости > 0
	|			ТОГДА ВЫРАЗИТЬ(ВТТрудозатраты.Количество * ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости КАК ЧИСЛО(15,3))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начислено.НомерСтроки КАК НомерСтроки,
	|	Начислено.ФизическоеЛицо КАК Сотрудник,
	|	Начислено.ПодразделениеПредприятия КАК Подразделение,
	|	Начислено.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Начислено.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НатуральныйДоход)
	|			ТОГДА 0
	|		ИНАЧЕ Начислено.Сумма
	|	КОНЕЦ КАК Сумма,
	|	Начислено.ВзносыВсего КАК ВзносыВсего,
	|	Начислено.Налог КАК Налог,
	|	0 КАК Взносы,
	|	0 КАК СуммаНУ,
	|	0 КАК ВзносыНУ,
	|	0 КАК ФССНесчастныеСлучаиНУ,
	|	ЛОЖЬ КАК РаспределятьНУ,
	|	Начислено.ВидОперации
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК Начислено
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Реквизиты
	|			ПО Начислено.СпособОтраженияЗарплатыВБухучете = Реквизиты.Ссылка
	|ГДЕ
	|	Начислено.Ссылка = &Ссылка
	|	И Начислено.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И НЕ &Сводно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Начислено.НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	Начислено.ПодразделениеПредприятия,
	|	Начислено.СпособОтраженияЗарплатыВБухучете,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА Начислено.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НатуральныйДоход)
	|			ТОГДА 0
	|		ИНАЧЕ Начислено.Сумма
	|	КОНЕЦ КАК Сумма,
	|	Начислено.ВзносыВсего КАК ВзносыВсего,
	|	Начислено.Налог КАК Налог,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЛОЖЬ,
	|	Начислено.ВидОперации
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК Начислено
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Реквизиты
	|			ПО Начислено.СпособОтраженияЗарплатыВБухучете = Реквизиты.Ссылка
	|ГДЕ
	|	Начислено.Ссылка = &Ссылка
	|	И Начислено.СпособОтраженияЗарплатыВБухучете.ОплатаСдельныхРабот
	|	И &Сводно
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстЗапроса = ТекстЗапроса +
	";
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция НоваяТаблицаВыработкаСотрудников()
	
	Таблица = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ОтражениеЗарплатыВФинансовомУчете.Период,
		|	ОтражениеЗарплатыВФинансовомУчете.Регистратор,
		|	ОтражениеЗарплатыВФинансовомУчете.НомерСтроки,
		|	ОтражениеЗарплатыВФинансовомУчете.Активность,
		|	ОтражениеЗарплатыВФинансовомУчете.Организация,
		|	ОтражениеЗарплатыВФинансовомУчете.Подразделение,
		|	ОтражениеЗарплатыВФинансовомУчете.НаправлениеДеятельности,
		|	ОтражениеЗарплатыВФинансовомУчете.ВидОперацииПоЗарплате,
		|	ОтражениеЗарплатыВФинансовомУчете.СтатьяРасходов,
		|	ОтражениеЗарплатыВФинансовомУчете.АналитикаРасходов,
		|	ОтражениеЗарплатыВФинансовомУчете.ВидРабот,
		|	ОтражениеЗарплатыВФинансовомУчете.ФизическоеЛицо КАК Сотрудник,
		|	ОтражениеЗарплатыВФинансовомУчете.ГруппаПродукции,
		|	ОтражениеЗарплатыВФинансовомУчете.ТипНалога,
		|	ОтражениеЗарплатыВФинансовомУчете.Налог,
		|	ОтражениеЗарплатыВФинансовомУчете.АналитикаАктивовПассивов,
		|	ОтражениеЗарплатыВФинансовомУчете.СчетУчета,
		|	ОтражениеЗарплатыВФинансовомУчете.Субконто1,
		|	ОтражениеЗарплатыВФинансовомУчете.Субконто2,
		|	ОтражениеЗарплатыВФинансовомУчете.Субконто3,
		|	ОтражениеЗарплатыВФинансовомУчете.СуммаРегл,
		|	ОтражениеЗарплатыВФинансовомУчете.ПостояннаяРазница,
		|	ОтражениеЗарплатыВФинансовомУчете.ВременнаяРазница,
		|	0 КАК Сумма,
		|	НЗП.ЗаказНаПроизводство                               КАК ЗаказНаПроизводство,
		|	0                                                     КАК КодСтрокиПродукция,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)   КАК Этап,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)   КАК СтатьяКалькуляции,
		|	НЗП.ПартияПроизводства                                КАК ПартияПроизводства,
		|	ЛОЖЬ КАК ПринятиеКНалоговомуУчетуПоСдельнымРаботам,
		|	ОтражениеЗарплатыВФинансовомУчете.ВидПлатежаВГосБюджет КАК ВидПлатежаВГосБюджет
		|ИЗ
		|	РегистрНакопления.ОтражениеЗарплатыВФинансовомУчете КАК ОтражениеЗарплатыВФинансовомУчете
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК НЗП
		|		ПО ИСТИНА
		|";
	
	Результат = Запрос.Выполнить();
	
	Таблица = Результат.Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

Функция ПараметрыРаспределенияСтроки(ВидСуммы, СтрокаБазы, ДанныеСпособа)
	
	Параметры = Новый Структура("ИдентификаторНабора, ТипНалога, ВидФондаВзносов, ВидПлатежаВГосБюджет");
	
	Параметры.ТипНалога            = ВидСуммы.ТипНалога;
	Параметры.ВидФондаВзносов      = ВидСуммы.ВидФондаВзносов;
	Параметры.ВидПлатежаВГосБюджет = ВидСуммы.ВидПлатежаВГосБюджет;
	
	Если СтрокаБазы.ИдентификаторНабора = "ПрочиеРасходыАктивыПассивы" Тогда
		
		СтруктураОтбора = Новый Структура("СпособОтражения, ОблагаетсяЕНВД", СтрокаБазы.СпособОтраженияПрочихРабот, СтрокаБазы.ОблагаетсяЕНВД);
		ДанныеСпособа.НайтиСледующий(СтруктураОтбора);
		
		Параметры.Вставить("СтатьяРасходов", ДанныеСпособа["СтатьяРасходов" + ВидСуммы.Постфикс]);
		Параметры.Вставить("НаправлениеДеятельности", ДанныеСпособа["НаправлениеДеятельности"]);
		
		Если ТипЗнч(Параметры.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
			Параметры.ИдентификаторНабора = "ПрочиеРасходы";
			Параметры.Вставить("АналитикаРасходов",         ДанныеСпособа["АналитикаРасходов" + ВидСуммы.Постфикс]);
			
			ИмяАналитики = "АналитикаРасходов";
			
		Иначе
			Параметры.ИдентификаторНабора = "ПрочиеАктивыПассивы";
			Параметры.Вставить("АналитикаАктивовПассивов",  ДанныеСпособа["АналитикаАктивовПассивов" + ВидСуммы.Постфикс]);
			Параметры.Вставить("СчетУчета",                 ДанныеСпособа["СчетУчета" + ВидСуммы.Постфикс]);
			Параметры.Вставить("Субконто1",                 ДанныеСпособа["Субконто1" + ВидСуммы.Постфикс]);
			Параметры.Вставить("Субконто2",                 ДанныеСпособа["Субконто2" + ВидСуммы.Постфикс]);
			Параметры.Вставить("Субконто3",                 ДанныеСпособа["Субконто3" + ВидСуммы.Постфикс]);
			
			ИмяАналитики = "АналитикаАктивовПассивов";
			
			// автоматический подбор Субконто
			Для НомерСубконто = 1 По 3 Цикл
				
				ИмяСубконто = "Субконто" + Строка(НомерСубконто);
				
				Если Не ЗначениеЗаполнено(Параметры[ИмяСубконто]) Тогда
					Если ТипЗнч(Параметры[ИмяСубконто]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
						Параметры[ИмяСубконто] = СтрокаБазы.Сотрудник;
					ИначеЕсли ТипЗнч(Параметры[ИмяСубконто]) = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
						Параметры[ИмяСубконто] = СтрокаБазы.ОбъектЭксплуатации;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		// автоматический подбор аналитики
		Если Не ЗначениеЗаполнено(Параметры[ИмяАналитики]) Тогда
			
			Если ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.Организации") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.Организация;
			ИначеЕсли ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.Сотрудник;
			ИначеЕсли ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.Подразделение;
			ИначеЕсли ТипЗнч(Параметры[ИмяАналитики]) = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
				Параметры[ИмяАналитики] = СтрокаБазы.ОбъектЭксплуатации;
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеСпособа.Сбросить();
		
	Иначе
		
		Если ЗначениеЗаполнено(ВидСуммы.ВидФондаВзносов) Тогда
			Параметры.Вставить("СтатьяКалькуляции", СтрокаБазы.СтатьяКалькуляцииВзносов);
		Иначе
			Параметры.Вставить("СтатьяКалькуляции", СтрокаБазы.СтатьяКалькуляции);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

Функция ВидыСуммВзносов()
	
	СписокВидовСумм = Новый Массив;

		СписокВидовСумм = Новый Массив;
	
	// начисление
	СтруктураСуммы = Новый Структура("Представление, Постфикс, ТипНалога",
		"Сумма", "", Перечисления.ТипыНалогов.ПустаяСсылка());
	СписокВидовСумм.Добавить(СтруктураСуммы);
	
	// Взносы
	СтруктураСуммы = Новый Структура("Представление, Постфикс, ТипНалога",
		"ВзносыВсего", "Взносов", Перечисления.ТипыНалогов.НачисленныйЕСВ);
	СписокВидовСумм.Добавить(СтруктураСуммы);
	
	Возврат СписокВидовСумм;
	
КонецФункции

Функция СформироватьСтруктуруВидаСуммыВзноса(ВидФондаВзносов, ТипНалога, Представление, ВидПлатежаВГосБюджет, Постфикс = "Взносов")
	
	ВидСуммыВзносов = Новый Структура();
	ВидСуммыВзносов.Вставить("ВидФондаВзносов",      ВидФондаВзносов);
	ВидСуммыВзносов.Вставить("ТипНалога",            ТипНалога);
	ВидСуммыВзносов.Вставить("Представление",        Представление);
	ВидСуммыВзносов.Вставить("Постфикс",             Постфикс);
	ВидСуммыВзносов.Вставить("ВидПлатежаВГосБюджет", ВидПлатежаВГосБюджет);
	
	Возврат ВидСуммыВзносов;
	
КонецФункции

Функция ИспользоватьДокументОтражениеЗарплатыДляСписанияРезервовОтпусков(ПериодРегистрации) Экспорт
	Возврат ПериодРегистрации > Константы.ДатаНачалаСписанияРезервовОтпусковДокументомОтражениеЗарплаты.Получить();
КонецФункции

#КонецОбласти