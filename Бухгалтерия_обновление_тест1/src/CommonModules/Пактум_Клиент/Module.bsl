// Код частично предоставлен разработчиками сервиса Пактум.Контрагент
// Сервисное обслуживание сервиса Пактум.Контрагент осуществляется 
// линией консультаций разработчиков:
// • e-mail pactum.contragent@gmail.com
// • тел. + 380 (67) 430-04-50.


// Инициирует структуру данных контрагента для сравнения с данными полученных с сервиса.
//
// Возвращаемое значение:
//   Булево   - инициированная значениями по умолчанию структура.
//
Функция ИнициироватьСтруктураДанныхДляСравнения() Экспорт

	ДанныеДляСравнения = Новый Структура;
	ДанныеДляСравнения.Вставить("КодПоЕДРПОУ", "");
	ДанныеДляСравнения.Вставить("ИННПлательщикаНДС", "");
    ДанныеДляСравнения.Вставить("ПлательщикНДС", Ложь);
	ДанныеДляСравнения.Вставить("Наименование", "");
	ДанныеДляСравнения.Вставить("НаименованиеПолное", "");
	ДанныеДляСравнения.Вставить("ЮридическийАдрес", "");
    ДанныеДляСравнения.Вставить("ДополнительнаяИнформация", "");
	ДанныеДляСравнения.Вставить("Телефон", "");
    ДанныеДляСравнения.Вставить("АдресЭП", "");
	
	Возврат ДанныеДляСравнения;
	
КонецФункции

// Сравнивает имеющиеся значения реквизитов контрагента с данными, полученными с сервиса.
//
// Параметры:
//  РеквизитыКонтрагента  - Структура - содержит данные, полученные с сервиса.
//  ДанныеОбъекта         - Структура - содержит данные контрагента.
//
// Возвращаемое значение:
//   Булево   - Истина, если есть различия, Ложь в обратном случае.
//
Функция РезультатСравненияПолученныхДанныхКонтрагентаСИмеющимися(РеквизитыКонтрагента, ДанныеОбъекта, ЗаполнятьЕДРПОУ) Экспорт
	
	Результат = Новый Структура("ЕстьИзменения, ЕстьИзмененияВЗаполненныхРеквизитах", Ложь, Ложь);
	
	Если РеквизитыКонтрагента.Свойство("КодПоЕДРПОУ") И РеквизитыКонтрагента.КодПоЕДРПОУ <> ДанныеОбъекта.КодПоЕДРПОУ Тогда
		Результат.ЕстьИзменения = Истина;
		Если ЗначениеЗаполнено(ДанныеОбъекта.КодПоЕДРПОУ) Тогда
			Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыКонтрагента.Свойство("ИННПлательщикаНДС") И РеквизитыКонтрагента.ИННПлательщикаНДС <> ДанныеОбъекта.ИННПлательщикаНДС Тогда
		Результат.ЕстьИзменения = Истина;
		Если ЗначениеЗаполнено(ДанныеОбъекта.ИННПлательщикаНДС) Тогда
			Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
		КонецЕсли;
		
		Возврат Результат;
    КонецЕсли;
    
    
	Если РеквизитыКонтрагента.Свойство("ПлательщикНДС") И РеквизитыКонтрагента.ПлательщикНДС <> ДанныеОбъекта.ПлательщикНДС Тогда
		Результат.ЕстьИзменения = Истина;
		
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыКонтрагента.Свойство("Наименование") 
		И РеквизитыКонтрагента.Наименование <> ДанныеОбъекта.Наименование Тогда
		Результат.ЕстьИзменения = Истина;
		Если ЗначениеЗаполнено(ДанныеОбъекта.Наименование) Тогда
			Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыКонтрагента.Свойство("НаименованиеПолное") 
		И РеквизитыКонтрагента.НаименованиеПолное <> ДанныеОбъекта.НаименованиеПолное Тогда
		Результат.ЕстьИзменения = Истина;
		Если ЗначениеЗаполнено(ДанныеОбъекта.НаименованиеПолное) Тогда
			Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыКонтрагента.Свойство("Телефон") 
		И РеквизитыКонтрагента.Телефон <> Неопределено Тогда
		Если РеквизитыКонтрагента.Телефон.Представление <> ДанныеОбъекта.Телефон Тогда
			
			Результат.ЕстьИзменения = Истина;
			Если ЗначениеЗаполнено(ДанныеОбъекта.Телефон) Тогда
				Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
			КонецЕсли;
			
			Возврат Результат;
		КонецЕсли;
    КонецЕсли;
    
	Если РеквизитыКонтрагента.Свойство("АдресЭП") 
		И РеквизитыКонтрагента.АдресЭП <> Неопределено Тогда
		Если РеквизитыКонтрагента.АдресЭП.Представление <> ДанныеОбъекта.АдресЭП Тогда
			
			Результат.ЕстьИзменения = Истина;
			Если ЗначениеЗаполнено(ДанныеОбъекта.АдресЭП) Тогда
				Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
			КонецЕсли;
			
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Если РеквизитыКонтрагента.Свойство("ЮридическийАдрес")
		И РеквизитыКонтрагента.ЮридическийАдрес <> Неопределено Тогда
		Если РеквизитыКонтрагента.ЮридическийАдрес.Представление <> ДанныеОбъекта.ЮридическийАдрес Тогда
			
			Результат.ЕстьИзменения = Истина;
			Если ЗначениеЗаполнено(ДанныеОбъекта.ЮридическийАдрес) Тогда
				Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
			КонецЕсли;
			
			Возврат Результат;
		КонецЕсли;
    КонецЕсли;
    
	Если РеквизитыКонтрагента.Свойство("ДополнительнаяИнформация")
		И РеквизитыКонтрагента.ДополнительнаяИнформация <> Неопределено Тогда
		Если РеквизитыКонтрагента.ДополнительнаяИнформация <> ДанныеОбъекта.ДополнительнаяИнформация Тогда
			
			Результат.ЕстьИзменения = Истина;
			Если ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
				Результат.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
			КонецЕсли;
			
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
    
	
	Возврат Результат;
	
КонецФункции

// Формирует текст ошибки и показывает пользователю предупреждение на основании данных
// предоставленных подсистемой Пактум.Контрагент
// Параметры:
//  ОписаниеОшибки - Строка - описание ошибки, предоставленное подсистемой Пактум.Контрагент
//
Процедура ПоказатьПользователюОшибкуПолученияРеквизитовКонтрагентаПоЕДРПОУ(ОписаниеОшибки) Экспорт
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		ТекстПредупреждения = НСтр("ru='При получении данных из сервиса Пактум.Контрагент произошла неизвестная ошибка.';uk='При отриманні даних з сервісу Пактум.Контрагент сталася невідома помилка.'");
	Иначе
		ТекстПредупреждения = ОписаниеОшибки;
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

// Показывает пользователю предупреждение на основании данных предоставленных подсистемой Пактум.Контрагент
// Параметры:
//  ОписаниеПредупреждения  - Строка - описание предупреждения, предоставленное подсистемой Пактум.Контрагент
//
Процедура ПоказатьПользователюПредупреждениеПолученияРеквизитовКонтрагентаПоЕДРПОУ(ОписаниеПредупреждения) Экспорт
    
    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеПредупреждения);
	
КонецПроцедуры

Процедура ЗаблокироватьФорму(пФорма) Экспорт
    пФорма.Элементы.ЗаполнитьПоЕГРПОУ.Видимость = Ложь;
    пФорма.Элементы.ДекорацияЗаполнитьПоЕГРПОУ.Видимость = Истина;
	пФорма.ТолькоПросмотр = Истина;
КонецПроцедуры

Процедура РазблокироватьФорму(пФорма) Экспорт
    пФорма.Элементы.ЗаполнитьПоЕГРПОУ.Видимость = Истина;
    пФорма.Элементы.ДекорацияЗаполнитьПоЕГРПОУ.Видимость = Ложь;
	пФорма.ТолькоПросмотр = Ложь;
КонецПроцедуры

Процедура Лого_Нажатие() Экспорт
	ЗапуститьПриложение("http://pactumsys.com");
КонецПроцедуры

#Область ОбработчикиКомандФормы_Контрагенты_Служебные

Процедура ЗаполнитьПоЕГРПОУ(Команда, Форма) Экспорт
    
	Если Не Пактум_Сервер.Пактум_Права() Тогда
		Оповестить("Пактум.СозданиеКарточки.Завершено");
		Возврат;
    КонецЕсли;
    
    Если НЕ Пактум_Сервер.НастройкиПодключенияКСервисуПактумЗаданы() Тогда
        Оповестить("Пактум.СозданиеКарточки.Завершено");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не заданы настройки подключения к сервису Пактум.Контрагент!';uk='Не задані настройки підключення до сервісу Пактум.Контрагент!'")
        );
        Возврат;
    КонецЕсли; 
    
    РеквизитыКонтрагента = ПартнерыИКонтрагентыЛокализацияКлиентСервер.РеквизитыОбъектаКонтрагента(Форма);
    
    КодПоЕДРПОУ = СокрЛП(РеквизитыКонтрагента.КодПоЕДРПОУ);
	Если КодПоЕДРПОУ = "" Тогда
        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
            НСтр("ru='Не заполнен код ЕГРПОУ';uk='Не заповнено код ЄДРПОУ'"),
            ,
            РеквизитыКонтрагента.ПутьКодПоЕДРПОУ
        );        
		Оповестить("Пактум.СозданиеКарточки.Завершено");
		Возврат;
    КонецЕсли;
    
	СуществующиеКонтрагенты = Пактум_Сервер.ПолучитьКонтрагента(КодПоЕДРПОУ);
	Если СуществующиеКонтрагенты.Кво > 0 Тогда
        ТекстВопроса = НСтр("ru='Контрагент(ы) с таким же ЕГРПОУ найден(ы) в базе. Открыть существующие карточки или заполнить данные в текущей карточке?';uk= 'Контрагента знайдено в базі. Відкрити існуючі картки чи заповнити дані відкритої картки?'");
        спКнопки=Новый СписокЗначений;
        спКнопки.Добавить(НСтр("ru='Открыть';uk='Відкрити'"));
        спКнопки.Добавить(НСтр("ru='Заполнить';uk='Заповнити'"));
        спКнопки.Добавить(НСтр("ru='Отменить';uk= 'Відмінити'"));
		ОписаниеОповещенияВопросНаОткрытиеКарточки = Новый ОписаниеОповещения(
        	"ВопросНаОткрытиеКарточки",
			ЭтотОбъект,
            Новый Структура("СуществующиеКонтрагенты, Форма", СуществующиеКонтрагенты, Форма)
		);
        ПоказатьВопрос(ОписаниеОповещенияВопросНаОткрытиеКарточки, ТекстВопроса, спКнопки);
	Иначе
		ВыполнитьЗаполнениеКарточки(Форма);
    КонецЕсли;
    
КонецПроцедуры

Процедура ВопросНаОткрытиеКарточки(Ответ, ДополнительныеПараметры) Экспорт
    Если Ответ = НСтр("ru='Открыть';uk='Відкрити'") Тогда
        Для Каждого Эл Из ДополнительныеПараметры.СуществующиеКонтрагенты Цикл
    		ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", Новый Структура("Ключ", Эл.Значение));
		КонецЦикла;
	ИначеЕсли Ответ = НСтр("ru='Заполнить';uk='Заповнити'") Тогда
		ВыполнитьЗаполнениеКарточки(ДополнительныеПараметры.Форма);
	Иначе
		Возврат;
    КонецЕсли;
КонецПроцедуры

Процедура ВыполнитьЗаполнениеКарточки(Форма)
    
    РеквизитыКонтрагента = ПартнерыИКонтрагентыЛокализацияКлиентСервер.РеквизитыОбъектаКонтрагента(Форма);
    
    КодПоЕДРПОУ = СокрЛП(РеквизитыКонтрагента.КодПоЕДРПОУ);
    
	СтруПараметры = Пактум_Сервер.ПроверкаКорректностиЕДРПОУ_Пактум(КодПоЕДРПОУ);
	Если Не СтруПараметры.КорректностьЕДРПОУ_Значение Тогда
        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
            СтруПараметры.КорректностьЕДРПОУ_ТекстОшибки,
            ,
            РеквизитыКонтрагента.ПутьКодПоЕДРПОУ
        );        
		Оповестить("Пактум.СозданиеКарточки.Завершено");
		Возврат;
    КонецЕсли;
    
    Пактум_Токен = ?(СтруПараметры.Токен=Неопределено, "", СтруПараметры.Токен);
    
    
    РезультатЗапуска = Пактум_Сервер.ФоновоеЗаданиеДанныеПартнераПоКодПоЕДРПОУЗапустить(
        КодПоЕДРПОУ,
        Пактум_Токен,
        Форма.УникальныйИдентификатор,
        Форма.ФоновоеЗаданиеИдентификатор
    );
    
    Форма.ФоновоеЗаданиеИдентификатор  = РезультатЗапуска.ИдентификаторЗадания;
    Форма.ФоновоеЗаданиеАдресХранилища = РезультатЗапуска.АдресХранилища;
    
    Если РезультатЗапуска.ЗаданиеЗапущено Тогда
        Форма.Пактум_КоличествоПроверок_ФоновогоЗадания = 0;
        ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(Форма.ПараметрыОбработчика);
        Форма.ПараметрыОбработчика.ТекущийИнтервал = 10;
        Форма.ПодключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте", 10, Истина);
        Пактум_Клиент.ЗаблокироватьФорму(Форма);
    Иначе
        ВыполнитьЗаполнениеРеквизитовПоЕДРПОУ(Форма, РезультатЗапуска.РеквизитыКонтрагента);
    КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьЗаполнениеРеквизитовПоЕДРПОУ(Форма, РеквизитыКонтрагента, ЗаполнятьЕДРПОУ = Ложь) Экспорт
	
	Если РеквизитыКонтрагента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
		Пактум_Клиент.ПоказатьПользователюОшибкуПолученияРеквизитовКонтрагентаПоЕДРПОУ(РеквизитыКонтрагента.ОписаниеОшибки);
        Возврат;
	ИначеЕсли ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеПредупреждения) Тогда
		Пактум_Клиент.ПоказатьПользователюПредупреждениеПолученияРеквизитовКонтрагентаПоЕДРПОУ(РеквизитыКонтрагента.ОписаниеПредупреждения);
    КонецЕсли;    
        
    РезультатСравнения = Пактум_Клиент.РезультатСравненияПолученныхДанныхКонтрагентаСИмеющимися(
        РеквизитыКонтрагента,
        ДанныеОбъектаДляСравнения(Форма),
        ЗаполнятьЕДРПОУ
    );
    
    Если РезультатСравнения.ЕстьИзменения Тогда
        
        Если РезультатСравнения.ЕстьИзмененияВЗаполненныхРеквизитах Тогда 
            
            ОписаниеОповещенияПерезаполнить = Новый ОписаниеОповещения(
                "ЗаполнениеРеквизитовКонтрагентаПоЕДРПОУЗавершение",
                ЭтотОбъект,
                Новый Структура("РеквизитыКонтрагента, Форма", РеквизитыКонтрагента, Форма)
            );
            ТекстВопроса = НСтр("ru='Перезаполнить реквизиты контрагента на основании данных сервиса Пактум.Контрагент?';uk='Перезаполнить реквізити контрагента на підставі даних сервісу Пактум.Контрагент?'");
            ПоказатьВопрос(ОписаниеОповещенияПерезаполнить, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
        
        Иначе
            
            ЗаполнитьРеквизитыКонтрагентаПоПолученнымДанным(РеквизитыКонтрагента, Форма);
            
        КонецЕсли;
        
    КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнениеРеквизитовКонтрагентаПоЕДРПОУЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьРеквизитыКонтрагентаПоПолученнымДанным(ДополнительныеПараметры.РеквизитыКонтрагента, ДополнительныеПараметры.Форма);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыКонтрагентаПоПолученнымДанным(РеквизитыКонтрагента, Форма)
    
	Элементы = Форма.Элементы;
	РеквизитыФормыКонтрагента = ПартнерыИКонтрагентыЛокализацияКлиентСервер.РеквизитыОбъектаКонтрагента(Форма);
    
    Если РеквизитыФормыКонтрагента.ЭтоФормаПомощника Тогда
        
		Форма.ПолноеНаименование      = РеквизитыКонтрагента.Наименование;
		Форма.НаименованиеКонтрагента = РеквизитыКонтрагента.Наименование;
		Если РеквизитыКонтрагента.Свойство("КодПоЕДРПОУ") Тогда
			Форма.КодПоЕДРПОУ = РеквизитыКонтрагента.КодПоЕДРПОУ;
		КонецЕсли;
		Если РеквизитыКонтрагента.Свойство("ИННПлательщикаНДС") Тогда
			Форма.ИННПлательщикаНДС = РеквизитыКонтрагента.ИННПлательщикаНДС;
        КонецЕсли;
		Если РеквизитыКонтрагента.Свойство("НомерСвидетельстваПлательщикаНДС") Тогда
			Форма.НомерСвидетельстваПлательщикаНДС = РеквизитыКонтрагента.НомерСвидетельстваПлательщикаНДС;
        КонецЕсли;
		Если РеквизитыКонтрагента.Свойство("ПлательщикНДС") Тогда
			Форма.ПлательщикНДС = РеквизитыКонтрагента.ПлательщикНДС;
        КонецЕсли;
		
		Форма.ПолноеЮридическоеНаименование = РеквизитыКонтрагента.НаименованиеПолное;
        
		Если РеквизитыКонтрагента.Свойство("Телефон") И РеквизитыКонтрагента.Телефон <> Неопределено Тогда
			Форма.ТелефонПартнера              = РеквизитыКонтрагента.Телефон.Представление;
			Форма.ТелефонПартнераЗначенияПолей = РеквизитыКонтрагента.Телефон.КонтактнаяИнформация;
			
			Если Не Форма.ИспользуютсяТолькоПартнеры Тогда
				Форма.ТелефонКонтрагента              = Форма.ТелефонПартнера;
	            Форма.ТелефонКонтрагентаЗначенияПолей = Форма.ТелефонПартнераЗначенияПолей;
			КонецЕсли;
				
		КонецЕсли;
		
		Если РеквизитыКонтрагента.Свойство("АдресЭП") И РеквизитыКонтрагента.АдресЭП <> Неопределено Тогда
			Форма.АдресЭППартнера              = РеквизитыКонтрагента.АдресЭП.Представление;				
		КонецЕсли;

		Если РеквизитыКонтрагента.Свойство("ЮридическийАдрес") И РеквизитыКонтрагента.ЮридическийАдрес <> Неопределено Тогда
			Форма.ЮридическийАдрес              = РеквизитыКонтрагента.ЮридическийАдрес.Представление;
			Форма.ЮридическийАдресЗначенияПолей = РеквизитыКонтрагента.ЮридическийАдрес.КонтактнаяИнформация;
			ПартнерыИКонтрагентыКлиент.ЗаполнитьИсториюАдресовВФорме(Форма, РеквизитыКонтрагента);
        КонецЕсли;
        
		Если РеквизитыКонтрагента.Свойство("ДополнительнаяИнформация") И РеквизитыКонтрагента.ДополнительнаяИнформация <> Неопределено Тогда
			Форма.ДополнительнаяИнформация      = Форма.ДополнительнаяИнформация + РеквизитыКонтрагента.ДополнительнаяИнформация;
        КонецЕсли;
        
        Если РеквизитыКонтрагента.Свойство("Руководитель") И РеквизитыКонтрагента.Руководитель <> Неопределено Тогда
            Форма.УказатьДанныеКонтактногоЛица = Истина;
            Форма.ФамилияКЛ  = РеквизитыКонтрагента.Руководитель.Фамилия;
            Форма.ИмяКЛ      = РеквизитыКонтрагента.Руководитель.Имя;
            Форма.ОтчествоКЛ = РеквизитыКонтрагента.Руководитель.Отчество;
            Форма.Роль       = ПредопределенноеЗначение("Справочник.РолиКонтактныхЛицПартнеров.Руководитель");
            Если РеквизитыКонтрагента.Свойство("Телефон") И РеквизитыКонтрагента.Телефон <> Неопределено Тогда
                Форма.ТелефонКЛ              = РеквизитыКонтрагента.Телефон.Представление;
                Форма.ТелефонКЛЗначенияПолей = РеквизитыКонтрагента.Телефон.КонтактнаяИнформация;
            КонецЕсли;
        КонецЕсли;
			
		ПартнерыИКонтрагентыЛокализацияКлиентСервер.УправлениеДоступностьюКонтрагент(Форма);
        
    Иначе
        
        Объект = Форма.Объект;
        Объект.Наименование = РеквизитыКонтрагента.Наименование;
        
        Если РеквизитыКонтрагента.Свойство("КодПоЕДРПОУ") Тогда
            Если РеквизитыФормыКонтрагента.ЭтоФормаПартнера Тогда
	            Форма.КодПоЕДРПОУ = РеквизитыКонтрагента.КодПоЕДРПОУ;
            Иначе
                Объект.КодПоЕДРПОУ = РеквизитыКонтрагента.КодПоЕДРПОУ;
	        КонецЕсли;        
	    КонецЕсли;
        
		Если РеквизитыКонтрагента.Свойство("ИННПлательщикаНДС") Тогда
			Если РеквизитыКонтрагента.ИННПлательщикаНДС <> РеквизитыФормыКонтрагента.ИННПлательщикаНДС Тогда
				Если РеквизитыФормыКонтрагента.ЭтоФормаПартнера Тогда
					Форма.ИННПлательщикаНДС = РеквизитыКонтрагента.ИННПлательщикаНДС;
				Иначе
					Объект.ИННПлательщикаНДС = РеквизитыКонтрагента.ИННПлательщикаНДС;
				КонецЕсли;
			КонецЕсли;
        КонецЕсли;
        
		Если РеквизитыКонтрагента.Свойство("ПлательщикНДС") Тогда
			Если РеквизитыКонтрагента.ПлательщикНДС <> РеквизитыФормыКонтрагента.ПлательщикНДС Тогда
				Если РеквизитыФормыКонтрагента.ЭтоФормаПартнера Тогда
					Форма.ПлательщикНДС = РеквизитыКонтрагента.ПлательщикНДС;
				Иначе
					Объект.ПлательщикНДС = РеквизитыКонтрагента.ПлательщикНДС;
				КонецЕсли;
			КонецЕсли;
        КонецЕсли;
        
        Если РеквизитыКонтрагента.Свойство("НомерСвидетельстваПлательщикаНДС") Тогда
            Если РеквизитыФормыКонтрагента.ЭтоФормаПартнера Тогда
	            Форма.НомерСвидетельстваПлательщикаНДС = РеквизитыКонтрагента.НомерСвидетельстваПлательщикаНДС;
            Иначе
                Объект.НомерСвидетельстваПлательщикаНДС = РеквизитыКонтрагента.НомерСвидетельстваПлательщикаНДС;
	        КонецЕсли;        
	    КонецЕсли;
        
        Если Объект.НаименованиеПолное <> РеквизитыКонтрагента.НаименованиеПолное Тогда
			Объект.НаименованиеПолное = РеквизитыКонтрагента.НаименованиеПолное;
		КонецЕсли;
        
	    Если РеквизитыКонтрагента.Свойство("Телефон") Тогда
	    	ПартнерыИКонтрагентыКлиент.ЗаполнитьЭлементКонтактнойИнформации(
	            Форма,
	    	    ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента"),
	    	    РеквизитыКонтрагента.Телефон
	        );
	    КонецЕсли;
	    
	    Если РеквизитыКонтрагента.Свойство("АдресЭП") Тогда
	    	ПартнерыИКонтрагентыКлиент.ЗаполнитьЭлементКонтактнойИнформации(
	            Форма,
	    	    ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.EmailКонтрагента"),
	    	    РеквизитыКонтрагента.АдресЭП
	        );
	    КонецЕсли;
	    
	    Если РеквизитыКонтрагента.Свойство("ЮридическийАдрес") Тогда
	    	ПартнерыИКонтрагентыКлиент.ЗаполнитьЭлементКонтактнойИнформации(
	            Форма,
	    	    ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента"),
	    	    РеквизитыКонтрагента.ЮридическийАдрес
	        );
	    КонецЕсли;
	    
        Если РеквизитыКонтрагента.Свойство("ДополнительнаяИнформация") И РеквизитыКонтрагента.ДополнительнаяИнформация <> Неопределено Тогда
            Если РеквизитыФормыКонтрагента.ЭтоФормаПартнера Тогда
	            Форма.ДополнительнаяИнформация = Форма.ДополнительнаяИнформация + РеквизитыКонтрагента.ДополнительнаяИнформация;
            Иначе
                Объект.ДополнительнаяИнформация = Объект.ДополнительнаяИнформация + РеквизитыКонтрагента.ДополнительнаяИнформация;
            КонецЕсли;    
	    КонецЕсли;
	    
	    //Если РеквизитыКонтрагента.Свойство("Руководитель") И РеквизитыКонтрагента.Руководитель <> Неопределено Тогда
	    //    УказатьДанныеКонтактногоЛица = Истина;
	    //    ФамилияКЛ  = РеквизитыКонтрагента.Руководитель.Фамилия;
	    //    ИмяКЛ      = РеквизитыКонтрагента.Руководитель.Имя;
	    //    ОтчествоКЛ = РеквизитыКонтрагента.Руководитель.Отчество;
	    //    Роль       = ПредопределенноеЗначение("Справочник.РолиКонтактныхЛицПартнеров.Руководитель");
	    //    Если РеквизитыКонтрагента.Свойство("Телефон") И РеквизитыКонтрагента.Телефон <> Неопределено Тогда
	    //        // отдельного телефона КЛ нет, присваиваем телефон контрагента
	    //        ТелефонКЛ              = РеквизитыКонтрагента.Телефон.Представление;
	    //        ТелефонКЛЗначенияПолей = РеквизитыКонтрагента.Телефон.КонтактнаяИнформация;
	    //    КонецЕсли;
	    //КонецЕсли;
        
        ПартнерыИКонтрагентыЛокализацияКлиентСервер.УправлениеДоступностьюКонтрагент(Форма);
        
    КонецЕсли;
	
	Форма.Модифицированность = Истина;

КонецПроцедуры

Функция ДанныеОбъектаДляСравнения(Форма)

    Элементы = Форма.Элементы;
    РеквизитыКонтрагента = ПартнерыИКонтрагентыЛокализацияКлиентСервер.РеквизитыОбъектаКонтрагента(Форма);
    
	ДанныеДляСравнения = Пактум_Клиент.ИнициироватьСтруктураДанныхДляСравнения();
    
    Если РеквизитыКонтрагента.ЭтоФормаПомощника Тогда
        
        ДанныеДляСравнения.КодПоЕДРПОУ              = Форма.КодПоЕДРПОУ;
        ДанныеДляСравнения.Наименование             = Форма.ПолноеНаименование;
        ДанныеДляСравнения.ИННПлательщикаНДС        = Форма.ИННПлательщикаНДС;
        ДанныеДляСравнения.ПлательщикНДС            = Форма.ПлательщикНДС;
        ДанныеДляСравнения.НаименованиеПолное       = Форма.ПолноеЮридическоеНаименование;
        ДанныеДляСравнения.Телефон                  = Форма.ТелефонПартнера;
        ДанныеДляСравнения.АдресЭП                  = Форма.АдресЭППартнера;
        ДанныеДляСравнения.ЮридическийАдрес         = Форма.ЮридическийАдрес;
        ДанныеДляСравнения.ДополнительнаяИнформация = Форма.ДополнительнаяИнформация;
        
    Иначе    
        Объект = Форма.Объект;
        
        ДанныеДляСравнения.Наименование             = Объект.Наименование;
        ДанныеДляСравнения.НаименованиеПолное       = Объект.НаименованиеПолное;
	    Если РеквизитыКонтрагента.ЭтоФормаПартнера Тогда
			ДанныеДляСравнения.КодПоЕДРПОУ              = РеквизитыКонтрагента.КодПоЕДРПОУ;
		Иначе 
        	ДанныеДляСравнения.КодПоЕДРПОУ              = Объект.КодПоЕДРПОУ;
		КонецЕсли;
        ДанныеДляСравнения.ИННПлательщикаНДС        = РеквизитыКонтрагента.ИННПлательщикаНДС;
        ДанныеДляСравнения.ПлательщикНДС            = РеквизитыКонтрагента.ПлательщикНДС;
        ДанныеДляСравнения.ДополнительнаяИнформация = Объект.ДополнительнаяИнформация;
	    
	    ДанныеКонтактнойИнформации = ПартнерыИКонтрагентыКлиент.ДанныеСтрокиКонтактнойИнформацииПоВиду(
	        Форма,
	        ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента")
	    );
	    Если ДанныеКонтактнойИнформации <> Неопределено Тогда
	    	ДанныеДляСравнения.Телефон = ДанныеКонтактнойИнформации.Представление;
	    КонецЕсли;
	    
	    ДанныеКонтактнойИнформации = ПартнерыИКонтрагентыКлиент.ДанныеСтрокиКонтактнойИнформацииПоВиду(
	        Форма,
	        ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.EmailКонтрагента")
	    );
	    Если ДанныеКонтактнойИнформации <> Неопределено Тогда
	    	ДанныеДляСравнения.АдресЭП = ДанныеКонтактнойИнформации.Представление;
	    КонецЕсли;
	    
	    ДанныеКонтактнойИнформации = ПартнерыИКонтрагентыКлиент.ДанныеСтрокиКонтактнойИнформацииПоВиду(
	        Форма,
	        ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента")
	    );
	    Если ДанныеКонтактнойИнформации <> Неопределено Тогда
	    	ДанныеДляСравнения.ЮридическийАдрес = ДанныеКонтактнойИнформации.Представление;
        КонецЕсли;
        
    КонецЕсли;    
	
	Возврат ДанныеДляСравнения;

КонецФункции 

#КонецОбласти
