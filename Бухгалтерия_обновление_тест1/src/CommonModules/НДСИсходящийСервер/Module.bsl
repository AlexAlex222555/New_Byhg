
#Область ОбработкаДвиженийПоРегистрамУчетаНДС

Процедура ОтразитьЗаданияКФормированиюИсходящихНалоговыхДокументов(Документ, МенеджерВременныхТаблиц) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ВременныеТаблицы = МенеджерВременныхТаблиц;
	
	Если ВременныеТаблицы.Таблицы.Найти("ТаблицаИзмененийНДСНоменклатурныйСоставЗадания") <> Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ДокументПоставки КАК ДокументПоставки,
		|	НАЧАЛОПЕРИОДА(МИНИМУМ(НДСРасчетНалоговыхОбязательств.Период), ДЕНЬ) КАК Период
		|ПОМЕСТИТЬ МинимальныеПериодыДокументовПоставки
		|ИЗ
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания КАК ТаблицаИзмененийНДСНоменклатурныйСоставЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСРасчетНалоговыхОбязательств КАК НДСРасчетНалоговыхОбязательств
		|		ПО ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.АналитикаУчетаПоПартнерам = НДСРасчетНалоговыхОбязательств.АналитикаУчетаПоПартнерам
		|			И ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ОбъектРасчетов = НДСРасчетНалоговыхОбязательств.ОбъектРасчетов
		|			И ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ДокументПоставки = НДСРасчетНалоговыхОбязательств.ДокументПоставки
		|ГДЕ
		|	НДСРасчетНалоговыхОбязательств.СуммаПоставкиТребующаяРегистрацииНН <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.АналитикаУчетаПоПартнерам,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ОбъектРасчетов,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ДокументПоставки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.Документ КАК Документ,
		|	МИНИМУМ(ВЫБОР
		|			КОГДА ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.День > МинимальныеПериодыДокументовПоставки.Период
		|				ТОГДА МинимальныеПериодыДокументовПоставки.Период
		|			ИНАЧЕ ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.День
		|		КОНЕЦ) КАК День
		|ИЗ
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания КАК ТаблицаИзмененийНДСНоменклатурныйСоставЗадания
		|		ЛЕВОЕ СОЕДИНЕНИЕ МинимальныеПериодыДокументовПоставки КАК МинимальныеПериодыДокументовПоставки
		|		ПО ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.АналитикаУчетаПоПартнерам = МинимальныеПериодыДокументовПоставки.АналитикаУчетаПоПартнерам
		|			И ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ОбъектРасчетов = МинимальныеПериодыДокументовПоставки.ОбъектРасчетов
		|			И ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ДокументПоставки = МинимальныеПериодыДокументовПоставки.ДокументПоставки
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.АналитикаУчетаПоПартнерам,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.ОбъектРасчетов,
		|	ТаблицаИзмененийНДСНоменклатурныйСоставЗадания.Документ";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Не ТранзакцияАктивна() Тогда
				НачатьТранзакцию();
			КонецЕсли;
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Константа.НомерЗаданияКФормированиюИсходящихНалоговыхДокументов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НаборЗаписей, Выборка);
			НаборЗаписей.НомерЗадания = Константы.НомерЗаданияКФормированиюИсходящихНалоговыхДокументов.Получить();
			НаборЗаписей.КодОтслеживаемогоРегистра = 1;
			
			Если ТипЗнч(Выборка.Документ) = Тип("ДокументСсылка.Приложение2КНалоговойНакладной") Тогда
				БазовыйДокумент = НДСИсходящийКлиентСервер.ПолучитьБазовыйНалоговыйДокументДляП2(Выборка.Документ);
				Если НЕ БазовыйДокумент = Неопределено Тогда
					НаборЗаписей.День = НачалоДня(БазовыйДокумент.Дата);
				КонецЕсли;
			КонецЕсли;
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
	КонецЕсли;

	ТаблицаИзмененийНДСРасчетНалоговыхОбязательствЗадания = ВременныеТаблицы.Таблицы.Найти("ТаблицаИзмененийНДСРасчетНалоговыхОбязательствЗадания");
	Если ТаблицаИзмененийНДСРасчетНалоговыхОбязательствЗадания <> Неопределено Тогда
		
		Выборка = ТаблицаИзмененийНДСРасчетНалоговыхОбязательствЗадания.ПолучитьДанные().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Константа.НомерЗаданияКФормированиюИсходящихНалоговыхДокументов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ЗаданияКФормированиюИсходящихНалоговыхДокументов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НаборЗаписей, Выборка);
			НаборЗаписей.НомерЗадания = Константы.НомерЗаданияКФормированиюИсходящихНалоговыхДокументов.Получить();
			НаборЗаписей.КодОтслеживаемогоРегистра = 2;
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс


#КонецОбласти

#Область НастройкиУчетаНДС

// Функция возвращает момент определения базы НДС по переданным параметрам
//
Функция ОпределитьМоментОпределенияБазыНДС(ВидПоставки, ХозяйственнаяОперация, ВалютаДокумента, Организация, ДоговорКонтрагента = Неопределено) Экспорт
	
	// Данные договора могут быть использованы только в случае когда валюта взаиморасчетов указанная в документах
	// совпадает с валютой договора. Для входящего НДС автоматизированны только операции в гривнах. Соответственно
	// анализируются только гривневые договора
	Договор = Неопределено;
	Если ЗначениеЗаполнено(ДоговорКонтрагента)
	   И ДоговорКонтрагента.ВалютаВзаиморасчетов = ВалютаДокумента Тогда
		Договор = ДоговорКонтрагента;
	КонецЕсли;
	
	ЭтоКомиссия = (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию) ИЛИ                   // РеализацияТоваровУслуг
	              (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера) ИЛИ                // ВозвратТоваровОтКлиента
	              (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию) ИЛИ // ПередачаТоваровМеждуОрганизациями
	              (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями) ИЛИ  // ВозвратТоваровМеждуОрганизациями
	              (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионера);                       // ОтчетКомиссионера, ОтчетПоКомиссииМеждуОрганизациями
				  
	Если ЗначениеЗаполнено(ВидПоставки) Тогда
		
		Если ВидПоставки = Перечисления.ВидыПоставки.ВводОстатков Тогда
			
			Возврат Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию;
			
		ИначеЕсли ЭтоКомиссия Тогда
			
			Возврат Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию; // Авансы не автоматизированы
			  
		ИначеЕсли ВидПоставки = Перечисления.ВидыПоставки.Розница
			  ИЛИ ВидПоставки = Перечисления.ВидыПоставки.РозницаВозврат Тогда
			  
			Возврат Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию;
			
		ИначеЕсли ВидПоставки = Перечисления.ВидыПоставки.Поставка
		      ИЛИ ВидПоставки = Перечисления.ВидыПоставки.Возврат Тогда
		 
			МоментПоДоговору = Неопределено;
			Если ЗначениеЗаполнено(Договор) Тогда
				Если ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
					Если Договор.ТипДоговора = Перечисления.ТипыДоговоровМеждуОрганизациями.Комиссионный Тогда
						МоментПоДоговору = Договор.МоментОпределенияБазыНДСПолучатель;
					Иначе
						МоментПоДоговору = Договор.МоментОпределенияБазыНДС;
					КонецЕсли;
				Иначе
					МоментПоДоговору = Договор.МоментОпределенияБазыНДС;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(МоментПоДоговору) Тогда
				Возврат МоментПоДоговору;
			Иначе
				Если ВалютаДокумента <> Константы.ВалютаРегламентированногоУчета.Получить() Тогда
					Возврат Организация.МоментОпределенияБазыНДСПоЭкспорту;
				Иначе
					Возврат Организация.МоментОпределенияБазыНДСПоПродажам;
				КонецЕсли;
			КонецЕсли;
						
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
		
КонецФункции

Функция ОпределитьНомеклатуруЗаполненияНалоговыхНаАванс(Организация, ДоговорКонтрагента = Неопределено, Кеш = Неопределено) Экспорт
	
	Если Кеш = Неопределено Тогда
		Кеш = Новый Структура("ПоОрганизациям, ПоДоговорам", Новый Соответствие, Новый Соответствие);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		НайденноеЗначение = Кеш.ПоДоговорам[ДоговорКонтрагента];
		Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
			Возврат НайденноеЗначение;
		КонецЕсли;
		
		НайденноеЗначение = ДоговорКонтрагента.НоменклатураЗаполненияНалоговыхНаАванс;
		Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
			Кеш.ПоДоговорам[ДоговорКонтрагента] = НайденноеЗначение;
			Возврат НайденноеЗначение;
		КонецЕсли;
		
	КонецЕсли;
	
	НайденноеЗначение = Кеш.ПоОрганизациям[Организация];
	Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
		Возврат НайденноеЗначение;
	КонецЕсли;
		
	НайденноеЗначение = Организация.НоменклатураЗаполненияНалоговыхНаАванс;
	Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
		Кеш.ПоОрганизациям[Организация] = НайденноеЗначение;
		
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			Кеш.ПоДоговорам[ДоговорКонтрагента] = НайденноеЗначение;			
		КонецЕсли;
		
		Возврат НайденноеЗначение;
	КонецЕсли;
	
	ТекстСсобщения = НСтр("ru='Формирование налоговых документов прервано с ошибкой';uk='Формування податкових документів перервано з помилкою'") + Символы.ПС 
				   + НСтр("ru='В настройках выписки налоговых документов организации ""';uk='В настройках виписки податкових документів організації ""'")
		           + Строка(Организация) 
				   + НСтр("ru='"" не определена номенклатура для заполнения налоговых на аванс';uk='"" не визначена номенклатура для заповнення податкових на аванс'");
				   
	ВызватьИсключение ТекстСсобщения;
			
КонецФункции

Функция ОпределитьОбособленноеПодразделениеПоУмолчанию(Организация, ДоговорКонтрагента = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		НайденноеЗначение = ДоговорКонтрагента.ОбособленноеПодразделениеПоУмолчанию;
		Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
			Возврат НайденноеЗначение;
		КонецЕсли;
	КонецЕсли;
	
	НайденноеЗначение = Организация.ОбособленноеПодразделениеПоУмолчанию;
	Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
		Возврат НайденноеЗначение;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ОпределитьОтветственногоЗаВыпискуНалоговыхДокументов(Организация, ДоговорКонтрагента = Неопределено, Кеш = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		НайденноеЗначение = ДоговорКонтрагента.ОтветственныйЗаВыпискуНалоговыхДокументов;
		Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
			Возврат НайденноеЗначение;
		КонецЕсли;
		
	КонецЕсли;
		
	НайденноеЗначение = Организация.ОтветственныйЗаВыпискуНалоговыхДокументов;
	Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
		Возврат НайденноеЗначение;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ФормированиеНалоговыхДокументовПоНалоговымОбязательствам_НН_и_П2

Процедура СформироватьНалоговыеДокументыЧерезРегламентированноеЗадание() Экспорт
	
	СформироватьНалоговыеНакладные(КонецДня(ТекущаяДата()));
	
КонецПроцедуры // СформироватьНалоговыеДокументыЧерезРегламентированноеЗадание()

// Процедура выполняет проведение документов по расчетам с клиентами.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
Процедура СформироватьНалоговыеНакладные(КонецПериодаРасчета, СтруктураПараметров = Неопределено, Пользователь = Неопределено) Экспорт
	
	Если Не Пользователи.РолиДоступны("ДобавлениеИзменениеДокументовРегламентированногоУчета", Пользователь) Тогда
		ТекстОшибки = НСтр("ru='Формирование исходящий налоговых документов прервано
                                 |Отсутсвует разрешение на действие (роль) ""Добавление изменение исходящих документов НДС""'
                                 |;uk='Формування вихідних податкових документів перервано
                                 |Відсутній дозвіл на дію (роль) ""Додавання зміна вихідних документів ПДВ""'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	АналитикаУчетаПоПартнерамОтбор = Неопределено;
    
	РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(КонецМесяца(КонецПериодаРасчета) + 1);
	
	НомерЗадания = УвеличитьНомерЗадания("НомерЗаданияКФормированиюИсходящихНалоговыхДокументов");
	
	НачалоПериодаРасчета = ПолучитьДатуНачалаФормированияНалоговыхДокументов(КонецПериодаРасчета, СтруктураПараметров, АналитикаУчетаПоПартнерамОтбор, НомерЗадания);
	
	Если НачалоПериодаРасчета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	
	КешНоменклатурыДляАвансовыхНН = Неопределено;

#Область Этап1 
//  Этап 1 - Превышение сумм существующих налоговых документов над сумамми налоговых обязательств (например - уменьшили реализацию, по которой уже была сформирован налоговый документ)
//  Корректируем и перепроводим существующие налоговые документы, отвязывая строки от документов поставки на сумму превышения 
//  (но суммы существующих налоговых документов остаются прежними)
//  Корректировка строк в налоговом документе может не совпадать с корректировкой строк в документе реализации
 
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НДСРасчетНалоговыхОбязательствОбороты.АналитикаУчетаПоПартнерам,
	|	НДСРасчетНалоговыхОбязательствОбороты.Валюта,
	|	СУММА(НДСРасчетНалоговыхОбязательствОбороты.СуммаПоставкиТребующаяРегистрацииННОборот) КАК СуммаВозникшихНОЗаДень,
	|	НАЧАЛОПЕРИОДА(НДСРасчетНалоговыхОбязательствОбороты.Период, ДЕНЬ) КАК День,
	|	НДСРасчетНалоговыхОбязательствОбороты.ОбъектРасчетов,
	|	НДСРасчетНалоговыхОбязательствОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСРасчетНалоговыхОбязательствОбороты.ДокументПоставки,
	|	ВЫБОР
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК РазрезВидПоставки
	|ПОМЕСТИТЬ ТаблицаНалоговыхОбязательств
	|ИЗ
	|	РегистрНакопления.НДСРасчетНалоговыхОбязательств.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			День,
	|			(&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСРасчетНалоговыхОбязательствОбороты
	|ГДЕ
	|	НДСРасчетНалоговыхОбязательствОбороты.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСРасчетНалоговыхОбязательствОбороты.Валюта,
	|	НДСРасчетНалоговыхОбязательствОбороты.АналитикаУчетаПоПартнерам,
	|	НАЧАЛОПЕРИОДА(НДСРасчетНалоговыхОбязательствОбороты.Период, ДЕНЬ),
	|	НДСРасчетНалоговыхОбязательствОбороты.ОбъектРасчетов,
	|	НДСРасчетНалоговыхОбязательствОбороты.СтавкаНДС,
	|	НДСРасчетНалоговыхОбязательствОбороты.ДокументПоставки,
	|	ВЫБОР
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ) КАК День,
	|	МАКСИМУМ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) КАК ОсновнаяНалоговая,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ КАК ВидОперацииОсновнойНалоговой,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК РазрезВидПоставки
	|ПОМЕСТИТЬ ТаблицаНалоговыхНакладных
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			(&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты
	|ГДЕ
	|	(ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|			ИЛИ ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.Приложение2КНалоговойНакладной)
	|				И (НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|					ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ),
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор КАК ТекущийНалоговыйДокумент,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА ТаблицаНалоговыхНакладных.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница) 
	|			ТОГДА НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС 
	|		ИНАЧЕ Значение(Справочник.СтавкиНДС.ПустаяСсылка) 
	|	КОНЕЦ КАК СтавкаНДСНО,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки,
	|	СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ТаблицаРеальныхНалоговыхПоДням
	|ИЗ
	|	ТаблицаНалоговыхНакладных КАК ТаблицаНалоговыхНакладных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|		ПО (ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|				ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.НалоговаяНакладная
	|					И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|					И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|				ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.ОсновноеП2ДляВозврата
	|					И (НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|						ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|ГДЕ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.День,
	|	ВложенныйЗапрос.РазрезВидПоставки,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ВложенныйЗапрос.СтавкаНДСНО,
	|	ВложенныйЗапрос.ДокументПоставки,
	|	ВложенныйЗапрос.СуммаВзаиморасчетов,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) КАК СуммаВозникшихНОЗаДень,
	|	ВложенныйЗапрос.СуммаВзаиморасчетов - ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) КАК СуммаПревышения
	|ПОМЕСТИТЬ ТаблицаПревышенийПоДокументамПоДням
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		ТаблицаРеальныхНалоговыхПоДням.Валюта КАК Валюта,
	|		ТаблицаРеальныхНалоговыхПоДням.День КАК День,
	|		ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|		ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|		ТаблицаРеальныхНалоговыхПоДням.СтавкаНДСНО КАК СтавкаНДСНО,
	|		ТаблицаРеальныхНалоговыхПоДням.ДокументПоставки КАК ДокументПоставки,
	|		СУММА(ТаблицаРеальныхНалоговыхПоДням.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|	ИЗ
	|		ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаРеальныхНалоговыхПоДням.День,
	|		ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|		ТаблицаРеальныхНалоговыхПоДням.ДокументПоставки,
	|		ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|		ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|		ТаблицаРеальныхНалоговыхПоДням.СтавкаНДСНО,
	|		ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов
	|	) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНалоговыхОбязательств КАК ТаблицаНалоговыхОбязательств
	|		ПО ВложенныйЗапрос.АналитикаУчетаПоПартнерам = ТаблицаНалоговыхОбязательств.АналитикаУчетаПоПартнерам
	|			И ВложенныйЗапрос.ОбъектРасчетов = ТаблицаНалоговыхОбязательств.ОбъектРасчетов
	|			И ВложенныйЗапрос.Валюта = ТаблицаНалоговыхОбязательств.Валюта
	|			И ВложенныйЗапрос.День = ТаблицаНалоговыхОбязательств.День
	|			И ВложенныйЗапрос.РазрезВидПоставки = ТаблицаНалоговыхОбязательств.РазрезВидПоставки
	|			И ВложенныйЗапрос.ДокументПоставки = ТаблицаНалоговыхОбязательств.ДокументПоставки
	|			И ВложенныйЗапрос.СтавкаНДСНО = ТаблицаНалоговыхОбязательств.СтавкаНДС
	|ГДЕ
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) < ВложенныйЗапрос.СуммаВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПревышенийПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаПревышенийПоДокументамПоДням.Валюта КАК Валюта,
	|	ТаблицаПревышенийПоДокументамПоДням.День КАК День,
	|	ТаблицаПревышенийПоДокументамПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|	ТаблицаПревышенийПоДокументамПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаПревышенийПоДокументамПоДням.СтавкаНДСНО КАК СтавкаНДС,
	|	ТаблицаПревышенийПоДокументамПоДням.ДокументПоставки КАК ДокументПоставки,
	|	ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент,
	|	ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент.Дата КАК ТекущийНалоговыйДокументДата,
	|	МАКСИМУМ(ТаблицаПревышенийПоДокументамПоДням.СуммаПревышения) КАК СуммаПревышения
	|ИЗ
	|	ТаблицаПревышенийПоДокументамПоДням КАК ТаблицаПревышенийПоДокументамПоДням
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|		ПО ТаблицаПревышенийПоДокументамПоДням.АналитикаУчетаПоПартнерам = ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам
	|			И ТаблицаПревышенийПоДокументамПоДням.ОбъектРасчетов = ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов
	|			И ТаблицаПревышенийПоДокументамПоДням.Валюта = ТаблицаРеальныхНалоговыхПоДням.Валюта
	|			И ТаблицаПревышенийПоДокументамПоДням.День = ТаблицаРеальныхНалоговыхПоДням.День
	|			И ТаблицаПревышенийПоДокументамПоДням.РазрезВидПоставки = ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки
	|			И ТаблицаПревышенийПоДокументамПоДням.ДокументПоставки = ТаблицаРеальныхНалоговыхПоДням.ДокументПоставки
	|			И ТаблицаПревышенийПоДокументамПоДням.СтавкаНДСНО = ТаблицаРеальныхНалоговыхПоДням.СтавкаНДСНО
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПревышенийПоДокументамПоДням.Валюта,
	|	ТаблицаПревышенийПоДокументамПоДням.День,
	|	ТаблицаПревышенийПоДокументамПоДням.РазрезВидПоставки,
	|	ТаблицаПревышенийПоДокументамПоДням.ОбъектРасчетов,
	|	ТаблицаПревышенийПоДокументамПоДням.СтавкаНДСНО,
	|	ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент,
	|	ТаблицаПревышенийПоДокументамПоДням.ДокументПоставки,
	|	ТаблицаПревышенийПоДокументамПоДням.АналитикаУчетаПоПартнерам,
	|	ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	Валюта,
	|	День,
	|	ОбъектРасчетов,
	|	ДокументПоставки,
	|	РазрезВидПоставки,
	|	ТекущийНалоговыйДокументДата УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаПревышенийПоДокументамПоДням
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаРеальныхНалоговыхПоДням
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаНалоговыхНакладных
	|";
	
				   
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериодаРасчета));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериодаРасчета));
	
	Запрос.УстановитьПараметр("НеИспользоватьОтборПоАналитикеУчетаПоПартнерам", АналитикаУчетаПоПартнерамОтбор = Неопределено);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерамОтбор", АналитикаУчетаПоПартнерамОтбор);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
				   
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаПоПревышениям = РезультатЗапроса[4].Выбрать();
	
	//СтарыеАналитики = Новый Структура("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, День, ДокументПоставки");
	СтарыеАналитики = Новый Структура("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, День, ДокументПоставки, РазрезВидПоставки, СтавкаНДС");
	
	Пока ВыборкаПоПревышениям.Следующий() Цикл
		
		Если АналитикиИзменились(СтарыеАналитики, ВыборкаПоПревышениям) = Истина Тогда
			
			Если ВыборкаПоПревышениям.РазрезВидПоставки = Перечисления.ВидыПоставки.Возврат Тогда
				КоэффициентНаправлениеДвижения = -1;
			Иначе
				КоэффициентНаправлениеДвижения = 1;
			КонецЕсли;
			
			СуммаПревышения = КоэффициентНаправлениеДвижения * ВыборкаПоПревышениям.СуммаПревышения;
			
			ЗаполнитьЗначенияСвойств(СтарыеАналитики, ВыборкаПоПревышениям);
			
		КонецЕсли;
		
		Если СуммаПревышения = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НалоговаяОбъект = ВыборкаПоПревышениям.ТекущийНалоговыйДокумент.ПолучитьОбъект();
		
		ОтборТоваров = Новый Структура("ДокументПоставки, ОбъектРасчетов" + ?(ЗначениеЗаполнено(ВыборкаПоПревышениям.СтавкаНДС), ", СтавкаНДС", ""));
		ЗаполнитьЗначенияСвойств(ОтборТоваров, ВыборкаПоПревышениям);
		НайденныеСтроки = НалоговаяОбъект.Товары.НайтиСтроки(ОтборТоваров);
		
		Для каждого Строка из НайденныеСтроки Цикл
			
			Если КоэффициентНаправлениеДвижения * (СуммаПревышения - Строка.СуммаСНДС) < 0 Тогда
					
				//Нужно разбить строку и отменить часть.
				ИндексТекущейСтроки 	 = НалоговаяОбъект.Товары.Индекс(Строка);
				НоваяСтрока 			 = НалоговаяОбъект.Товары.Вставить(ИндексТекущейСтроки + 1);
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				Если ТипЗнч(НалоговаяОбъект) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
					НоваяСтрока.ДокументПоставкиДляВозвратов = Неопределено;
				КонецЕсли;
				НоваяСтрока.ДокументПоставки = Неопределено;
				
				ПолнаяСумма				= Строка.СуммаСНДС;
				Строка.СуммаСНДС		= Строка.СуммаСНДС - СуммаПревышения;
				НоваяСтрока.СуммаСНДС	= СуммаПревышения;
				
				Строка.КоличествоУпаковок = Окр((Строка.КоличествоУпаковок * Строка.СуммаСНДС) / ПолнаяСумма, 2);
				НоваяСтрока.КоличествоУпаковок = НоваяСтрока.КоличествоУпаковок - Строка.КоличествоУпаковок;
				
				СуммаПревышения = 0;
				
			Иначе
				
				СуммаПревышения = СуммаПревышения - Строка.СуммаСНДС;
				Если ТипЗнч(НалоговаяОбъект) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
					Строка.ДокументПоставкиДляВозвратов = Неопределено;
				КонецЕсли;
				Строка.ДокументПоставки = Неопределено;
				
			КонецЕсли;
			
			Если СуммаПревышения = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		НалоговаяОбъект.ЗаполнитьРеквизитыПослеАвтоформирования(Ложь);
		НалоговаяОбъект.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
		НалоговаяОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		
	КонецЦикла;
	
#КонецОбласти // Этап1 

#Область Этап2
 
	
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Валюта,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СпособЗаполнения,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СтавкаНДС,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Номенклатура,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Характеристика,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Упаковка,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставкиДляВозвратов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.КоличествоУпаковокОстаток
	|ПОМЕСТИТЬ ТаблицаОтрицательныхОстатков
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных.Остатки(
	|			,
	|			(НЕ ДокументПоставки = НЕОПРЕДЕЛЕНО
	|				ИЛИ НЕ ДокументПоставкиДляВозвратов = НЕОПРЕДЕЛЕНО)
	|				И (&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки
	|ГДЕ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.КоличествоУпаковокОстаток < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.КоличествоУпаковок,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидДвижения,
	|	ТаблицаОтрицательныхОстатков.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаОтрицательныхОстатков.ВидПоставки КАК ВидПоставки,
	|	ТаблицаОтрицательныхОстатков.Валюта КАК Валюта,
	|	ТаблицаОтрицательныхОстатков.СпособЗаполнения КАК СпособЗаполнения,
	|	ТаблицаОтрицательныхОстатков.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаОтрицательныхОстатков.НомерГТД КАК НомерГТД,
	|	ТаблицаОтрицательныхОстатков.Номенклатура КАК Номенклатура,
	|	ТаблицаОтрицательныхОстатков.Характеристика КАК Характеристика,
	|	ТаблицаОтрицательныхОстатков.Упаковка КАК Упаковка,
	|	ТаблицаОтрицательныхОстатков.ЦенаНН,
	|	ТаблицаОтрицательныхОстатков.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаОтрицательныхОстатков.ДокументПоставки КАК ДокументПоставки,
	|	ТаблицаОтрицательныхОстатков.ДокументПоставкиДляВозвратов КАК ДокументПоставкиДляВозвратов,
	|	ТаблицаОтрицательныхОстатков.КоличествоУпаковокОстаток КАК КоличествоУпаковокОстаток
	|ИЗ
	|	ТаблицаОтрицательныхОстатков КАК ТаблицаОтрицательныхОстатков
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|		ПО ТаблицаОтрицательныхОстатков.АналитикаУчетаПоПартнерам = НДСНоменклатурныйСоставДляНалоговыхНакладных.АналитикаУчетаПоПартнерам
	|			И ТаблицаОтрицательныхОстатков.ОбъектРасчетов = НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов
	|			И ТаблицаОтрицательныхОстатков.ВидПоставки = НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки
	|			И ТаблицаОтрицательныхОстатков.Валюта = НДСНоменклатурныйСоставДляНалоговыхНакладных.Валюта
	|			И ТаблицаОтрицательныхОстатков.СпособЗаполнения = НДСНоменклатурныйСоставДляНалоговыхНакладных.СпособЗаполнения
	|			И ТаблицаОтрицательныхОстатков.СтавкаНДС = НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС
	|			И ТаблицаОтрицательныхОстатков.НомерГТД = НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД
	|			И ТаблицаОтрицательныхОстатков.Номенклатура = НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура
	|			И ТаблицаОтрицательныхОстатков.Характеристика = НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика
	|			И ТаблицаОтрицательныхОстатков.Упаковка = НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка
	|			И ТаблицаОтрицательныхОстатков.ЦенаНН = НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН
	|			И ТаблицаОтрицательныхОстатков.ДокументПоставки = НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки
	|			И ТаблицаОтрицательныхОстатков.ДокументПоставкиДляВозвратов = НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставкиДляВозвратов
	|ГДЕ
	|	(ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|			ИЛИ ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.Приложение2КНалоговойНакладной))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Период УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(КоличествоУпаковокОстаток)
	|ПО
	|	ВидПоставки,
	|	Валюта,
	|	СтавкаНДС,
	|	ДокументПоставки,
	|	ДокументПоставкиДляВозвратов,
	|	СпособЗаполнения,
	|	ОбъектРасчетов,
	|	Характеристика,
	|	АналитикаУчетаПоПартнерам,
	|	НомерГТД,
	|	Номенклатура,
	|	Упаковка
	|";
				   


	ВыборкаПоОстаткам = Запрос.Выполнить().Выбрать();
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, НомерГТД, Упаковка, СтавкаНДС, Цена, ОбъектРасчетов, ДокументПоставки");
	СтруктураОтбораП2 = Новый Структура("Номенклатура, Характеристика, НомерГТД, Упаковка, СтавкаНДС, Цена, ОбъектРасчетов, ДокументПоставки, ДокументПоставкиДляВозвратов");
	
	Пока Истина Цикл
		
		Если ВыборкаПоОстаткам.Уровень() <> 11 Тогда
			Если НЕ ВыборкаПоОстаткам.Следующий() Тогда
				Прервать;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		КоличествоОтмены = - ВыборкаПоОстаткам.КоличествоУпаковокОстаток;
		
		ВыборкаПоРегистратору = ВыборкаПоОстаткам;

		Пока ВыборкаПоРегистратору.Следующий() И КоличествоОтмены > 0 Цикл
			
			Если ВыборкаПоОстаткам.Уровень() <> 12 Тогда
				Прервать;
			КонецЕсли;
			
			НалоговаяДляКорректировки = ВыборкаПоРегистратору.Регистратор.ПолучитьОбъект();
			
			Если ТипЗнч(НалоговаяДляКорректировки) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
				ЗаполнитьЗначенияСвойств(СтруктураОтбораП2, ВыборкаПоРегистратору);
				СтруктураОтбораП2.Цена = ВыборкаПоРегистратору.ЦенаНН;
				СтрокиДляКорректировки = НалоговаяДляКорректировки.Товары.НайтиСтроки(СтруктураОтбораП2);
			Иначе
				ЗаполнитьЗначенияСвойств(СтруктураОтбора, ВыборкаПоРегистратору);
				СтруктураОтбора.Цена = ВыборкаПоРегистратору.ЦенаНН;
				СтрокиДляКорректировки = НалоговаяДляКорректировки.Товары.НайтиСтроки(СтруктураОтбора);
			КонецЕсли;
			
			
			Для Каждого Строка Из СтрокиДляКорректировки Цикл
				
				Если КоличествоОтмены < Строка.КоличествоУпаковок Тогда
					
					// Нужно разбить строку и отменить часть.
					ИндексТекущейСтроки 	 = НалоговаяДляКорректировки.Товары.Индекс(Строка);
					НоваяСтрока 			 = НалоговаяДляКорректировки.Товары.Вставить(ИндексТекущейСтроки + 1);
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
					НоваяСтрока.ДокументПоставки = Неопределено;
					Если ТипЗнч(НалоговаяДляКорректировки) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
						НоваяСтрока.ДокументПоставкиДляВозвратов = Неопределено;
					КонецЕсли;
					
					ПолноеКоличество 				= Строка.КоличествоУпаковок;
					Строка.КоличествоУпаковок 		= Строка.КоличествоУпаковок - КоличествоОтмены;
					НоваяСтрока.КоличествоУпаковок 	= КоличествоОтмены;
					
					Строка.СуммаСНДС = Окр((Строка.СуммаСНДС * Строка.КоличествоУпаковок) / ПолноеКоличество, 2);
					НоваяСтрока.СуммаСНДС = НоваяСтрока.СуммаСНДС - Строка.СуммаСНДС;
					
					КоличествоОтмены = 0;
					
				Иначе
					
					КоличествоОтмены = КоличествоОтмены - Строка.КоличествоУпаковок;
					Строка.ДокументПоставки = Неопределено;
					Если ТипЗнч(НалоговаяДляКорректировки) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
						Строка.ДокументПоставкиДляВозвратов = Неопределено;
					КонецЕсли;
					
				КонецЕсли;
				
				Если КоличествоОтмены = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			НалоговаяДляКорректировки.ЗаполнитьРеквизитыПослеАвтоформирования(Ложь);
			НалоговаяДляКорректировки.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
			НалоговаяДляКорректировки.Записать(РежимЗаписиДокумента.Проведение);
			
		КонецЦикла;
		
	КонецЦикла;
	
#КонецОбласти // Этап2	

#Область Этап3

	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ) КАК День,
	|	МАКСИМУМ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) КАК ОсновнаяНалоговая,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ КАК ВидОперацииОсновнойНалоговой,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК РазрезВидПоставки
	|ПОМЕСТИТЬ ТаблицаНалоговыхНакладных
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			(&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты
	|ГДЕ
	|	(ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|			ИЛИ ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.Приложение2КНалоговойНакладной)
	|				И (НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|					ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ),
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор КАК ТекущийНалоговыйДокумент,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки,
	|	СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ТаблицаРеальныхНалоговыхПоДням
	|ИЗ
	|	ТаблицаНалоговыхНакладных КАК ТаблицаНалоговыхНакладных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|		ПО (ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|				ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.НалоговаяНакладная
	|					И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|					И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|				ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.ОсновноеП2ДляВозврата
	|					И (НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|						ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|ГДЕ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНалоговыхОбязательств.АналитикаУчетаПоПартнерам,
	|	ТаблицаНалоговыхОбязательств.Валюта,
	|	ТаблицаНалоговыхОбязательств.День,
	|	ТаблицаНалоговыхОбязательств.РазрезВидПоставки,
	|	ТаблицаНалоговыхОбязательств.ОбъектРасчетов,
	|	ТаблицаНалоговыхОбязательств.ДокументПоставки,
	|	ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0) КАК СуммаВзаиморасчетов,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) КАК СуммаВозникшихНОЗаДень,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) - ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0) КАК СуммаПревышенияНО
	|ПОМЕСТИТЬ ТаблицаПревышенийНОПоДокументамПоДням
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		ТаблицаРеальныхНалоговыхПоДням.Валюта КАК Валюта,
	|		ТаблицаРеальныхНалоговыхПоДням.День КАК День,
	|		ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|		ТаблицаРеальныхНалоговыхПоДням.ДокументПоставки КАК ДокументПоставки,
	|		ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|		СУММА(ТаблицаРеальныхНалоговыхПоДням.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|	ИЗ
	|		ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаРеальныхНалоговыхПоДням.День,
	|		ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|		ТаблицаРеальныхНалоговыхПоДням.ДокументПоставки,
	|		ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|		ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|		ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов) КАК ВложенныйЗапрос
	|		ПРАВОЕ СОЕДИНЕНИЕ ТаблицаНалоговыхОбязательств КАК ТаблицаНалоговыхОбязательств
	|		ПО ВложенныйЗапрос.АналитикаУчетаПоПартнерам = ТаблицаНалоговыхОбязательств.АналитикаУчетаПоПартнерам
	|			И ВложенныйЗапрос.ОбъектРасчетов = ТаблицаНалоговыхОбязательств.ОбъектРасчетов
	|			И ВложенныйЗапрос.Валюта = ТаблицаНалоговыхОбязательств.Валюта
	|			И ВложенныйЗапрос.День = ТаблицаНалоговыхОбязательств.День
	|			И ВложенныйЗапрос.РазрезВидПоставки = ТаблицаНалоговыхОбязательств.РазрезВидПоставки
	|			И ВложенныйЗапрос.ДокументПоставки = ТаблицаНалоговыхОбязательств.ДокументПоставки
	|ГДЕ
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) > ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Валюта,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СпособЗаполнения,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СтавкаНДС,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Номенклатура,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Характеристика,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Упаковка,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставкиДляВозвратов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.КоличествоУпаковокОстаток,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СуммаВзаиморасчетовОстаток
	|ПОМЕСТИТЬ ТаблицаОстатковПоДокументамОтгрузки
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных.Остатки(
	|			,
	|			ДокументПоставки В
	|					(ВЫБРАТЬ
	|						ТаблицаПревышенийНОПоДокументамПоДням.ДокументПоставки
	|					ИЗ
	|						ТаблицаПревышенийНОПоДокументамПоДням)
	|			И (&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки
	|ГДЕ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|	И НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СуммаВзаиморасчетовОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНалоговыхНакладныхАванс.АналитикаУчетаПоПартнерам,
	|	ТаблицаНалоговыхНакладныхАванс.Валюта,
	|	ТаблицаНалоговыхНакладныхАванс.День,
	|	ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая,
	|	ТаблицаНалоговыхНакладныхАванс.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладныхАванс.РазрезВидПоставки,
	|	ЕСТЬNULL(ВложенныйЗапрос.ТекущийНалоговыйДокумент, ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая) КАК ТекущийНалоговыйДокумент,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.ЦенаНН,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ВложенныйЗапрос.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ТаблицаРеальныхНалоговыхПоДнямАванс
	|ИЗ
	|	ТаблицаНалоговыхНакладных КАК ТаблицаНалоговыхНакладныхАванс
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаНалоговыхНакладныхАванс.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|			ТаблицаНалоговыхНакладныхАванс.Валюта КАК Валюта,
	|			ТаблицаНалоговыхНакладныхАванс.День КАК День,
	|			ТаблицаНалоговыхНакладныхАванс.РазрезВидПоставки КАК РазрезВидПоставки,
	|			ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая КАК ОсновнаяНалоговая,
	|			ТаблицаНалоговыхНакладныхАванс.ВидОперацииОсновнойНалоговой КАК ВидОперацииОсновнойНалоговой,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор КАК ТекущийНалоговыйДокумент,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС КАК СтавкаНДС,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД КАК НомерГТД,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура КАК Номенклатура,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика КАК Характеристика,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка КАК Упаковка,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН КАК ЦенаНН,
	|			СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.КоличествоУпаковок) КАК КоличествоУпаковок,
	|			СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|		ИЗ
	|			ТаблицаНалоговыхНакладных КАК ТаблицаНалоговыхНакладныхАванс
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|				ПО (ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|						ИЛИ ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.НалоговаяНакладная
	|							И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|							И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|						ИЛИ ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.ОсновноеП2ДляВозврата
	|							И (НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|								ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|		ГДЕ
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки = НЕОПРЕДЕЛЕНО
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаНалоговыхНакладныхАванс.АналитикаУчетаПоПартнерам,
	|			ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая,
	|			ТаблицаНалоговыхНакладныхАванс.День,
	|			ТаблицаНалоговыхНакладныхАванс.РазрезВидПоставки,
	|			ТаблицаНалоговыхНакладныхАванс.ВидОперацииОсновнойНалоговой,
	|			ТаблицаНалоговыхНакладныхАванс.Валюта,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН) КАК ВложенныйЗапрос
	|		ПО ТаблицаНалоговыхНакладныхАванс.АналитикаУчетаПоПартнерам = ВложенныйЗапрос.АналитикаУчетаПоПартнерам
	|			И ТаблицаНалоговыхНакладныхАванс.Валюта = ВложенныйЗапрос.Валюта
	|			И ТаблицаНалоговыхНакладныхАванс.День = ВложенныйЗапрос.День
	|			И ТаблицаНалоговыхНакладныхАванс.РазрезВидПоставки = ВложенныйЗапрос.РазрезВидПоставки
	|			И ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая = ВложенныйЗапрос.ОсновнаяНалоговая
	|			И ТаблицаНалоговыхНакладныхАванс.ВидОперацииОсновнойНалоговой = ВложенныйЗапрос.ВидОперацииОсновнойНалоговой
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.НомерГТД,
	|	ТаблицаНалоговыхНакладныхАванс.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Упаковка,
	|	ТаблицаНалоговыхНакладныхАванс.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладныхАванс.Валюта,
	|	ЕСТЬNULL(ВложенныйЗапрос.ТекущийНалоговыйДокумент, ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая),
	|	ТаблицаНалоговыхНакладныхАванс.День,
	|	ТаблицаНалоговыхНакладныхАванс.РазрезВидПоставки,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ТаблицаНалоговыхНакладныхАванс.ОсновнаяНалоговая,
	|	ВложенныйЗапрос.ЦенаНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатковПоДокументамОтгрузки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаОстатковПоДокументамОтгрузки.ВидПоставки,
	|	ТаблицаОстатковПоДокументамОтгрузки.Валюта КАК Валюта,
	|	ТаблицаОстатковПоДокументамОтгрузки.СпособЗаполнения,
	|	ТаблицаОстатковПоДокументамОтгрузки.СтавкаНДС,
	|	ТаблицаОстатковПоДокументамОтгрузки.НомерГТД,
	|	ТаблицаОстатковПоДокументамОтгрузки.Номенклатура,
	|	ТаблицаОстатковПоДокументамОтгрузки.Характеристика,
	|	ТаблицаОстатковПоДокументамОтгрузки.Упаковка,
	|	ТаблицаОстатковПоДокументамОтгрузки.ЦенаНН,
	|	ТаблицаОстатковПоДокументамОтгрузки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставки КАК ДокументПоставки,
	|	ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставкиДляВозвратов,
	|	ТаблицаОстатковПоДокументамОтгрузки.КоличествоУпаковокОстаток,
	|	ТаблицаОстатковПоДокументамОтгрузки.СуммаВзаиморасчетовОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		ТаблицаПревышенийНОПоДокументамПоДням.Валюта КАК Валюта
	|	ИЗ
	|		ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаПревышенийНОПоДокументамПоДням.Валюта,
	|		ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатковПоДокументамОтгрузки КАК ТаблицаОстатковПоДокументамОтгрузки
	|		ПО ВложенныйЗапрос.АналитикаУчетаПоПартнерам = ТаблицаОстатковПоДокументамОтгрузки.АналитикаУчетаПоПартнерам
	|			И ВложенныйЗапрос.Валюта = ТаблицаОстатковПоДокументамОтгрузки.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	Валюта,
	|	ОбъектРасчетов,
	|	ДокументПоставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.Валюта КАК Валюта,
	|	ВложенныйЗапрос.День КАК День,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.ОсновнаяНалоговая,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.ВидОперацииОсновнойНалоговой,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.РазрезВидПоставки,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.ТекущийНалоговыйДокумент КАК ТекущийНалоговыйДокумент,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.СтавкаНДС,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.НомерГТД,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.Номенклатура,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.Характеристика,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.Упаковка,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.ЦенаНН,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.КоличествоУпаковок,
	|	ТаблицаРеальныхНалоговыхПоДнямАванс.СуммаВзаиморасчетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		ТаблицаПревышенийНОПоДокументамПоДням.Валюта КАК Валюта,
	|		ТаблицаПревышенийНОПоДокументамПоДням.День КАК День
	|	ИЗ
	|		ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаПревышенийНОПоДокументамПоДням.Валюта,
	|		ТаблицаПревышенийНОПоДокументамПоДням.День,
	|		ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРеальныхНалоговыхПоДнямАванс КАК ТаблицаРеальныхНалоговыхПоДнямАванс
	|		ПО ВложенныйЗапрос.АналитикаУчетаПоПартнерам = ТаблицаРеальныхНалоговыхПоДнямАванс.АналитикаУчетаПоПартнерам
	|			И ВложенныйЗапрос.Валюта = ТаблицаРеальныхНалоговыхПоДнямАванс.Валюта
	|			И ВложенныйЗапрос.День = ТаблицаРеальныхНалоговыхПоДнямАванс.День
	|
	|УПОРЯДОЧИТЬ ПО
	|	День,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта,
	|	ТекущийНалоговыйДокумент
	|ИТОГИ ПО
	|	День
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаПревышенийНОПоДокументамПоДням.Валюта КАК Валюта,
	|	ТаблицаПревышенийНОПоДокументамПоДням.День КАК День,
	|	ТаблицаПревышенийНОПоДокументамПоДням.РазрезВидПоставки,
	|	ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаПревышенийНОПоДокументамПоДням.ДокументПоставки КАК ДокументПоставки,
	|	ТаблицаПревышенийНОПоДокументамПоДням.СуммаВзаиморасчетов,
	|	ТаблицаПревышенийНОПоДокументамПоДням.СуммаВозникшихНОЗаДень,
	|	ТаблицаПревышенийНОПоДокументамПоДням.СуммаПревышенияНО
	|ИЗ
	|	ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|
	|УПОРЯДОЧИТЬ ПО
	|	День,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта,
	|	ОбъектРасчетов,
	|	ДокументПоставки
	|ИТОГИ ПО
	|	День
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаНалоговыхНакладных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаРеальныхНалоговыхПоДням
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаПревышенийНОПоДокументамПоДням
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаОстатковПоДокументамОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаРеальныхНалоговыхПоДнямАванс
	|";
						
						
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаОстатковНоменклатурыДляПодбора = РезультатЗапроса[5].Выгрузить();
	
	ВыборкаАвансовНалоговыхПоДням = РезультатЗапроса[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаПоПревышениямПоДням = РезультатЗапроса[7].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	

	Пока ВыборкаПоПревышениямПоДням.Следующий() Цикл
		
        ВыборкаПоПревышениямЗаДень = ВыборкаПоПревышениямПоДням.Выбрать();
		
		ВыборкаАвансовНалоговыхПоДням.Следующий();
		
		ВыборкаАвансовНалоговыхЗаДень = ВыборкаАвансовНалоговыхПоДням.Выбрать();
		
		ТаблицаСуммПревышенийПоДокументамПоставкиЗаДень = СформироватьТаблицуСуммПревышенийПоДокументаПоставкиЗаДень(ВыборкаПоПревышениямЗаДень);

		СопоставитьСтрокиНаСуммуПоДокументамОтгрузкиЗаДень(ТаблицаОстатковНоменклатурыДляПодбора, ТаблицаСуммПревышенийПоДокументамПоставкиЗаДень, ВыборкаАвансовНалоговыхЗаДень);
		
	КонецЦикла;

#КонецОбласти // Этап3

#Область Этап4


	ТаблицаСформированыхНалоговых = Новый ТаблицаЗначений;//"АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, День, ВидОперацииОсновнойНалоговой"
	ТаблицаСформированыхНалоговых.Колонки.Добавить("ОсновнаяНалоговая");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("НалоговыйДокументДляКорректировки");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("ВидОперацииОсновнойНалоговой");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("РазрезВидПоставки");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("АналитикаУчетаПоПартнерам");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("ОбъектРасчетов");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("Валюта");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("День");
	ТаблицаСформированыхНалоговых.Колонки.Добавить("ДеньДокумента");
	
	МожноЗаписыватьТекущий = Ложь;


	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ) КАК День,
	|	МАКСИМУМ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) КАК ОсновнаяНалоговая,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ КАК ВидОперацииОсновнойНалоговой,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК РазрезВидПоставки
	|ПОМЕСТИТЬ ТаблицаНалоговыхНакладных
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			(&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты
	|ГДЕ
	|	(ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|	 ИЛИ ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.Приложение2КНалоговойНакладной)
	|	 И (НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ),
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор КАК ТекущийНалоговыйДокумент,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА ТаблицаНалоговыхНакладных.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница) 
	|			ТОГДА НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС 
	|		ИНАЧЕ Значение(Справочник.СтавкиНДС.ПустаяСсылка) 
	|	КОНЕЦ КАК СтавкаНДСНО,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки,
	|	СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ТаблицаРеальныхНалоговыхПоДням
	|ИЗ
	|	ТаблицаНалоговыхНакладных КАК ТаблицаНалоговыхНакладных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|		ПО (ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|				ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.НалоговаяНакладная
	|					И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|					И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|				ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.ОсновноеП2ДляВозврата
	|					И (НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|						ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА ТаблицаНалоговыхНакладных.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница) 
	|			ТОГДА НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС 
	|		ИНАЧЕ Значение(Справочник.СтавкиНДС.ПустаяСсылка) 
	|	КОНЕЦ,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0) КАК СуммаВзаиморасчетов,
	|	ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень КАК СуммаВозникшихНОЗаДень,
	|	ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень - ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0) КАК СуммаПревышения,
	|	ТаблицаНалоговыхОбязательств.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаНалоговыхОбязательств.Валюта КАК Валюта,
	|	ТаблицаНалоговыхОбязательств.День КАК День,
	|	ТаблицаНалоговыхОбязательств.РазрезВидПоставки КАК РазрезВидПоставки,
	|	ТаблицаНалоговыхОбязательств.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаНалоговыхОбязательств.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаНалоговыхОбязательств.ДокументПоставки КАК ДокументПоставки
	|ПОМЕСТИТЬ ТаблицаПревышенийНОПоДокументамПоДням
	|ИЗ
	|	ТаблицаНалоговыхОбязательств КАК ТаблицаНалоговыхОбязательств
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|			ТаблицаРеальныхНалоговыхПоДням.Валюта КАК Валюта,
	|			ТаблицаРеальныхНалоговыхПоДням.День КАК День,
	|			ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|			ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|			ТаблицаРеальныхНалоговыхПоДням.СтавкаНДСНО КАК СтавкаНДСНО,
	|			ТаблицаРеальныхНалоговыхПоДням.ДокументПоставки КАК ДокументПоставки,
	|			СУММА(ТаблицаРеальныхНалоговыхПоДням.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|		ИЗ
	|			ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаРеальныхНалоговыхПоДням.День,
	|			ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|			ТаблицаРеальныхНалоговыхПоДням.ДокументПоставки,
	|			ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|			ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|			ТаблицаРеальныхНалоговыхПоДням.СтавкаНДСНО,
	|			ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов
	|		) КАК ВложенныйЗапрос
	|		ПО (ВложенныйЗапрос.АналитикаУчетаПоПартнерам = ТаблицаНалоговыхОбязательств.АналитикаУчетаПоПартнерам)
	|			И (ВложенныйЗапрос.ОбъектРасчетов = ТаблицаНалоговыхОбязательств.ОбъектРасчетов)
	|			И (ВложенныйЗапрос.Валюта = ТаблицаНалоговыхОбязательств.Валюта)
	|			И (ВложенныйЗапрос.День = ТаблицаНалоговыхОбязательств.День)
	|			И (ВложенныйЗапрос.РазрезВидПоставки = ТаблицаНалоговыхОбязательств.РазрезВидПоставки)
	|			И (ВложенныйЗапрос.ДокументПоставки = ТаблицаНалоговыхОбязательств.ДокументПоставки)
	|			И (ВложенныйЗапрос.СтавкаНДСНО = ТаблицаНалоговыхОбязательств.СтавкаНДС)
	|ГДЕ
	|	ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0) < ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаПревышенийНОПоДокументамПоДням.Валюта КАК Валюта,
	|	ТаблицаПревышенийНОПоДокументамПоДням.День КАК День,
	|	ТаблицаПревышенийНОПоДокументамПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|	ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаПревышенийНОПоДокументамПоДням.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПревышенийНОПоДокументамПоДням.ДокументПоставки КАК ДокументПоставки,
	|	ТаблицаПревышенийНОПоДокументамПоДням.СуммаПревышения
	|ИЗ
	|	ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|
	|УПОРЯДОЧИТЬ ПО
	|	День,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта,
	|	РазрезВидПоставки,
	|	ОбъектРасчетов,
	|	СтавкаНДС,
	|	ДокументПоставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|	ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|	ТаблицаРеальныхНалоговыхПоДням.День,
	|	ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая,
	|	ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой,
	|	ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|	ВЫБОР
	|		КОГДА ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|				ИЛИ ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЕСТЬNULL(ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая.НалоговаяНакладная, НЕОПРЕДЕЛЕНО)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НалоговаяНакладнаяДляВозвратов,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент.РазрешенаАвтоКорректировка
	|				ТОГДА ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ) КАК НалоговыйДокументДляКорректировки
	|ИЗ
	|	ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРеальныхНалоговыхПоДням.День,
	|	ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|	ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|	ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой,
	|	ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|	ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая,
	|	ВЫБОР
	|		КОГДА ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|				ИЛИ ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЕСТЬNULL(ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая.НалоговаяНакладная, НЕОПРЕДЕЛЕНО)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ВидПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Валюта,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СпособЗаполнения,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СтавкаНДС,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.НомерГТД,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Номенклатура,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Характеристика,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.Упаковка,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ЦенаНН,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ОбъектРасчетов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставки,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставкиДляВозвратов,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.КоличествоУпаковокОстаток,
	|   НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СуммаВзаиморасчетовОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатковПоДокументамОтгрузки
	|
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных.Остатки(
	|			,
	|			ДокументПоставки В
	|					(ВЫБРАТЬ
	|						ТаблицаПревышенийНОПоДокументамПоДням.ДокументПоставки
	|					ИЗ
	|						ТаблицаПревышенийНОПоДокументамПоДням)
	|			И (&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|   ) КАК НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки
	|ГДЕ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.ДокументПоставки <> НЕОПРЕДЕЛЕНО
	|	И НДСНоменклатурныйСоставДляНалоговыхНакладныхОстатки.СуммаВзаиморасчетовОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатковПоДокументамОтгрузки.АналитикаУчетаПоПартнерам,
	|	ТаблицаОстатковПоДокументамОтгрузки.ВидПоставки,
	|	ТаблицаОстатковПоДокументамОтгрузки.Валюта,
	|	ТаблицаОстатковПоДокументамОтгрузки.СпособЗаполнения,
	|	ТаблицаОстатковПоДокументамОтгрузки.СтавкаНДС,
	|	ТаблицаОстатковПоДокументамОтгрузки.НомерГТД,
	|	ТаблицаОстатковПоДокументамОтгрузки.Номенклатура,
	|	ТаблицаОстатковПоДокументамОтгрузки.Характеристика,
	|	ТаблицаОстатковПоДокументамОтгрузки.Упаковка,
	|	ТаблицаОстатковПоДокументамОтгрузки.ЦенаНН,
	|	ТаблицаОстатковПоДокументамОтгрузки.ОбъектРасчетов,
	|	ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставки,
	|	ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставкиДляВозвратов,
	|	ТаблицаОстатковПоДокументамОтгрузки.КоличествоУпаковокОстаток,
	|	ТаблицаОстатковПоДокументамОтгрузки.СуммаВзаиморасчетовОстаток,
	|	ЕСТЬNULL(ВложенныйЗапрос.НалоговаяНакладнаяДляВозвратов, ЕСТЬNULL(ВложенныйЗапрос1.НалоговаяНакладнаяДляПереоценки, НЕОПРЕДЕЛЕНО)) КАК НалоговаяНакладнаяДляВозвратов
	|ИЗ
	|	ТаблицаОстатковПоДокументамОтгрузки КАК ТаблицаОстатковПоДокументамОтгрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВложенныйЗапрос.ДокументПоставкиДляВозвратов КАК ДокументПоставкиДляВозвратов,
	|			МАКСИМУМ(ВЫБОР
	|					КОГДА ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|						ТОГДА НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|					ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.НалоговаяНакладная
	|				КОНЕЦ) КАК НалоговаяНакладнаяДляВозвратов
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставкиДляВозвратов КАК ДокументПоставкиДляВозвратов
	|			ИЗ
	|				ТаблицаОстатковПоДокументамОтгрузки КАК ТаблицаОстатковПоДокументамОтгрузки
	|			ГДЕ
	|				ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставкиДляВозвратов <> НЕОПРЕДЕЛЕНО
	|				И ТаблицаОстатковПоДокументамОтгрузки.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставкиДляВозвратов) КАК ВложенныйЗапрос
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|				ПО ВложенныйЗапрос.ДокументПоставкиДляВозвратов = НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки
	|		ГДЕ
	|			(ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|					ИЛИ ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.Приложение2КНалоговойНакладной))
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВложенныйЗапрос.ДокументПоставкиДляВозвратов) КАК ВложенныйЗапрос
	|		ПО ТаблицаОстатковПоДокументамОтгрузки.ДокументПоставкиДляВозвратов = ВложенныйЗапрос.ДокументПоставкиДляВозвратов
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) КАК НалоговаяНакладнаяДляПереоценки,
	|			ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|			ВложенныйЗапрос.Валюта КАК Валюта
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ТаблицаОстатковПоДокументамОтгрузки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|				ТаблицаОстатковПоДокументамОтгрузки.Валюта КАК Валюта
	|			ИЗ
	|				ТаблицаОстатковПоДокументамОтгрузки КАК ТаблицаОстатковПоДокументамОтгрузки
	|			ГДЕ
	|				ТаблицаОстатковПоДокументамОтгрузки.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)) КАК ВложенныйЗапрос
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|				ПО ВложенныйЗапрос.АналитикаУчетаПоПартнерам = НДСНоменклатурныйСоставДляНалоговыхНакладных.АналитикаУчетаПоПартнерам
	|					И ВложенныйЗапрос.Валюта = НДСНоменклатурныйСоставДляНалоговыхНакладных.Валюта
	|		ГДЕ
	|			ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВложенныйЗапрос.Валюта,
	|			ВложенныйЗапрос.АналитикаУчетаПоПартнерам) КАК ВложенныйЗапрос1
	|		ПО ТаблицаОстатковПоДокументамОтгрузки.АналитикаУчетаПоПартнерам = ВложенныйЗапрос1.АналитикаУчетаПоПартнерам
	|			И ТаблицаОстатковПоДокументамОтгрузки.Валюта = ВложенныйЗапрос1.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Организация КАК Объект
	|ИЗ
	|	ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаНалоговыхНакладных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаРеальныхНалоговыхПоДням
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаПревышенийНОПоДокументамПоДням
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаНалоговыхОбязательств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаОстатковПоДокументамОтгрузки
	|";

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаПоПревышениямНО = РезультатЗапроса[3].Выбрать();
	
	ТаблицаНалоговыхПоДням = РезультатЗапроса[4].Выгрузить();
	ТаблицаНоменклатурыДляЗаполнения = РезультатЗапроса[6].Выгрузить();
	

	ВыборкаДанныхДляПроверкиДатЗапрета = РезультатЗапроса[7].Выбрать();
	
	ТаблицаОграниченийПоДатамЗапрета = ПолучитьТаблицуОграниченийПоДатамЗапрета(ВыборкаДанныхДляПроверкиДатЗапрета, Пользователь, Метаданные.Документы.НалоговаяНакладная.ПолноеИмя());
	
	
	
	ТаблицаНоменклатурыНаСумму = ТаблицаНоменклатурыДляЗаполнения.СкопироватьКолонки();
	ТаблицаНоменклатурыНаСумму.Колонки.Добавить("ВидОперации");
	
	ОтборПоВидуОперации = Новый Структура("ВидОперации, НалоговаяНакладнаяДляВозвратов");
	ОтборПоВидуОперацииДляТаблицыПоНалоговым = Новый Структура("ВидОперацииОсновнойНалоговой, НалоговаяНакладнаяДляВозвратов");
	ОтборПоСтавке = Новый Структура("СтавкаНДС");
	МассивСтрокСНалоговымиЗаДень = Новый Массив;
	ТаблицаСНалоговымиЗаДень = Новый ТаблицаЗначений;
	
	ДатаЗапрета = Неопределено;
	
	ОтборПоСтроке = Новый Структура("Номенклатура, Характеристика, Цена, СтавкаНДС");
	
	СтарыеАналитики = Новый Структура("АналитикаУчетаПоПартнерам, Валюта, РазрезВидПоставки, День");
	
	Пока ВыборкаПоПревышениямНО.Следующий() Цикл
		
		Если АналитикиИзменились(СтарыеАналитики, ВыборкаПоПревышениямНО) Тогда
			
			ЗаполнитьЗначенияСвойств(СтарыеАналитики, ВыборкаПоПревышениямНО);
			
			МассивСтрокСНалоговымиЗаДень = ТаблицаНалоговыхПоДням.НайтиСтроки(СтарыеАналитики);
			
			Если МассивСтрокСНалоговымиЗаДень.Количество() > 0 Тогда
				ТаблицаСНалоговымиЗаДень = ТаблицаНалоговыхПоДням.Скопировать(МассивСтрокСНалоговымиЗаДень, "ВидОперацииОсновнойНалоговой, НалоговаяНакладнаяДляВозвратов, НалоговыйДокументДляКорректировки, ОсновнаяНалоговая");
			Иначе
				ТаблицаСНалоговымиЗаДень = ТаблицаНалоговыхПоДням.СкопироватьКолонки("ВидОперацииОсновнойНалоговой, НалоговаяНакладнаяДляВозвратов, НалоговыйДокументДляКорректировки, ОсновнаяНалоговая");
			КонецЕсли;
			
			ДатаЗапрета = ПолучитьКонечнуюДатуЗапретаИзТаблицы(ТаблицаОграниченийПоДатамЗапрета, СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация, СтарыеАналитики.День);
			
		КонецЕсли;
		
		НайтиСтрокиНаСуммуПревышения(ВыборкаПоПревышениямНО, ТаблицаНоменклатурыДляЗаполнения, ТаблицаНоменклатурыНаСумму);
		
		ТаблицаВидовОпераций = ТаблицаНоменклатурыНаСумму.Скопировать(,"ВидОперации, НалоговаяНакладнаяДляВозвратов");
		ТаблицаВидовОпераций.Свернуть("ВидОперации, НалоговаяНакладнаяДляВозвратов");
		
		Для каждого СтрокаВидаОперации ИЗ ТаблицаВидовОпераций Цикл
			
			ОтборПоВидуОперации.ВидОперации = СтрокаВидаОперации.ВидОперации;
			ОтборПоВидуОперации.НалоговаяНакладнаяДляВозвратов = СтрокаВидаОперации.НалоговаяНакладнаяДляВозвратов;
			ОтборПоВидуОперацииДляТаблицыПоНалоговым.ВидОперацииОсновнойНалоговой = СтрокаВидаОперации.ВидОперации;
			ОтборПоВидуОперацииДляТаблицыПоНалоговым.НалоговаяНакладнаяДляВозвратов = СтрокаВидаОперации.НалоговаяНакладнаяДляВозвратов;
			
			СтрокиПоВидуОперации = ТаблицаНоменклатурыНаСумму.НайтиСтроки(ОтборПоВидуОперации);
			
			
			СтрокиСНалоговыми = ТаблицаСНалоговымиЗаДень.НайтиСтроки(ОтборПоВидуОперацииДляТаблицыПоНалоговым);
			СтрокаСНалоговой = ?(СтрокиСНалоговыми.Количество() > 0, СтрокиСНалоговыми[0], Неопределено);
			
			Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Возврат Тогда
				КоэффициентНаправлениеДвижения = -1;
			Иначе
				КоэффициентНаправлениеДвижения = 1;
			КонецЕсли;
			
			Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Переоценка Тогда
				ЭтоПереоценка = Истина;
			Иначе
				ЭтоПереоценка = Ложь;
			КонецЕсли;
			
/////////////////////////////////////////Отдельной процедурой///////////////////////////////////////////////

			Если СтрокаСНалоговой = Неопределено ИЛИ (ДатаЗапрета <> Неопределено) Тогда
				
				НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("ВидОперации, ДатаЗапрета, НалоговаяНакладнаяДляВозвратов",
																											СтрокаВидаОперации.ВидОперации,
																											ДатаЗапрета,
																											СтрокаВидаОперации.НалоговаяНакладнаяДляВозвратов));
			
				СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
								
				СтрокаНовойНалоговой.ОсновнаяНалоговая = НалоговыйОбъект;
				СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = СтрокаВидаОперации.ВидОперации;
				СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
				СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
				СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
				СтрокаНовойНалоговой.День = СтарыеАналитики.День;
				СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
				СтрокаНовойНалоговой.ДеньДокумента = СтарыеАналитики.День;
				
				МожноЗаписыватьТекущий = Ложь;
			
			ИначеЕсли ЗначениеЗаполнено(СтрокаСНалоговой.НалоговыйДокументДляКорректировки) Тогда
			
				НалоговыйОбъект = СтрокаСНалоговой.НалоговыйДокументДляКорректировки.ПолучитьОбъект();	
				
				НалоговыйОбъект.Статус = Перечисления.СтатусыНалоговыхДокументов.Сформирован;
				
				МожноЗаписыватьТекущий = Истина;
				
			ИначеЕсли ЗначениеЗаполнено(СтрокаСНалоговой.ОсновнаяНалоговая) Тогда
				
				НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("НалоговаяНакладнаяДляВозвратов, ОсновнаяНалоговаяДляП2",
																											СтрокаСНалоговой.НалоговаяНакладнаяДляВозвратов,
																											СтрокаСНалоговой.ОсновнаяНалоговая));
			
				СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
				
				СтрокаНовойНалоговой.ОсновнаяНалоговая = СтрокаСНалоговой.ОсновнаяНалоговая;
				СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = ?(СтрокаСНалоговой.ОсновнаяНалоговая.СпецРежимНалогообложения = 7,
																	  СформироватьВидОперацииПоСтавке7(СтрокаСНалоговой.ОсновнаяНалоговая.ВидОперации),
																	  СтрокаСНалоговой.ОсновнаяНалоговая.ВидОперации);
				СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
				СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
				СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
				СтрокаНовойНалоговой.День = СтарыеАналитики.День;
				СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
				СтрокаНовойНалоговой.ДеньДокумента = ТекущаяДата();
				
				МожноЗаписыватьТекущий = Ложь;
			
			КонецЕсли;

				
			Для каждого СтрокаТовара Из СтрокиПоВидуОперации Цикл
				
				Если СтрокаТовара.СпособЗаполнения = Перечисления.СпособыЗаполненияНоменклатурыНалоговыхДокументов.ТоварыУслуги Тогда
					
					НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
					НоваяСтрока.Цена = СтрокаТовара.ЦенаНН;
					НоваяСтрока.КоличествоУпаковок = КоэффициентНаправлениеДвижения * СтрокаТовара.КоличествоУпаковокОстаток;
					Если ЭтоПереоценка Тогда
						НоваяСтрока.ЭтоКорректировкаКоличества = Ложь;
						НоваяСтрока.СуммаСНДС = ?(НоваяСтрока.Цена < 0, -1, 1) * СтрокаТовара.СуммаВзаиморасчетовОстаток;
					Иначе
						НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СтрокаТовара.СуммаВзаиморасчетовОстаток;
					КонецЕсли;
					
				ИначеЕсли СтрокаТовара.СпособЗаполнения = Перечисления.СпособыЗаполненияНоменклатурыНалоговыхДокументов.УслугиССдержанием Тогда
					
					НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
					НоваяСтрока.Цена = СтрокаТовара.ЦенаНН;
					НоваяСтрока.КоличествоУпаковок = КоэффициентНаправлениеДвижения * СтрокаТовара.КоличествоУпаковокОстаток;
					Если ЭтоПереоценка Тогда
						НоваяСтрока.ЭтоКорректировкаКоличества = Ложь;
						НоваяСтрока.СуммаСНДС = ?(НоваяСтрока.Цена < 0, -1, 1) * СтрокаТовара.СуммаВзаиморасчетовОстаток;
					Иначе
						НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СтрокаТовара.СуммаВзаиморасчетовОстаток;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(ОтборПоСтроке, СтрокаТовара);
                    Если СтрокаТовара.ДокументПоставки.ЦенаВключаетНДС Тогда
                        ПроцентНДС = УчетНДСУПВызовСервера.ЗначениеСтавкиНДС(ОтборПоСтроке.СтавкаНДС) / 100;
						ОтборПоСтроке.Цена = Окр(СтрокаТовара.ЦенаНН * (1 + ПроцентНДС), 2);
					Иначе
						ОтборПоСтроке.Цена = СтрокаТовара.ЦенаНН;
					КонецЕсли;
					
						ТЧДляПоиска = СтрокаТовара.ДокументПоставки.Услуги;
					
					МассивСтрок = ТЧДляПоиска.НайтиСтроки(ОтборПоСтроке);
					
					Если МассивСтрок.Количество() > 0 Тогда
						НоваяСтрока.Содержание = МассивСтрок[0].Содержание;
					КонецЕсли;
					
				ИначеЕсли СтрокаТовара.СпособЗаполнения = Перечисления.СпособыЗаполненияНоменклатурыНалоговыхДокументов.ТоварыПрочее Тогда
					
					ОтборПоСтавке.СтавкаНДС = СтрокаТовара.СтавкаНДС;
					
					ТаблицаСтрокТЧ = СтрокаТовара.ДокументПоставки.Доходы.Выгрузить(ОтборПоСтавке);
					
					СуммаСНДСПолная = ТаблицаСтрокТЧ.Итог("СуммаСНДС");
					
					Для каждого Строка Из ТаблицаСтрокТЧ Цикл
						
						НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
					
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
						
						НоваяСтрока.ОбъектРасчетов = СтрокаТовара.ОбъектРасчетов;
						НоваяСтрока.ДокументПоставки = СтрокаТовара.ДокументПоставки;
						
						НоваяСтрока.КоличествоУпаковок = КоэффициентНаправлениеДвижения * ((Строка.Количество * СтрокаТовара.СуммаВзаиморасчетовОстаток) / СуммаСНДСПолная);
						НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * ((Строка.СуммаСНДС * СтрокаТовара.СуммаВзаиморасчетовОстаток) / СуммаСНДСПолная);
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если МожноЗаписыватьТекущий Тогда
				НалоговыйОбъект.ЗаполнитьРеквизитыПослеАвтоформирования();
				НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			
			Если СтрокаСНалоговой = Неопределено ИЛИ (ДатаЗапрета <> Неопределено) Тогда
				НоваяСтрока = ТаблицаСНалоговымиЗаДень.Добавить();
				НоваяСтрока.ВидОперацииОсновнойНалоговой = ?(НалоговыйОбъект.СпецРежимНалогообложения = 7,
														     СформироватьВидОперацииПоСтавке7(НалоговыйОбъект.ВидОперации),
															 НалоговыйОбъект.ВидОперации);
				НоваяСтрока.НалоговыйДокументДляКорректировки = НалоговыйОбъект.Ссылка;
				НоваяСтрока.ОсновнаяНалоговая = НалоговыйОбъект.Ссылка;
			ИначеЕсли ЗначениеЗаполнено(СтрокаСНалоговой.ОсновнаяНалоговая) Тогда
				СтрокаСНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект.Ссылка;
			КонецЕсли;
			
			
		КонецЦикла;
		
		
	КонецЦикла;
	
#КонецОбласти // Этап4

#Область Этап5 

	
	ЗапросАванс = Новый Запрос;
	ЗапросАванс.Текст = "
	|ВЫБРАТЬ
	|	НДСРасчетНалоговыхОбязательствОбороты.АналитикаУчетаПоПартнерам,
	|	НДСРасчетНалоговыхОбязательствОбороты.ОбъектРасчетов,
	|	НДСРасчетНалоговыхОбязательствОбороты.СтавкаНДС,
	|	НДСРасчетНалоговыхОбязательствОбороты.Валюта,
	|	СУММА(НДСРасчетНалоговыхОбязательствОбороты.СуммаПоставкиТребующаяРегистрацииННОборот) КАК СуммаВозникшихНОЗаДень,
	|	НАЧАЛОПЕРИОДА(НДСРасчетНалоговыхОбязательствОбороты.Период, ДЕНЬ) КАК День,
	|	ВЫБОР
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК РазрезВидПоставки
	|ПОМЕСТИТЬ ТаблицаНалоговыхОбязательств
	|ИЗ
	|	РегистрНакопления.НДСРасчетНалоговыхОбязательств.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			День,
	|			(&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСРасчетНалоговыхОбязательствОбороты
	|ГДЕ
	|	НДСРасчетНалоговыхОбязательствОбороты.ДокументПоставки = НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСРасчетНалоговыхОбязательствОбороты.Валюта,
	|	НДСРасчетНалоговыхОбязательствОбороты.ОбъектРасчетов,
	|	НДСРасчетНалоговыхОбязательствОбороты.СтавкаНДС,
	|	НДСРасчетНалоговыхОбязательствОбороты.АналитикаУчетаПоПартнерам,
	|	НАЧАЛОПЕРИОДА(НДСРасчетНалоговыхОбязательствОбороты.Период, ДЕНЬ),
	|	ВЫБОР
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		КОГДА НДСРасчетНалоговыхОбязательствОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ) КАК День,
	|	МАКСИМУМ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) КАК ОсновнаяНалоговая,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ КАК ВидОперацииОсновнойНалоговой,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ КАК РазрезВидПоставки
	|ПОМЕСТИТЬ ТаблицаНалоговыхНакладных
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			(&НеИспользоватьОтборПоАналитикеУчетаПоПартнерам ИЛИ АналитикаУчетаПоПартнерам В (&АналитикаУчетаПоПартнерамОтбор))
	|	) КАК НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты
	|ГДЕ
	|	(ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|			ИЛИ ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор) = ТИП(Документ.Приложение2КНалоговойНакладной)
	|				И (НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|					ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.АналитикаУчетаПоПартнерам,
	|	НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Валюта,
	|	НАЧАЛОПЕРИОДА(НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Период, ДЕНЬ),
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации)
	|			    ТОГДА ""07_ОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
	|			    ТОГДА ""07_ОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
	|			    ТОГДА ""07_НеНДСОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации)
	|			    ТОГДА ""07_ИтоговаяРозницаОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа)
	|			    ТОГДА ""07_УсловнаяПродажа""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОблагаемыеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации)
	|			    ТОГДА ""07_РозницаКонрагентуОсвобожденныеОперации""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента)
	|			    ТОГДА ""07_РаботыОтНерезидента""
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.СпецРежимНалогообложения = 7
	|            И НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|			    ТОГДА ""07_ПродажаНижеОбычнойЦены""
	|		ИНАЧЕ НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.Регистратор.ВидОперации
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|		КОГДА НДСНоменклатурныйСоставДляНалоговыхНакладныхОбороты.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Поставка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	ЕСТЬNULL(ВложенныйЗапрос.ТекущийНалоговыйДокумент, ТаблицаНалоговыхНакладных.ОсновнаяНалоговая) КАК ТекущийНалоговыйДокумент,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА ВложенныйЗапрос.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница) 
	|			ТОГДА ВложенныйЗапрос.СтавкаНДС 
	|		ИНАЧЕ Значение(Справочник.СтавкиНДС.ПустаяСсылка) 
	|	КОНЕЦ КАК СтавкаНДСНО,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.ЦенаНН,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ВложенныйЗапрос.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ТаблицаРеальныхНалоговыхПоДням
	|ИЗ
	|	ТаблицаНалоговыхНакладных КАК ТаблицаНалоговыхНакладных
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|			ТаблицаНалоговыхНакладных.Валюта КАК Валюта,
	|			ТаблицаНалоговыхНакладных.День КАК День,
	|			ТаблицаНалоговыхНакладных.РазрезВидПоставки КАК РазрезВидПоставки,
	|			ТаблицаНалоговыхНакладных.ОсновнаяНалоговая КАК ОсновнаяНалоговая,
	|			ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой КАК ВидОперацииОсновнойНалоговой,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор КАК ТекущийНалоговыйДокумент,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС КАК СтавкаНДС,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД КАК НомерГТД,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура КАК Номенклатура,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика КАК Характеристика,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка КАК Упаковка,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН КАК ЦенаНН,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов КАК ОбъектРасчетов,
	|			СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.КоличествоУпаковок) КАК КоличествоУпаковок,
	|			СУММА(НДСНоменклатурныйСоставДляНалоговыхНакладных.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|		ИЗ
	|			ТаблицаНалоговыхНакладных КАК ТаблицаНалоговыхНакладных
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|				ПО (ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор
	|						ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.НалоговаяНакладная
	|							И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|							И НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки <> ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|						ИЛИ ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор.ОсновноеП2ДляВозврата
	|							И (НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|								ИЛИ НДСНоменклатурныйСоставДляНалоговыхНакладных.ВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)))
	|		ГДЕ
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки = НЕОПРЕДЕЛЕНО
	|		
	|		СГРУППИРОВАТЬ ПО
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов,
	|			ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|			ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|			ТаблицаНалоговыхНакладных.День,
	|			ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|			ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|			ТаблицаНалоговыхНакладных.Валюта,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.СтавкаНДС,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.НомерГТД,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Номенклатура,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Характеристика,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.Упаковка,
	|			НДСНоменклатурныйСоставДляНалоговыхНакладных.ЦенаНН) КАК ВложенныйЗапрос
	|		ПО ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам = ВложенныйЗапрос.АналитикаУчетаПоПартнерам
	|			И ТаблицаНалоговыхНакладных.Валюта = ВложенныйЗапрос.Валюта
	|			И ТаблицаНалоговыхНакладных.День = ВложенныйЗапрос.День
	|			И ТаблицаНалоговыхНакладных.РазрезВидПоставки = ВложенныйЗапрос.РазрезВидПоставки
	|			И ТаблицаНалоговыхНакладных.ОсновнаяНалоговая = ВложенныйЗапрос.ОсновнаяНалоговая
	|			И ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой = ВложенныйЗапрос.ВидОперацииОсновнойНалоговой
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.НомерГТД,
	|	ТаблицаНалоговыхНакладных.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Упаковка,
	|	ТаблицаНалоговыхНакладных.ВидОперацииОсновнойНалоговой,
	|	ТаблицаНалоговыхНакладных.Валюта,
	|	ЕСТЬNULL(ВложенныйЗапрос.ТекущийНалоговыйДокумент, ТаблицаНалоговыхНакладных.ОсновнаяНалоговая),
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ТаблицаНалоговыхНакладных.День,
	|	ТаблицаНалоговыхНакладных.РазрезВидПоставки,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА ВложенныйЗапрос.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Розница) 
	|			ТОГДА ВложенныйЗапрос.СтавкаНДС 
	|		ИНАЧЕ Значение(Справочник.СтавкиНДС.ПустаяСсылка) 
	|	КОНЕЦ,
	|	ТаблицаНалоговыхНакладных.ОсновнаяНалоговая,
	|	ВложенныйЗапрос.ЦенаНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0) КАК СуммаВзаиморасчетов,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) КАК СуммаВозникшихНОЗаДень,
	|	ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0) - ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) КАК СуммаПревышения,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.АналитикаУчетаПоПартнерам, ВложенныйЗапрос.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.ОбъектРасчетов, ВложенныйЗапрос.ОбъектРасчетов) КАК ОбъектРасчетов,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СтавкаНДС, ВложенныйЗапрос.СтавкаНДСНО) КАК СтавкаНДС,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.Валюта, ВложенныйЗапрос.Валюта) КАК Валюта,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.День, ВложенныйЗапрос.День) КАК День,
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.РазрезВидПоставки, ВложенныйЗапрос.РазрезВидПоставки) КАК РазрезВидПоставки
	|ПОМЕСТИТЬ ТаблицаПревышенийНОПоДокументамПоДням
	|ИЗ
	|	ТаблицаНалоговыхОбязательств КАК ТаблицаНалоговыхОбязательств
	|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|			ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|			ТаблицаРеальныхНалоговыхПоДням.СтавкаНДСНО КАК СтавкаНДСНО,
	|			ТаблицаРеальныхНалоговыхПоДням.Валюта КАК Валюта,
	|			ТаблицаРеальныхНалоговыхПоДням.День КАК День,
	|			ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|			СУММА(ТаблицаРеальныхНалоговыхПоДням.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|		ИЗ
	|			ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаРеальныхНалоговыхПоДням.День,
	|			ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|			ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|			ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|			ТаблицаРеальныхНалоговыхПоДням.СтавкаНДСНО,
	|			ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов) КАК ВложенныйЗапрос
	|		ПО (ВложенныйЗапрос.АналитикаУчетаПоПартнерам = ТаблицаНалоговыхОбязательств.АналитикаУчетаПоПартнерам)
	|			И (ВложенныйЗапрос.ОбъектРасчетов = ТаблицаНалоговыхОбязательств.ОбъектРасчетов)
	|			И (ВложенныйЗапрос.СтавкаНДСНО = ТаблицаНалоговыхОбязательств.СтавкаНДС)
	|			И (ВложенныйЗапрос.Валюта = ТаблицаНалоговыхОбязательств.Валюта)
	|			И (ВложенныйЗапрос.День = ТаблицаНалоговыхОбязательств.День)
	|			И (ВложенныйЗапрос.РазрезВидПоставки = ТаблицаНалоговыхОбязательств.РазрезВидПоставки)
	|ГДЕ
	|	ЕСТЬNULL(ТаблицаНалоговыхОбязательств.СуммаВозникшихНОЗаДень, 0) <> ЕСТЬNULL(ВложенныйЗапрос.СуммаВзаиморасчетов, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ВложенныйЗапрос.Валюта,
	|	МАКСИМУМ(ЕСТЬNULL(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор, НЕОПРЕДЕЛЕНО)) КАК НалоговаяНакладнаяДляВозвратов
	|ПОМЕСТИТЬ АналитикаСНалоговымиНаВозврат
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|		ТаблицаПревышенийНОПоДокументамПоДням.Валюта КАК Валюта
	|	ИЗ
	|		ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|	ГДЕ
	|		ТаблицаПревышенийНОПоДокументамПоДням.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Возврат)
	|		ИЛИ ТаблицаПревышенийНОПоДокументамПоДням.РазрезВидПоставки = ЗНАЧЕНИЕ(Перечисление.ВидыПоставки.Переоценка)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаПревышенийНОПоДокументамПоДням.Валюта,
	|		ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам,
	|		ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов
	|	) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|		ПО ВложенныйЗапрос.АналитикаУчетаПоПартнерам = НДСНоменклатурныйСоставДляНалоговыхНакладных.АналитикаУчетаПоПартнерам
	|			И ВложенныйЗапрос.ОбъектРасчетов = НДСНоменклатурныйСоставДляНалоговыхНакладных.ОбъектРасчетов
	|			И ВложенныйЗапрос.Валюта = НДСНоменклатурныйСоставДляНалоговыхНакладных.Валюта
	|ГДЕ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки = НЕОПРЕДЕЛЕНО
	|	И ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВЫБОР
	|		КОГДА ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОБъектовРасчетов.Заказ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОбъектРасчетовЭтоЗаказ,
	|	ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов.Объект КАК ОбъектРасчетовОбъект,			   
	|	ТаблицаПревышенийНОПоДокументамПоДням.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПревышенийНОПоДокументамПоДням.Валюта КАК Валюта,
	|	ТаблицаПревышенийНОПоДокументамПоДням.День КАК День,
	|	ТаблицаПревышенийНОПоДокументамПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|	МАКСИМУМ(ТаблицаПревышенийНОПоДокументамПоДням.СуммаПревышения) КАК СуммаПревышения,
	|	ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая,
	|	ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент.РазрешенаАвтоКорректировка
	|				ТОГДА ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ) КАК НалоговыйДокументДляКорректировки,
	|	ЕСТЬNULL(ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая.НалоговаяНакладная, ЕСТЬNULL(АналитикаСНалоговымиНаВозврат.НалоговаяНакладнаяДляВозвратов, НЕОПРЕДЕЛЕНО)) КАК НалоговаяНакладнаяДляВозвратов
	|ИЗ
	|	ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|			ТаблицаРеальныхНалоговыхПоДням.Валюта КАК Валюта,
	|			ТаблицаРеальныхНалоговыхПоДням.День КАК День,
	|			ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки КАК РазрезВидПоставки,
	|			ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая КАК ОсновнаяНалоговая,
	|			ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой КАК ВидОперацииОсновнойНалоговой,
	|			ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент КАК ТекущийНалоговыйДокумент
	|		ИЗ
	|			ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаРеальныхНалоговыхПоДням.День,
	|			ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|			ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|			ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|			ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая,
	|			ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой,
	|			ТаблицаРеальныхНалоговыхПоДням.ТекущийНалоговыйДокумент) КАК ТаблицаРеальныхНалоговыхПоДням
	|		ПО ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам = ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам
	|			И ТаблицаПревышенийНОПоДокументамПоДням.Валюта = ТаблицаРеальныхНалоговыхПоДням.Валюта
	|			И ТаблицаПревышенийНОПоДокументамПоДням.День = ТаблицаРеальныхНалоговыхПоДням.День
	|			И ТаблицаПревышенийНОПоДокументамПоДням.РазрезВидПоставки = ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки
	|		ЛЕВОЕ СОЕДИНЕНИЕ АналитикаСНалоговымиНаВозврат КАК АналитикаСНалоговымиНаВозврат
	|		ПО ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам = АналитикаСНалоговымиНаВозврат.АналитикаУчетаПоПартнерам
	|			И ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов = АналитикаСНалоговымиНаВозврат.ОбъектРасчетов
	|			И ТаблицаПревышенийНОПоДокументамПоДням.Валюта = АналитикаСНалоговымиНаВозврат.Валюта
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов,
	|	ТаблицаПревышенийНОПоДокументамПоДням.СтавкаНДС,
	|	ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая,
	|	ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам,
	|	ТаблицаПревышенийНОПоДокументамПоДням.Валюта,
	|	ТаблицаПревышенийНОПоДокументамПоДням.День,
	|	ТаблицаПревышенийНОПоДокументамПоДням.РазрезВидПоставки,
	|	ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой,
	|	ЕСТЬNULL(ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая.НалоговаяНакладная, ЕСТЬNULL(АналитикаСНалоговымиНаВозврат.НалоговаяНакладнаяДляВозвратов, НЕОПРЕДЕЛЕНО))
	|
	|УПОРЯДОЧИТЬ ПО
	|	День,
	|	РазрезВидПоставки,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта,
	|	ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|	ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов,
	|	ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|	ТаблицаРеальныхНалоговыхПоДням.День,
	|	ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|	ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая,
	|	ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой,
	|	ТаблицаРеальныхНалоговыхПоДням.СтавкаНДС,
	|	ТаблицаРеальныхНалоговыхПоДням.НомерГТД,
	|	ТаблицаРеальныхНалоговыхПоДням.Номенклатура,
	|	ТаблицаРеальныхНалоговыхПоДням.Характеристика,
	|	ТаблицаРеальныхНалоговыхПоДням.Упаковка,
	|	ТаблицаРеальныхНалоговыхПоДням.ЦенаНН КАК Цена,
	|	ЕСТЬNULL(СУММА(ТаблицаРеальныхНалоговыхПоДням.КоличествоУпаковок), 0) КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СУММА(ТаблицаРеальныхНалоговыхПоДням.СуммаВзаиморасчетов), 0) КАК СуммаСНДС
	|ИЗ
	|	ТаблицаРеальныхНалоговыхПоДням КАК ТаблицаРеальныхНалоговыхПоДням
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРеальныхНалоговыхПоДням.День,
	|	ТаблицаРеальныхНалоговыхПоДням.РазрезВидПоставки,
	|	ТаблицаРеальныхНалоговыхПоДням.АналитикаУчетаПоПартнерам,
	|	ТаблицаРеальныхНалоговыхПоДням.ОбъектРасчетов,
	|	ТаблицаРеальныхНалоговыхПоДням.Валюта,
	|	ТаблицаРеальныхНалоговыхПоДням.СтавкаНДС,
	|	ТаблицаРеальныхНалоговыхПоДням.ОсновнаяНалоговая,
	|	ТаблицаРеальныхНалоговыхПоДням.ВидОперацииОсновнойНалоговой,
	|	ТаблицаРеальныхНалоговыхПоДням.Номенклатура,
	|	ТаблицаРеальныхНалоговыхПоДням.Характеристика,
	|	ТаблицаРеальныхНалоговыхПоДням.НомерГТД,
	|	ТаблицаРеальныхНалоговыхПоДням.Упаковка,
	|	ТаблицаРеальныхНалоговыхПоДням.ЦенаНН
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Организация КАК Объект
	|ИЗ
	|	ТаблицаПревышенийНОПоДокументамПоДням КАК ТаблицаПревышенийНОПоДокументамПоДням
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(ТаблицаПревышенийНОПоДокументамПоДням.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКлиентовОстатки.ЗаказКлиента,
	|	ЗаказыКлиентовОстатки.Номенклатура,
	|	ЗаказыКлиентовОстатки.Характеристика,
	|	ЗаказыКлиентовОстатки.КодСтроки,
	|	СУММА(ЗаказыКлиентовОстатки.ЗаказаноОстаток) КАК ЗаказаноОстаток,
	|	СУММА(ЗаказыКлиентовОстатки.СуммаОстаток) КАК СуммаОстаток
	|ПОМЕСТИТЬ ОстаткиПоЗаказам
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов.Остатки(
	|			,
	|			ЗаказКлиента В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов.Объект
	|				ИЗ
	|					ТаблицаПревышенийНОПоДокументамПоДням
	|				ГДЕ
	|					ТаблицаПревышенийНОПоДокументамПоДням.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОБъектовРасчетов.Заказ))) КАК ЗаказыКлиентовОстатки
	|
	|ГДЕ
	|	ЗаказыКлиентовОстатки.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ НЕ ЕСТЬNULL(ЗаказыКлиентовОстатки.ЗаказКлиента.ВернутьМногооборотнуюТару, ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыКлиентовОстатки.ЗаказКлиента,
	|	ЗаказыКлиентовОстатки.Характеристика,
	|	ЗаказыКлиентовОстатки.Номенклатура,
	|	ЗаказыКлиентовОстатки.КодСтроки
	|ИМЕЮЩИЕ СУММА(ЗаказыКлиентовОстатки.ЗаказаноОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКлиентовОстатки.ЗаказКлиента,
	|	ЗаказКлиентаТовары.Номенклатура,
	|	ЗаказКлиентаТовары.Содержание КАК Содержание,	
	|	ЗаказКлиентаТовары.Номенклатура.НоменклатураГТД КАК НомерГТД,
	|	ЗаказКлиентаТовары.Номенклатура.ВестиУчетПоГТД КАК ВестиУчетПоГТД,
	|	ЗаказКлиентаТовары.Характеристика,
	|	ЗаказКлиентаТовары.Упаковка,
	|	ЗаказКлиентаТовары.СтавкаНДС,
	|	(ЗаказКлиентаТовары.СуммаСНДС - ЗаказКлиентаТовары.СуммаНДС) / ЗаказКлиентаТовары.КоличествоУпаковок КАК ЦенаНН,
	|	ЗаказыКлиентовОстатки.ЗаказаноОстаток / ЕСТЬNULL(&ЗаказКлиентаТоварыКоэффициентУпаковки, 1) КАК КоличествоУпаковокОстаток,
	|	ЗаказыКлиентовОстатки.СуммаОстаток
	|ИЗ
	|	ОстаткиПоЗаказам КАК ЗаказыКлиентовОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ПО ЗаказыКлиентовОстатки.ЗаказКлиента = ЗаказКлиентаТовары.Ссылка
	|			И ЗаказыКлиентовОстатки.Номенклатура = ЗаказКлиентаТовары.Номенклатура
	|			И ЗаказыКлиентовОстатки.Характеристика = ЗаказКлиентаТовары.Характеристика
	|			И ЗаказыКлиентовОстатки.КодСтроки = ЗаказКлиентаТовары.КодСтроки
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ЗаказыКлиентовОстатки.ЗаказКлиента) = ТИП(Документ.ЗаказКлиента)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыКлиентовОстатки.ЗаказКлиента,
	|	ЗаявкаЗаменяющиеТовары.Номенклатура,
	|	ЗаявкаЗаменяющиеТовары.Содержание,
	|	ЗаявкаЗаменяющиеТовары.Номенклатура.НоменклатураГТД,
	|	ЗаявкаЗаменяющиеТовары.Номенклатура.ВестиУчетПоГТД,
	|	ЗаявкаЗаменяющиеТовары.Характеристика,
	|	ЗаявкаЗаменяющиеТовары.Упаковка,
	|	ЗаявкаЗаменяющиеТовары.СтавкаНДС,
	|	(ЗаявкаЗаменяющиеТовары.СуммаСНДС - ЗаявкаЗаменяющиеТовары.СуммаНДС) / ЗаявкаЗаменяющиеТовары.КоличествоУпаковок КАК ЦенаНН,
	|	ЗаказыКлиентовОстатки.ЗаказаноОстаток / ЕСТЬNULL(&ЗаявкаЗаменяющиеТоварыКоэффициентУпаковки, 1) КАК КоличествоУпаковокОстаток,
	|	ЗаказыКлиентовОстатки.СуммаОстаток
	|ИЗ
	|	ОстаткиПоЗаказам КАК ЗаказыКлиентовОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК ЗаявкаЗаменяющиеТовары
	|		ПО ЗаказыКлиентовОстатки.ЗаказКлиента = ЗаявкаЗаменяющиеТовары.Ссылка
	|			И ЗаказыКлиентовОстатки.Номенклатура = ЗаявкаЗаменяющиеТовары.Номенклатура
	|			И ЗаказыКлиентовОстатки.Характеристика = ЗаявкаЗаменяющиеТовары.Характеристика
	|			И ЗаказыКлиентовОстатки.КодСтроки = ЗаявкаЗаменяющиеТовары.КодСтроки
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ЗаказыКлиентовОстатки.ЗаказКлиента) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента)";
	
	
	ЗапросАванс.УстановитьПараметр("НачалоПериода", Запрос.Параметры.НачалоПериода);
	ЗапросАванс.УстановитьПараметр("КонецПериода", Запрос.Параметры.КонецПериода);
	
	ЗапросАванс.УстановитьПараметр("НеИспользоватьОтборПоАналитикеУчетаПоПартнерам", Запрос.Параметры.НеИспользоватьОтборПоАналитикеУчетаПоПартнерам);
	ЗапросАванс.УстановитьПараметр("АналитикаУчетаПоПартнерамОтбор", Запрос.Параметры.АналитикаУчетаПоПартнерамОтбор);
	
	ЗапросАванс.Текст = СтрЗаменить(ЗапросАванс.Текст,
		"&ЗаказКлиентаТоварыКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЗаказКлиентаТовары.Упаковка",
		"ЗаказКлиентаТовары.Номенклатура"));
		
	ЗапросАванс.Текст = СтрЗаменить(ЗапросАванс.Текст,
		"&ЗаявкаЗаменяющиеТоварыКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЗаявкаЗаменяющиеТовары.Упаковка",
		"ЗаявкаЗаменяющиеТовары.Номенклатура"));
	
	
	РезультатЗапроса = ЗапросАванс.ВыполнитьПакет();
	
	ВыборкаПоПревышениям = РезультатЗапроса[5].Выбрать();
	
	ТаблицаНоменклатурыДляОчистки = РезультатЗапроса[6].Выгрузить();
	

	ВыборкаДанныхДляПроверкиДатЗапрета = РезультатЗапроса[7].Выбрать();
	
	ТаблицаОграниченийПоДатамЗапрета = ПолучитьТаблицуОграниченийПоДатамЗапрета(ВыборкаДанныхДляПроверкиДатЗапрета, Пользователь, Метаданные.Документы.НалоговаяНакладная.ПолноеИмя());
	

	ТаблицаЗаказов = РезультатЗапроса[9].Выгрузить();
	
	ОтборПоискаПоЗаказу = Новый Структура("ЗаказКлиента");
	
	ОтборСформированыхНалоговых = Новый Структура("АналитикаУчетаПоПартнерам, Валюта, РазрезВидПоставки, День, ВидОперацииОсновнойНалоговой");

	СтарыеАналитики = Новый Структура("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, РазрезВидПоставки, День, НалоговаяНакладнаяДляВозвратов, СтавкаНДС, ОбъектРасчетовОбъект, ОбъектРасчетовЭтоЗаказ");
	АналитикиБезОбъектРасчетов = Новый Структура("АналитикаУчетаПоПартнерам, Валюта, РазрезВидПоставки, День, НалоговаяНакладнаяДляВозвратов");
	
	ОтборПоСтавкамНДС = Новый Структура("СтавкаНДС");
	ОбъектРасчетовЭтоЗаказ = Ложь;
	СуммаАвансаПослеЗаказа = 0;
	ТаблицаСтрокЗаказа = Неопределено;
	ПогрешностиОкругления = Новый Соответствие();
	ТаблицаВидовОперацийННДляЗаказа = Неопределено;
	ТаблицаДопНалоговыхДляПоиска = Новый ТаблицаЗначений;//"АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, День, ВидОперацииОсновнойНалоговой"
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("ОсновнаяНалоговая");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("НалоговыйДокументДляКорректировки");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("ВидОперацииОсновнойНалоговой");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("РазрезВидПоставки");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("АналитикаУчетаПоПартнерам");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("ОбъектРасчетов");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("Валюта");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("День");
	ТаблицаДопНалоговыхДляПоиска.Колонки.Добавить("ДеньДокумента");
	
	ИзменилсяТолькоОбъектРасчетов = Ложь;
	
	ДатаЗапрета = Неопределено;
	СуммаПревышенияНО = 0;
	
	Пока ВыборкаПоПревышениям.Следующий() Цикл
		
		Если АналитикиИзменились(СтарыеАналитики, ВыборкаПоПревышениям) = Истина Тогда
			
			ЗаполнитьЗначенияСвойств(АналитикиБезОбъектРасчетов, СтарыеАналитики);
			Если АналитикиИзменились(АналитикиБезОбъектРасчетов, ВыборкаПоПревышениям) = Истина Тогда
				ИзменилсяТолькоОбъектРасчетов = Ложь;
			Иначе
				ИзменилсяТолькоОбъектРасчетов = Истина;
			КонецЕсли;
			
			Если СуммаПревышенияНО > 0 Тогда
				
				Если ОбъектРасчетовЭтоЗаказ Тогда
					
					СтарыйВидОперации = Неопределено;
					
					Для каждого СтрокаВидаОперации из ТаблицаВидовОперацийННДляЗаказа Цикл
						
						Если СтарыйВидОперации <> СтрокаВидаОперации.ВидОперации Тогда
							
							СтарыйВидОперации = СтрокаВидаОперации.ВидОперации;
							
							ЗаполнитьЗначенияСвойств(ОтборСформированыхНалоговых, СтарыеАналитики);
							ОтборСформированыхНалоговых.ВидОперацииОсновнойНалоговой = СтарыйВидОперации;
							НайденныеНалоговые = ТаблицаСформированыхНалоговых.НайтиСтроки(ОтборСформированыхНалоговых);
							
							Если НайденныеНалоговые.Количество() > 0 Тогда
				
								НалоговыйОбъект = НайденныеНалоговые[0].НалоговыйДокументДляКорректировки;
							
							Иначе
								
								НайденныеНалоговые = ТаблицаДопНалоговыхДляПоиска.НайтиСтроки(ОтборСформированыхНалоговых);
								
								Если НайденныеНалоговые.Количество() > 0 Тогда
				
									НалоговыйОбъект = НайденныеНалоговые[0].НалоговыйДокументДляКорректировки;
									
								Иначе
									
									НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("ВидОперации, ДатаЗапрета",
																																СтарыйВидОперации,
																																ДатаЗапрета));
									
									СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
													
									СтрокаНовойНалоговой.ОсновнаяНалоговая = НалоговыйОбъект;
									СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = СтарыйВидОперации;
									СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
									СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
									СтрокаНовойНалоговой.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
									СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
									СтрокаНовойНалоговой.День = СтарыеАналитики.День;
									СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
									СтрокаНовойНалоговой.ДеньДокумента = СтарыеАналитики.День;
									
									МожноЗаписыватьТекущий = Ложь;
									
								КонецЕсли;
								
							КонецЕсли;
								
						КонецЕсли;
						
						ОтборПоСтавкамНДС.СтавкаНДС = СтрокаВидаОперации.СтавкаНДС;
						СтрокиДляЗаполненияПоВидуОперации = ТаблицаСтрокЗаказа.НайтиСтроки(ОтборПоСтавкамНДС);
						
						Для каждого СтрокаДляЗаполнения Из СтрокиДляЗаполненияПоВидуОперации Цикл
							
							НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
							
							НоваяСтрока.Номенклатура = СтрокаДляЗаполнения.Номенклатура;
							НоваяСтрока.Содержание = СтрокаДляЗаполнения.Содержание;
							НоваяСтрока.Характеристика = СтрокаДляЗаполнения.Характеристика;
							НоваяСтрока.Упаковка = СтрокаДляЗаполнения.Упаковка;
							НоваяСтрока.СтавкаНДС = СтрокаДляЗаполнения.СтавкаНДС;
							Если СтрокаДляЗаполнения.ВестиУчетПоГТД Тогда
								НоваяСтрока.НомерГТД = СтрокаДляЗаполнения.НомерГТД;
							КонецЕсли; 
							НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
							
							НоваяСтрока.КоличествоУпаковок = СтрокаДляЗаполнения.КоличествоУпаковокОстаток;
							НоваяСтрока.СуммаСНДС = СтрокаДляЗаполнения.СуммаПревышенияПострочно;
							НоваяСтрока.Цена = СтрокаДляЗаполнения.ЦенаНН;
							
						КонецЦикла;
						
					КонецЦикла;
					
					Если НЕ НалоговыйОбъект.ЭтоНовый() Тогда
						НалоговыйОбъект.ЗаполнитьРеквизитыПослеАвтоформирования();
						НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
					КонецЕсли;
					
				Иначе
				
					Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
						НоменклатураДляРозницы = СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация.НоменклатураЗаполненияИтоговыхНалоговыхПоРознице;
						ВидОперацииДляЗаполнения = ПолучитьВидОперации(СтарыеАналитики.ОбъектРасчетов, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
					Иначе
						НоменклатураДляАвансовыхНН = ОпределитьНомеклатуруЗаполненияНалоговыхНаАванс(НалоговыйОбъект.Организация, НалоговыйОбъект.Договор,  КешНоменклатурыДляАвансовыхНН);
						ВидОперацииДляЗаполнения = ПолучитьВидОперации(НоменклатураДляАвансовыхНН.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
					КонецЕсли;
					
					Если НЕ ИзменилсяТолькоОбъектРасчетов Тогда
						НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("ВидОперации, ДатаЗапрета",
																													ВидОперацииДляЗаполнения,
																													ДатаЗапрета));
					КонецЕсли;
					
					НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
					
					Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
						
						НоваяСтрока.Номенклатура = НоменклатураДляРозницы;
						НоваяСтрока.СтавкаНДС = СтарыеАналитики.ОбъектРасчетов;
						Если НоменклатураДляРозницы.ВестиУчетПоГТД Тогда
							НоваяСтрока.НомерГТД = НоменклатураДляРозницы.НоменклатураГТД;
						КонецЕсли; 
						НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
						
						НоваяСтрока.КоличествоУпаковок = 0;
						НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СуммаПревышенияНО;
						НоваяСтрока.Цена = 0;
						
					Иначе
						
						НоваяСтрока.Номенклатура = НоменклатураДляАвансовыхНН;
						НоваяСтрока.СтавкаНДС = НоменклатураДляАвансовыхНН.СтавкаНДС;
						Если НоменклатураДляАвансовыхНН.ВестиУчетПоГТД Тогда
							НоваяСтрока.НомерГТД = НоменклатураДляАвансовыхНН.НоменклатураГТД;
						КонецЕсли;
						НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
						
						НоваяСтрока.КоличествоУпаковок = КоэффициентНаправлениеДвижения;
						НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СуммаПревышенияНО;
						НоваяСтрока.Цена = ПолучитьЦенуДляНН(СуммаПревышенияНО, НоменклатураДляАвансовыхНН.СтавкаНДС);
						
					КонецЕсли;
					
					
					СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
									
					СтрокаНовойНалоговой.ОсновнаяНалоговая = НалоговыйОбъект;
					СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = ВидОперацииДляЗаполнения;
					СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
					СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
					СтрокаНовойНалоговой.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
					СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
					СтрокаНовойНалоговой.День = СтарыеАналитики.День;
					СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
					СтрокаНовойНалоговой.ДеньДокумента = СтарыеАналитики.День;
					
					МожноЗаписыватьТекущий = Ложь;

				КонецЕсли;

			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СтарыеАналитики, ВыборкаПоПревышениям);
			
			ЗаполнитьЗначенияСвойств(ОтборСформированыхНалоговых, СтарыеАналитики);

			ДатаЗапрета = ПолучитьКонечнуюДатуЗапретаИзТаблицы(ТаблицаОграниченийПоДатамЗапрета, СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация, СтарыеАналитики.День);
			
			//МассивСтрокСНалоговымиЗаДень = ТаблицаНалоговыхПоДням.НайтиСтроки(СтарыеАналитики);
			СуммаПревышенияНО = - ВыборкаПоПревышениям.СуммаПревышения;
			
			Если СтарыеАналитики.ОбъектРасчетовЭтоЗаказ	И СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Поставка И СуммаПревышенияНО > 0 Тогда
				ОбъектРасчетовЭтоЗаказ = Истина;
				ОтборПоискаПоЗаказу.ЗаказКлиента = СтарыеАналитики.ОбъектРасчетовОбъект;
				ТаблицаСтрокЗаказа = ТаблицаЗаказов.Скопировать(ОтборПоискаПоЗаказу);
				ТаблицаВидовОперацийННДляЗаказа = ТаблицаСтрокЗаказа.Скопировать();
				ТаблицаВидовОперацийННДляЗаказа.Свернуть("СтавкаНДС");
				ТаблицаВидовОперацийННДляЗаказа.Колонки.Добавить("ВидОперации");
				Для каждого Строка из ТаблицаВидовОперацийННДляЗаказа Цикл
					Строка.ВидОперации = ПолучитьВидОперации(Строка.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
				КонецЦикла;
				ТаблицаСтрокЗаказа.Колонки.Добавить("СуммаПревышенияПострочно");
				СуммаЗаказа = ТаблицаСтрокЗаказа.Итог("СуммаОстаток");
				СуммаАвансаПослеЗаказа = ?(СуммаПревышенияНО - СуммаЗаказа < 0, 0, СуммаПревышенияНО - СуммаЗаказа);
				ПогрешностиОкругления.Очистить();
				Для каждого Строка из ТаблицаСтрокЗаказа Цикл
					Если СуммаАвансаПослеЗаказа = 0 Тогда
						Строка.СуммаПревышенияПострочно = ОкруглитьСУчетомПогрешности(Строка.СуммаОстаток * СуммаПревышенияНО / СуммаЗаказа, 2, , ПогрешностиОкругления, "Сумма");
						Строка.КоличествоУпаковокОстаток = ОкруглитьСУчетомПогрешности(Строка.КоличествоУпаковокОстаток * СуммаПревышенияНО / СуммаЗаказа, 3, , ПогрешностиОкругления, "Количество");
					Иначе
						Строка.СуммаПревышенияПострочно = Строка.СуммаОстаток;
					КонецЕсли;
				КонецЦикла;
				Если СуммаАвансаПослеЗаказа > 0 Тогда
					
					НоменклатураДляАвансовыхНН = ОпределитьНомеклатуруЗаполненияНалоговыхНаАванс(СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация, СтарыеАналитики.АналитикаУчетаПоПартнерам.Договор,  КешНоменклатурыДляАвансовыхНН);
					
					Если ТаблицаВидовОперацийННДляЗаказа.Найти(НоменклатураДляАвансовыхНН.СтавкаНДС, "СтавкаНДС") = Неопределено Тогда
						СтрокаВидаОперации = ТаблицаВидовОперацийННДляЗаказа.Добавить();
						СтрокаВидаОперации.СтавкаНДС = НоменклатураДляАвансовыхНН.СтавкаНДС;
						СтрокаВидаОперации.ВидОперации = ПолучитьВидОперации(НоменклатураДляАвансовыхНН.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
					КонецЕсли;
					
					Строка = ТаблицаСтрокЗаказа.Добавить();
					Строка.Номенклатура = НоменклатураДляАвансовыхНН;
					Строка.Характеристика = Неопределено;
					Строка.Упаковка = Неопределено;
					Строка.СтавкаНДС = НоменклатураДляАвансовыхНН.СтавкаНДС;
					Строка.ВестиУчетПоГТД = НоменклатураДляАвансовыхНН.ВестиУчетПоГТД;
					Если НоменклатураДляАвансовыхНН.ВестиУчетПоГТД Тогда
						Строка.НомерГТД = НоменклатураДляАвансовыхНН.НоменклатураГТД;
					КонецЕсли; 
					
					Строка.КоличествоУпаковокОстаток = 1;
					Строка.СуммаПревышенияПострочно = 1 * СуммаАвансаПослеЗаказа;
					Строка.СуммаОстаток = 1 * СуммаАвансаПослеЗаказа;
					Строка.ЦенаНН = ПолучитьЦенуДляНН(СуммаАвансаПослеЗаказа, НоменклатураДляАвансовыхНН.СтавкаНДС);
					
				КонецЕсли;
				ТаблицаДопНалоговыхДляПоиска.Очистить();
			Иначе
				ОбъектРасчетовЭтоЗаказ = Ложь;
				ТаблицаСтрокЗаказа = Неопределено;
				ТаблицаВидовОперацийННДляЗаказа = Неопределено;
				ТаблицаДопНалоговыхДляПоиска.Очистить();
			КонецЕсли;
			
		КонецЕсли;
		
		// сумма превышения могла обнулиться на предыдущем витке по тем же значениям аналитики, дальнейшие документы корректировать нет необходимости
		Если СуммаПревышенияНО = 0 Тогда
			
			Продолжить;
			
		Иначе
			

			Если ОбъектРасчетовЭтоЗаказ Тогда
				ПропускатьДоОтбора = Истина;
			Иначе
				ПропускатьДоОтбора = Ложь;
			КонецЕсли;

			Если ВыборкаПоПревышениям.ВидОперацииОсновнойНалоговой = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации Тогда
				ПропускатьПоВидуОперации = Истина;
				ВидОперацииОсновнойНалоговой = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации;
			ИначеЕсли ВыборкаПоПревышениям.ВидОперацииОсновнойНалоговой = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации Тогда
				ПропускатьПоВидуОперации = Истина;
				ВидОперацииОсновнойНалоговой = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации;
			ИначеЕсли НЕ ЗначениеЗаполнено(ВыборкаПоПревышениям.ВидОперацииОсновнойНалоговой) Тогда
				Если ОбъектРасчетовЭтоЗаказ И (СуммаАвансаПослеЗаказа > 0) Тогда
					ПропускатьДоОтбора = Ложь;
				КонецЕсли;
				ПропускатьПоВидуОперации = Ложь;
				НоменклатураДляАвансовыхНН = ОпределитьНомеклатуруЗаполненияНалоговыхНаАванс(ВыборкаПоПревышениям.АналитикаУчетаПоПартнерам.Организация, Неопределено, КешНоменклатурыДляАвансовыхНН);
				ВидОперацииОсновнойНалоговой = ПолучитьВидОперации(НоменклатураДляАвансовыхНН.СтавкаНДС, ВыборкаПоПревышениям.РазрезВидПоставки, ВыборкаПоПревышениям.День)
			Иначе
				ПропускатьПоВидуОперации = Ложь;
				ВидОперацииОсновнойНалоговой = ВыборкаПоПревышениям.ВидОперацииОсновнойНалоговой;
				Если ОбъектРасчетовЭтоЗаказ Тогда
					Если НЕ (ТаблицаВидовОперацийННДляЗаказа.Найти(ВидОперацииОсновнойНалоговой, "ВидОперации") = Неопределено) Тогда
						ПропускатьДоОтбора = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			Если ПропускатьДоОтбора Тогда
				Продолжить;
			КонецЕсли;
			
			ОтборСформированыхНалоговых.ВидОперацииОсновнойНалоговой = ВидОперацииОсновнойНалоговой;
			НайденныеНалоговые = ТаблицаСформированыхНалоговых.НайтиСтроки(ОтборСформированыхНалоговых);
			
			Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Возврат Тогда
				КоэффициентНаправлениеДвижения = -1;
			Иначе
				КоэффициентНаправлениеДвижения = 1;
			КонецЕсли;
			
			Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Переоценка Тогда
				ЭтоПереоценка = Истина;
			Иначе
				ЭтоПереоценка = Ложь;
			КонецЕсли;
			
			Если НайденныеНалоговые.Количество() > 0 Тогда
				
				НалоговыйОбъект = НайденныеНалоговые[0].НалоговыйДокументДляКорректировки;

			ИначеЕсли ПропускатьПоВидуОперации Тогда
				
				 Продолжить;

/////////////////////////////////////////Отдельной процедурой///////////////////////////////////////////////
			ИначеЕсли ЗначениеЗаполнено(ВыборкаПоПревышениям.НалоговыйДокументДляКорректировки) И (ДатаЗапрета = Неопределено) Тогда
			
				НалоговыйОбъект = ВыборкаПоПревышениям.НалоговыйДокументДляКорректировки.ПолучитьОбъект();	
				
				НалоговыйОбъект.Статус = Перечисления.СтатусыНалоговыхДокументов.Сформирован;
				
				МожноЗаписыватьТекущий = Истина;

			ИначеЕсли ЗначениеЗаполнено(ВыборкаПоПревышениям.ОсновнаяНалоговая) И (ДатаЗапрета = Неопределено) Тогда
				
				НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("НалоговаяНакладнаяДляВозвратов, ОсновнаяНалоговаяДляП2",
																											ВыборкаПоПревышениям.НалоговаяНакладнаяДляВозвратов,
																											ВыборкаПоПревышениям.ОсновнаяНалоговая));

				СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
								
				СтрокаНовойНалоговой.ОсновнаяНалоговая = ВыборкаПоПревышениям.ОсновнаяНалоговая;
				СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = ВыборкаПоПревышениям.ВидОперацииОсновнойНалоговой;
				СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
				СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
				СтрокаНовойНалоговой.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
				СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
				СтрокаНовойНалоговой.День = СтарыеАналитики.День;
				СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
				СтрокаНовойНалоговой.ДеньДокумента = ТекущаяДата();
				
				МожноЗаписыватьТекущий = Ложь;
			
			Иначе
				
				Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
					НоменклатураДляРозницы = СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация.НоменклатураЗаполненияИтоговыхНалоговыхПоРознице;
					ВидОперацииОсновнойНалоговой = ПолучитьВидОперации(СтарыеАналитики.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
				Иначе
					НоменклатураДляАвансовыхНН = ОпределитьНомеклатуруЗаполненияНалоговыхНаАванс(СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация, СтарыеАналитики.АналитикаУчетаПоПартнерам.Договор, КешНоменклатурыДляАвансовыхНН);
					ВидОперацииОсновнойНалоговой = ПолучитьВидОперации(НоменклатураДляАвансовыхНН.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
				КонецЕсли;

				НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("ВидОперации, ДатаЗапрета",
																											ВидОперацииОсновнойНалоговой,
																											ДатаЗапрета));
			
				СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
								
				СтрокаНовойНалоговой.ОсновнаяНалоговая = НалоговыйОбъект;
				СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = ВидОперацииОсновнойНалоговой;
				СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
				СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
				СтрокаНовойНалоговой.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
				СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
				СтрокаНовойНалоговой.День = СтарыеАналитики.День;
				СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
				СтрокаНовойНалоговой.ДеньДокумента = СтарыеАналитики.День;
				
				МожноЗаписыватьТекущий = Ложь;
			
			КонецЕсли;

			Если ОбъектРасчетовЭтоЗаказ Тогда
				СтрокаНовойНалоговой = ТаблицаДопНалоговыхДляПоиска.Добавить();
								
				СтрокаНовойНалоговой.ОсновнаяНалоговая = НалоговыйОбъект;
				СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = ВидОперацииОсновнойНалоговой;
				СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
				СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
				СтрокаНовойНалоговой.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
				СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
				СтрокаНовойНалоговой.День = СтарыеАналитики.День;
				СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
				СтрокаНовойНалоговой.ДеньДокумента = СтарыеАналитики.День;
				Продолжить;
			КонецЕсли;

			Если СуммаПревышенияНО > 0 Тогда
			
				// Нужно ДОПИСАТЬ строки товаров на абсолютную сумму превышения
				
				Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
					НоменклатураДляРозницы = СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация.НоменклатураЗаполненияИтоговыхНалоговыхПоРознице;
					ВидОперации = ПолучитьВидОперации(СтарыеАналитики.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
				Иначе
					НоменклатураДляАвансовыхНН = ОпределитьНомеклатуруЗаполненияНалоговыхНаАванс(НалоговыйОбъект.Организация, НалоговыйОбъект.Договор, КешНоменклатурыДляАвансовыхНН);
					ВидОперации = ПолучитьВидОперации(НоменклатураДляАвансовыхНН.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
				КонецЕсли;
				
				Если ВидОперации = ВидОперацииОсновнойНалоговой Тогда
					
					НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
					
					Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
						
						НоваяСтрока.Номенклатура = НоменклатураДляРозницы;
						НоваяСтрока.СтавкаНДС = СтарыеАналитики.СтавкаНДС;
						Если НоменклатураДляРозницы.ВестиУчетПоГТД Тогда
							НоваяСтрока.НомерГТД = НоменклатураДляРозницы.НоменклатураГТД;
						КонецЕсли; 
						НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
						
						НоваяСтрока.КоличествоУпаковок = 0;
						НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СуммаПревышенияНО;
						НоваяСтрока.Цена = 0;
						
					Иначе
						
						НоваяСтрока.Номенклатура = НоменклатураДляАвансовыхНН;
						НоваяСтрока.СтавкаНДС = НоменклатураДляАвансовыхНН.СтавкаНДС;
						Если НоменклатураДляАвансовыхНН.ВестиУчетПоГТД Тогда
							НоваяСтрока.НомерГТД = НоменклатураДляАвансовыхНН.НоменклатураГТД;
						КонецЕсли; 
						НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
						
						НоваяСтрока.КоличествоУпаковок = КоэффициентНаправлениеДвижения;
						НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СуммаПревышенияНО;
						НоваяСтрока.Цена = ПолучитьЦенуДляНН(СуммаПревышенияНО, НоменклатураДляАвансовыхНН.СтавкаНДС);
						
					КонецЕсли;
					
					
					СуммаПревышенияНО = 0;
					
				КонецЕсли;
				
			Иначе
				//Если СуммаПревышенияНО < 0 Тогда
				
				НайтиИСторнироватьСтроки(СуммаПревышенияНО, НалоговыйОбъект, ТаблицаНоменклатурыДляОчистки, ВыборкаПоПревышениям.ОбъектРасчетов, ВыборкаПоПревышениям.ОсновнаяНалоговая
											, КоэффициентНаправлениеДвижения, ЭтоПереоценка, ВыборкаПоПревышениям.ВидОперацииОсновнойНалоговой, ВыборкаПоПревышениям.День);
				
			КонецЕсли;
			
			Если МожноЗаписыватьТекущий Тогда
				НалоговыйОбъект.ЗаполнитьРеквизитыПослеАвтоформирования();
				НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			
		КонецЕсли;
		
		
		
	КонецЦикла;
	
	
	Если СуммаПревышенияНО > 0 Тогда
		
		Если ОбъектРасчетовЭтоЗаказ Тогда
					
			СтарыйВидОперации = Неопределено;
			
			Для каждого СтрокаВидаОперации из ТаблицаВидовОперацийННДляЗаказа Цикл
				
				Если СтарыйВидОперации <> СтрокаВидаОперации.ВидОперации Тогда
					
					СтарыйВидОперации = СтрокаВидаОперации.ВидОперации;
							
					ЗаполнитьЗначенияСвойств(ОтборСформированыхНалоговых, СтарыеАналитики);
					ОтборСформированыхНалоговых.ВидОперацииОсновнойНалоговой = СтарыйВидОперации;
					НайденныеНалоговые = ТаблицаСформированыхНалоговых.НайтиСтроки(ОтборСформированыхНалоговых);
					
					Если НайденныеНалоговые.Количество() > 0 Тогда
		
						НалоговыйОбъект = НайденныеНалоговые[0].НалоговыйДокументДляКорректировки;
					
					Иначе
						
						НайденныеНалоговые = ТаблицаДопНалоговыхДляПоиска.НайтиСтроки(ОтборСформированыхНалоговых);
						
						Если НайденныеНалоговые.Количество() > 0 Тогда
		
							НалоговыйОбъект = НайденныеНалоговые[0].НалоговыйДокументДляКорректировки;
							
						Иначе
							
							НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("ВидОперации, ДатаЗапрета",
																														СтарыйВидОперации,
																														ДатаЗапрета));
									
							СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
											
							СтрокаНовойНалоговой.ОсновнаяНалоговая = НалоговыйОбъект;
							СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = СтарыйВидОперации;
							СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
							СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
							СтрокаНовойНалоговой.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
							СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
							СтрокаНовойНалоговой.День = СтарыеАналитики.День;
							СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
							СтрокаНовойНалоговой.ДеньДокумента = СтарыеАналитики.День;
									
						КонецЕсли;
						
					КонецЕсли;
						
				КонецЕсли;
				
				ОтборПоСтавкамНДС.СтавкаНДС = СтрокаВидаОперации.СтавкаНДС;
				СтрокиДляЗаполненияПоВидуОперации = ТаблицаСтрокЗаказа.НайтиСтроки(ОтборПоСтавкамНДС);
				
				Для Каждого СтрокаДляЗаполнения Из СтрокиДляЗаполненияПоВидуОперации Цикл
					
					
					НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
					
					НоваяСтрока.Номенклатура = СтрокаДляЗаполнения.Номенклатура;
					НоваяСтрока.Содержание = СтрокаДляЗаполнения.Содержание;
					НоваяСтрока.Характеристика = СтрокаДляЗаполнения.Характеристика;
					НоваяСтрока.Упаковка = СтрокаДляЗаполнения.Упаковка;
					НоваяСтрока.СтавкаНДС = СтрокаДляЗаполнения.СтавкаНДС;
					Если СтрокаДляЗаполнения.ВестиУчетПоГТД Тогда
						НоваяСтрока.НомерГТД = СтрокаДляЗаполнения.НомерГТД;
					КонецЕсли; 
					НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
					
					НоваяСтрока.КоличествоУпаковок = СтрокаДляЗаполнения.КоличествоУпаковокОстаток;
					НоваяСтрока.СуммаСНДС = СтрокаДляЗаполнения.СуммаПревышенияПострочно;
					НоваяСтрока.Цена = СтрокаДляЗаполнения.ЦенаНН;
					
					
				КонецЦикла;
				
			КонецЦикла;
			
			Если НЕ НалоговыйОбъект.ЭтоНовый() Тогда
				НалоговыйОбъект.ЗаполнитьРеквизитыПослеАвтоформирования();
				НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			
		Иначе
		
			Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
				НоменклатураДляРозницы = СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация.НоменклатураЗаполненияИтоговыхНалоговыхПоРознице;
				ВидОперацииДляЗаполнения = ПолучитьВидОперации(СтарыеАналитики.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
			Иначе
				НоменклатураДляАвансовыхНН = ОпределитьНомеклатуруЗаполненияНалоговыхНаАванс(НалоговыйОбъект.Организация, НалоговыйОбъект.Договор,  КешНоменклатурыДляАвансовыхНН);
				ВидОперацииДляЗаполнения = ПолучитьВидОперации(НоменклатураДляАвансовыхНН.СтавкаНДС, СтарыеАналитики.РазрезВидПоставки, СтарыеАналитики.День);
			КонецЕсли;
			
			Если НЕ ИзменилсяТолькоОбъектРасчетов Тогда
				НалоговыйОбъект = СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, Новый Структура("ВидОперации, ДатаЗапрета",
																											ВидОперацииДляЗаполнения,
																											ДатаЗапрета));
			КонецЕсли;
			
			НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
			
			Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
				
				НоваяСтрока.Номенклатура = НоменклатураДляРозницы;
				НоваяСтрока.СтавкаНДС = СтарыеАналитики.СтавкаНДС;
				Если НоменклатураДляРозницы.ВестиУчетПоГТД Тогда
					НоваяСтрока.НомерГТД = НоменклатураДляРозницы.НоменклатураГТД;
				КонецЕсли; 
				НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
				
				НоваяСтрока.КоличествоУпаковок = 0;
				НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СуммаПревышенияНО;
				НоваяСтрока.Цена = 0;
				
			Иначе
				
				НоваяСтрока.Номенклатура = НоменклатураДляАвансовыхНН;
				НоваяСтрока.СтавкаНДС = НоменклатураДляАвансовыхНН.СтавкаНДС;
				Если НоменклатураДляАвансовыхНН.ВестиУчетПоГТД Тогда
					НоваяСтрока.НомерГТД = НоменклатураДляАвансовыхНН.НоменклатураГТД;
				КонецЕсли; 
				НоваяСтрока.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
				
				НоваяСтрока.КоличествоУпаковок = КоэффициентНаправлениеДвижения;
				НоваяСтрока.СуммаСНДС = КоэффициентНаправлениеДвижения * СуммаПревышенияНО;
				НоваяСтрока.Цена = ПолучитьЦенуДляНН(СуммаПревышенияНО, НоменклатураДляАвансовыхНН.СтавкаНДС);
				
			КонецЕсли;
			
		
			СтрокаНовойНалоговой = ТаблицаСформированыхНалоговых.Добавить();
							
			СтрокаНовойНалоговой.ОсновнаяНалоговая = НалоговыйОбъект;
			СтрокаНовойНалоговой.ВидОперацииОсновнойНалоговой = ВидОперацииДляЗаполнения;
			СтрокаНовойНалоговой.НалоговыйДокументДляКорректировки = НалоговыйОбъект;
			СтрокаНовойНалоговой.АналитикаУчетаПоПартнерам = СтарыеАналитики.АналитикаУчетаПоПартнерам;
			СтрокаНовойНалоговой.ОбъектРасчетов = СтарыеАналитики.ОбъектРасчетов;
			СтрокаНовойНалоговой.Валюта = СтарыеАналитики.Валюта;
			СтрокаНовойНалоговой.День = СтарыеАналитики.День;
			СтрокаНовойНалоговой.РазрезВидПоставки = СтарыеАналитики.РазрезВидПоставки;
			СтрокаНовойНалоговой.ДеньДокумента = СтарыеАналитики.День;
			
			МожноЗаписыватьТекущий = Ложь;

		КонецЕсли;

	КонецЕсли;
	//////////////////////////////////////////////////////////////////////////////////
	
#КонецОбласти // Этап5

	ТаблицаСформированыхНалоговых.Сортировать("ДеньДокумента");

	Для каждого СтрокаНалоговой Из ТаблицаСформированыхНалоговых Цикл
		ПровестиНалоговыйДокумент(СтрокаНалоговой.НалоговыйДокументДляКорректировки);
	КонецЦикла;

	Если ЗначениеЗаполнено(КонецПериодаРасчета) Тогда
		ОбновитьГраницыФормированияНалоговыхДокументов(НачалоПериодаРасчета, КонецПериодаРасчета, АналитикаУчетаПоПартнерамОтбор, НомерЗадания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПровестиНалоговыйДокумент(НалоговыйДокумент)
	
	НалоговыйДокумент.ЗаполнитьРеквизитыПослеАвтоформирования();
	НалоговыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

Функция УвеличитьНомерЗадания(ИмяКонстанты)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Константа."+ИмяКонстанты);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	НомерДоРасчета = Константы[ИмяКонстанты].Получить();
	Константы[ИмяКонстанты].Установить(НомерДоРасчета + 1);
	ЗафиксироватьТранзакцию();
	
	Возврат НомерДоРасчета;
	
КонецФункции

Функция ПолучитьДатуНачалаФормированияНалоговыхДокументов(КонецПериодаРасчета = Неопределено, СтруктураПараметров = Неопределено, АналитикаУчетаПоПартнерамОтбор = Неопределено, НомерЗадания = 0) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СтруктураПараметров = Неопределено Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли;
	
	Организация = Неопределено; Партнер = Неопределено; Контрагент = Неопределено;
	СтруктураПараметров.Свойство("Организация", Организация);
	СтруктураПараметров.Свойство("Партнер",     Партнер);
	СтруктураПараметров.Свойство("Контрагент",  Контрагент);
	
	ПоВсемАналитикам = (Организация = Неопределено И Партнер = Неопределено И Контрагент = Неопределено);
	
	ИмяРегистраЗаданий = "ЗаданияКФормированиюИсходящихНалоговыхДокументов";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Задания.День КАК День,
	|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ НачальныеГраницыФормированияНН
	|ИЗ
	|	ИмяРегистраЗаданий КАК Задания
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|		ПО Задания.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	(Задания.День <= &ОкончаниеРасчета ИЛИ &ЗаВесьПериод)
	|	И (
	|			&ПоВсемАналитикам
	|			ИЛИ ((&Организация = НЕОПРЕДЕЛЕНО
	|					ИЛИ РегистрАналитикаУчетаПоПартнерам.Организация = &Организация)
	|				И (&Партнер = НЕОПРЕДЕЛЕНО
	|					ИЛИ РегистрАналитикаУчетаПоПартнерам.Партнер = &Партнер)
	|				И (&Контрагент = НЕОПРЕДЕЛЕНО
	|					ИЛИ РегистрАналитикаУчетаПоПартнерам.Контрагент = &Контрагент))
	|	)
	|	И (Задания.НомерЗадания <= &НомерЗадания
	|			ИЛИ &НеУчитыватьНомерЗадания)
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачальныеГраницыФормированияНН.АналитикаУчетаПоПартнерам
	|ИЗ
	|	НачальныеГраницыФормированияНН КАК НачальныеГраницыФормированияНН
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(Задания.День) КАК НачалоПериодаРасчета
	|ИЗ
	|	НачальныеГраницыФормированияНН КАК Задания
	|ИМЕЮЩИЕ
	|	НЕ МИНИМУМ(Задания.День) ЕСТЬ NULL
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НачальныеГраницыФормированияНН
	|;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяРегистраЗаданий", "РегистрСведений."+ИмяРегистраЗаданий);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПустаяДата",       Дата("00010101"));
	Запрос.УстановитьПараметр("ОкончаниеРасчета", ?(КонецПериодаРасчета = Неопределено, Неопределено, КонецДня(КонецПериодаРасчета)));
	Запрос.УстановитьПараметр("ЗаВесьПериод",     ?(КонецПериодаРасчета = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("Партнер",          Партнер);
	Запрос.УстановитьПараметр("Контрагент",       Контрагент);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", ПоВсемАналитикам);
	Запрос.УстановитьПараметр("НомерЗадания",     НомерЗадания);
	Запрос.УстановитьПараметр("НеУчитыватьНомерЗадания", НомерЗадания = 0);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Если ПоВсемАналитикам Тогда
		АналитикаУчетаПоПартнерамОтбор = Неопределено;
	Иначе
		АналитикаУчетаПоПартнерамОтбор = Новый Массив;
		ВыборкаПоАналитикамУчетаПартнеров = Результат[1].Выбрать();
		Пока ВыборкаПоАналитикамУчетаПартнеров.Следующий() Цикл
			АналитикаУчетаПоПартнерамОтбор.Добавить(ВыборкаПоАналитикамУчетаПартнеров.АналитикаУчетаПоПартнерам);
		КонецЦикла;
	КонецЕсли;
	
	Выборка = Результат[2].Выбрать();
	
	Результат = Неопределено;
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.НачалоПериодаРасчета;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьГраницыФормированияНалоговыхДокументов(НачалоПериодаРасчета, КонецПериодаРасчета, АналитикаУчетаПоПартнерамОтбор, НомерЗадания)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяРегистраЗаданий = "ЗаданияКФормированиюИсходящихНалоговыхДокументов";
	ИмяОперативногоРегистраНоменклатурныйСостав = "НДСНоменклатурныйСоставДляНалоговыхНакладных";
	ИмяОперативногоРегистраРасчетНалоговыхОбязательств = "НДСРасчетНалоговыхОбязательств";
	
	ТекстЗапроса = "
	// Выборка аналитик по которым есть движения после рассчитываемого периода, КодОтслеживаемогоРегистра = 1 (НДСНоменклатурныйСоставДляНалоговыхНакладных).
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОперативныйРегистр.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ АналитикиНДСНоменклатурныйСоставДляНалоговыхНакладных
	|ИЗ
	|	ИмяОперативногоРегистраКод1 КАК ОперативныйРегистр
	|ГДЕ
	|	ОперативныйРегистр.Период >= &ОкончаниеПериода
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// Выборка аналитик по которым есть движения после рассчитываемого периода, КодОтслеживаемогоРегистра = 2 (НДСРасчетНалоговыхОбязательств).
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОперативныйРегистр.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ АналитикиНДСРасчетНалоговыхОбязательств
	|ИЗ
	|	ИмяОперативногоРегистраКод2 КАК ОперативныйРегистр
	|ГДЕ
	|	ОперативныйРегистр.Период >= &ОкончаниеПериода
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.День,
	|	ДД.НомерЗадания,
	|	ДД.АналитикаУчетаПоПартнерам,
	|	ДД.ОбъектРасчетов,
	|	ДД.Документ,
	|	ДД.КодОтслеживаемогоРегистра
	|ПОМЕСТИТЬ КэшГраниц
	|ИЗ
	|	ИмяРегистраЗаданий КАК ДД
	|ГДЕ
	|	ДД.День МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ДД.НомерЗадания <= &НомерЗадания
	|	И (ДД.АналитикаУчетаПоПартнерам В (&АналитикиРасчета) ИЛИ &ПоВсемАналитикам)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// По рассчитанным аналитикам удаляются старые движения в заданиях
	|ВЫБРАТЬ
	|	Задания.День,
	|	Задания.НомерЗадания,
	|	Задания.АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов,
	|	Задания.Документ,
	|	Задания.КодОтслеживаемогоРегистра
	|ИЗ
	|	КэшГраниц КАК Задания
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// Задания, по аналитикам которых есть движения в последующем периоде, после расчета.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&СледующийДеньПослеРасчета КАК День,
	|	МАКСИМУМ(Задания.НомерЗадания) КАК НомерЗадания,
	|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК Документ,
	|	Задания.КодОтслеживаемогоРегистра КАК КодОтслеживаемогоРегистра
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		ДД.НомерЗадания,
	|		ДД.АналитикаУчетаПоПартнерам,
	|		ДД.ОбъектРасчетов,
	|		ДД.КодОтслеживаемогоРегистра
	|	ИЗ
	|		КэшГраниц КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиНДСНоменклатурныйСоставДляНалоговыхНакладных КАК Аналитики
	|		ПО ДД.АналитикаУчетаПоПартнерам = Аналитики.АналитикаУчетаПоПартнерам
	|			И ДД.КодОтслеживаемогоРегистра = 1
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.НомерЗадания,
	|		ДД.АналитикаУчетаПоПартнерам,
	|		ДД.ОбъектРасчетов,
	|		ДД.КодОтслеживаемогоРегистра
	|	ИЗ
	|		КэшГраниц КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиНДСРасчетНалоговыхОбязательств КАК Аналитики
	|		ПО ДД.АналитикаУчетаПоПартнерам = Аналитики.АналитикаУчетаПоПартнерам
	|			И ДД.КодОтслеживаемогоРегистра = 2
	|
	|	) КАК Задания
	|
	|СГРУППИРОВАТЬ ПО
	|	Задания.АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов,
	|	Задания.КодОтслеживаемогоРегистра
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АналитикиНДСНоменклатурныйСоставДляНалоговыхНакладных
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АналитикиНДСРасчетНалоговыхОбязательств
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КэшГраниц
	|;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяРегистраЗаданий", "РегистрСведений." + ИмяРегистраЗаданий);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяОперативногоРегистраКод1", "РегистрНакопления." + ИмяОперативногоРегистраНоменклатурныйСостав);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяОперативногоРегистраКод2", "РегистрНакопления." + ИмяОперативногоРегистраРасчетНалоговыхОбязательств);
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериодаРасчета));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецДня(КонецПериодаРасчета));
	Запрос.УстановитьПараметр("СледующийДеньПослеРасчета", КонецДня(КонецПериодаРасчета) + 1);
	Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
	Запрос.УстановитьПараметр("АналитикиРасчета", АналитикаУчетаПоПартнерамОтбор);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", НЕ ЗначениеЗаполнено(АналитикаУчетаПоПартнерамОтбор));
	Результаты = Запрос.ВыполнитьПакет();
	
	// Очищаются движения по рассчитанным аналитикам
	Выборка = Результаты[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = РегистрыСведений[ИмяРегистраЗаданий].СоздатьНаборЗаписей();
		Набор.Отбор.День.Установить(Выборка.День);
		Набор.Отбор.НомерЗадания.Установить(Выборка.НомерЗадания);
		Набор.Отбор.АналитикаУчетаПоПартнерам.Установить(Выборка.АналитикаУчетаПоПартнерам);
		Набор.Отбор.ОбъектРасчетов.Установить(Выборка.ОбъектРасчетов);
		Набор.Отбор.Документ.Установить(Выборка.Документ);
		Набор.Отбор.КодОтслеживаемогоРегистра.Установить(Выборка.КодОтслеживаемогоРегистра);
		Набор.Записать(); // Очистили рассчитанные аналитики в прошлом периоде
	КонецЦикла;
	
	// Перенос рассчитанных границ.
	Выборка = Результаты[4].Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = РегистрыСведений[ИмяРегистраЗаданий].СоздатьНаборЗаписей();
		Набор.Отбор.День.Установить(Выборка.День);
		Набор.Отбор.НомерЗадания.Установить(Выборка.НомерЗадания);
		Набор.Отбор.АналитикаУчетаПоПартнерам.Установить(Выборка.АналитикаУчетаПоПартнерам);
		Набор.Отбор.ОбъектРасчетов.Установить(Выборка.ОбъектРасчетов);
		Набор.Отбор.Документ.Установить(Выборка.Документ);
		Набор.Отбор.КодОтслеживаемогоРегистра.Установить(Выборка.КодОтслеживаемогоРегистра);
		СтрокаНабора = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора, Выборка);
		Набор.Записать(Истина); // Перенесли рассчитанные аналитики в новый период
	КонецЦикла;
	
КонецПроцедуры


Процедура НайтиИСторнироватьСтроки(СуммаПревышенияНО, НалоговыйОбъект, ТаблицаНоменклатурыКОчистке, ОбъектРасчетов, ОсновнаяНалоговая, КоэффициентНаправлениеДвижения, ЭтоПереоценка, ВидОперацииОсновнойНалоговой, День);
	
	ПараметрыПоискаПоНалоговойДополнительно = Новый Структура;
	ВидОперацииОсновнойНалоговойДополнительно = Неопределено;
	ПараметрыПоискаПоНалоговой = Новый Структура;
	СтрокиДляСторно = Новый Массив;
	
	Если ВидОперацииОсновнойНалоговой = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации
		 ИЛИ ВидОперацииОсновнойНалоговой = СформироватьВидОперацииПоСтавке7(Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации) Тогда
		ВидОперацииОсновнойНалоговойДополнительно = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации;
	ИначеЕсли ВидОперацииОсновнойНалоговой = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации
			  ИЛИ ВидОперацииОсновнойНалоговой = СформироватьВидОперацииПоСтавке7(Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации) Тогда
		ВидОперацииОсновнойНалоговойДополнительно = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации;
	КонецЕсли;
	
	Если НЕ ВидОперацииОсновнойНалоговойДополнительно = Неопределено Тогда
		
		ПараметрыПоискаПоНалоговойДополнительно.Вставить("ВидОперацииОсновнойНалоговой", ВидОперацииОсновнойНалоговойДополнительно);
		ПараметрыПоискаПоНалоговойДополнительно.Вставить("День", День);
		ПараметрыПоискаПоНалоговойДополнительно.Вставить("ОбъектРасчетов", Справочники.ОбъектыРасчетов.ПустаяСсылка());
		
		СтрокиДляСторноДополнительно = ТаблицаНоменклатурыКОчистке.НайтиСтроки(ПараметрыПоискаПоНалоговойДополнительно);
		Для каждого Строка Из СтрокиДляСторноДополнительно Цикл
			СтрокиДляСторно.Добавить(Строка);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ОбъектРасчетов) Тогда
			
			ПараметрыПоискаПоНалоговойДополнительно.ОбъектРасчетов = ОбъектРасчетов;
			
			СтрокиДляСторноДополнительно = ТаблицаНоменклатурыКОчистке.НайтиСтроки(ПараметрыПоискаПоНалоговойДополнительно);
			Для каждого Строка Из СтрокиДляСторноДополнительно Цикл
				СтрокиДляСторно.Добавить(Строка);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыПоискаПоНалоговой.Вставить("ОсновнаяНалоговая", ОсновнаяНалоговая);
	ПараметрыПоискаПоНалоговой.Вставить("ОбъектРасчетов", Справочники.ОбъектыРасчетов.ПустаяСсылка());
	
	СтрокиДляСторноОсновное = ТаблицаНоменклатурыКОчистке.НайтиСтроки(ПараметрыПоискаПоНалоговой);
	Для каждого Строка Из СтрокиДляСторноОсновное Цикл
		СтрокиДляСторно.Добавить(Строка);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОбъектРасчетов) Тогда
	
		ПараметрыПоискаПоНалоговой.ОбъектРасчетов = ОбъектРасчетов;
		
		СтрокиДляСторноОсновное = ТаблицаНоменклатурыКОчистке.НайтиСтроки(ПараметрыПоискаПоНалоговой);
		Для каждого Строка Из СтрокиДляСторноОсновное Цикл
			СтрокиДляСторно.Добавить(Строка);
		КонецЦикла;
		
	КонецЕсли;
	
	КоличествоСтрок = СтрокиДляСторно.Количество();
	
	Для Ин=1 по КоличествоСтрок Цикл
		
		Строка = СтрокиДляСторно[КоличествоСтрок - Ин];
		
		Если (-СуммаПревышенияНО) > Строка.СуммаСНДС Тогда
			НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			Если ЭтоПереоценка Тогда
				НоваяСтрока.ЭтоКорректировкаКоличества = Ложь;
				НоваяСтрока.СуммаСНДС = - Строка.СуммаСНДС * ?(НоваяСтрока.Цена < 0, -1, 1);
			Иначе
				НоваяСтрока.СуммаСНДС = - Строка.СуммаСНДС * КоэффициентНаправлениеДвижения;
			КонецЕсли;
			НоваяСтрока.КоличествоУпаковок = - Строка.КоличествоУпаковок * КоэффициентНаправлениеДвижения;
			
			СуммаПревышенияНО = СуммаПревышенияНО + Строка.СуммаСНДС;
			ТаблицаНоменклатурыКОчистке.Удалить(Строка);
			
		ИначеЕсли (-СуммаПревышенияНО) = Строка.СуммаСНДС Тогда
			НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			Если ЭтоПереоценка Тогда
				НоваяСтрока.ЭтоКорректировкаКоличества = Ложь;
				НоваяСтрока.СуммаСНДС = - Строка.СуммаСНДС * ?(НоваяСтрока.Цена < 0, -1, 1);
			Иначе
				НоваяСтрока.СуммаСНДС = - Строка.СуммаСНДС * КоэффициентНаправлениеДвижения;
			КонецЕсли;
			НоваяСтрока.КоличествоУпаковок = - Строка.КоличествоУпаковок * КоэффициентНаправлениеДвижения;
			
			СуммаПревышенияНО = 0;
			ТаблицаНоменклатурыКОчистке.Удалить(Строка);
			Прервать;
			
		Иначе
			ПолнаяСумма = Строка.СуммаСНДС;
			ПолноеКоличество = Строка.КоличествоУпаковок;
			Строка.СуммаСНДС = Строка.СуммаСНДС + СуммаПревышенияНО;
			Строка.КоличествоУпаковок = Окр((Строка.КоличествоУпаковок * Строка.СуммаСНДС) / ПолнаяСумма, 3);
			
			НоваяСтрока = НалоговыйОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			Если ЭтоПереоценка Тогда
				НоваяСтрока.ЭтоКорректировкаКоличества = Ложь;
				НоваяСтрока.СуммаСНДС = СуммаПревышенияНО * ?(НоваяСтрока.Цена < 0, -1, 1);
			Иначе
				НоваяСтрока.СуммаСНДС = СуммаПревышенияНО * КоэффициентНаправлениеДвижения;
			КонецЕсли;
			НоваяСтрока.КоличествоУпаковок = - (ПолноеКоличество - Строка.КоличествоУпаковок) * КоэффициентНаправлениеДвижения;
			
			СуммаПревышенияНО = 0;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
		

	НалоговыйОбъект.Товары.Свернуть("Номенклатура, Содержание, Характеристика, НомерГТД, Упаковка, Цена, СтавкаНДС, ОбъектРасчетов, ДокументПоставки"
											+?(ТипЗнч(НалоговыйОбъект) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной"), ", ДокументПоставкиДляВозвратов, ЭтоКорректировкаКоличества", "")
									, "КоличествоУпаковок, СуммаСНДС");
	
КонецПроцедуры

Функция АналитикиИзменились(СтараяАналитика, НоваяАналитика)
	
	Для каждого Аналитика из СтараяАналитика Цикл
		
		Если НоваяАналитика[Аналитика.Ключ] <> Аналитика.Значение Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура НайтиСтрокиНаСуммуПревышения(ВыборкаПоПревышениямНО, ТаблицаНоменклатурыДляЗаполнения, ТаблицаНоменклатурыНаСумму)

	СуммаПревышенияНО = ВыборкаПоПревышениямНО.СуммаПревышения;
	ДатаОперации = ВыборкаПоПревышениямНО.День;
	СтруктураПоиска = Новый Структура("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, ВидПоставки, ДокументПоставки, " + ?(ЗначениеЗаполнено(ВыборкаПоПревышениямНО.СтавкаНДС), ", СтавкаНДС", ""));
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаПоПревышениямНО);
	СтруктураПоиска.ВидПоставки = ВыборкаПоПревышениямНО.РазрезВидПоставки;
	СтрокиПоДокументуОтгрузки = ТаблицаНоменклатурыДляЗаполнения.НайтиСтроки(СтруктураПоиска);
	ТаблицаНоменклатурыНаСумму.Очистить();
	
	Для каждого Строка Из СтрокиПоДокументуОтгрузки Цикл
		
		ВидОперации = ПолучитьВидОперации(Строка.СтавкаНДС, Строка.ВидПоставки, ДатаОперации);
		
		Если СуммаПревышенияНО > Строка.СуммаВзаиморасчетовОстаток Тогда
			
			НоваяСтрока = ТаблицаНоменклатурыНаСумму.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ВидОперации = ВидОперации;
			
			СуммаПревышенияНО = СуммаПревышенияНО - Строка.СуммаВзаиморасчетовОстаток;
			ТаблицаНоменклатурыДляЗаполнения.Удалить(Строка);
			
		ИначеЕсли СуммаПревышенияНО = Строка.СуммаВзаиморасчетовОстаток Тогда
			
			НоваяСтрока = ТаблицаНоменклатурыНаСумму.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ВидОперации = ВидОперации;
			
			СуммаПревышенияНО = 0;
			ТаблицаНоменклатурыДляЗаполнения.Удалить(Строка);
			Прервать;
			
		Иначе
			
			ПолнаяСумма = Строка.СуммаВзаиморасчетовОстаток;
			ПолноеКоличество = Строка.КоличествоУпаковокОстаток;
			Строка.СуммаВзаиморасчетовОстаток = Строка.СуммаВзаиморасчетовОстаток - СуммаПревышенияНО;
			Строка.КоличествоУпаковокОстаток = Окр((Строка.КоличествоУпаковокОстаток * Строка.СуммаВзаиморасчетовОстаток) / ПолнаяСумма, 3);
			
			НоваяСтрока = ТаблицаНоменклатурыНаСумму.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ВидОперации = ВидОперации;
			НоваяСтрока.СуммаВзаиморасчетовОстаток = СуммаПревышенияНО;
			НоваяСтрока.КоличествоУпаковокОстаток = ПолноеКоличество - Строка.КоличествоУпаковокОстаток;
			
			СуммаПревышенияНО = 0;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // НайтиСтрокиНаСуммуПревышения

Функция ПолучитьВидОперации(СтавкаНДС, ВидПоставки, ДатаОперации)
	
	ОтделятьСтавку7 = ДатаОперации < '2014-12-01';

	СтавкаНДСПеречисление = НДСКлиентСерверПовтИсп.ПеречислениеСтавкаНДСПоСтавкеНДС(СтавкаНДС);

	Если ВидПоставки = Перечисления.ВидыПоставки.Розница Тогда
		
		Если СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС20
			ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС14 
			ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС0 
			ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС7 И НЕ ОтделятьСтавку7 Тогда
			ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации;
		ИначеЕсли СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС7 Тогда
			ВидОперации = "07_"+ "ИтоговаяРозницаОблагаемыеОперации";
		ИначеЕсли СтавкаНДСПеречисление = Перечисления.СтавкиНДС.БезНДС Тогда
			ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации;
		Иначе
			ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации;
		КонецЕсли;
		
	Иначе
		
		Если СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС20
			ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС14 
			ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС0
			ИЛИ СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС7 И НЕ ОтделятьСтавку7 Тогда
			ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации;
		ИначеЕсли СтавкаНДСПеречисление = Перечисления.СтавкиНДС.НДС7 Тогда
			ВидОперации = "07_"+ "ОблагаемыеОперации";
		ИначеЕсли СтавкаНДСПеречисление = Перечисления.СтавкиНДС.БезНДС Тогда
			ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации;
		Иначе
			ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ВидОперации;
	
КонецФункции

Функция ПолучитьЦенуДляНН(СуммаПревышенияНО, СтавкаНДС)
	ПроцентНДС = УчетНДСУПВызовСервера.ЗначениеСтавкиНДС(СтавкаНДС) / 100;
	Возврат СуммаПревышенияНО / (1 + ПроцентНДС);
	
КонецФункции

Функция СформироватьТаблицуСуммПревышенийПоДокументаПоставкиЗаДень(ВыборкаПоПревышениямПоДням)
	
	ТаблицаРезультата = Новый ТаблицаЗначений;
	
	ТаблицаРезультата.Колонки.Добавить("АналитикаУчетаПоПартнерам");
	ТаблицаРезультата.Колонки.Добавить("Валюта");
	ТаблицаРезультата.Колонки.Добавить("ОбъектРасчетов");
	ТаблицаРезультата.Колонки.Добавить("ДокументПоставки");
	ТаблицаРезультата.Колонки.Добавить("СуммаПревышенияНО");
	
	Пока ВыборкаПоПревышениямПоДням.Следующий() Цикл
		
		НоваяСтрока = ТаблицаРезультата.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоПревышениямПоДням);
		
	КонецЦикла;
	
	Возврат ТаблицаРезультата;
	
КонецФункции

Функция НайтиСтрокиОстатковПоНоменклатуреИДокументамПоставок(МассивОстатков, СтруктураПоискаНоменклатуры, МассивСтрокПревышенийПоДокументаПоставки)
	
	МассивРезультата = Новый Массив;
	
	СуммаПревышения = 0;
	
	Для каждого СтрокаМассиваОстатков Из МассивОстатков Цикл
		
		Совпадает = Истина;
		
		Для каждого Элемент из СтруктураПоискаНоменклатуры Цикл
			Если НЕ (Элемент.Значение = СтрокаМассиваОстатков[Элемент.Ключ]) Тогда
				Совпадает = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ Совпадает Тогда
			Продолжить;
		КонецЕсли;
		
		Совпадает = Ложь;
		
		Для каждого СтрокаДокументаПоставки из МассивСтрокПревышенийПоДокументаПоставки Цикл
			Если (СтрокаДокументаПоставки.ДокументПоставки = СтрокаМассиваОстатков.ДокументПоставки) Тогда
				Совпадает = Истина;                                         
				СуммаПревышения = Мин(СтрокаДокументаПоставки.СуммаПревышенияНО, СтрокаМассиваОстатков.СуммаВзаиморасчетовОстаток);
				СтрокаДокументаПоставки.СуммаПревышенияНО = СтрокаДокументаПоставки.СуммаПревышенияНО - СуммаПревышения;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ Совпадает Тогда
			Продолжить;
		КонецЕсли;
		
		ВложеныйМассив = Новый Массив;
		
		ВложеныйМассив.Добавить(СтрокаМассиваОстатков);
		ВложеныйМассив.Добавить(СуммаПревышения);
		
		МассивРезультата.Добавить(ВложеныйМассив);
		
	КонецЦикла;
	
	Возврат МассивРезультата;
	
КонецФункции

Процедура СопоставитьСтрокиНаСуммуПоДокументамОтгрузкиЗаДень(ТаблицаОстатков, ТаблицаПревышенийПоАналитикамЗаДень, ВыборкаАвансовЗаДень)
	
	СтруктураПоискаНоменклатуры = Новый Структура("Номенклатура, Характеристика, Упаковка, ЦенаНН, СтавкаНДС, НомерГТД");
	СтруктураПоискаАналитики = Новый Структура("АналитикаУчетаПоПартнерам, Валюта");
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, НомерГТД, Упаковка, СтавкаНДС, Цена, ДокументПоставки");
	СтруктураОтбораП2 = Новый Структура("Номенклатура, Характеристика, НомерГТД, Упаковка, СтавкаНДС, Цена, ДокументПоставки, ДокументПоставкиДляВозвратов");
	
	ТекущийНалоговыйДокумент = Неопределено;
	НалоговаяДляКорректировки = Неопределено;
	
	ПропускатьДоСледующейАналитики = Ложь;
	
	МассивСтрокДляУдаления = Новый Массив;
	
	Пока ВыборкаАвансовЗаДень.Следующий() Цикл
		
		Если АналитикиИзменились(СтруктураПоискаАналитики, ВыборкаАвансовЗаДень) = Истина Тогда
			
			ЗаполнитьЗначенияСвойств(СтруктураПоискаАналитики, ВыборкаАвансовЗаДень);
			
			МассивСтрокПревышений = ТаблицаПревышенийПоАналитикамЗаДень.НайтиСтроки(СтруктураПоискаАналитики);
			
			Если МассивСтрокПревышений.Количество() = 0 Тогда
				ПропускатьДоСледующейАналитики = Истина;
				Продолжить;
			КонецЕсли;
			
			Если ПропускатьДоСледующейАналитики Тогда
				ПропускатьДоСледующейАналитики = Ложь;
			КонецЕсли;
			
			МассивОстатковПоАналитикам = ТаблицаОстатков.НайтиСтроки(СтруктураПоискаАналитики);
			
		ИначеЕсли ПропускатьДоСледующейАналитики Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоискаНоменклатуры, ВыборкаАвансовЗаДень);
		
		МассивСтрокОстатков = НайтиСтрокиОстатковПоНоменклатуреИДокументамПоставок(МассивОстатковПоАналитикам, СтруктураПоискаНоменклатуры, МассивСтрокПревышений);
		
		Если МассивСтрокОстатков.Количество() = 0 Тогда
			 Продолжить;
		Иначе
			
			Если НЕ ТекущийНалоговыйДокумент = ВыборкаАвансовЗаДень.ТекущийНалоговыйДокумент Тогда
				
				Если НЕ (ТекущийНалоговыйДокумент = Неопределено) Тогда
					НалоговаяДляКорректировки.ЗаполнитьРеквизитыПослеАвтоформирования(Ложь);
					НалоговаяДляКорректировки.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
					НалоговаяДляКорректировки.Записать(РежимЗаписиДокумента.Проведение);
				КонецЕсли;
				
				ТекущийНалоговыйДокумент = ВыборкаАвансовЗаДень.ТекущийНалоговыйДокумент;
				НалоговаяДляКорректировки = ВыборкаАвансовЗаДень.ТекущийНалоговыйДокумент.ПолучитьОбъект();
				
			КонецЕсли;
			
			Если ТипЗнч(НалоговаяДляКорректировки) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
				ЗаполнитьЗначенияСвойств(СтруктураОтбораП2, ВыборкаАвансовЗаДень);
				СтруктураОтбораП2.Цена = ВыборкаАвансовЗаДень.ЦенаНН;
				СтруктураОтбораП2.ДокументПоставки = Неопределено;
				СтруктураОтбораП2.ДокументПоставкиДляВозвратов = Неопределено;
				СтрокиДляКорректировки = НалоговаяДляКорректировки.Товары.НайтиСтроки(СтруктураОтбораП2);
			Иначе
				ЗаполнитьЗначенияСвойств(СтруктураОтбора, ВыборкаАвансовЗаДень);
				СтруктураОтбора.Цена = ВыборкаАвансовЗаДень.ЦенаНН;
				СтруктураОтбора.ДокументПоставки = Неопределено;
				СтрокиДляКорректировки = НалоговаяДляКорректировки.Товары.НайтиСтроки(СтруктураОтбора);
			КонецЕсли;
			
			СуммаСтроки = ВыборкаАвансовЗаДень.СуммаВзаиморасчетов;
			
			Для Инд = 1 По СтрокиДляКорректировки.Количество() Цикл
				
				Если СуммаСтроки = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Строка = СтрокиДляКорректировки[Инд - 1];
				
				ДопСчетчик = 0;
				
				Для ИндОстатка = 1 По МассивСтрокОстатков.Количество() Цикл
					
					МассивСтрокаИСуммаОстатка = МассивСтрокОстатков[ИндОстатка - ДопСчетчик - 1];
					
					СтрокаОстатка = МассивСтрокаИСуммаОстатка[0];
					
					Если ЗначениеЗаполнено(Строка.ОбъектРасчетов) И Строка.ОбъектРасчетов <> СтрокаОстатка.ОбъектРасчетов Тогда
						Продолжить;
					КонецЕсли;
					
					ОбъектРасчетов = СтрокаОстатка.ОбъектРасчетов;
					ДокументПоставкиДляВозвратов = СтрокаОстатка.ДокументПоставкиДляВозвратов;
					ДокументПоставки = СтрокаОстатка.ДокументПоставки;
					СуммаРаспределения = Мин(МассивСтрокаИСуммаОстатка[1], СуммаСтроки);
										
					Если СуммаРаспределения > Строка.СуммаСНДС Тогда
						
						Строка.ОбъектРасчетов = ОбъектРасчетов;
						Строка.ДокументПоставки = ДокументПоставки;
						Если ТипЗнч(НалоговаяДляКорректировки) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
							Строка.ДокументПоставкиДляВозвратов = ДокументПоставкиДляВозвратов;
						КонецЕсли;
						
						МассивСтрокаИСуммаОстатка[1] = МассивСтрокаИСуммаОстатка[1] - Строка.СуммаСНДС;
						СуммаСтроки = СуммаСтроки - Строка.СуммаСНДС;
						
						Прервать;
												
					ИначеЕсли СуммаРаспределения = Строка.СуммаСНДС Тогда
						
						Строка.ОбъектРасчетов = ОбъектРасчетов;
						Строка.ДокументПоставки = ДокументПоставки;
						Если ТипЗнч(НалоговаяДляКорректировки) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
							Строка.ДокументПоставкиДляВозвратов = ДокументПоставкиДляВозвратов;
						КонецЕсли;
						
						Если МассивСтрокаИСуммаОстатка[1] - Строка.СуммаСНДС <= 0 Тогда
							Индекс = МассивОстатковПоАналитикам.Найти(СтрокаОстатка);
							Если Индекс <> Неопределено Тогда
								МассивОстатковПоАналитикам.Удалить(Индекс);
							КонецЕсли;
							ТаблицаОстатков.Удалить(СтрокаОстатка);
							МассивСтрокОстатков.Удалить(ИндОстатка - ДопСчетчик - 1);
							ДопСчетчик = ДопСчетчик + 1;
						КонецЕсли;
						СуммаСтроки = СуммаСтроки - Строка.СуммаСНДС;

						Прервать;

					Иначе // СуммаРаспределения < Строка.СуммаСНДС
						
						//Нужно разбить строку и отменить часть.
						ИндексТекущейСтроки 	 = НалоговаяДляКорректировки.Товары.Индекс(Строка);
						НоваяСтрока 			 = НалоговаяДляКорректировки.Товары.Вставить(ИндексТекущейСтроки + 1);
						
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
						НоваяСтрока.ОбъектРасчетов = ОбъектРасчетов;
						НоваяСтрока.ДокументПоставки = ДокументПоставки;
						Если ТипЗнч(НалоговаяДляКорректировки) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
							НоваяСтрока.ДокументПоставкиДляВозвратов = ДокументПоставкиДляВозвратов;
						КонецЕсли;
						
						ПолнаяСумма = Строка.СуммаСНДС;
						ПолноеКоличество = Строка.КоличествоУпаковок;
						
						Строка.СуммаСНДС = Строка.СуммаСНДС - СуммаРаспределения;
						НоваяСтрока.СуммаСНДС = СуммаРаспределения;
						
						Строка.КоличествоУпаковок = Окр((Строка.КоличествоУпаковок * Строка.СуммаСНДС) / ПолнаяСумма, 3);
						НоваяСтрока.КоличествоУпаковок = ПолноеКоличество - Строка.КоличествоУпаковок;
						
						Если МассивСтрокаИСуммаОстатка[1] - Строка.СуммаСНДС <= 0 Тогда
							Индекс = МассивОстатковПоАналитикам.Найти(СтрокаОстатка);
							Если Индекс <> Неопределено Тогда
								МассивОстатковПоАналитикам.Удалить(Индекс);
							КонецЕсли;
							ТаблицаОстатков.Удалить(СтрокаОстатка);
							МассивСтрокОстатков.Удалить(ИндОстатка - ДопСчетчик - 1);
							ДопСчетчик = ДопСчетчик + 1;
						КонецЕсли;
						Если СуммаСтроки - Строка.СуммаСНДС <= 0 Тогда
							СуммаСтроки = 0;
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
					
					
				КонецЦикла;
				
			КонецЦикла;
				
		КонецЕсли;
			
	КонецЦикла;
	
	Если НЕ (ТекущийНалоговыйДокумент = Неопределено) Тогда
		НалоговаяДляКорректировки.ЗаполнитьРеквизитыПослеАвтоформирования(Ложь);
		НалоговаяДляКорректировки.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
		НалоговаяДляКорректировки.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьИЗаполнитьНалоговыйОбъект(СтарыеАналитики, СтруктураДопРеквизитовЗаполнения)
	
	ВидОперации = Неопределено;
	ДатаЗапрета = Неопределено;
	НалоговаяНакладнаяДляВозвратов = Неопределено;
	ОсновнаяНалоговаяДляП2 = Неопределено;
	
	СтруктураДопРеквизитовЗаполнения.Свойство("ВидОперации", ВидОперации);
	СтруктураДопРеквизитовЗаполнения.Свойство("ДатаЗапрета", ДатаЗапрета);
	ПереданаНалоговаяНакладнаяДляВозвратов = СтруктураДопРеквизитовЗаполнения.Свойство("НалоговаяНакладнаяДляВозвратов", НалоговаяНакладнаяДляВозвратов);
	ПереданаОсновнаяНалоговаяДляП2 = СтруктураДопРеквизитовЗаполнения.Свойство("ОсновнаяНалоговаяДляП2", ОсновнаяНалоговаяДляП2);
	
	
	Если СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Возврат Тогда
		НалоговыйОбъект = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
		НалоговыйОбъект.ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Возврат;
		НалоговыйОбъект.НалоговаяНакладная = ?(НЕ ПереданаНалоговаяНакладнаяДляВозвратов, СтарыеАналитики.НалоговаяНакладнаяДляВозвратов, НалоговаяНакладнаяДляВозвратов);
		Если ПереданаОсновнаяНалоговаяДляП2 Тогда
			НалоговыйОбъект.ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.КорректировкаВозврата;
			НалоговыйОбъект.ОсновноеП2ДляВозврата = ОсновнаяНалоговаяДляП2;
		КонецЕсли
	ИначеЕсли СтарыеАналитики.РазрезВидПоставки = Перечисления.ВидыПоставки.Переоценка Тогда
		НалоговыйОбъект = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
		НалоговыйОбъект.ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Корректировка;
		НалоговыйОбъект.Переоценка = Истина;
		НалоговыйОбъект.НалоговаяНакладная = ?(НЕ ПереданаНалоговаяНакладнаяДляВозвратов, СтарыеАналитики.НалоговаяНакладнаяДляВозвратов, НалоговаяНакладнаяДляВозвратов);
		Если ПереданаОсновнаяНалоговаяДляП2 Тогда
			НалоговыйОбъект.ОсновноеП2ДляВозврата = ОсновнаяНалоговаяДляП2;
		КонецЕсли
	ИначеЕсли ПереданаОсновнаяНалоговаяДляП2 Тогда
		НалоговыйОбъект = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
		НалоговыйОбъект.ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Корректировка;
		НалоговыйОбъект.НалоговаяНакладная = ОсновнаяНалоговаяДляП2;
	Иначе
		НалоговыйОбъект = Документы.НалоговаяНакладная.СоздатьДокумент();
	КонецЕсли;
	
	Если ПереданаОсновнаяНалоговаяДляП2 Тогда
		НалоговыйОбъект.Дата = КонецДня(ТекущаяДата());
	ИначеЕсли ДатаЗапрета <> Неопределено Тогда
		НалоговыйОбъект.Дата = КонецДня(ДатаЗапрета+86400);
		НалоговыйОбъект.ДатаОтгрузкиОплаты = КонецДня(СтарыеАналитики.День);
	Иначе
		НалоговыйОбъект.Дата = КонецДня(СтарыеАналитики.День);
	КонецЕсли;
	
	НалоговыйОбъект.Валюта = СтарыеАналитики.Валюта;
	
	
	НалоговыйОбъект.Партнер = СтарыеАналитики.АналитикаУчетаПоПартнерам.Партнер;
	НалоговыйОбъект.Контрагент = СтарыеАналитики.АналитикаУчетаПоПартнерам.Контрагент;
	НалоговыйОбъект.Организация = СтарыеАналитики.АналитикаУчетаПоПартнерам.Организация;
	НалоговыйОбъект.Договор = СтарыеАналитики.АналитикаУчетаПоПартнерам.Договор;
	НалоговыйОбъект.НаправлениеДеятельности = СтарыеАналитики.АналитикаУчетаПоПартнерам.НаправлениеДеятельности;
	
	НалоговыйОбъект.Статус = Перечисления.СтатусыНалоговыхДокументов.Сформирован;
	
	НалоговыйОбъект.РазрешенаАвтоКорректировка = Истина;		
	
	Если ВидОперации <> Неопределено Тогда
		ЗаполнитьВидОперации(НалоговыйОбъект.ВидОперации, ВидОперации);
	КонецЕсли;
	
	Возврат НалоговыйОбъект;
	
КонецФункции

Процедура ЗаполнитьВидОперации(ВидОперацииДляЗаполнения, ВидОперацииИсточникЗаполнения)
	
	Если ТипЗнч(ВидОперацииИсточникЗаполнения) = Тип("Строка") Тогда
		// виберем все символы до конца сторки начиная с 4-го, принятое шифрование такое: 2 силвола ставка НДС, символ "_", вид операции, например - "07_ОблагаемыеОперации"
		ВидОперацииДляЗаполнения = Перечисления.ВидыОперацийНалоговыхДокументов[Сред(ВидОперацииИсточникЗаполнения, 4)];
	Иначе
		ВидОперацииДляЗаполнения = ВидОперацииИсточникЗаполнения;
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьВидОперацииПоСтавке7(ВидОперации)
	
	НовыйВидОперации = "07_" + ОбщегоНазначения.ИмяЗначенияПеречисления(ВидОперации);
	
	Возврат НовыйВидОперации;
	
КонецФункции


Функция ПолучитьТаблицуОграниченийПоДатамЗапрета(ВыборкаДанныхОбъектов, Пользователь, ПолноеИмяОбъектаМетаданныхДляПроверки)
	
	СвойстваВстраивания = ДатыЗапретаИзмененияСлужебный.СвойстваРазделов();
	ПустойРаздел = ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка();
	
	ДействующиеДаты = ДатыЗапретаИзмененияСлужебный.ДействующиеДатыЗапрета();
	
	Таблица = ПолноеИмяОбъектаМетаданныхДляПроверки;
	ИсточникиДанных = ДатыЗапретаИзмененияСлужебный.ПолучитьИсточникиДанных(ДействующиеДаты, Таблица);
	Раздел = ИсточникиДанных.Состав[0].Раздел;
	
	ДанныеДляПроверки = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	
	Пока ВыборкаДанныхОбъектов.Следующий() Цикл
		Строка = ДанныеДляПроверки.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, ВыборкаДанныхОбъектов);
		Строка.Раздел = Раздел;
	КонецЦикла;
	
	// Приведение данных в соответствие варианту встраивания.
	Для каждого Строка Из ДанныеДляПроверки Цикл
		
		Если СвойстваВстраивания.БезРазделовИОбъектов Тогда
			Строка.Раздел = ПустойРаздел;
			Строка.Объект = ПустойРаздел;
		Иначе
			Если ЗначениеЗаполнено(СвойстваВстраивания.ЕдинственныйРаздел) Тогда
				Строка.Раздел = СвойстваВстраивания.ЕдинственныйРаздел;
			КонецЕсли;
			
			Если СвойстваВстраивания.ВсеРазделыБезОбъектов
			 Или Не ЗначениеЗаполнено(Строка.Объект) Тогда
				
				Строка.Объект = Строка.Раздел;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Свертка лишних строк, чтобы сократить число проверок и сообщений.
	РазделыИОбъекты = ДанныеДляПроверки.Скопировать();
	РазделыИОбъекты.Свернуть("Раздел, Объект");
	Отбор = Новый Структура("Раздел, Объект");
	РазделыИОбъекты.Колонки.Добавить("Дата",
		Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	
	Для Каждого РазделИОбъект Из РазделыИОбъекты Цикл
		ЗаполнитьЗначенияСвойств(Отбор, РазделИОбъект);
		Строки = ДанныеДляПроверки.НайтиСтроки(Отбор);
		МинимальнаяДата = Неопределено;
		Для Каждого Строка Из Строки Цикл
			ТекущаяДата = НачалоДня(Строка.Дата);
			Если МинимальнаяДата = Неопределено Тогда
				МинимальнаяДата = ТекущаяДата;
			КонецЕсли;
			Если ТекущаяДата < МинимальнаяДата Тогда
				МинимальнаяДата = ТекущаяДата;
			КонецЕсли;
		КонецЦикла;
		РазделИОбъект.Дата = МинимальнаяДата;
	КонецЦикла;
	ДанныеДляПроверки = РазделыИОбъекты;
	
	// Приоритеты дат запрета изменения.
	// 1. Для раздела, объекта и пользователя.
	// 2. Для раздела, объекта и группы пользователей.
	// 3. Для раздела, объекта и любого пользователя.
	// 4. Для раздела, любого объекта (объект = раздел) и пользователя.
	// 5. Для раздела, любого объекта (объект = раздел) и группы пользователей.
	// 6. Для раздела, любого объекта (объект = раздел) и любого пользователя.
	// 7. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и пользователя.
	// 8. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и группы пользователей.
	// 9. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и любого пользователя.
	
	// Приоритеты дат запрета загрузки.
	// 1. Для раздела, объекта и узла.
	// 2. Для раздела, объекта и любого узла.
	// 3. Для раздела, любого объекта (объект = раздел) и узла.
	// 4. Для раздела, любого объекта (объект = раздел) и любого узла.
	// 5. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и узла.
	// 6. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и любого узла.
	
	ЗапретыИзменения = ДанныеДляПроверки.Скопировать(Новый Массив, "Объект, Дата");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УзелПроверкиЗапретаЗагрузки", Неопределено);
	
	// Поиск запретов изменения данных.
	Разделы = ДействующиеДаты.ДляПользователей.Разделы;
	ГруппыПользователя = ДействующиеДаты.ГруппыПользователей.Получить(Пользователь);
	Если ГруппыПользователя = Неопределено Тогда
		ГруппыПользователя = Новый Массив;
	КонецЕсли;
	ДополнительныеПараметры.Вставить("Пользователь",       Пользователь);
	ДополнительныеПараметры.Вставить("ГруппыПользователя", ГруппыПользователя);
	
	Для Каждого Данные Из ДанныеДляПроверки Цикл
		РазделЗапрета = Данные.Раздел;
		ОбъектЗапрета = Данные.Объект;
		
		Объекты = Разделы.Получить(РазделЗапрета);
		Адресаты = Неопределено;
		ДатаЗапрета = Неопределено;
		
		Если Объекты <> Неопределено Тогда
			Адресаты = Объекты.Получить(ОбъектЗапрета);
			Если Адресаты <> Неопределено Тогда
				// Поиск для раздела и объекта.
				ДатаЗапрета = ДатыЗапретаИзменения.НайтиДатуЗапрета(Адресаты, РазделЗапрета, ОбъектЗапрета, ДополнительныеПараметры);
			КонецЕсли;
			Если ДатаЗапрета = Неопределено Тогда
				ОбъектЗапрета = РазделЗапрета;
				Адресаты = Объекты.Получить(ОбъектЗапрета);
				Если Адресаты <> Неопределено Тогда
					// Поиск для раздела и любого объекта.
					ДатаЗапрета = ДатыЗапретаИзменения.НайтиДатуЗапрета(Адресаты, РазделЗапрета, ОбъектЗапрета, ДополнительныеПараметры);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если ДатаЗапрета = Неопределено Тогда
			РазделЗапрета = ПустойРаздел;
			ОбъектЗапрета = РазделЗапрета;
			Объекты = Разделы.Получить(РазделЗапрета);
			Если Объекты = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Адресаты = Объекты.Получить(ОбъектЗапрета);
			Если Адресаты = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			// Поиск для любого раздела и любого объекта (общая дата).
			ДатаЗапрета = ДатыЗапретаИзменения.НайтиДатуЗапрета(Адресаты, РазделЗапрета, ОбъектЗапрета, ДополнительныеПараметры);
			Если ДатаЗапрета = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Адресат = Пользователь;
		
		Строка = ЗапретыИзменения.Добавить();
		Строка.Объект  = ОбъектЗапрета;
		Строка.Дата    = ДатаЗапрета;
		
	КонецЦикла;
	
	Возврат ЗапретыИзменения;
	
КонецФункции // ПолучитьТаблицуОграниченийПоДатамЗапрета

Функция ПолучитьКонечнуюДатуЗапретаИзТаблицы(ТаблицаОграниченийПоДатамЗапрета, ОрганизацияПроверки, ДатаПроверки)
	
	ДатаЗапрета = Неопределено;
	ПустойРаздел = ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка();
	
	СтрокаОграниченийПоДатамЗапрета = ТаблицаОграниченийПоДатамЗапрета.Найти(ОрганизацияПроверки, "Объект");
	
	Если СтрокаОграниченийПоДатамЗапрета = Неопределено Тогда
		СтрокаОграниченийПоДатамЗапрета = ТаблицаОграниченийПоДатамЗапрета.Найти(ПустойРаздел, "Объект");
	КонецЕсли;
	
	Если СтрокаОграниченийПоДатамЗапрета <> Неопределено И СтрокаОграниченийПоДатамЗапрета.Дата >= НачалоДня(ДатаПроверки) Тогда
		ДатаЗапрета = СтрокаОграниченийПоДатамЗапрета.Дата;
	КонецЕсли;
	
	Возврат ДатаЗапрета;
	
КонецФункции

#КонецОбласти

#Область РасчетВозникшихОбязательствПоНДС

// Процедура заполняет и записывает наборы записей регистров.
//
// Параметры
//	СтруктураНаборыЗаписей - Структура - Структура наборов записей регистров
//	СтруктураШапки - Структура - Реквизиты документа
//
Процедура ЗаписатьНаборыЗаписей(СтруктураНаборыЗаписей, УдалятьПустые = Ложь)
	
	Для Каждого СтрокаСтруктуры Из СтруктураНаборыЗаписей Цикл
		
		НаборЗаписей = СтрокаСтруктуры.Значение;
		Если НаборЗаписей <> Неопределено И (УдалятьПустые ИЛИ НаборЗаписей.Количество() > 0) Тогда

			НаборЗаписей.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ТаблицыКонтроля");
			
			НаборЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаписатьНаборыЗаписейРасчетыСКлиентами()

// Процедура выполняет проведение документов по расчетам с клиентами.
//
// Параметры:
//	НачальныеГраницы - таблица начальных границ с колонками КлючАналитики, Период
//
Процедура ВыполнитьПроведениеДокументовПоВозникшемуНДС(НачальныеГраницы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Движения = Новый Структура;
	Движения.Вставить("НДСРасчетНалоговыхОбязательств", РегистрыНакопления.НДСРасчетНалоговыхОбязательств.СоздатьНаборЗаписей());
	
	//актуализация движений включает в себя следующие действия:
	//расчет новых движений по измененной аналитике
	//очистка старых движений по измененной аналитике, по которым рассчитанные движения отсутствуют
	//сохранение у регистратора движений по неизмененной аналитике
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачальныеГраницы", НачальныеГраницы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачальныеГраницы.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	НачальныеГраницы.Период КАК Период
	|ПОМЕСТИТЬ НачальныеГраницы
	|ИЗ
	|	&НачальныеГраницы КАК НачальныеГраницы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачальныеГраницы.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РегистрРасчетов.ЗаказКлиента КАК ОбъектРасчетов,
	|	РегистрРасчетов.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РегистрРасчетов.Валюта КАК Валюта
	|ПОМЕСТИТЬ ИзмененныеИзмерения
	|ИЗ
	|	НачальныеГраницы КАК НачальныеГраницы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РегистрРасчетов
	|		ПО (РегистрРасчетов.АналитикаУчетаПоПартнерам = НачальныеГраницы.АналитикаУчетаПоПартнерам)
	|ГДЕ
	|	(РегистрРасчетов.Долг <> 0
	|			ИЛИ РегистрРасчетов.Предоплата <> 0)
	|	И РегистрРасчетов.Период >= НачальныеГраницы.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	МИНИМУМ(ВложенныйЗапрос.Период) КАК Период
	|ПОМЕСТИТЬ НачальныеГраницыРасчетныхДокументов
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РегистрРасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РегистрРасчетов.Период КАК Период
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РегистрРасчетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИзмененныеИзмерения КАК ИзмененныеИзмерения
	|			ПО РегистрРасчетов.АналитикаУчетаПоПартнерам = ИзмененныеИзмерения.АналитикаУчетаПоПартнерам
	|				И РегистрРасчетов.ЗаказКлиента = ИзмененныеИзмерения.ОбъектРасчетов
	|				И РегистрРасчетов.РасчетныйДокумент = ИзмененныеИзмерения.РасчетныйДокумент
	|				И РегистрРасчетов.Валюта = ИзмененныеИзмерения.Валюта
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НачальныеГраницы.АналитикаУчетаПоПартнерам,
	|		НачальныеГраницы.Период
	|	ИЗ
	|		НачальныеГраницы КАК НачальныеГраницы) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегистрНДС.Регистратор КАК Регистратор,
	|	РегистрНДС.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РегистрНДС.Период КАК Период
	|ПОМЕСТИТЬ РегистраторыНДСКЗаписи
	|ИЗ
	|	РегистрНакопления.НДСРасчетНалоговыхОбязательств КАК РегистрНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачальныеГраницыРасчетныхДокументов КАК НачальныеГраницыРасчетныхДокументов
	|		ПО РегистрНДС.АналитикаУчетаПоПартнерам = НачальныеГраницыРасчетныхДокументов.АналитикаУчетаПоПартнерам
	|			И (РегистрНДС.Период >= НачальныеГраницыРасчетныхДокументов.Период
	|				ИЛИ РегистрНДС.ДокументПоставки.Дата >= НачальныеГраницыРасчетныхДокументов.Период)
	|ГДЕ
	|	НЕ РегистрНДС.НеЗависитОтВзаиморасчетов
	|	И НЕ РегистрНДС.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	МИНИМУМ(ВложенныйЗапрос.Период) КАК Период
	|ПОМЕСТИТЬ НачальныеГраницыФактические
	|ИЗ
	|	(ВЫБРАТЬ
	|		НачальныеГраницыРасчетныхДокументов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		НачальныеГраницыРасчетныхДокументов.Период КАК Период
	|	ИЗ
	|		НачальныеГраницыРасчетныхДокументов КАК НачальныеГраницыРасчетныхДокументов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистраторыНДСКЗаписи.АналитикаУчетаПоПартнерам,
	|		МИНИМУМ(РегистраторыНДСКЗаписи.Период)
	|	ИЗ
	|		РегистраторыНДСКЗаписи КАК РегистраторыНДСКЗаписи
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РегистраторыНДСКЗаписи.АналитикаУчетаПоПартнерам) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрРасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РегистрРасчетов.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РегистрРасчетов.ЗаказКлиента КАК ОбъектРасчетов,
	|	РегистрРасчетов.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РегистрРасчетов.Валюта КАК Валюта,
	|	РегистрРасчетов.Регистратор КАК Регистратор,
	|	ВЫБОР
	|		КОГДА РегистрРасчетов.ВидДвижения = &НаправлениеУвеличенияДолга
	|			ТОГДА РегистрРасчетов.Долг
	|		ИНАЧЕ -РегистрРасчетов.Долг
	|	КОНЕЦ КАК Долг,
	|	ВЫБОР
	|		КОГДА РегистрРасчетов.ВидДвижения = &НаправлениеУвеличенияПредоплаты
	|			ТОГДА РегистрРасчетов.Предоплата
	|		ИНАЧЕ -РегистрРасчетов.Предоплата
	|	КОНЕЦ КАК Предоплата,
	|	ВЫБОР
	|		КОГДА РегистрРасчетов.ВидДвижения = &НаправлениеУвеличенияДолга
	|			ТОГДА РегистрРасчетов.Долг
	|		ИНАЧЕ -РегистрРасчетов.Долг
	|	КОНЕЦ - ВЫБОР
	|		КОГДА РегистрРасчетов.ВидДвижения = &НаправлениеУвеличенияПредоплаты
	|			ТОГДА РегистрРасчетов.Предоплата
	|		ИНАЧЕ -РегистрРасчетов.Предоплата
	|	КОНЕЦ КАК ИзменениеЗадолженности,
	|	РегистрРасчетов.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА РегистрРасчетов.ХозяйственнаяОперация В (&МассивОперацийОплаты)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоОперацияПродажи,
	|	РегистрРасчетов.Период КАК Период,
	|	НАЧАЛОПЕРИОДА(РегистрРасчетов.Период, МЕСЯЦ) КАК ПериодМесяц
	|ПОМЕСТИТЬ ДвиженияПоРасчетам
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РегистрРасчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачальныеГраницыФактические КАК НачальныеГраницыФактические
	|		ПО РегистрРасчетов.АналитикаУчетаПоПартнерам = НачальныеГраницыФактические.АналитикаУчетаПоПартнерам
	|			И РегистрРасчетов.Период >= НачальныеГраницыФактические.Период
	|ГДЕ
	|	НЕ РегистрРасчетов.ХозяйственнаяОперация В (&МассивИсключаемыхХозопераций)
	|	И (РегистрРасчетов.Долг <> 0
	|			ИЛИ РегистрРасчетов.Предоплата <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияПоРасчетам.ПериодМесяц КАК Месяц,
	|	ДвиженияПоРасчетам.Организация КАК Организация
	|ПОМЕСТИТЬ ТаблицаМесяцев
	|ИЗ
	|	ДвиженияПоРасчетам КАК ДвиженияПоРасчетам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаМесяцев.Месяц КАК Месяц,
	|	ТаблицаМесяцев.Организация КАК Организация,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СистемыНалогообложенияОрганизаций.Период <= ТаблицаМесяцев.Месяц
	|				ТОГДА СистемыНалогообложенияОрганизаций.Период
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ) КАК ПериодЗаписи
	|ПОМЕСТИТЬ ТаблицаПериодов
	|ИЗ
	|	ТаблицаМесяцев КАК ТаблицаМесяцев
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК СистемыНалогообложенияОрганизаций
	|		ПО ТаблицаМесяцев.Организация = СистемыНалогообложенияОрганизаций.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаМесяцев.Месяц,
	|	ТаблицаМесяцев.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПериодов.Организация КАК Организация,
	|	ТаблицаПериодов.Месяц КАК Месяц
	|ПОМЕСТИТЬ ТаблицаНеплательщиков
	|ИЗ
	|	ТаблицаПериодов КАК ТаблицаПериодов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК СистемыНалогообложенияОрганизаций
	|		ПО ТаблицаПериодов.ПериодЗаписи = СистемыНалогообложенияОрганизаций.Период
	|			И ТаблицаПериодов.Организация = СистемыНалогообложенияОрганизаций.Организация
	|ГДЕ
	|	(СистемыНалогообложенияОрганизаций.СистемаНалогообложения ЕСТЬ NULL
	|			ИЛИ СистемыНалогообложенияОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Неплательщик)
	|			ИЛИ СистемыНалогообложенияОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ЕдиныйНалог)
	|			ИЛИ СистемыНалогообложенияОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.НалогНаПрибыль))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДвиженияПоРасчетам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДвиженияПоРасчетам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ДвиженияПоРасчетам.Валюта КАК Валюта,
	|	ДвиженияПоРасчетам.Регистратор КАК Регистратор,
	|	ДвиженияПоРасчетам.Период КАК Период
	|ПОМЕСТИТЬ ИсключаемыеИзмерения
	|ИЗ
	|	ДвиженияПоРасчетам КАК ДвиженияПоРасчетам
	|ГДЕ
	|	ДвиженияПоРасчетам.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|	ИЛИ ДвиженияПоРасчетам.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам,
	|	ДвиженияПоРасчетам.Регистратор,
	|	ДвиженияПоРасчетам.РасчетныйДокумент,
	|	ДвиженияПоРасчетам.ОбъектРасчетов,
	|	ДвиженияПоРасчетам.Валюта,
	|	ДвиженияПоРасчетам.Период
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияПоРасчетам.Долг) = 0 И
	|	СУММА(ДвиженияПоРасчетам.Предоплата) = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияПоРасчетам.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ РегистраторыКЗаписи
	|ИЗ
	|	ДвиженияПоРасчетам КАК ДвиженияПоРасчетам
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегистраторыНДСКЗаписи.Регистратор
	|ИЗ
	|	РегистраторыНДСКЗаписи КАК РегистраторыНДСКЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияПоРасчетам.Период) КАК ПериодРасчетногоДокумента,
	|	АВТОНОМЕРЗАПИСИ() КАК ИндексАналитики,
	|	ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДвиженияПоРасчетам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДвиженияПоРасчетам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ДвиженияПоРасчетам.Валюта КАК Валюта
	|ПОМЕСТИТЬ ПериодыРасчетныхДокументов
	|ИЗ
	|	ДвиженияПоРасчетам КАК ДвиженияПоРасчетам
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам,
	|	ДвиженияПоРасчетам.ОбъектРасчетов,
	|	ДвиженияПоРасчетам.РасчетныйДокумент,
	|	ДвиженияПоРасчетам.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрНДС.Период КАК Период,
	|	РегистрНДС.Регистратор КАК Регистратор,
	|	РегистрНДС.НомерСтроки КАК НомерСтроки,
	|	РегистрНДС.Активность КАК Активность,
	|	РегистрНДС.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РегистрНДС.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РегистрНДС.ВидПоставки КАК ВидПоставки,
	|	РегистрНДС.Валюта КАК Валюта,
	|	РегистрНДС.ДокументПоставки КАК ДокументПоставки,
	|	РегистрНДС.МоментОпределенияБазыНДС КАК МоментОпределенияБазыНДС,
	|	РегистрНДС.СуммаПоставкиТребующаяРегистрацииНН КАК СуммаПоставкиТребующаяРегистрацииНН,
	|	РегистрНДС.СуммаПоставкиНеТребующаяРегистрацииНН КАК СуммаПоставкиНеТребующаяРегистрацииНН,
	|	РегистрНДС.НеЗависитОтВзаиморасчетов КАК НеЗависитОтВзаиморасчетов
	|ПОМЕСТИТЬ СохраняемыеДвижения
	|ИЗ
	|	РегистрНакопления.НДСРасчетНалоговыхОбязательств КАК РегистрНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКЗаписи КАК РегистраторыКЗаписи
	|		ПО РегистрНДС.Регистратор = РегистраторыКЗаписи.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачальныеГраницыФактические КАК НачальныеГраницыФактические
	|		ПО РегистрНДС.АналитикаУчетаПоПартнерам = НачальныеГраницыФактические.АналитикаУчетаПоПартнерам
	|			И РегистрНДС.Период >= НачальныеГраницыФактические.Период
	|ГДЕ
	|	(НачальныеГраницыФактические.АналитикаУчетаПоПартнерам ЕСТЬ NULL
	|			ИЛИ РегистрНДС.НеЗависитОтВзаиморасчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СохраняемыеДвижения.Период КАК Период,
	|	СохраняемыеДвижения.Регистратор КАК Регистратор,
	|	СохраняемыеДвижения.НомерСтроки КАК НомерСтроки,
	|	СохраняемыеДвижения.Активность КАК Активность,
	|	СохраняемыеДвижения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	СохраняемыеДвижения.ОбъектРасчетов КАК ОбъектРасчетов,
	|	СохраняемыеДвижения.ВидПоставки КАК ВидПоставки,
	|	СохраняемыеДвижения.Валюта КАК Валюта,
	|	СохраняемыеДвижения.ДокументПоставки КАК ДокументПоставки,
	|	СохраняемыеДвижения.МоментОпределенияБазыНДС КАК МоментОпределенияБазыНДС,
	|	СохраняемыеДвижения.СуммаПоставкиТребующаяРегистрацииНН КАК СуммаПоставкиТребующаяРегистрацииНН,
	|	СохраняемыеДвижения.СуммаПоставкиНеТребующаяРегистрацииНН КАК СуммаПоставкиНеТребующаяРегистрацииНН,
	|	СохраняемыеДвижения.НеЗависитОтВзаиморасчетов КАК НеЗависитОтВзаиморасчетов
	|ИЗ
	|	СохраняемыеДвижения КАК СохраняемыеДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияПоРасчетам.Период) КАК Период,
	|	ДвиженияПоРасчетам.Регистратор КАК ДокументПоставки,
	|	СУММА(ДвиженияПоРасчетам.ИзменениеЗадолженности) КАК Сумма,
	|	ПериодыРасчетныхДокументов.ИндексАналитики КАК ИндексАналитики
	|ИЗ
	|	ДвиженияПоРасчетам КАК ДвиженияПоРасчетам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыРасчетныхДокументов КАК ПериодыРасчетныхДокументов
	|		ПО ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам = ПериодыРасчетныхДокументов.АналитикаУчетаПоПартнерам
	|			И ДвиженияПоРасчетам.ОбъектРасчетов = ПериодыРасчетныхДокументов.ОбъектРасчетов
	|			И ДвиженияПоРасчетам.РасчетныйДокумент = ПериодыРасчетныхДокументов.РасчетныйДокумент
	|			И ДвиженияПоРасчетам.Валюта = ПериодыРасчетныхДокументов.Валюта
	|ГДЕ
	|	ДвиженияПоРасчетам.ИзменениеЗадолженности > 0
	|	И ДвиженияПоРасчетам.ЭтоОперацияПродажи
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияПоРасчетам.Регистратор,
	|	ПериодыРасчетныхДокументов.ИндексАналитики
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИндексАналитики,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияПоРасчетам.Период КАК Период,
	|	ПериодыРасчетныхДокументов.ПериодРасчетногоДокумента КАК ПериодРасчетногоДокумента,
	|	РегистраторыКЗаписи.Регистратор КАК Регистратор,
	|	ПериодыРасчетныхДокументов.ИндексАналитики КАК ИндексАналитики,
	|	ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДвиженияПоРасчетам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДвиженияПоРасчетам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ДвиженияПоРасчетам.Валюта КАК Валюта,
	|	ДвиженияПоРасчетам.Долг КАК Долг,
	|	ДвиженияПоРасчетам.Предоплата КАК Предоплата,
	|	ДвиженияПоРасчетам.ИзменениеЗадолженности КАК ИзменениеЗадолженности,
	|	ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	ДвиженияПоРасчетам.Организация КАК Организация,
	|	ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	ДвиженияПоРасчетам.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДвиженияПоРасчетам.ЭтоОперацияПродажи КАК ЭтоОперацияПродажи,
	|	ВЫБОР
	|		КОГДА ДвиженияПоРасчетам.ЭтоОперацияПродажи
	|				И ДвиженияПоРасчетам.ИзменениеЗадолженности > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоОтгрузка,
	|	ВЫБОР
	|		КОГДА ДвиженияПоРасчетам.ЭтоОперацияПродажи
	|				И ДвиженияПоРасчетам.ИзменениеЗадолженности < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозврат,
	|	ВЫБОР
	|		КОГДА НЕ ДвиженияПоРасчетам.ЭтоОперацияПродажи
	|				И ДвиженияПоРасчетам.ИзменениеЗадолженности < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоОплата,
	|	ВЫБОР
	|		КОГДА НЕ ДвиженияПоРасчетам.ЭтоОперацияПродажи
	|				И ДвиженияПоРасчетам.ИзменениеЗадолженности > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратОплаты,
	|	ЕСТЬNULL(ВозвратРасчетныйДокумент.СпособКомпенсации, НЕОПРЕДЕЛЕНО) КАК СпособКомпенсации,
	|	ВЫБОР
	|		КОГДА РегистраторыСохраняемыхДвижений.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьСохраняемыеДвижения
	|ИЗ
	|	РегистраторыКЗаписи КАК РегистраторыКЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияПоРасчетам КАК ДвиженияПоРасчетам
	|		ПО РегистраторыКЗаписи.Регистратор = ДвиженияПоРасчетам.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНеплательщиков КАК ТаблицаНеплательщиков
	|		ПО (ДвиженияПоРасчетам.Организация = ТаблицаНеплательщиков.Организация)
	|			И (ДвиженияПоРасчетам.ПериодМесяц = ТаблицаНеплательщиков.Месяц)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсключаемыеИзмерения КАК ИсключаемыеИзмерения
	|		ПО (ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам = ИсключаемыеИзмерения.АналитикаУчетаПоПартнерам)
	|			И (ДвиженияПоРасчетам.ОбъектРасчетов = ИсключаемыеИзмерения.ОбъектРасчетов)
	|			И (ДвиженияПоРасчетам.РасчетныйДокумент = ИсключаемыеИзмерения.РасчетныйДокумент)
	|			И (ДвиженияПоРасчетам.Валюта = ИсключаемыеИзмерения.Валюта)
	|			И (ДвиженияПоРасчетам.Регистратор = ИсключаемыеИзмерения.Регистратор)
	|			И (ДвиженияПоРасчетам.Период = ИсключаемыеИзмерения.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ВозвратРасчетныйДокумент
	|		ПО (ДвиженияПоРасчетам.РасчетныйДокумент = ВозвратРасчетныйДокумент.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыРасчетныхДокументов КАК ПериодыРасчетныхДокументов
	|		ПО (ДвиженияПоРасчетам.АналитикаУчетаПоПартнерам = ПериодыРасчетныхДокументов.АналитикаУчетаПоПартнерам)
	|			И (ДвиженияПоРасчетам.ОбъектРасчетов = ПериодыРасчетныхДокументов.ОбъектРасчетов)
	|			И (ДвиженияПоРасчетам.РасчетныйДокумент = ПериодыРасчетныхДокументов.РасчетныйДокумент)
	|			И (ДвиженияПоРасчетам.Валюта = ПериодыРасчетныхДокументов.Валюта)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			СохраняемыеДвижения.Регистратор КАК Регистратор
	|		ИЗ
	|			СохраняемыеДвижения КАК СохраняемыеДвижения) КАК РегистраторыСохраняемыхДвижений
	|		ПО РегистраторыКЗаписи.Регистратор = РегистраторыСохраняемыхДвижений.Регистратор
	|ГДЕ
	|	ИсключаемыеИзмерения.Регистратор ЕСТЬ NULL
	|	И ТаблицаНеплательщиков.Организация ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	АналитикаУчетаПоПартнерам,
	|	ИндексАналитики
	|ИТОГИ
	|	МАКСИМУМ(ЕстьСохраняемыеДвижения)
	|ПО
	|	Регистратор,
	|	АналитикаУчетаПоПартнерам
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	МассивОперацийОплаты = Новый Массив;
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ПустаяСсылка()); //ввод остатков
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ВводОстатковАвансовКлиентов);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента);
	МассивОперацийОплаты.Добавить(Перечисления.ХозяйственныеОперации.ПереносАванса);
	
	//по ряду хозопераций нет прямой связи между взаиморасчетами и налоговыми обязательствами 
	//либо операции не являются объектом налогообложения - не изменяют состояние отгрузки или оплаты, а лишь корректируют взаиморасчеты
	МассивИсключаемыхХозопераций = Новый Массив;
	МассивИсключаемыхХозопераций.Добавить(Перечисления.ХозяйственныеОперации.ОтчетКомиссионера);
	МассивИсключаемыхХозопераций.Добавить(Перечисления.ХозяйственныеОперации.ОтчетКомиссионераОСписании);
	МассивИсключаемыхХозопераций.Добавить(Перечисления.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациямиОСписании);
	МассивИсключаемыхХозопераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности);
	МассивИсключаемыхХозопераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности);
	МассивИсключаемыхХозопераций.Добавить(Перечисления.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента);
	МассивИсключаемыхХозопераций.Добавить(Перечисления.ХозяйственныеОперации.ВыкупВозвратнойТарыКлиентом);
	
	МассивОперацийВзаимозачета = Новый Массив;
	МассивОперацийВзаимозачета.Добавить(Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности);
	МассивОперацийВзаимозачета.Добавить(Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента);
	МассивОперацийВзаимозачета.Добавить(Перечисления.ХозяйственныеОперации.ПереносАванса);
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		Запрос.УстановитьПараметр("НаправлениеУвеличенияДолга", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("НаправлениеУвеличенияПредоплаты", ВидДвиженияНакопления.Приход);
		ТекстЗапроса = СтрЗаменить(Запрос.Текст, "РегистрНакопления.РасчетыСКлиентамиПоДокументам", "РегистрНакопления.РасчетыСКлиентамиПоСрокам");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрРасчетов.ЗаказКлиента", "РегистрРасчетов.ОбъектРасчетов");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрРасчетов.Регистратор", "РегистрРасчетов.ДокументРегистратор");
		Запрос.Текст = ТекстЗапроса;
	Иначе
		Запрос.УстановитьПараметр("НаправлениеУвеличенияДолга", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("НаправлениеУвеличенияПредоплаты", ВидДвиженияНакопления.Расход);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОперацийОплаты", МассивОперацийОплаты);
	Запрос.УстановитьПараметр("МассивИсключаемыхХозопераций", МассивИсключаемыхХозопераций);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РазмерПакета = РезультатЗапроса.ВГраница();
	СохраняемыеДвижения = РезультатЗапроса[РазмерПакета - 2].Выгрузить();
	ТаблицаДокументовПоставки = РезультатЗапроса[РазмерПакета - 1].Выгрузить();
	ВыборкаПоРегистратору = РезультатЗапроса[РазмерПакета].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТаблицаДвижений = Новый ТаблицаЗначений;
	Для каждого ТекущаяКолонка Из РезультатЗапроса[РазмерПакета].Колонки Цикл
		Если ТекущаяКолонка.Имя <> "ЕстьСохраняемыеДвижения" Тогда
			ТаблицаДвижений.Колонки.Добавить(ТекущаяКолонка.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ПоляСвертки = "";
	Для каждого ТекущаяКолонка ИЗ Движения.НДСРасчетНалоговыхОбязательств.ВыгрузитьКолонки().Колонки Цикл
		
		Если ТаблицаДвижений.Колонки.Найти(ТекущаяКолонка.Имя) = Неопределено Тогда
			ТаблицаДвижений.Колонки.Добавить(ТекущаяКолонка.Имя);
		КонецЕсли;
		
		Если ТекущаяКолонка.Имя <> "СуммаПоставкиТребующаяРегистрацииНН" И ТекущаяКолонка.Имя <> "СуммаПоставкиНеТребующаяРегистрацииНН" Тогда
			ПоляСвертки = ПоляСвертки + ", " + ТекущаяКолонка.Имя;
		КонецЕсли;
		
	КонецЦикла;
	ПоляСвертки = Сред(ПоляСвертки, 3);
	
	ТаблицаОбязательствПоСтрокеРасчетов = Новый ТаблицаЗначений;
	ТаблицаОбязательствПоСтрокеРасчетов.Колонки.Добавить("ВидПоставки");
	ТаблицаОбязательствПоСтрокеРасчетов.Колонки.Добавить("ПервоеСобытие");
	ТаблицаОбязательствПоСтрокеРасчетов.Колонки.Добавить("ДокументПоставки");
	ТаблицаОбязательствПоСтрокеРасчетов.Колонки.Добавить("ТребуетсяРаспределениеПоДокументамПоставки", Новый ОписаниеТипов("Булево"));
	ТаблицаОбязательствПоСтрокеРасчетов.Колонки.Добавить("Сумма");
	ТаблицаОбязательствПоСтрокеРасчетов.Колонки.Добавить("ОбязательноеУказаниеДокументаПоставки", Новый ОписаниеТипов("Булево"));
	
	ПредыдущийИндексАналитики = 0;
	
	Пока ВыборкаПоРегистратору.Следующий() Цикл
		
		ТипРегистратора = ТипЗнч(ВыборкаПоРегистратору.Регистратор);
		//в движениях по оперативным взаиморасчетам хозоперация списания задолженности подменяется на нейтральную, поэтому исключать будем по типу регистратора
		Если ТипРегистратора = Тип("ДокументСсылка.СписаниеЗадолженности") Тогда
			Продолжить;
		КонецЕсли;
		
		Движения.НДСРасчетНалоговыхОбязательств.Отбор.Регистратор.Установить(ВыборкаПоРегистратору.Регистратор);
		
		Если Не ЗначениеЗаполнено(Движения.НДСРасчетНалоговыхОбязательств.Отбор.Регистратор.Значение) Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось установить документ регистратор %1 для проведения по регистру НДСРасчетНалоговыхОбязательств';uk='Не вдалося встановити документ реєстратор %1 для проведення по регістру НДСРасчетНалоговыхОбязательств'"), ВыборкаПоРегистратору.Регистратор);
		КонецЕсли;
		
		ТаблицаДвижений.Очистить();
		
		ВыборкаПоАналитике = ВыборкаПоРегистратору.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоАналитике.Следующий() Цикл
			
			Если ВыборкаПоАналитике.АналитикаУчетаПоПартнерам = NULL Тогда
				Продолжить;
			КонецЕсли;
			
			ДвиженияПоАналитике = ТаблицаДвижений.СкопироватьКолонки();
			
			ВыборкаСтрока = ВыборкаПоАналитике.Выбрать();
			Пока ВыборкаСтрока.Следующий() Цикл
				
				//по вводу остатков искусственно формируются НО на полученные авансы, чтобы НН по ним отражали использование номенклатурного состава
				Если (ТипРегистратора = Тип("ДокументСсылка.ВводОстатков") ИЛИ ТипРегистратора = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов")) И НЕ ВыборкаСтрока.ЭтоОплата Тогда
					Продолжить;
				КонецЕсли;
				
				//упрощенная обработка ситуации, когда за возвратом товаров сразу последует возврат денег, чтобы избежать выписки дополнительной НН на аванс и П2 к ней
				Если ВыборкаСтрока.ЭтоВозвратОплаты И ВыборкаСтрока.СпособКомпенсации = Перечисления.СпособыКомпенсацииВозвратовТоваров.ВернутьДенежныеСредства Тогда
					Продолжить;
				КонецЕсли;
				
				ТаблицаОбязательствПоСтрокеРасчетов.Очистить();
				
				ВидПоставки = ?(ВыборкаСтрока.ЭтоВозврат ИЛИ ВыборкаСтрока.ЭтоВозвратОплаты, Перечисления.ВидыПоставки.Возврат, Перечисления.ВидыПоставки.Поставка);
				
				МоментОпределенияБазы = ОпределитьМоментОпределенияБазыНДС(
				ВидПоставки, 
				ВыборкаСтрока.ХозяйственнаяОперация,					
				ВыборкаСтрока.Валюта, 
				ВыборкаСтрока.Организация, 
				ВыборкаСтрока.Договор
				);
				
				Если НЕ ЗначениеЗаполнено(МоментОпределенияБазы) Тогда
					МоментОпределенияБазы = Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию;
				КонецЕсли;
				
				ЭтоПервоеСобытие = Ложь;
				
				Если МоментОпределенияБазы = Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию Тогда
					
					Если ВыборкаСтрока.ЭтоОперацияПродажи И ЗначениеЗаполнено(ВыборкаСтрока.Долг)
						ИЛИ НЕ ВыборкаСтрока.ЭтоОперацияПродажи И ЗначениеЗаполнено(ВыборкаСтрока.Предоплата) Тогда
						//продажа или возврат оплаты увеличивает долг или уменьшает предоплату
						//оплата или возврат товара уменьшают долг или увеличивают предоплату
						//если продажа или возврат товара изменяют долг, значит это первое событие - изменяется уровень отгрузки, и он больше уровня оплаты
						//если продажа или возврат товара изменяют предоплату, значит это второе событие - изменяется уровень отгрузки в пределах уровня оплаты
						//если оплата или возврат оплаты изменяют предоплату, значит это первое событие - изменяется уровень оплаты, и он больше уровня отгрузки
						//если оплата или возврат оплаты изменяют долг, значит это второе событие - изменяется уровень оплаты в пределах уровня отгрузки
						
						ЭтоПервоеСобытие = Истина;
						
					КонецЕсли;
					
				ИначеЕсли МоментОпределенияБазы = Перечисления.МоментыОпределенияНалоговойБазы.ПоОплате Тогда
					Если НЕ ВыборкаСтрока.ЭтоОперацияПродажи Тогда
						ЭтоПервоеСобытие = Истина;
					КонецЕсли;
				ИначеЕсли МоментОпределенияБазы = Перечисления.МоментыОпределенияНалоговойБазы.ПоОтгрузке Тогда
					Если ВыборкаСтрока.ЭтоОперацияПродажи Тогда
						ЭтоПервоеСобытие = Истина;
					КонецЕсли;
				КонецЕсли;
				
				//долг и предоплата изменяются документами в противоположном направление: когда долг растет, предоплата уменьшается, и наоборот
				//поскольку в ресурсах регистра в каждом движении заполнено только одно значение, либо долг, либо предоплата, для определения общей суммы неважно, суммируются они или вычитаются
				//если из изменения долга вычитать изменение предоплаты, результатом будет общее изменение задолженности
				//для определения налогооблагаемого события важно знать не изменение задолженности, а изменение уровня отгрузки или оплаты
				//они будут отрицательными у операций возврата и положительными у операций продаж
				//поэтому далее оперируем суммами по модулю, понимая, что при возвратах они отрицательные
				
				СуммаОперации = ВыборкаСтрока.ИзменениеЗадолженности;
				Если СуммаОперации < 0 Тогда
					СуммаОперации = - СуммаОперации;
				КонецЕсли;
				
				//обычно одной строке движений по расчетам соответствует одна строка движений по обязательствам
				СтрокаОбязательств = ТаблицаОбязательствПоСтрокеРасчетов.Добавить();
				СтрокаОбязательств.ВидПоставки = ВидПоставки;
				СтрокаОбязательств.Сумма = СуммаОперации;
				СтрокаОбязательств.ПервоеСобытие = ЭтоПервоеСобытие;
				
				//товарные документы сами являются документами поставки
				//у возврата оплаты документа поставки нет - товарный состав зависит от составна налоговых накладных, выписанных на аванс
				//для оплат (в т. ч. возвратов товаров, образующих аванс) требуется определять документ поставки исходя из данных об отгрузках
				//по одному расчетному документу оплата и продажа могут образовываться несколькими регистраторами, поэтому может возникать неопределенность документа поставки
				//для ее разрешения используется учет остатка доступной суммы по документу поставки, подобно списанию при партионном учете товаров
				Если ВыборкаСтрока.ЭтоОтгрузка ИЛИ ВыборкаСтрока.ЭтоВозврат Тогда
					СтрокаОбязательств.ДокументПоставки = ВыборкаСтрока.Регистратор;	
				ИначеЕсли ВыборкаСтрока.ЭтоВозвратОплаты Тогда
					СтрокаОбязательств.ДокументПоставки = Неопределено;	
				Иначе
					СтрокаОбязательств.ТребуетсяРаспределениеПоДокументамПоставки = Истина;
				КонецЕсли;
				
				//особый случай - при возврате товаров нужно всегда выписывать корректировку на всю сумму возврата
				//если возврат - первое событие, то все в порядке, сумма события равна сумме оформляемой П2
				//если возврат - второе событие, то нужно дополнительно образовать два первых события с нулевой общей суммой:
				//П2 - на сумму возврата с минусом, НН - на сумму образовавшегося аванса с плюсом
				Если МоментОпределенияБазы = Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию И ВыборкаСтрока.ЭтоВозврат И НЕ ЭтоПервоеСобытие Тогда
					
					//первое событие по возврату на сумму второго - доводим сумму первого события до суммы всей операции
					СтрокаОбязательствДополнительно = ТаблицаОбязательствПоСтрокеРасчетов.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаОбязательствДополнительно, СтрокаОбязательств);
					СтрокаОбязательствДополнительно.ПервоеСобытие = Истина;
					
					//первое событие по поставке - НН на аванс
					СтрокаОбязательствДополнительно = ТаблицаОбязательствПоСтрокеРасчетов.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаОбязательствДополнительно, СтрокаОбязательств);
					СтрокаОбязательствДополнительно.ПервоеСобытие = Истина;
					СтрокаОбязательствДополнительно.ВидПоставки = Перечисления.ВидыПоставки.Поставка;
					СтрокаОбязательствДополнительно.ТребуетсяРаспределениеПоДокументамПоставки = Истина;
					//упрощенная обработка ситуации, когда за возвратом товаров сразу последует возврат денег, чтобы избежать выписки дополнительной НН на аванс и П2 к ней
					//если за счет аванса была выполнена отгрузка, тогда понадобится создать НН на этот аванс с известным товарным составо
					Если ВыборкаСтрока.СпособКомпенсации = Перечисления.СпособыКомпенсацииВозвратовТоваров.ВернутьДенежныеСредства Тогда
						СтрокаОбязательствДополнительно.ОбязательноеУказаниеДокументаПоставки = Истина;	
					КонецЕсли;
					
				КонецЕсли;
				
				//определим документ поставки для записей, которые этого требуют
				СтрокиДляОпределенияДокументаПоставки = ТаблицаОбязательствПоСтрокеРасчетов.НайтиСтроки(Новый Структура("ТребуетсяРаспределениеПоДокументамПоставки", Истина));
				
				Если СтрокиДляОпределенияДокументаПоставки.Количество() > 0 Тогда
					
					Если ВыборкаСтрока.ИндексАналитики <> ПредыдущийИндексАналитики Тогда
						ДокументыПоставкиПоИндексу = ТаблицаДокументовПоставки.НайтиСтроки(Новый Структура("ИндексАналитики", ВыборкаСтрока.ИндексАналитики));
						ПредыдущийИндексАналитики = ВыборкаСтрока.ИндексАналитики;
					КонецЕсли;
					
					Для каждого СтрокаОбязательств ИЗ СтрокиДляОпределенияДокументаПоставки Цикл
						
						СуммаОперацииКРаспределению = СтрокаОбязательств.Сумма;
						ИндексВставки = ТаблицаОбязательствПоСтрокеРасчетов.Индекс(СтрокаОбязательств) + 1;
						
						Для каждого СтрокаДокументаПоставки Из ДокументыПоставкиПоИндексу Цикл
							
							РаспределеннаяСумма = Мин(СтрокаДокументаПоставки.Сумма, СуммаОперацииКРаспределению);
							
							Если РаспределеннаяСумма <=0 Тогда
								Продолжить;
							КонецЕсли;
							
							НоваяСтрока = ТаблицаОбязательствПоСтрокеРасчетов.Вставить(ИндексВставки);
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОбязательств);
							НоваяСтрока.ДокументПоставки = СтрокаДокументаПоставки.ДокументПоставки;
							НоваяСтрока.Сумма = РаспределеннаяСумма;
							
							СуммаОперацииКРаспределению = СуммаОперацииКРаспределению - РаспределеннаяСумма;
							СтрокаДокументаПоставки.Сумма = СтрокаДокументаПоставки.Сумма - РаспределеннаяСумма;
							ИндексВставки = ИндексВставки + 1;
							
						КонецЦикла;
						
						Если СуммаОперацииКРаспределению = 0 ИЛИ СтрокаОбязательств.ОбязательноеУказаниеДокументаПоставки Тогда
							ТаблицаОбязательствПоСтрокеРасчетов.Удалить(СтрокаОбязательств);
						Иначе
							СтрокаОбязательств.Сумма = СуммаОперацииКРаспределению;
							СтрокаОбязательств.ДокументПоставки = Неопределено;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				//формирование записей по регистру
				Для каждого СтрокаОбязательств Из ТаблицаОбязательствПоСтрокеРасчетов Цикл
					
					СтрокаДвижения = ДвиженияПоАналитике.Добавить();
					
					ЗаполнитьЗначенияСвойств(СтрокаДвижения, ВыборкаСтрока);
					ЗаполнитьЗначенияСвойств(СтрокаДвижения, СтрокаОбязательств);
					СтрокаДвижения.МоментОпределенияБазыНДС = МоментОпределенияБазы;
					СтрокаДвижения.СуммаПоставкиТребующаяРегистрацииНН = ?(СтрокаОбязательств.ПервоеСобытие, СтрокаОбязательств.Сумма, 0);
					СтрокаДвижения.СуммаПоставкиНеТребующаяРегистрацииНН = ?(СтрокаОбязательств.ПервоеСобытие, 0, СтрокаОбязательств.Сумма);
					
				КонецЦикла;
				
			КонецЦикла;
			
			//если у регистратора есть движения взаимозачета, попытаемся минимизировать количество налоговых документов, если это разрешено
			//преобразуем движения так, будто у документов расчета, по которым возникали исходные обязательства, сразу была указана такая аналитика, на которую выполняется взаимозачет
			//тогда вместо корректировок могут выписываться налоговые накладные по другой аналитике/номеклатуре
			//и не будут выписывать налоговые накладные, когда первое событие не должно было возникнуть
			//корректировать аналитику можно только в оплатах, аналитика отгрузок не меняется, но может измениться первое/второе событие
			
			ВыполненаКорректировкаДвиженийВзаимозачета = Ложь;
			
			МассивИсходныхДвиженийЗачета = Новый Массив;
			МоментыОпределенияБазы = Новый Массив;
			ТаблицаДвиженийЗачета = ДвиженияПоАналитике.СкопироватьКолонки();
			
			Для каждого СтрокаДвижения Из ДвиженияПоАналитике Цикл
				
				Если МассивОперацийВзаимозачета.Найти(СтрокаДвижения.ХозяйственнаяОперация) <> Неопределено Тогда
					
					МассивИсходныхДвиженийЗачета.Добавить(СтрокаДвижения);
					НоваяЗапись = ТаблицаДвиженийЗачета.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаДвижения);
					Если НоваяЗапись.ВидПоставки = Перечисления.ВидыПоставки.Возврат Тогда
						НоваяЗапись.СуммаПоставкиТребующаяРегистрацииНН = - НоваяЗапись.СуммаПоставкиТребующаяРегистрацииНН; 
						НоваяЗапись.СуммаПоставкиНеТребующаяРегистрацииНН = - НоваяЗапись.СуммаПоставкиНеТребующаяРегистрацииНН;
						НоваяЗапись.ВидПоставки = Перечисления.ВидыПоставки.Поставка;
					КонецЕсли;
					
					Если МоментыОпределенияБазы.Найти(СтрокаДвижения.МоментОпределенияБазыНДС) = Неопределено Тогда
						МоментыОпределенияБазы.Добавить(СтрокаДвижения.МоментОпределенияБазыНДС);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			//при моменте определения базы "по отгрузке" корректировать взаимозачет не нужно - налоговое событие и документ поставки совпадают
			Если МассивИсходныхДвиженийЗачета.Количество() > 0
				И МоментыОпределенияБазы.Количество() = 1
				И (МоментыОпределенияБазы[0] = Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию ИЛИ МоментыОпределенияБазы[0] = Перечисления.МоментыОпределенияНалоговойБазы.ПоОплате) Тогда
				
				ТаблицаДвиженийЗачета.Свернуть("Период, Регистратор, АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВидПоставки, Валюта, ДокументПоставки, МоментОпределенияБазыНДС, РасчетныйДокумент, ПериодРасчетногоДокумента",
				"СуммаПоставкиТребующаяРегистрацииНН, СуммаПоставкиНеТребующаяРегистрацииНН");
				
				Для каждого ТекущаяСтрока Из ТаблицаДвиженийЗачета.НайтиСтроки(Новый Структура("СуммаПоставкиТребующаяРегистрацииНН, СуммаПоставкиНеТребующаяРегистрацииНН", 0, 0)) Цикл
					ТаблицаДвиженийЗачета.Удалить(ТекущаяСтрока);
				КонецЦикла;
				
				ПоложительнаяСуммаОпераций = 0;
				ОтрицательнаяСуммаОпераций = 0;
				
				Для каждого ТекущаяСтрока Из ТаблицаДвиженийЗачета Цикл
					
					ТекущаяСтрока.Период = ТекущаяСтрока.ПериодРасчетногоДокумента;
				
					Если ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН > 0 ИЛИ ТекущаяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН > 0 Тогда
						ПоложительнаяСуммаОпераций = ПоложительнаяСуммаОпераций + ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН + ТекущаяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН;
					Иначе
						ОтрицательнаяСуммаОпераций = ОтрицательнаяСуммаОпераций + ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН + ТекущаяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН;
					КонецЕсли;
					
				КонецЦикла;
				
				//суммы должны совпадать, иначе такую ситуацию непонятно как исправлять
				ВыполненаКорректировкаДвиженийВзаимозачета = ПоложительнаяСуммаОпераций = - ОтрицательнаяСуммаОпераций;
				
				Если ВыполненаКорректировкаДвиженийВзаимозачета Тогда
					
					Если МоментОпределенияБазы = Перечисления.МоментыОпределенияНалоговойБазы.ПоПервомуСобытию Тогда
						
						//взаимозачет "уравнивает" аналитику всех операций (расчетных документов), которые он затронул
						//поэтому можно считать, что первые из этих операций должны быть первыми событиями, а последние - вторыми
						//отсортируем таблицу по дате расчетного документа, так получим движения в порядке исходных документов
						//разобьем исходную таблицу пополам, в каждой половине сумма операций будет равна сумма взаимозачета
						//в первой половине все операции должны остаться первым событием, во второй первое событие должно быть отсторнировано, будто и не возникало
						//в первой половине аналитику оплат нужно скорректировать на аналитику реализаций из второй половины, будто оплата была первым событием, а реализация вторым
						//во второй половине аналитику оплат нужно скорректировать на аналитику реализаций из первой половины, будто оплата была вторым событием, а реализация первым
						
						ТаблицаДвиженийЗачета.Сортировать("Период");
						
						ОсталосьРаспределить = ПоложительнаяСуммаОпераций;
						ТаблицаПервыхСобытий = ТаблицаДвиженийЗачета.СкопироватьКолонки();
						ТаблицаВторыхСобытий = ТаблицаДвиженийЗачета.СкопироватьКолонки();
						
						//первая таблица
						Для каждого ТекущаяСтрока Из ТаблицаДвиженийЗачета Цикл
							
							СуммаОперацииПоСтроке = ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН + ТекущаяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН;
							Если СуммаОперацииПоСтроке > 0 Тогда
								ЗнакОперации = 1;
							Иначе
								ЗнакОперации = -1;
								СуммаОперацииПоСтроке = - СуммаОперацииПоСтроке;
							КонецЕсли;
							
							РаспределеннаяСумма = Мин(ОсталосьРаспределить, СуммаОперацииПоСтроке);
							
							Если РаспределеннаяСумма > 0 Тогда
								
								НоваяСтрока = ТаблицаПервыхСобытий.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
								
								Если ЗначениеЗаполнено(ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН) Тогда
									НоваяСтрока.СуммаПоставкиТребующаяРегистрацииНН = ЗнакОперации * РаспределеннаяСумма;
									ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН = ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН - ЗнакОперации * РаспределеннаяСумма;
								Иначе
									НоваяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН = ЗнакОперации * РаспределеннаяСумма;
									ТекущаяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН = ТекущаяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН - ЗнакОперации * РаспределеннаяСумма;
								КонецЕсли;
								
								ОсталосьРаспределить = ОсталосьРаспределить - РаспределеннаяСумма;
								
							КонецЕсли;
							
							Если ОсталосьРаспределить = 0 Тогда
								Прервать;
							КонецЕсли;
							
						КонецЦикла;
						
						//вторая таблица
						Для каждого ТекущаяСтрока Из ТаблицаДвиженийЗачета Цикл
							
							Если ЗначениеЗаполнено(ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН + ТекущаяСтрока.СуммаПоставкиНеТребующаяРегистрацииНН) Тогда
								НоваяСтрока = ТаблицаВторыхСобытий.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
							КонецЕсли;
							
						КонецЦикла;
						
						//сюда будем помещать результирующие движения
						ТаблицаДвиженийЗачета.Очистить();
						
						//распределяем оплаты-первые события на реализации-вторые события
						Для каждого ТекущаяСтрока Из ТаблицаПервыхСобытий Цикл
							
							СуммаОперацииПоСтроке = - ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН;
							
							//возврат предоплаты будет первым событием с минусом
							//ситуация, когда вместо возврата предоплаты образуется долг, корректировке не подлежит
							Если СуммаОперацииПоСтроке > 0 Тогда
								
								//1. исходная строка датой расчетного документа - сторно 1 события оплаты по старой аналитике
								СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаДвижений, ТекущаяСтрока);
								
								ОсталосьРаспределить = СуммаОперацииПоСтроке;
								
								Для каждого СтрокаВторогоСобытия Из ТаблицаВторыхСобытий Цикл
									
									СуммаОперацииВторогоСобытия = СтрокаВторогоСобытия.СуммаПоставкиТребующаяРегистрацииНН + СтрокаВторогоСобытия.СуммаПоставкиНеТребующаяРегистрацииНН;
									Если СуммаОперацииВторогоСобытия > 0 Тогда
										
										РаспределеннаяСумма = Мин(ОсталосьРаспределить, СуммаОперацииВторогоСобытия);
										
										Если РаспределеннаяСумма > 0 Тогда
											
											//2. первое событие оплаты по правильной аналитике
											СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
											ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаВторогоСобытия);
											СтрокаДвижений.Период = ТекущаяСтрока.Период;
											СтрокаДвижений.СуммаПоставкиТребующаяРегистрацииНН = РаспределеннаяСумма;
											СтрокаДвижений.СуммаПоставкиНеТребующаяРегистрацииНН = 0;
											
											Если ЗначениеЗаполнено(СтрокаВторогоСобытия.СуммаПоставкиНеТребующаяРегистрацииНН) Тогда
												
												//3. сторно 1 события реализации
												СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
												ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаВторогоСобытия);
												СтрокаДвижений.СуммаПоставкиТребующаяРегистрацииНН = - РаспределеннаяСумма;
												СтрокаДвижений.СуммаПоставкиНеТребующаяРегистрацииНН = 0;
												
												//4. 2 событие по реализации
												СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
												ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаВторогоСобытия);
												СтрокаДвижений.СуммаПоставкиТребующаяРегистрацииНН = 0;
												СтрокаДвижений.СуммаПоставкиНеТребующаяРегистрацииНН = РаспределеннаяСумма;
												
												СтрокаВторогоСобытия.СуммаПоставкиНеТребующаяРегистрацииНН = СтрокаВторогоСобытия.СуммаПоставкиНеТребующаяРегистрацииНН - РаспределеннаяСумма;
												
											Иначе
												
												//если взаимозачет формирует первое событие, то это не закрытие дебиторки, а образование аванса на новой аналитике, но нам он не нужен, первое событие сформирует первичная оплата
												СтрокаВторогоСобытия.СуммаПоставкиТребующаяРегистрацииНН = СтрокаВторогоСобытия.СуммаПоставкиТребующаяРегистрацииНН - РаспределеннаяСумма;
												
											КонецЕсли;
											
											ОсталосьРаспределить = ОсталосьРаспределить - РаспределеннаяСумма;
											
										КонецЕсли;
										
									КонецЕсли;
									
									Если ОсталосьРаспределить = 0 Тогда
										Прервать;
									КонецЕсли;
									
								КонецЦикла;
								
								ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН = ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН + (СуммаОперацииПоСтроке - ОсталосьРаспределить);
								
							КонецЕсли;
							
						КонецЦикла;
						
						//распределяем оплаты-вторые события на реализации-первые события
						Для каждого ТекущаяСтрока Из ТаблицаВторыхСобытий Цикл
							
							СуммаОперацииПоСтроке = - ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН;
							
							//возврат предоплаты будет первым событием с минусом
							Если СуммаОперацииПоСтроке > 0 Тогда
								
								//1. исходная строка датой расчетного документа - сторно 1 события оплаты по старой аналитике
								СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаДвижений, ТекущаяСтрока);
								
								ОсталосьРаспределить = СуммаОперацииПоСтроке;
								
								Для каждого СтрокаПервогоСобытия Из ТаблицаПервыхСобытий Цикл
									
									//списание долга по отгрузке будет вторым событием
									//первого события (образование аванса) в таблице первых событий быть не должно, такую ситуацию не корректируем
									СуммаОперацииПервогоСобытия = СтрокаПервогоСобытия.СуммаПоставкиНеТребующаяРегистрацииНН;
									Если СуммаОперацииПервогоСобытия > 0 Тогда
										
										РаспределеннаяСумма = Мин(ОсталосьРаспределить, СуммаОперацииПервогоСобытия);
										
										Если РаспределеннаяСумма > 0 Тогда
											
											//2. второе событие оплаты по правильной аналитике
											СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
											ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаПервогоСобытия);
											СтрокаДвижений.Период = ТекущаяСтрока.Период;
											СтрокаДвижений.СуммаПоставкиНеТребующаяРегистрацииНН = РаспределеннаяСумма;
											СтрокаДвижений.СуммаПоставкиТребующаяРегистрацииНН = 0;
											
											СтрокаПервогоСобытия.СуммаПоставкиНеТребующаяРегистрацииНН = СтрокаПервогоСобытия.СуммаПоставкиНеТребующаяРегистрацииНН - РаспределеннаяСумма;
											
											ОсталосьРаспределить = ОсталосьРаспределить - РаспределеннаяСумма;
											
										КонецЕсли;
										
									КонецЕсли;
									
									Если ОсталосьРаспределить = 0 Тогда
										Прервать;
									КонецЕсли;
									
								КонецЦикла;
								
								ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН = ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН + (СуммаОперацииПоСтроке - ОсталосьРаспределить);
								
							КонецЕсли;
							
						КонецЦикла;
						
						//если в таблицах событий остались ненулевые суммы, значит распределение прошло не до конца и его результаты использовать нельзя
						Если ТаблицаПервыхСобытий.НайтиСтроки(Новый Структура("СуммаПоставкиТребующаяРегистрацииНН, СуммаПоставкиНеТребующаяРегистрацииНН", 0, 0)).Количество() <> ТаблицаПервыхСобытий.Количество()
							ИЛИ ТаблицаВторыхСобытий.НайтиСтроки(Новый Структура("СуммаПоставкиТребующаяРегистрацииНН, СуммаПоставкиНеТребующаяРегистрацииНН", 0, 0)).Количество() <> ТаблицаВторыхСобытий.Количество() Тогда			
							
							ВыполненаКорректировкаДвиженийВзаимозачета = Ложь;
							
						КонецЕсли;
						
					ИначеЕсли МоментОпределенияБазы = Перечисления.МоментыОпределенияНалоговойБазы.ПоОплате Тогда
						
						//"по оплате" - просто распределяем оплаты по отгрузкам, чтобы скорректировать документ поставки
						// оплаты остаются первыми событиями, по отгрузкам ничего корректировать не нужно
						ТаблицаДвиженийЗачета.Сортировать("Период");
						ТаблицаИсходныхДвижений = ТаблицаДвиженийЗачета;
						ТаблицаДвиженийЗачета = ТаблицаИсходныхДвижений.СкопироватьКолонки();
						
						Для каждого ТекущаяСтрока Из ТаблицаИсходныхДвижений Цикл
							
							СуммаОперацииПоСтроке = - ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН;
							
							//возврат предоплаты будет первым событием с минусом
							//ситуация, когда вместо возврата предоплаты образуется долг, корректировке не подлежит
							Если СуммаОперацииПоСтроке > 0 Тогда
								
								//1. исходная строка датой расчетного документа - сторно 1 события оплаты по старой аналитике
								СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаДвижений, ТекущаяСтрока);
								
								ОсталосьРаспределить = СуммаОперацииПоСтроке;
								
								Для каждого СтрокаОтгрузки Из ТаблицаИсходныхДвижений Цикл
									
									//отгрузка будет первым событием с плюсом
									СуммаОтгрузки = СтрокаОтгрузки.СуммаПоставкиТребующаяРегистрацииНН;
									Если СуммаОтгрузки > 0 Тогда
										
										РаспределеннаяСумма = Мин(ОсталосьРаспределить, СуммаОтгрузки);
										
										Если РаспределеннаяСумма > 0 Тогда
											
											//2. первое событие оплаты по правильной аналитике
											СтрокаДвижений = ТаблицаДвиженийЗачета.Добавить();
											ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаОтгрузки);
											СтрокаДвижений.Период = ТекущаяСтрока.Период;
											СтрокаДвижений.СуммаПоставкиТребующаяРегистрацииНН = РаспределеннаяСумма;
											СтрокаДвижений.СуммаПоставкиНеТребующаяРегистрацииНН = 0;
											
											СтрокаОтгрузки.СуммаПоставкиТребующаяРегистрацииНН = СтрокаОтгрузки.СуммаПоставкиТребующаяРегистрацииНН - РаспределеннаяСумма;
											
											ОсталосьРаспределить = ОсталосьРаспределить - РаспределеннаяСумма;
											
										КонецЕсли;
										
									КонецЕсли;
									
									Если ОсталосьРаспределить = 0 Тогда
										Прервать;
									КонецЕсли;
									
								КонецЦикла;
								
								ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН = ТекущаяСтрока.СуммаПоставкиТребующаяРегистрацииНН + (СуммаОперацииПоСтроке - ОсталосьРаспределить);
								
							КонецЕсли;
							
						КонецЦикла;
						
						//если в таблицах событий остались ненулевые суммы, значит распределение прошло не до конца и его результаты использовать нельзя
						Если ТаблицаИсходныхДвижений.НайтиСтроки(Новый Структура("СуммаПоставкиТребующаяРегистрацииНН, СуммаПоставкиНеТребующаяРегистрацииНН", 0, 0)).Количество() <> ТаблицаИсходныхДвижений.Количество() Тогда			
							
							ВыполненаКорректировкаДвиженийВзаимозачета = Ложь;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ВыполненаКорректировкаДвиженийВзаимозачета Тогда
				
				Для каждого ИсходнаяСтрока Из МассивИсходныхДвиженийЗачета Цикл
					ДвиженияПоАналитике.Удалить(ИсходнаяСтрока);
				КонецЦикла;
				
				Для каждого ТекущаяСтрока Из ТаблицаДвиженийЗачета Цикл
					ЗаполнитьЗначенияСвойств(ДвиженияПоАналитике.Добавить(), ТекущаяСтрока);
				КонецЦикла;
				
			КонецЕсли;
			
			ДвиженияПоАналитике.Свернуть(ПоляСвертки, "СуммаПоставкиТребующаяРегистрацииНН, СуммаПоставкиНеТребующаяРегистрацииНН");
			Для каждого СтрокаДвижения из ДвиженияПоАналитике.НайтиСтроки(Новый Структура("СуммаПоставкиТребующаяРегистрацииНН, СуммаПоставкиНеТребующаяРегистрацииНН", 0, 0)) Цикл
				ДвиженияПоАналитике.Удалить(СтрокаДвижения);
			КонецЦикла;
			
			Для каждого СтрокаДвижения Из ДвиженияПоАналитике Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаДвижений.Добавить(), СтрокаДвижения);
			КонецЦикла;
			
		КонецЦикла;
		
		Если ВыборкаПоРегистратору.ЕстьСохраняемыеДвижения Тогда
			
			НайденныеСтроки = СохраняемыеДвижения.НайтиСтроки(Новый Структура("Регистратор", ВыборкаПоРегистратору.Регистратор));
			Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаДвижений.Добавить(), НайденнаяСтрока);
			КонецЦикла;
			
		КонецЕсли;
		
		Движения.НДСРасчетНалоговыхОбязательств.Загрузить(ТаблицаДвижений);
		
		ЛокальнаяТранзация = НЕ ТранзакцияАктивна();
		Если ЛокальнаяТранзация Тогда
			НачатьТранзакцию();
		КонецЕсли;
		
		// Запишем наборы записей в базу.
		ЗаписатьНаборыЗаписей(Движения, Истина);
		
		ОтразитьЗаданияКФормированиюИсходящихНалоговыхДокументов(, Движения.НДСРасчетНалоговыхОбязательств.ДополнительныеСвойства.МенеджерВременныхТаблиц);

		Если ЛокальнаяТранзация Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ВыполнитьПроведениеДокументовПоВозникшемуНДС

#КонецОбласти

#Область УсловныеПродажи

Процедура СформироватьНалоговыеДокументыПоУсловнымПродажам(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Данные.Организация КАК Организация,
	|	Данные.Дата,
	|	Данные.Номенклатура,
	|	Данные.Характеристика,
	|	Данные.СтавкаНДС,
	|	Данные.НалоговоеНазначение,
	|	Данные.НалоговоеНазначениеПоФакту,
	|	Данные.ПредполагаемоеКоличество,
	|	Данные.Количество,
	|	Данные.ПредполагаемаяСтоимостьРегл,
	|	Данные.СтоимостьРегл,
	|	Данные.ПредполагаемыйНДСРегл,
	|	Данные.НДСРегл,
	|	Данные.ВидУсловнойПродажи КАК ВидУсловнойПродажи,
	|	ВЫБОР
	|		КОГДА Данные.ПредполагаемоеКоличество > Данные.Количество
	|				И Данные.НалоговоеНазначениеПоФакту = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|				И (Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.СводнаяУсловнаяПродажа)
	|					ИЛИ ЕСТЬNULL(СпрНоменклатура.ВестиУчетПоГТД, ЛОЖЬ))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВозможнаДетализация,
	|	АВТОНОМЕРЗАПИСИ() КАК ИндексЗаписи
	|ПОМЕСТИТЬ ВсеДанные
	|ИЗ
	|	ДанныеФормирования КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО Данные.Номенклатура = СпрНоменклатура.Ссылка
	|ГДЕ
	|	(Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.УсловнаяПродажа)
	|			ИЛИ Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.СводнаяУсловнаяПродажа)

	|			ИЛИ Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.УсловнаяПродажаНеоборотныхАктивов)

	|			ИЛИ Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ВосстановлениеНДС))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.Организация КАК Организация,
	|	Данные.Номенклатура,
	|	Данные.Характеристика,
	|	Данные.СтавкаНДС,
	|	Данные.НалоговоеНазначение,
	|	Данные.НалоговоеНазначениеПоФакту,
	|	Данные.ВидУсловнойПродажи КАК ВидУсловнойПродажи,
	|	ВЫБОР
	|		КОГДА Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.УсловнаяПродажа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажа)
	|		КОГДА Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ВосстановлениеНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемоеВосстановлениеНДС)
	|		КОГДА Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.СводнаяУсловнаяПродажа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяСводнаяУсловнаяПродажа)
	|		КОГДА Данные.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.УсловнаяПродажаНеоборотныхАктивов)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажаНеоборотныхАктивов)
	|	КОНЕЦ КАК ПредполагаемыйВидУсловнойПродажи,
	|	Данные.ИндексЗаписи КАК ИндексЗаписи,
	|	НАЧАЛОПЕРИОДА(Данные.Дата, МЕСЯЦ) КАК НачалоПериода,
	|	КОНЕЦПЕРИОДА(Данные.Дата, МЕСЯЦ) КАК КонецПериода
	|ПОМЕСТИТЬ ДанныеДляДетализации
	|ИЗ
	|	ВсеДанные КАК Данные
	|ГДЕ
	|	Данные.ВозможнаДетализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляДетализации.Организация,
	|	ДанныеДляДетализации.Номенклатура,
	|	ДанныеДляДетализации.Характеристика,
	|	ДанныеДляДетализации.СтавкаНДС,
	|	ДанныеДляДетализации.НалоговоеНазначение,
	|	ДанныеДляДетализации.НалоговоеНазначениеПоФакту,
	|	ДанныеДляДетализации.ВидУсловнойПродажи,
	|	ЕСТЬNULL(НДСУсловныеПродажи.НомерГТД.КодУКТВЭД, НДСУсловныеПродажи.НомерГТД) КАК КодНоменклатурыПоКлассификатору,
	|	НДСУсловныеПродажи.НомерСтрокиДокумента,
	|	ДанныеДляДетализации.ИндексЗаписи,
	|	НДСУсловныеПродажи.Количество,
	|	НДСУсловныеПродажи.СтоимостьРегл КАК СуммаБезНДС,
	|	НДСУсловныеПродажи.НДСРегл КАК СуммаНДС
	|ПОМЕСТИТЬ ДанныеСРеквизитами
	|ИЗ
	|	ДанныеДляДетализации КАК ДанныеДляДетализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСУсловныеПродажи КАК НДСУсловныеПродажи
	|		ПО ДанныеДляДетализации.Организация = НДСУсловныеПродажи.Организация
	|			И ДанныеДляДетализации.ПредполагаемыйВидУсловнойПродажи = НДСУсловныеПродажи.ВидУсловнойПродажи
	|			И ДанныеДляДетализации.Номенклатура = НДСУсловныеПродажи.Номенклатура
	|			И ДанныеДляДетализации.Характеристика = НДСУсловныеПродажи.Характеристика
	|			И ДанныеДляДетализации.СтавкаНДС = НДСУсловныеПродажи.СтавкаНДС
	|			И ДанныеДляДетализации.НалоговоеНазначение = НДСУсловныеПродажи.НалоговоеНазначение
	|			И ДанныеДляДетализации.НалоговоеНазначениеПоФакту = НДСУсловныеПродажи.НалоговоеНазначениеПоФакту
	|			И (НДСУсловныеПродажи.Период МЕЖДУ ДанныеДляДетализации.НачалоПериода И ДанныеДляДетализации.КонецПериода)
	|			И (НДСУсловныеПродажи.Активность)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеСРеквизитами.ИндексЗаписи,
	|	ДанныеСРеквизитами.КодНоменклатурыПоКлассификатору,
	|	РегистрацияВходящегоНалоговогоДокументаПоставка.НаименованиеТовара КАК Содержание,
	|	РегистрацияВходящегоНалоговогоДокументаПоставка.ЕдиницаИзмерения,
	|	РегистрацияВходящегоНалоговогоДокументаПоставка.Цена,
	|	ДанныеСРеквизитами.Количество,
	|	ДанныеСРеквизитами.СуммаБезНДС,
	|	ДанныеСРеквизитами.СуммаНДС
	|ИЗ
	|	ДанныеСРеквизитами КАК ДанныеСРеквизитами
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияВходящегоНалоговогоДокумента.Поставка КАК РегистрацияВходящегоНалоговогоДокументаПоставка
	|		ПО ДанныеСРеквизитами.Номенклатура = РегистрацияВходящегоНалоговогоДокументаПоставка.Ссылка
	|			И ДанныеСРеквизитами.НомерСтрокиДокумента = РегистрацияВходящегоНалоговогоДокументаПоставка.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеДанные.Организация,
	|	ВсеДанные.Дата,
	|	ВсеДанные.Номенклатура,
	|	ВсеДанные.Характеристика,
	|	ВсеДанные.СтавкаНДС,
	|	ВсеДанные.НалоговоеНазначение,
	|	ВсеДанные.НалоговоеНазначениеПоФакту,
	|	ВсеДанные.ПредполагаемоеКоличество,
	|	ВсеДанные.Количество,
	|	ВсеДанные.ПредполагаемаяСтоимостьРегл,
	|	ВсеДанные.СтоимостьРегл,
	|	ВсеДанные.ПредполагаемыйНДСРегл,
	|	ВсеДанные.НДСРегл,
	|	ВсеДанные.ВидУсловнойПродажи,
	|	ВсеДанные.ВозможнаДетализация,
	|	ВсеДанные.ИндексЗаписи
	|ИЗ
	|	ВсеДанные КАК ВсеДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВсеДанные.ВидУсловнойПродажи,
	|	ВсеДанные.Организация,
	|	ВсеДанные.НалоговоеНазначениеПоФакту";



	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаУсловныхПродаж = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выбрать();
	ВыборкаДетализация = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выбрать();
	
	Организация = Неопределено;
	НалоговыйОбъект = Неопределено;
	ВидУсловнойПродажи = Перечисления.ВидУсловнойПродажи.УсловнаяПродажа;
	НалоговоеНазначениеПоФакту = Неопределено;
	
	Пока ВыборкаУсловныхПродаж.Следующий() Цикл
		
		Если (НЕ Организация = ВыборкаУсловныхПродаж.Организация)
			 ИЛИ (НЕ ВидУсловнойПродажи = ВыборкаУсловныхПродаж.ВидУсловнойПродажи)
			 ИЛИ (НЕ НалоговоеНазначениеПоФакту = ВыборкаУсловныхПродаж.НалоговоеНазначениеПоФакту)
			 ИЛИ (ВидУсловнойПродажи = Перечисления.ВидУсловнойПродажи.ВосстановлениеНДС) Тогда
			
			Если НЕ Организация = Неопределено Тогда
				//записать налоговую
				НалоговыйОбъект.ЗаполнитьРеквизитыПослеФормированияПоУсловнымПродажам();
				НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			
			//создать новую налоговую
			Если ВыборкаУсловныхПродаж.ВидУсловнойПродажи = Перечисления.ВидУсловнойПродажи.ВосстановлениеНДС Тогда
				НалоговыйОбъект = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
				НалоговыйОбъект.ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Возврат;
			Иначе
				НалоговыйОбъект = Документы.НалоговаяНакладная.СоздатьДокумент();
				НалоговыйОбъект.КодПризнакаСводной = ?(ВыборкаУсловныхПродаж.НалоговоеНазначениеПоФакту = 
					Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально, 2, 1);
				НалоговыйОбъект.ТипПричиныНевыдачиПокупателю = ?(ВыборкаУсловныхПродаж.НалоговоеНазначениеПоФакту = 
					Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность, 13, 9);	
			КонецЕсли;
			
			НалоговыйОбъект.Организация = ВыборкаУсловныхПродаж.Организация;
			НалоговыйОбъект.Дата = ВыборкаУсловныхПродаж.Дата;
			Если ВыборкаУсловныхПродаж.ВидУсловнойПродажи = Перечисления.ВидУсловнойПродажи.СводнаяУсловнаяПродажа Тогда
				НалоговыйОбъект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа;
			ИначеЕсли ВыборкаУсловныхПродаж.ВидУсловнойПродажи = Перечисления.ВидУсловнойПродажи.УсловнаяПродажаНеоборотныхАктивов Тогда
				НалоговыйОбъект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажаНеоборотныхАктивов;
			Иначе
				НалоговыйОбъект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа;
			КонецЕсли;
			НалоговыйОбъект.Статус = Перечисления.СтатусыНалоговыхДокументов.КПроверке;
			
			Организация = ВыборкаУсловныхПродаж.Организация;
			ВидУсловнойПродажи = ВыборкаУсловныхПродаж.ВидУсловнойПродажи;
			НалоговоеНазначениеПоФакту = ВыборкаУсловныхПродаж.НалоговоеНазначениеПоФакту;
			
		КонецЕсли;
		
		//добавить строку ТЧ
		НоваяСтрока = НалоговыйОбъект.ТоварыПоДаннымПользователя.Добавить();
		
		НаправлениеДвижения = ?(ВидУсловнойПродажи = Перечисления.ВидУсловнойПродажи.ВосстановлениеНДС, -1, 1);
		
		Если НЕ ТипЗнч(ВыборкаУсловныхПродаж.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			НоваяСтрока.ОбъектЗаполненияСодержанияУсловнаяПродажа = ВыборкаУсловныхПродаж.Номенклатура;
		Иначе
			НоваяСтрока.Номенклатура = ВыборкаУсловныхПродаж.Номенклатура;
			НоваяСтрока.Характеристика = ВыборкаУсловныхПродаж.Характеристика;
		КонецЕсли;
		НоваяСтрока.СтавкаНДС = ВыборкаУсловныхПродаж.СтавкаНДС;
		НоваяСтрока.НалоговоеНазначение = ВыборкаУсловныхПродаж.НалоговоеНазначение;
		НоваяСтрока.НалоговоеНазначениеПоФактуУсловнаяПродажа = ВыборкаУсловныхПродаж.НалоговоеНазначениеПоФакту;
		НоваяСтрока.Количество = НаправлениеДвижения * (ВыборкаУсловныхПродаж.ПредполагаемоеКоличество - ВыборкаУсловныхПродаж.Количество);
		НоваяСтрока.СуммаНДСРеглПоРегиструУсловнаяПродажа = НаправлениеДвижения * (ВыборкаУсловныхПродаж.ПредполагаемыйНДСРегл - ВыборкаУсловныхПродаж.НДСРегл);
		НоваяСтрока.СуммаНДС = НаправлениеДвижения * (ВыборкаУсловныхПродаж.ПредполагаемыйНДСРегл - ВыборкаУсловныхПродаж.НДСРегл);
		НоваяСтрока.СуммаБезНДС = НаправлениеДвижения * (ВыборкаУсловныхПродаж.ПредполагаемаяСтоимостьРегл - ВыборкаУсловныхПродаж.СтоимостьРегл); 
		
		Если ВыборкаУсловныхПродаж.ВозможнаДетализация Тогда
			
			ВыборкаДетализация.Сбросить();
			
			Пока ВыборкаДетализация.НайтиСледующий(ВыборкаУсловныхПродаж.ИндексЗаписи, "ИндексЗаписи") Цикл
				
				СтрокаДетализации = НалоговыйОбъект.ТоварыПоДаннымПользователя.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДетализации, НоваяСтрока); 
				ЗаполнитьЗначенияСвойств(СтрокаДетализации, ВыборкаДетализация); 
				
				СтрокаДетализации.СуммаБезНДС = НаправлениеДвижения * Мин(НаправлениеДвижения * НоваяСтрока.СуммаБезНДС, ВыборкаДетализация.СуммаБезНДС);
				СтрокаДетализации.СуммаНДС = НаправлениеДвижения * Мин(НаправлениеДвижения * НоваяСтрока.СуммаНДС, ВыборкаДетализация.СуммаНДС);
				Если ЗначениеЗаполнено(НоваяСтрока.Количество) Тогда
					СтрокаДетализации.Количество = НаправлениеДвижения * Мин(НаправлениеДвижения * НоваяСтрока.Количество, ВыборкаДетализация.Количество);
					НоваяСтрока.Количество = НоваяСтрока.Количество - СтрокаДетализации.Количество;
				Иначе
					СтрокаДетализации.Количество = НаправлениеДвижения * ВыборкаДетализация.Количество;
				КонецЕсли; 
				СтрокаДетализации.СуммаНДСРеглПоРегиструУсловнаяПродажа = СтрокаДетализации.СуммаНДС;
				
				НоваяСтрока.СуммаНДС = НоваяСтрока.СуммаНДС - СтрокаДетализации.СуммаНДС;
				НоваяСтрока.СуммаБезНДС = НоваяСтрока.СуммаБезНДС - СтрокаДетализации.СуммаБезНДС;
				НоваяСтрока.СуммаНДСРеглПоРегиструУсловнаяПродажа = НоваяСтрока.СуммаНДСРеглПоРегиструУсловнаяПродажа - СтрокаДетализации.СуммаНДСРеглПоРегиструУсловнаяПродажа;
				
				Если НоваяСтрока.СуммаБезНДС = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если НоваяСтрока.СуммаБезНДС = 0 И НоваяСтрока.СуммаНДС = 0 Тогда
				НалоговыйОбъект.ТоварыПоДаннымПользователя.Удалить(НоваяСтрока);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	//записать налоговую
	Если НЕ НалоговыйОбъект = Неопределено Тогда
		НалоговыйОбъект.ЗаполнитьРеквизитыПослеФормированияПоУсловнымПродажам();
		НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляАнализаУсловныхПродаж(МенеджерВременныхТаблиц, СписокОрганизаций, ДатаПериода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНач",       		НачалоМесяца(ДатаПериода));
	Запрос.УстановитьПараметр("ДатаКон",    		КонецМесяца(ДатаПериода));
	Запрос.УстановитьПараметр("ДатаНачНеВключая",   Новый Граница(НачалоМесяца(ДатаПериода), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",        СписокОрганизаций);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(СписокОрганизаций));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация
	|ПОМЕСТИТЬ ПлательщикиНДС
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(
	|			&ДатаКон,
	|			Организация В (&Организация)
	|				ИЛИ &ПоВсемОрганизациям) КАК УчетнаяПолитикаОрганизацийСрезПоследних
	|ГДЕ
	|	УчетнаяПолитикаОрганизацийСрезПоследних.ПлательщикНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусыПлательщиковЕдиногоНалогаСрезПоследних.Организация,
	|	МИНИМУМ(СтатусыПлательщиковЕдиногоНалогаСрезПоследних.Период) КАК ПериодПереходаНаЕН2
	|ПОМЕСТИТЬ ПериодыПереходаНаЕН2
	|ИЗ
	|	РегистрСведений.СтатусыПлательщиковЕдиногоНалога КАК СтатусыПлательщиковЕдиногоНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлательщикиНДС КАК ПлательщикиНДС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПлательщиковЕдиногоНалога.СрезПоследних(&ДатаНачНеВключая, ) КАК СтатусыПлательщиковЕдиногоНалогаСрезПоследних
	|			ПО (СтатусыПлательщиковЕдиногоНалогаСрезПоследних.Организация = ПлательщикиНДС.Организация)
	|		ПО СтатусыПлательщиковЕдиногоНалога.Организация = ПлательщикиНДС.Организация
	|ГДЕ
	|	СтатусыПлательщиковЕдиногоНалога.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И СтатусыПлательщиковЕдиногоНалога.ПлательщикНДС
	|	И СтатусыПлательщиковЕдиногоНалогаСрезПоследних.ГруппаПлательщикаЕдиногоНалога = ЗНАЧЕНИЕ(Перечисление.ГруппыПлательщиковЕдиногоНалога.ТретьяГруппаОсобая)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусыПлательщиковЕдиногоНалогаСрезПоследних.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлательщикиНДС.Организация,
	|	ЕСТЬNULL(ПериодыПереходаНаЕН2.ПериодПереходаНаЕН2, &ДатаНач) КАК НачалоПериодаАнализаУсловныхПродаж,
	|	ВЫБОР
	|		КОГДА ПериодыПереходаНаЕН2.Организация ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасширенныйПериод
	|ПОМЕСТИТЬ ПериодыАнализаПродажПоОрганизациям
	|ИЗ
	|	ПлательщикиНДС КАК ПлательщикиНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыПереходаНаЕН2 КАК ПериодыПереходаНаЕН2
	|		ПО ПлательщикиНДС.Организация = ПериодыПереходаНаЕН2.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыАнализаПродажПоОрганизациям.Организация,
	|	ПериодыАнализаПродажПоОрганизациям.НачалоПериодаАнализаУсловныхПродаж КАК НачалоПериодаАнализаУсловныхПродаж
	|ИЗ
	|	ПериодыАнализаПродажПоОрганизациям КАК ПериодыАнализаПродажПоОрганизациям
	|ИТОГИ ПО
	|	НачалоПериодаАнализаУсловныхПродаж";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ШаблонЗапроса = 
	"ВЫБРАТЬ
	|	НДСУсловныеПродажиОбороты.Организация,
	|	ВЫБОР
	|		КОГДА НДСУсловныеПродажиОбороты.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.УсловнаяПродажа)
	|		КОГДА НДСУсловныеПродажиОбороты.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемоеВосстановлениеНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ВосстановлениеНДС)
	|		КОГДА НДСУсловныеПродажиОбороты.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяСводнаяУсловнаяПродажа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.СводнаяУсловнаяПродажа)
	|		КОГДА НДСУсловныеПродажиОбороты.ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажаНеоборотныхАктивов)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.УсловнаяПродажаНеоборотныхАктивов)
	|		ИНАЧЕ НДСУсловныеПродажиОбороты.ВидУсловнойПродажи
	|	КОНЕЦ КАК ВидУсловнойПродажи,
	|	НДСУсловныеПродажиОбороты.Номенклатура,
	|	НДСУсловныеПродажиОбороты.Характеристика,
	|	НДСУсловныеПродажиОбороты.СтавкаНДС,
	|	НДСУсловныеПродажиОбороты.НалоговоеНазначение,
	|	НДСУсловныеПродажиОбороты.НалоговоеНазначениеПоФакту,
	|	НДСУсловныеПродажиОбороты.КоличествоОборот КАК Количество,
	|	НДСУсловныеПродажиОбороты.СтоимостьРеглОборот КАК СтоимостьРегл,
	|	НДСУсловныеПродажиОбороты.НДСРеглОборот КАК НДСРегл,
	|	НДСУсловныеПродажиОбороты.НДСРеглРучныеКорректировкиОборот КАК НДСРеглРучныеКорректировки,
	|	ВЫБОР
	|		КОГДА НДСУсловныеПродажиОбороты.ВидУсловнойПродажи В (ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажа), ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемоеВосстановлениеНДС), ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяСводнаяУсловнаяПродажа), ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажаНеоборотныхАктивов))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПредполагаемаяОперация
	|ИЗ
	|	РегистрНакопления.НДСУсловныеПродажи.Обороты(&ДатаНач_ИндексПериода, &ДатаКон_ИндексПериода, , Организация В (&МассивОрганизаций_ИндексПериода)) КАК НДСУсловныеПродажиОбороты";
	
	
	ТекстЗапросаПериоды = "";
	
	ИндексПериода = 1;
	ВыборкаПериод = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПериод.Следующий() Цикл
		
		МассивОрганизаций = Новый Массив;
		
		Выборка = ВыборкаПериод.Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивОрганизаций.Добавить(Выборка.Организация);
		КонецЦикла;
		
		
		Если НЕ ПустаяСтрока(ТекстЗапросаПериоды) Тогда
			ТекстЗапросаПериоды = ТекстЗапросаПериоды + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		ИндексПериодаСтрокой = Формат(ИндексПериода, "ЧГ=");		
		ТекстЗапросаПериоды = ТекстЗапросаПериоды + СтрЗаменить(ШаблонЗапроса, "ИндексПериода", ИндексПериодаСтрокой);
		Запрос.УстановитьПараметр("ДатаНач_" + ИндексПериодаСтрокой, ВыборкаПериод.НачалоПериодаАнализаУсловныхПродаж);
		Запрос.УстановитьПараметр("ДатаКон_" + ИндексПериодаСтрокой, КонецМесяца(ДатаПериода));
		Запрос.УстановитьПараметр("МассивОрганизаций_" + ИндексПериодаСтрокой, МассивОрганизаций);
		
		ИндексПериода = ИндексПериода + 1;
		
	КонецЦикла; 
	
	Если ПустаяСтрока(ТекстЗапросаПериоды) Тогда
		
		ТекстЗапросаПериоды = 
		"ВЫБРАТЬ ПЕРВЫЕ 0 
		|NULL КАК Организация,
		|NULL КАК ВидУсловнойПродажи,
		|NULL КАК Номенклатура,
		|NULL КАК Характеристика,
		|NULL КАК СтавкаНДС,
		|NULL КАК НалоговоеНазначение,
		|NULL КАК НалоговоеНазначениеПоФакту,
		|0 КАК Количество,
		|0 КАК СтоимостьРегл,
		|0 КАК НДСРегл,
		|0 КАК НДСРеглРучныеКорректировки,
		|ЛОЖЬ КАК ПредполагаемаяОперация";
		
	КонецЕсли;

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УсловныеПродажи.ВидУсловнойПродажи,
	|	ВЫБОР
	|		КОГДА НЕ СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА УсловныеПродажи.Количество ИНАЧЕ 0 КОНЕЦ) = СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА 0 ИНАЧЕ УсловныеПродажи.Количество КОНЕЦ)
	|			ТОГДА 4
	|		КОГДА НЕ СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА УсловныеПродажи.НДСРегл ИНАЧЕ 0 КОНЕЦ) = СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА 0 ИНАЧЕ УсловныеПродажи.НДСРегл КОНЕЦ)
	|			ТОГДА 3
	|		КОГДА НЕ СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА УсловныеПродажи.СтоимостьРегл ИНАЧЕ 0 КОНЕЦ) = СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА 0 ИНАЧЕ УсловныеПродажи.СтоимостьРегл КОНЕЦ)
	|				ИЛИ НЕ СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА 0 ИНАЧЕ УсловныеПродажи.НДСРегл КОНЕЦ) = СУММА(УсловныеПродажи.НДСРеглРучныеКорректировки)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтатусРасхождения,
	|	УсловныеПродажи.Организация,
	|	УсловныеПродажи.Номенклатура,
	|	УсловныеПродажи.Характеристика,
	|	УсловныеПродажи.СтавкаНДС,
	|	УсловныеПродажи.НалоговоеНазначение,
	|	УсловныеПродажи.НалоговоеНазначениеПоФакту,
	|	СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА УсловныеПродажи.Количество ИНАЧЕ 0 КОНЕЦ) КАК ПредполагаемоеКоличество,
	|	СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА 0 ИНАЧЕ УсловныеПродажи.Количество КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА УсловныеПродажи.СтоимостьРегл ИНАЧЕ 0 КОНЕЦ) КАК ПредполагаемаяСтоимостьРегл,
	|	СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА 0 ИНАЧЕ УсловныеПродажи.СтоимостьРегл КОНЕЦ) КАК СтоимостьРегл,
	|	СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА УсловныеПродажи.НДСРегл ИНАЧЕ 0 КОНЕЦ) КАК ПредполагаемыйНДСРегл,
	|	СУММА(ВЫБОР КОГДА УсловныеПродажи.ПредполагаемаяОперация ТОГДА 0 ИНАЧЕ УсловныеПродажи.НДСРегл КОНЕЦ) КАК НДСРегл,
	|	СУММА(УсловныеПродажи.НДСРеглРучныеКорректировки) КАК НДСРеглРучныеКорректировки
	|ПОМЕСТИТЬ АнализУсловныхПродаж
	|ИЗ
	|	(
	|" + ТекстЗапросаПериоды + " 
	|	) КАК УсловныеПродажи
	|СГРУППИРОВАТЬ ПО
	|	УсловныеПродажи.Организация,
	|	УсловныеПродажи.Номенклатура,
	|	УсловныеПродажи.Характеристика,
	|	УсловныеПродажи.СтавкаНДС,
	|	УсловныеПродажи.НалоговоеНазначение,
	|	УсловныеПродажи.НалоговоеНазначениеПоФакту,
	|	УсловныеПродажи.ВидУсловнойПродажи";
	
	
	Запрос.Выполнить();
	
КонецПроцедуры

#Область ОбработчикиЭтаповЗакрытияМесяца

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияЭтапов
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_ОформлениеННУсловныеПродажи(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(
		ТаблицаЭтапов, 
		ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.ОформлениеННУсловныеПродажи,
		,
		,
		,
		?(ПолучитьФункциональнуюОпцию("УправлениеТорговлей"), 
			Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости,
			Перечисления.ОперацииЗакрытияМесяца.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций
		)
	);      
	
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости);
	//++ НЕ УТ
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций);
	//-- НЕ УТ
	НоваяСтрока.ВыполняетсяВручную = Истина;
	НоваяСтрока.Информационный = Истина;
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"НДСИсходящийСервер.Использование_ОформлениеННУсловныеПродажи");
	НоваяСтрока.ДействиеОформление = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"НДСИсходящийСервер.Оформление_ОформлениеННУсловныеПродажи");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Обработки.НДСФормированиеУсловныхПродаж.ОсновнаяФорма.ПолноеИмя());

КонецПроцедуры

Процедура Использование_ОформлениеННУсловныеПродажи(ПараметрыОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	ПодготовитьДанныеДляАнализаУсловныхПродаж(Запрос.МенеджерВременныхТаблиц, Запрос.Параметры.МассивОрганизаций, Запрос.Параметры.КонецПериода); 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АнализУсловныхПродаж.Организация КАК Организация,
	|	АнализУсловныхПродаж.ВидУсловнойПродажи КАК ВидУсловнойПродажи,
	|	АнализУсловныхПродаж.Номенклатура КАК Номенклатура,
	|	АнализУсловныхПродаж.Характеристика КАК Характеристика,
	|	АнализУсловныхПродаж.СтавкаНДС КАК СтавкаНДС,
	|	АнализУсловныхПродаж.НалоговоеНазначение КАК НалоговоеНазначение,
	|	АнализУсловныхПродаж.НалоговоеНазначениеПоФакту КАК НалоговоеНазначениеПоФакту,
	|	АнализУсловныхПродаж.Количество КАК Количество,
	|	АнализУсловныхПродаж.ПредполагаемоеКоличество КАК ПредполагаемоеКоличество,
	|	АнализУсловныхПродаж.СтоимостьРегл КАК СтоимостьРегл,
	|	АнализУсловныхПродаж.ПредполагаемаяСтоимостьРегл КАК ПредполагаемаяСтоимостьРегл,
	|	АнализУсловныхПродаж.НДСРегл КАК НДСРегл,
	|	АнализУсловныхПродаж.ПредполагаемыйНДСРегл КАК ПредполагаемыйНДСРегл
	|ПОМЕСТИТЬ ОформлениеННУсловныеПродажи
	|ИЗ
	|	АнализУсловныхПродаж КАК АнализУсловныхПродаж
	|ГДЕ
	|	
	|	АнализУсловныхПродаж.СтатусРасхождения > 2";
	
	Запрос.Выполнить();
	
	Если ЗакрытиеМесяцаСервер.РазмерВременнойТаблицы(Запрос, "ОформлениеННУсловныеПродажи", ПараметрыОбработчика) = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(ПараметрыОбработчика);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Оформление_ОформлениеННУсловныеПродажи(ПараметрыОбработчика) Экспорт
	
	ПараметрыОбработчика.ДанныеЭтапа.ТекстВыполнить = НСтр("ru='Оформить';uk='Оформити'");
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика);
	
КонецПроцедуры

Процедура ОписаниеПроверок_ОформлениеННУсловныеПродажи(ТаблицаПроверок) Экспорт
	
	// Отрицательные остатки товаров организаций.
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"ОформлениеННУсловныеПродажи",
		Перечисления.ОперацииЗакрытияМесяца.ОформлениеННУсловныеПродажи,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ДоИПослеРасчета,
		"НДСИсходящийСервер.ПроверкаОформленияННУсловныеПродажи");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru='Наличие налоговых документов на операции использования в необлагаемой деятельности';uk='Наявність податкових документів на операції використання у неоподатковуваній діяльності'",ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru='На все операции приобретения/использования запасов и необоротных активов для/в необлагаемой деятельности необходимо оформить налоговые документы и начислить налоговые обязательства.';uk='На всі операції придбання/використання запасів і необоротних активів для неоподатковуваної діяльності необхідно оформити податкові документи та нарахувати податкові зобов''язання.'",ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

Процедура ПроверкаОформленияННУсловныеПродажи(ПараметрыПроверки) Экспорт
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация", 				НСтр("ru='Организация';uk='Організація'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("ВидУсловнойПродажи", 			НСтр("ru='Вид налоговых обязательств';uk='Вид податкових зобов''язань'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Номенклатура", 				НСтр("ru='Номенклатура';uk='Номенклатура'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("СтавкаНДС", 					НСтр("ru='Ставка НДС';uk='Ставка ПДВ'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НалоговоеНазначение", 		НСтр("ru='Налоговое назначение';uk='Податкове призначення'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НалоговоеНазначениеПоФакту", 	НСтр("ru='Налоговое назначение по факту';uk='Податкове призначення по факту'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("ПредполагаемоеКоличество",	НСтр("ru='Количество к оформлению';uk='Кількість до оформлення'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Количество",		 			НСтр("ru='Количество оформлено';uk='Кількість оформлено'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("ПредполагаемаяСтоимостьРегл",	НСтр("ru='Стоимость к оформлению';uk='Вартість до оформлення'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("СтоимостьРегл",		 		НСтр("ru='Стоимость оформлено';uk='Вартість оформлено'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("ПредполагаемыйНДСРегл",		НСтр("ru='НДС к оформлению';uk='ПДВ до оформлення'",ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НДСРегл",		 				НСтр("ru='НДС оформлено';uk='ПДВ оформлено'",ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"ОформлениеННУсловныеПродажи",
		НСтр("ru='Требуется оформления налоговых документов на операции использования в необлагаемой деятельности в организации ""%1"" за %2';uk='Потрібно оформлення податкових документів на операції використання в неоподатковуваній діяльності в організації ""%1"" за %2'",ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей,, Метаданные.РегистрыНакопления.НДСУсловныеПродажи.ПолноеИмя());
		
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрации);
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область КорректировкиУсловныхПродажПоПропорциональномуНДС

Процедура СформироватьДокументыПриложение2ПоУсловнымПродажамПропорциональныйНДС(ПараметрыОтбора, НалоговыеНакладные = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	УстановитьПараметрыЗапросаКорректировкаПоПропорциональномуНДС(Запрос, ПараметрыОтбора);
	
	Если НалоговыеНакладные = Неопределено Тогда
		
		Запрос.Текст = ТекстЗапросаНалоговыхДокументов(,Ложь);
		
		РезультатЗапроса = Запрос.Выполнить();
	    Если РезультатЗапроса.Пустой() Тогда
			ТекстСообщения = НСтр("ru='Нет налоговых накладных удовлетворяющих условию. Формирование документов ""Приложение 2 к налоговой накладной"" не выполнено!';uk='Немає податкових накладних що задовільняють умові. Формування документів ""Додаток 2 до податкової накладної"" не виконано!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ВыборкаНалоговыхНакладныхПоОрганизации = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Иначе
		Запрос.Текст = ТекстЗапросаНалоговыхДокументов();
		Запрос.УстановитьПараметр("МассивНалоговыхДокументов", НалоговыеНакладные);
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаНалоговыхНакладныхПоОрганизации = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	КонецЕсли;
	
	// Также получим уже сформированные документы П2 для последующего удаления (пометки на удаления), если выполняется переформирование
	Запрос.Текст = ТекстЗапросаНалоговыхДокументов(Ложь);	
	ВыборкаП2 = Запрос.Выполнить().Выбрать(); 
	
	// Суммы для корректировки
	Запрос.Текст = ТекстЗапросаСуммыКорректировокПропорциональногоНДС();
	ТаблицаСуммыКорректировок = Запрос.Выполнить().Выгрузить();
	
	СтруктураОтбора = Новый Структура("Организация,СпецРежимНалогообложения");
	
	ДатаП2 = Запрос.Параметры.КонецГода;
	
	Пока ВыборкаНалоговыхНакладныхПоОрганизации.Следующий() Цикл
		
		ВыборкаПоСпецРежиму = ВыборкаНалоговыхНакладныхПоОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСпецРежиму.Следующий() Цикл
		
			ОбщаяСуммаПоНН = ВыборкаПоСпецРежиму.СуммаНДС;
			Если ОбщаяСуммаПоНН = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, ВыборкаПоСпецРежиму);
			СтрокиТЗСуммыКорректировок = ТаблицаСуммыКорректировок.НайтиСтроки(СтруктураОтбора);
			
			Если СтрокиТЗСуммыКорректировок.Количество() = 0 Или СтрокиТЗСуммыКорректировок[0].ПредполагаемаяСуммаКорректировкиНДС = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			СтрокаТЗСуммыКорректировок = СтрокиТЗСуммыКорректировок[0];
			СуммаКРаспределению = СтрокаТЗСуммыКорректировок.ПредполагаемаяСуммаКорректировкиНДС;
			Коэффициент = СуммаКРаспределению / ОбщаяСуммаПоНН;
			РаспределеннаяСумма = 0;
			НалоговыйОбъект = Неопределено;
			
			Если СуммаКРаспределению < 0 И (-СуммаКРаспределению) > ОбщаяСуммаПоНН Тогда
				ТекстСообщения = НСтр("ru='Для организации %1 (спец режим %2) отрицательная сумма корректировки больше выписанных (выбранных) налоговых обязательств. По такой организации документы ""Приложение 2 к налоговой накладной"" созданы не будут. Рекомендуется исправить и повторить действие для указанной организации.';uk='Для організації %1 (спец режим %2) від''ємна сума коригування більше виписаних (обраних) податкових зобов''язань. За такої організації документи ""Додаток 2 до податкової накладної"" створені не будуть. Рекомендується виправити і повторити дію для зазначеної організації.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТЗСуммыКорректировок.Организация, СтрокаТЗСуммыКорректировок.СпецРежимНалогообложения); 
			    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Продолжить;
			КонецЕсли;
			
			// Пометим на удаление найденные П2, скорее всего, это повторное формирование (переформирование)
			ВыборкаП2.Сбросить();
			Пока ВыборкаП2.НайтиСледующий(СтруктураОтбора) Цикл
				ДокументП2Объект = ВыборкаП2.Ссылка.ПолучитьОбъект();
				Попытка
					ДокументП2Объект.УстановитьПометкуУдаления(Истина);
				Исключение
				КонецПопытки;
			КонецЦикла;
			
			// Создаем П2 для НН на все строки пропрционально суммам
			ВыборкаНалоговыхНакладных = ВыборкаПоСпецРежиму.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНалоговыхНакладных.Следующий() Цикл
				
				НалоговыйОбъект = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
				// Реквизиты шапки
				НалоговыйОбъект.НалоговаяНакладная = ВыборкаНалоговыхНакладных.НалоговаяНакладная;
				НалоговыйОбъект.Дата = ДатаП2;
				НалоговыйОбъект.ВидОперацииВозвратКорректировка = Перечисления.ВидыОперацийПриложений2КНалоговойНакладной.Корректировка;	
				НалоговыйОбъект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа;	
				НалоговыйОбъект.Организация = ВыборкаНалоговыхНакладных.Организация;
				НалоговыйОбъект.ОбособленноеПодразделение = ВыборкаНалоговыхНакладных.ОбособленноеПодразделение;
				НалоговыйОбъект.СпецРежимНалогообложения = ВыборкаНалоговыхНакладных.СпецРежимНалогообложения;
				НалоговыйОбъект.ТипПричиныНевыдачиПокупателю = ВыборкаНалоговыхНакладных.ТипПричиныНевыдачиПокупателю;
				НалоговыйОбъект.Валюта = ВыборкаНалоговыхНакладных.Валюта;
				НалоговыйОбъект.Статус = Перечисления.СтатусыНалоговыхДокументов.КПроверке;
				НалоговыйОбъект.ГодовойПерерасчетПропорциональногоНДС = Истина;
				НалоговыйОбъект.Комментарий = НСтр("ru='Документ создан автоматически при корректировке пропорционального НДС на основании годового перерасчета';uk='Документ створений автоматично при коригуванні пропорційного ПДВ на підставі річного перерахунку'");
				
				НомерСтрокиДляНовыхСтрок = ВыборкаНалоговыхНакладных.НомерСтрокиНН + 1;
				КодПричины = 202;
				НомерГруппы = 1;
				
				ВыборкаСтрокНН = ВыборкаНалоговыхНакладных.Выбрать();
				Пока ВыборкаСтрокНН.Следующий() Цикл
					
					// Сторно строки из НН  
					СтрокаСторноНН = НалоговыйОбъект.ТоварыПоДаннымПользователя.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаСторноНН, ВыборкаСтрокНН);
					СтрокаСторноНН.КодПричины = КодПричины;
					СтрокаСторноНН.НомерГруппы = НомерГруппы;
					СтрокаСторноНН.КоличествоУпаковок = КодПричины;
					СтрокаСторноНН.Цена = -ВыборкаСтрокНН.СуммаБезНДС;
					СтрокаСторноНН.СуммаБезНДС = -ВыборкаСтрокНН.СуммаБезНДС;
					СтрокаСторноНН.СуммаНДС = -ВыборкаСтрокНН.СуммаНДС;
					
					// Новая строка, остаточная, после корректировки
					СтрокаНовая = НалоговыйОбъект.ТоварыПоДаннымПользователя.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаНовая, СтрокаСторноНН);
					
					СуммаКорректировкиНДССтроки = Окр(ВыборкаСтрокНН.СуммаНДС * (1 + Коэффициент), 2);
                    ПроцентНДС = УчетНДСУПВызовСервера.ЗначениеСтавкиНДС(ВыборкаСтрокНН.СтавкаНДС) / 100;
					СуммаБазыНДС = СуммаКорректировкиНДССтроки / ПроцентНДС;
					
					СтрокаНовая.Цена = СуммаБазыНДС;
					СтрокаНовая.СуммаБезНДС = СуммаБазыНДС;
					СтрокаНовая.СуммаНДС = СуммаКорректировкиНДССтроки;
					СтрокаНовая.НомерСтрокиНН = НомерСтрокиДляНовыхСтрок;
					
					НомерГруппы = НомерГруппы + 1;
					НомерСтрокиДляНовыхСтрок = НомерСтрокиДляНовыхСтрок + 1;
					РаспределеннаяСумма = РаспределеннаяСумма + (СуммаКорректировкиНДССтроки - ВыборкаСтрокНН.СуммаНДС);
					
				КонецЦикла;
				
				Если НЕ НалоговыйОбъект = Неопределено Тогда
					НалоговыйОбъект.ЗаполнитьРеквизитыПослеФормированияПоУсловнымПродажам();
					НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
				КонецЕсли;
				
			КонецЦикла;
			
			Если РаспределеннаяСумма <> СуммаКРаспределению Тогда
				
				СуммаНДСНераспределенная = СуммаКРаспределению - РаспределеннаяСумма;
				
				Если НалоговыйОбъект <> Неопределено Тогда
					
					// Добавим разницу к последней строке последней НН по организации (спец режиму)
					ПоследняяСтрока = НалоговыйОбъект.ТоварыПоДаннымПользователя[НалоговыйОбъект.ТоварыПоДаннымПользователя.Количество()-1];
                    
                    ПроцентНДС = УчетНДСУПВызовСервера.ЗначениеСтавкиНДС(ПоследняяСтрока.СтавкаНДС) / 100;
					БазаНДСНераспределенная = СуммаНДСНераспределенная / ПроцентНДС;
					
					ПоследняяСтрока.Цена = ПоследняяСтрока.Цена + БазаНДСНераспределенная;
					ПоследняяСтрока.СуммаБезНДС = ПоследняяСтрока.СуммаБезНДС + БазаНДСНераспределенная;
					ПоследняяСтрока.СуммаНДС = ПоследняяСтрока.СуммаНДС + СуммаНДСНераспределенная;
					
					НалоговыйОбъект.ЗаполнитьРеквизитыПослеФормированияПоУсловнымПродажам();
					НалоговыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
				КонецЕсли;	
			КонецЕсли;
		
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаДинамическогоСпискаНалоговыхДокументов(НалоговаяНакладная = Истина) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НалоговыйДокумент.Ссылка КАК Ссылка,
	|	НалоговыйДокумент.Дата КАК Дата,
	|	НалоговыйДокумент.Номер КАК Номер,
	|	НалоговыйДокумент.ВидОперации КАК ВидОперации,
	|	НалоговыйДокумент.Статус КАК Статус,
	|	НалоговыйДокумент.СтатусРегистрацииВЕРНН КАК СтатусРегистрацииВЕРНН,
	|	НалоговыйДокумент.Организация КАК Организация,
	|	НалоговыйДокумент.СпецРежимНалогообложения КАК СпецРежимНалогообложения,
	|	НалоговыйДокумент.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	НалоговыйДокумент.Валюта КАК Валюта,
	|	НалоговыйДокумент.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.НалоговаяНакладная КАК НалоговыйДокумент
	|ГДЕ
	|	НалоговыйДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа)
	|	И (НалоговыйДокумент.Организация = &Организация
	|			ИЛИ &ПоВсемОрганизациям)
	|	И (НалоговыйДокумент.СпецРежимНалогообложения = &СпецРежимНалогообложения
	|			ИЛИ &ПоВсемСпецРежимамНалогообложения)
	|	И НалоговыйДокумент.Дата МЕЖДУ &НачалоГода И &КонецГода";
	
	Если НалоговаяНакладная Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|	И НалоговыйДокумент.Проведен
			|	И (НалоговыйДокумент.ТипПричиныНевыдачиПокупателю = 9 или НалоговыйДокумент.ТипПричиныНевыдачиПокупателю = 8)
			|	И (НалоговыйДокумент.КодПризнакаСводной = 0 или НалоговыйДокумент.КодПризнакаСводной = 2)";
		
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.НалоговаяНакладная", "Документ.Приложение2КНалоговойНакладной");
		РеквизитННДляП2 = 	"	НалоговыйДокумент.СуммаДокумента КАК СуммаДокумента,
							|	НалоговыйДокумент.НалоговаяНакладная КАК НалоговаяНакладная";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НалоговыйДокумент.СуммаДокумента КАК СуммаДокумента", РеквизитННДляП2);
		ТекстЗапроса = ТекстЗапроса + "
			|	И НалоговыйДокумент.ГодовойПерерасчетПропорциональногоНДС";
		
	КонецЕсли; 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаНалоговыхДокументов(НалоговаяНакладная = Истина, ОтборПоДокументам = Истина)
	
	Если НалоговаяНакладная Тогда
	 
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НалоговыйДокумент.Ссылка КАК НалоговаяНакладная,
		|	НалоговыйДокумент.Ссылка.Организация КАК Организация,
		|	НалоговыйДокумент.Ссылка.ВидОперации КАК ВидОперации,
		|	НалоговыйДокумент.Ссылка.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|	НалоговыйДокумент.Ссылка.СпецРежимНалогообложения КАК СпецРежимНалогообложения,
		|	НалоговыйДокумент.Ссылка.ТипПричиныНевыдачиПокупателю КАК ТипПричиныНевыдачиПокупателю,
		|	НалоговыйДокумент.Ссылка.Валюта КАК Валюта,
		|	НалоговыйДокумент.Ссылка.СуммаДокумента КАК СуммаДокумента,
		|	НалоговыйДокумент.НомерСтроки КАК НомерСтрокиНН,
		|	НалоговыйДокумент.Содержание,
		|	НалоговыйДокумент.Упаковка,
		|	НалоговыйДокумент.ЕдиницаИзмерения,
		|	НалоговыйДокумент.НомерГТД,
		|	НалоговыйДокумент.СтавкаНДС,
		|	НалоговыйДокумент.НалоговоеНазначение,
		|	НалоговыйДокумент.НалоговоеНазначениеПоФактуУсловнаяПродажа,
		|	НалоговыйДокумент.ОбъектЗаполненияСодержанияУсловнаяПродажа,
		|	НалоговыйДокумент.СуммаБезНДСРегл КАК СуммаБезНДС,
		|	НалоговыйДокумент.СуммаНДСРегл КАК СуммаНДС
		|ИЗ
		|	Документ.НалоговаяНакладная.ТоварыПоДаннымПользователя КАК НалоговыйДокумент
		|ГДЕ
		|	НалоговыйДокумент.Ссылка В(&МассивНалоговыхДокументов)
		|	И (НалоговыйДокумент.СтавкаНДС.ПеречислениеСтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
        |			ИЛИ НалоговыйДокумент.СтавкаНДС.ПеречислениеСтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС14)
		|			ИЛИ НалоговыйДокумент.СтавкаНДС.ПеречислениеСтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7))
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтрокиНН
		|ИТОГИ
		|	СУММА(СуммаНДС),
		|	МАКСИМУМ(НомерСтрокиНН)
		|ПО
		|	Организация,
		|	СпецРежимНалогообложения,
		|	НалоговаяНакладная";
		
		Если Не ОтборПоДокументам Тогда
			
			ТекстОтбора = "
			|	НалоговыйДокумент.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа)
			|	И НалоговыйДокумент.Ссылка.Проведен
			|	И (НалоговыйДокумент.Ссылка.Организация = &Организация
			|			ИЛИ &ПоВсемОрганизациям)
			|	И (НалоговыйДокумент.Ссылка.СпецРежимНалогообложения = &СпецРежимНалогообложения
			|			ИЛИ &ПоВсемСпецРежимамНалогообложения)
			|	И НалоговыйДокумент.Ссылка.Дата МЕЖДУ &НачалоГода И &КонецГода
			|	И (НалоговыйДокумент.Ссылка.ТипПричиныНевыдачиПокупателю = 9 или НалоговыйДокумент.Ссылка.ТипПричиныНевыдачиПокупателю = 8)
			|	И (НалоговыйДокумент.Ссылка.КодПризнакаСводной = 0 или НалоговыйДокумент.Ссылка.КодПризнакаСводной = 2)";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НалоговыйДокумент.Ссылка В(&МассивНалоговыхДокументов)", ТекстОтбора);	
		КонецЕсли;
		
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Приложение2КНалоговойНакладной.Ссылка,
		|	Приложение2КНалоговойНакладной.Организация КАК Организация,
		|	Приложение2КНалоговойНакладной.СпецРежимНалогообложения КАК СпецРежимНалогообложения
		|ИЗ
		|	Документ.Приложение2КНалоговойНакладной КАК Приложение2КНалоговойНакладной
		|ГДЕ
		|	Приложение2КНалоговойНакладной.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа)
		|	И Приложение2КНалоговойНакладной.ГодовойПерерасчетПропорциональногоНДС
		|	И Приложение2КНалоговойНакладной.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	СпецРежимНалогообложения";
		
	КонецЕсли;	
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСуммыКорректировокПропорциональногоНДС(ПоместитьВоВременнуюТаблицу = Ложь) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА КорректировкаУсловнойПродажи.Организация ЕСТЬ NULL 
	|			ТОГДА ПредполагаемаяКорректировкаУсловнойПродажи.Организация
	|		ИНАЧЕ КорректировкаУсловнойПродажи.Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА КорректировкаУсловнойПродажи.СпецРежимНалогообложения ЕСТЬ NULL 
	|			ТОГДА ПредполагаемаяКорректировкаУсловнойПродажи.СпецРежимНалогообложения
	|		ИНАЧЕ КорректировкаУсловнойПродажи.СпецРежимНалогообложения
	|	КОНЕЦ КАК СпецРежимНалогообложения,
	|	ЕСТЬNULL(ПредполагаемаяКорректировкаУсловнойПродажи.СуммаКорректировкиНДСОборот, 0) КАК ПредполагаемаяСуммаКорректировкиНДС,
	|	ЕСТЬNULL(КорректировкаУсловнойПродажи.СуммаКорректировкиНДСОборот, 0) КАК СуммаКорректировкиНДС
	|ПОМЕСТИТЬ ВТКорректировкиУсловныхПродаж
	|ИЗ
	|	РегистрНакопления.СуммыКорректировокПропорциональногоНДС.Обороты(
	|			&НачалоГода,
	|			&КонецГода,
	|			Год,
	|			(Организация В (&Организация)
	|				ИЛИ &ПоВсемОрганизациям)
	|				И (СпецРежимНалогообложения = &СпецРежимНалогообложения
	|					ИЛИ &ПоВсемСпецРежимамНалогообложения)
	|				И ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяКорректировкаУсловнойПродажи)) КАК ПредполагаемаяКорректировкаУсловнойПродажи
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.СуммыКорректировокПропорциональногоНДС.Обороты(
	|				&НачалоГода,
	|				&КонецГода,
	|				Год,
	|				(Организация В (&Организация)
	|					ИЛИ &ПоВсемОрганизациям)
	|					И (СпецРежимНалогообложения = &СпецРежимНалогообложения
	|						ИЛИ &ПоВсемСпецРежимамНалогообложения)
	|					И ВидУсловнойПродажи = ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.КорректировкаУсловнойПродажи)) КАК КорректировкаУсловнойПродажи
	|		ПО ПредполагаемаяКорректировкаУсловнойПродажи.Организация = КорректировкаУсловнойПродажи.Организация
	|			И ПредполагаемаяКорректировкаУсловнойПродажи.СпецРежимНалогообложения = КорректировкаУсловнойПродажи.СпецРежимНалогообложения"; 
	
	Если Не ПоместитьВоВременнуюТаблицу Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВТКорректировкиУсловныхПродаж", "");
	КонецЕсли; 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрыЗапросаКорректировкаПоПропорциональномуНДС(Запрос, ПараметрыОтбора) Экспорт
		
	Запрос.УстановитьПараметр("НачалоГода", 						НачалоГода(ПараметрыОтбора.ПериодРасчетов));
	Запрос.УстановитьПараметр("КонецГода",  						КонецГода (ПараметрыОтбора.ПериодРасчетов));
	Запрос.УстановитьПараметр("Организация",						ПараметрыОтбора.Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", 				Не ЗначениеЗаполнено(ПараметрыОтбора.Организация));
	Запрос.УстановитьПараметр("СпецРежимНалогообложения",			ПараметрыОтбора.СпецРежимНалогообложения);
	Запрос.УстановитьПараметр("ПоВсемСпецРежимамНалогообложения", 	ПараметрыОтбора.СпецРежимНалогообложения = -1);
	
КонецПроцедуры

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#Область Валюта

Процедура УстановитьДоступностьВалюты(ЭлементыФормы, Организация, ИмяРеквизитаВалюта = "Валюта") Экспорт
	
	ЭтоУправленческаяОрганизация = (Организация = ПредопределенноеЗначение("Справочник.Организации.УправленческаяОрганизация"));
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяРеквизитаВалюта, "Доступность", ЭтоУправленческаяОрганизация);
	
КонецПроцедуры

#КонецОбласти

#Область АвторасчетНДС

// Выполняет округление числовых значений с накоплением погрешностей округления, образовавшихся
//		в результате предыдущих вызовов функции
//
// Параметры
//  Число 		– Число. Округляемое значение
//  Точность	– Число. Точность округления
//	Погрешность	- Число. Переменная, в которой накапливается погрешность с предыдущих вызовов
//
// Возвращаемое значение:
//   Число   – округленное значение
//
Функция ОкруглитьСУчетомПогрешности(Число, Точность, Погрешность = 0, 
	               СоответствиеПогрешностей = Неопределено, Ключ = Неопределено) Экспорт

	Если НЕ СоответствиеПогрешностей = Неопределено И ЗначениеЗаполнено(Ключ) Тогда
	
		// считываем погрешность округления, накопленную ранее при расчетах
		Погрешность = СоответствиеПогрешностей[Ключ];
		// погрешности округления еще нет -- первая сумма
		Если Погрешность = Неопределено Тогда
			Погрешность = 0;
		КонецЕсли;
		// округлим с учетом погрешности
		Округленное = ОкруглитьСУчетомПогрешности(Число, Точность, Погрешность);
		// сохраним погрешность округления
		СоответствиеПогрешностей.Вставить(Ключ, Погрешность);
	
	Иначе
		
		Если Число = 0 Тогда
			Возврат 0;
		КонецЕсли; 
	
		// выравнивание разрядности
		Число = Окр(Число, 27, ?(Число<0, РежимОкругления.Окр15как10, РежимОкругления.Окр15как20));
		
		// сумма с учетом погрешности предыдущих вычислений
		Округляемое = Число + Погрешность;

		// для отрицательного числа меняем направление округления, чтобы избежать ошибки Окр(-0.5) = -1
		Округленное	= Окр(Округляемое, Точность, ?(Округляемое<0, РежимОкругления.Окр15как10, РежимОкругления.Окр15как20));
		
		// рассчитаем новую погрешность округления
		Погрешность	= Округляемое - Округленное;
		
	КонецЕсли;
	
	Возврат Округленное;

КонецФункции // ОкруглитьСУчетомПогрешности()

// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            - число, сумма от которой надо рассчитывать налоги, 
//  УчитыватьНДС     - булево, признак учета НДС в сумме, 
//  СуммаВключаетНДС - булево, признак включения НДС в сумму ("внутри" или "сверху"),
//  СтавкаНДС        - число , процентная ставка НДС,
//
// Возвращаемое значение:
//  Число, полученная сумма НДС
//
Функция РассчитатьСуммуНДСсУчетомПогрешности(Сумма, ЦенаВключаетНДС, СправочникСтавкаНДС,
	               СоответствиеПогрешностей = Неопределено) Экспорт

	ПроцентНДС = УчетНДСУПВызовСервера.ЗначениеСтавкиНДС(СправочникСтавкаНДС) / 100;
	
	Если ЦенаВключаетНДС Тогда
		СуммаНДС = ОкруглитьСУчетомПогрешности(Сумма * ПроцентНДС / (1 + ПроцентНДС), 2, , СоответствиеПогрешностей, СправочникСтавкаНДС);
	Иначе
		СуммаНДС = ОкруглитьСУчетомПогрешности(Сумма * ПроцентНДС                   , 2, , СоответствиеПогрешностей, СправочникСтавкаНДС);
	КонецЕсли;

	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДССУчетомПогрешности()

// Процедура корректирует построчные ошибки округления НДС так, 
// чтобы итоговый НДС по ставке совпадал с расчитанным НДС по базе
//
// Параметры:
//  ТабличнаяЧасть       - табличная часть, для которой следует пересчитать НДС
//  КлючДанных           - объект редактируемого документа.
//
Процедура ПересчитатьНДСсУчетомПогрешностиОкругления(ТабличнаяЧасть, КлючДанных, ЦенаВключаетНДС, ПогрешностиОкругления, ПредставлениеТабличнойЧасти = "", ПредставлениеВалюты = "", 
													ИмяРеквизитаСумма = "Сумма", ИмяРеквизитаСуммаНДС = "СуммаНДС", ИмяРеквизитаСуммаСНДС = "СуммаСНДС", 
													ИмяРеквизитаОтменено = Неопределено, ЗначениеФлагаДляОтменыСтроки = Истина, ДополнениеКТексту ="", СтавкаНДС = Неопределено) Экспорт
	
	Перем СуммаНДСДоОкругления;    // для проверки наличия изменения в сумме НДС по документу
    Перем СуммаНДСПослеОкругления; // для проверки наличия изменения в сумме НДС по документу
	
	
	// Запомним сумма с НДС и сумму НДС
	СуммаНДСДоОкругления     = 0;
	СуммаСНДСДоОкругления    = 0;
	СуммаНДСПослеОкругления  = 0;
	СуммаСНДСПослеОкругления = 0;
	
	Для Каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		
		Если (НЕ ИмяРеквизитаОтменено = Неопределено) И СтрокаТаблицы[ИмяРеквизитаОтменено] = ЗначениеФлагаДляОтменыСтроки Тогда
			Продолжить;
		КонецЕсли;
		
		// Запомним сумма и сумму НДС до округления
		СуммаНДСДоОкругления  = СуммаНДСДоОкругления + СтрокаТаблицы[ИмяРеквизитаСуммаНДС];
		СуммаСНДСДоОкругления = СуммаСНДСДоОкругления + СтрокаТаблицы[ИмяРеквизитаСуммаСНДС];
		
		СтрокаТаблицы[ИмяРеквизитаСуммаНДС] = РассчитатьСуммуНДСсУчетомПогрешности(
            СтрокаТаблицы[ИмяРеквизитаСумма],
	        ЦенаВключаетНДС,
            ?(СтавкаНДС = Неопределено, СтрокаТаблицы.СтавкаНДС, СтавкаНДС),
	        ПогрешностиОкругления
        );
													   
		Если НЕ ЦенаВключаетНДС Тогда   
			СтрокаТаблицы[ИмяРеквизитаСуммаСНДС] = СтрокаТаблицы[ИмяРеквизитаСумма] + СтрокаТаблицы[ИмяРеквизитаСуммаНДС];
		КонецЕсли;
		
		// Рассчитаем сумму НДС после устранения ошибок округления
		СуммаНДСПослеОкругления  = СуммаНДСПослеОкругления + СтрокаТаблицы[ИмяРеквизитаСуммаНДС];
		СуммаСНДСПослеОкругления = СуммаСНДСПослеОкругления + СтрокаТаблицы[ИмяРеквизитаСуммаСНДС];
		
	КонецЦикла;
	
	// Сравним суммы до и после округления
	Если СуммаНДСПослеОкругления <> СуммаНДСДоОкругления Тогда
		
		ПриставкаСообщенияСуммаНДС = НСтр("ru='После исправления погрешностей округления сумма НДС ';uk='Після виправлення похибок округлення сума ПДВ '");
		ПриставкаСообщенияОбщаяСумма = НСтр("ru='После исправления погрешностей округления общая сумма с НДС ';uk='Після виправлення похибок округлення загальна сума з ПДВ '");
		
		ШаблонТекстаСообщения = НСтр("ru='%1 табличной части %2 изменилась (было %3 %4, стало %5 %4)';uk='%1 табличній частині %2 змінилася (було %3 %4, стало %5 %4)'");
		
		// Если суммы отличаются, сообщим об этом пользователю
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		    ШаблонТекстаСообщения,
			ДополнениеКТексту,
			ПредставлениеТабличнойЧасти,
			Строка(СуммаНДСДоОкругления),
			ПредставлениеВалюты,
			Строка(СуммаНДСПослеОкругления)
        );
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ПриставкаСообщенияСуммаНДС + ТекстСообщения, КлючДанных);
		
		// изменилась общая сумма документа - сообщим об этом пользователю 
		Если НЕ ЦенаВключаетНДС Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонТекстаСообщения,
							ДополнениеКТексту,
							ПредставлениеТабличнойЧасти,
							Строка(СуммаСНДСДоОкругления),
							ПредставлениеВалюты,
							Строка(СуммаСНДСПослеОкругления));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ПриставкаСообщенияОбщаяСумма + ТекстСообщения, КлючДанных);
			
		КонецЕсли;	
	КонецЕсли;
	

КонецПроцедуры // ПересчитатьНДСсУчетомПогрешностиОкругления

// Выполняет проверку нужен ли перерасчет НДС
//
// Параметры: 
//  СуммаНДС         - число, сумма НДС по документу, 
//  СуммаСНДС        - число, сумма с НДС по документу,
//
// Возвращаемое значение:
//  Булево, признак необходимости провести расчет
//
Функция НуженАвторасчетНДС(ТабличнаяЧасть, ИмяРеквизитаСтавкаНДС = "СтавкаНДС", ИмяРеквизитаСуммаНДС = "СуммаНДС", ИмяРеквизитаСуммаСНДС ="СуммаСНДС",
							ИмяРеквизитаОтменено = Неопределено, ЗначениеФлагаДляОтменыСтроки = Истина, СтавкаНДС = Неопределено) Экспорт
	
    МассивЗначащихСтавок = УчетНДСУП.МассивЗначащихСтавок();
	
	СуммаНДС = 0;
	СуммаНДСРасчетная = 0;
	
	Для Каждого СправочникСтавкаНДС Из МассивЗначащихСтавок Цикл
		
		СтруктураОтбора = Новый Структура;
		Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда
			СтруктураОтбора.Вставить(ИмяРеквизитаСтавкаНДС, СправочникСтавкаНДС);
		Иначе
			Если СправочникСтавкаНДС <> СтавкаНДС Тогда
				Продолжить;
			КонецЕсли; 	
		КонецЕсли; 
        
		Если (НЕ ИмяРеквизитаОтменено = Неопределено) Тогда
			СтруктураОтбора.Вставить(ИмяРеквизитаОтменено, НЕ ЗначениеФлагаДляОтменыСтроки);
		КонецЕсли;
		
		ПроцентНДС = УчетНДСУПВызовСервера.ЗначениеСтавкиНДС(СправочникСтавкаНДС) / 100;
		
		Таблица = ТабличнаяЧасть.Выгрузить(СтруктураОтбора, ИмяРеквизитаСуммаНДС + ", " + ИмяРеквизитаСуммаСНДС);
		
		СуммаНДС = СуммаНДС + Таблица.Итог(ИмяРеквизитаСуммаНДС);
		СуммаСНДС = Таблица.Итог(ИмяРеквизитаСуммаСНДС);
		
		СуммаНДСРасчетная = СуммаНДСРасчетная + ОкруглитьСУчетомПогрешности(СуммаСНДС * ПроцентНДС / (1 + ПроцентНДС), 2);
		
	КонецЦикла;
	
	Если СуммаНДСРасчетная = СуммаНДС Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Возвращает галку авторасчет НДС
//
// Параметры:
// Организация - СправочникСсылка.Организации
// Контрагент - СправочникСсылка.Контрагент
//
// Возвращаемое значение:
// ПеречислениеСсылка.ТипыНалогообложенияНДС
//
Функция ПолучитьФлагАвторасчетНДС(НалогообложениеНДС) Экспорт
	
	Если НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС") Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции // ПолучитьФлагАвторасчетНДС

// Процедура сбрасывает флаг СкидкиРассчитаны и делает недоступными колонки табличной части.
//
Функция ПолучитьФлагИОбработатьДоступностьАвторасчетаНДС(НалогообложениеНДС, ЭлементыФормы, МассивИменЭлементов) Экспорт
	
	ФлагАвторасчетНДСПоУмолчанию = ПолучитьФлагАвторасчетНДС(НалогообложениеНДС);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭлементыФормы, МассивИменЭлементов, "Доступность", ФлагАвторасчетНДСПоУмолчанию);
	
	Возврат ФлагАвторасчетНДСПоУмолчанию;
	
КонецФункции

// Возвращает флаг авторасчет НДС по ставке НДС 
&НаСервере
Функция ПолучитьФлагАвторасчетНДСПоСтавкеНДС(Знач СтавкаНДС) Экспорт
	
    ПроцентНДС = УчетНДСУПВызовСервера.ЗначениеСтавкиНДС(СтавкаНДС);
    Возврат ПроцентНДС > 0;
	
КонецФункции // ПолучитьФлагАвторасчетНДСПоСтавкеНДС

&НаСервере
Функция ПолучитьФлагИОбработатьДоступностьАвторасчетаНДСПоСтавкеНДС(СтавкаНДС, ЭлементыФормы, МассивИменЭлементов) Экспорт
	
	ФлагАвторасчетНДСПоУмолчанию = ПолучитьФлагАвторасчетНДСПоСтавкеНДС(СтавкаНДС);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭлементыФормы, МассивИменЭлементов, "Доступность", ФлагАвторасчетНДСПоУмолчанию);
	Возврат ФлагАвторасчетНДСПоУмолчанию;
	
КонецФункции

#КонецОбласти

#Область ТабличныеЧастиНалоговыхДокументов

Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, ОбработатьРеквизитыРегл = Ложь) Экспорт
	
	ТекущаяСтрокаДляОбработки = НДСИсходящийКлиентСервер.СоздатьСтруктуруДляОбработкиПоТекущейСтроке(ТекущаяСтрока, ОбработатьРеквизитыРегл);
	
    // Для услуг измеряемых только в суммовом выражении, количество может быть не задано
	Если СтруктураДействий.Свойство("ПересчитатьСумму") И Не ЗначениеЗаполнено(ТекущаяСтрокаДляОбработки.КоличествоУпаковок) Тогда
		ИмяКолонкиКоличествоУпаковок = "КоличествоУпаковок_УсловнаяЕдиница";
		ТекущаяСтрокаДляОбработки.Вставить(ИмяКолонкиКоличествоУпаковок, 1);
		СтруктураДействий.Вставить("ПересчитатьСумму", ИмяКолонкиКоличествоУпаковок);
	КонецЕсли;
	Если СтруктураДействий.Свойство("ПересчитатьВесКГ") Тогда
		РассчитатьВесДляЭкспортногоОбеспечения(ТекущаяСтрокаДляОбработки, КэшированныеЗначения); 
	КонецЕсли;

	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрокаДляОбработки, СтруктураДействий, КэшированныеЗначения);
	
	НДСИсходящийКлиентСервер.ЗаполнитьТекущуюСтрокуПоСтруктуреДляОбработки(ТекущаяСтрока, ТекущаяСтрокаДляОбработки, ОбработатьРеквизитыРегл);
	
КонецПроцедуры

Процедура РассчитатьВесДляЭкспортногоОбеспечения(ТекущаяСтрокаДляОбработки, КэшированныеЗначения) Экспорт
	НДСИсходящийКлиентСервер.РассчитатьВесДляЭкспортногоОбеспечения(ТекущаяСтрокаДляОбработки, КэшированныеЗначения); 
КонецПроцедуры

Функция ВДокументеЕстьСтавкаНДС7(Документ) Экспорт
	
	Отбор = Новый Структура("СтавкаНДС", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС7));
	
	Если    (Документ.Товары.НайтиСтроки(Отбор).Количество() > 0)
		ИЛИ (Документ.ТоварыПоДаннымПользователя.НайтиСтроки(Отбор).Количество() > 0)
		Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ВДокументеЕстьСтавкаНДС7ИДругиеСтавки(Документ, ВыдаватьСообщенение = Истина) Экспорт
                                                       
	Отбор = Новый Структура("СтавкаНДС", НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС7));
	
	Если    (Документ.Товары.НайтиСтроки(Отбор).Количество() > 0 И Документ.Товары.НайтиСтроки(Отбор).Количество() <> Документ.Товары.Количество())
		ИЛИ (Документ.ТоварыПоДаннымПользователя.НайтиСтроки(Отбор).Количество() > 0 И Документ.ТоварыПоДаннымПользователя.НайтиСтроки(Отбор).Количество() <> Документ.ТоварыПоДаннымПользователя.Количество())
		Тогда
		
			Если ВыдаватьСообщенение = Истина Тогда
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Форма налоговой накладной, утвержденная 14.01.2014 приказом Миндоходов № 10, не предусматривает наличие ставки НДС  7%. 
                               |В данной налоговой накладной присутствует номенклатура, которая облагается как по ставке 7% так и по другим ставкам.
                               |Согласно рекомендации Миндоходов, изложенной в письме  от 04.04.2014  № 7860/7/99-99-19-04-02-17, на номенклатуру, облагаемую НДС по ставке 7%, необходимо выписывать отдельную налоговую накладную!';uk= 'Форма податкової накладню, що затверждена 14.01.2014 наказом Міндоходів № 10, не передбачає наявності ставки ПДВ 7%.
                               |В податковій накладній наявна номенклатура, що оподатковується ПДВ як за ставкою 7% так і за іншими ставками.
                               |Згідно рекомендації Міндоходів, що викладена у листі від 04.04.2014  № 7860/7/99-99-19-04-02-17, на номенклатуру, що оподатковується ПДВ за ставкою 7%, необхідно складати окрему податкову накладну!'"));
				
			КонецЕсли;
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Процедура ЗаполнитьНоменклатуруГТДПоУмолчанию(СтрокаТовары, ЗаполнятьГалочкуРазрешитьРедактированиеКодаПоКлассификатору = Истина) Экспорт
	
	Если ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
		
		Если ЗаполнятьГалочкуРазрешитьРедактированиеКодаПоКлассификатору Тогда
			Если НЕ СтрокаТовары.Номенклатура.ВестиУчетПоГТД Тогда
				СтрокаТовары.РазрешитьРедактированиеКодаПоКлассификатору = Истина;
			Иначе
				СтрокаТовары.РазрешитьРедактированиеКодаПоКлассификатору = Ложь;
				СтрокаТовары.КодНоменклатурыПоКлассификатору = Справочники.КлассификаторУКТВЭД.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТовары.НомерГТД) Тогда
			СтрокаТовары.КодНоменклатурыПоКлассификатору = СтрокаТовары.НомерГТД.КодУКТВЭД;
		ИначеЕсли ЗначениеЗаполнено(СтрокаТовары.Номенклатура.НоменклатураГТД) Тогда
			СтрокаТовары.КодНоменклатурыПоКлассификатору = СтрокаТовары.Номенклатура.НоменклатураГТД.КодУКТВЭД;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтборИВыборНалоговыхДокументов

Функция НалоговыеДокументыПоОснованию(Основание, Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.НДСНоменклатурныйСоставДляНалоговыхНакладных КАК НДСНоменклатурныйСоставДляНалоговыхНакладных
	|ГДЕ
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.ДокументПоставки = &Основание
	|	И (ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.НалоговаяНакладная)
	|			ИЛИ ТИПЗНАЧЕНИЯ(НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор) = ТИП(Документ.Приложение2КНалоговойНакладной))
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНоменклатурныйСоставДляНалоговыхНакладных.Регистратор");
	
	Запрос.УстановитьПараметр("Основание",   Основание);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	ТаблицаНалоговыхДокументов = Новый ТаблицаЗначений;
	ТаблицаНалоговыхДокументов.Колонки.Добавить("Ссылка");
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТаблицаНалоговыхДокументов = РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
	Возврат ТаблицаНалоговыхДокументов;
	
КонецФункции

// Возвращает структуру параметров представления налоговых накладных в документе.
//
// Параметры:
//  Основание - ДокументСсылка - Документ, на основании которого вводится налоговый документ;
//  Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется налоговый документ;
//	НеТребуется - Булево - Истина - для документа не требуется вводить налоговый документ.
//
// Возвращаемое значение:
//	Строка - Представление налоговых документов.
//
Функция ПредставленияНалоговыхДокументовВДокументеПродажи(Основание, Организация, НеТребуется = Ложь) Экспорт
	
	НалоговыеДокументы = НДСИсходящийСервер.НалоговыеДокументыПоОснованию(Основание, Организация);
	МассивСтрок = Новый Массив;
	
	Если НалоговыеДокументы.Количество() > 0 Тогда
		
		ТекстНалоговыеДокументы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Налоговые документы (%1)';uk='Податкові документи (%1)'"), Строка(НалоговыеДокументы.Количество()));
			
		СтрокаГиперссылки = Новый ФорматированнаяСтрока(
			ТекстНалоговыеДокументы, , 
			ЦветаСтиля.ЦветГиперссылки, ,
			"ВвестиНовыйНалоговыйДокумент");
		
		МассивСтрок.Добавить(СтрокаГиперссылки);
		
	ИначеЕсли НеТребуется Тогда
		
		ТекстНалоговыеДокументы = НСтр("ru='Налоговые документы не требуются';uk='Податкові документи не потрібні'");
		
		МассивСтрок.Добавить(ТекстНалоговыеДокументы);
		
	ИначеЕсли Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.НалоговаяНакладная) Тогда
		
		ТекстНалоговыеДокументы = НСтр("ru='Налоговые документы отсутствуют';uk='Податкові документи відсутні'");

		МассивСтрок.Добавить(ТекстНалоговыеДокументы);
		
	Иначе
		
		ТекстНалоговыеДокументы = НСтр("ru='Оформить налоговые документы';uk='Оформити податкові документи'");
		
		СтрокаГиперссылки = Новый ФорматированнаяСтрока(
			ТекстНалоговыеДокументы, ,
			ЦветаСтиля.ЦветГиперссылки, ,
			"ВвестиНовыйНалоговыйДокумент");
		
		МассивСтрок.Добавить(СтрокаГиперссылки);
		
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецФункции // ПараметрыПредставленияНалоговыхДокументов

#КонецОбласти

#Область УсловноеОформление

Процедура УстановитьУсловноеОформлениеПоАвтоРасчетуНДС(
	Форма,	
	ИмяПоляСуммаНДС             = "СуммаНДС",
	ИмяПоляСуммаВсегоСНДС       = "СуммаВсегоСНДС",
	ИмяПоляПерерасчетПроизведен = "ПерерасчетПроизведен"
	) Экспорт
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	Если НЕ ПустаяСтрока(ИмяПоляСуммаНДС) Тогда

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляСуммаНДС].Имя);
		
		ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
		
		ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.АвторасчетНДС");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;

		ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляПерерасчетПроизведен);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;

		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
		
	КонецЕсли;	
	
	Если НЕ ПустаяСтрока(ИмяПоляСуммаВсегоСНДС) Тогда
		
		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляСуммаВсегоСНДС].Имя);
		
		ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
		
		ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ЦенаВключаетНДС");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.АвторасчетНДС");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;

		ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляПерерасчетПроизведен);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;

		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	КонецЕсли;	

КонецПроцедуры // УстановитьУсловноеОформлениеПоАвтоРасчетуНДС

// Устанавливаем условное оформление для реквизитов по виду операции "условная продажа"
//
// Параметры:
// 		Форма - Форма - Содержит данную форму 
Процедура УстановитьУсловноеОформлениеПоУсловнойПродаже(Форма) Экспорт
														   
	//УсловноеОформление = Форма.УсловноеОформление;
	//ЭлементыФормы = Форма.Элементы;
	//
	//Элемент = УсловноеОформление.Элементы.Добавить();	
	//ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	//ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляВводаСуммаНДСПропорционально);

	//ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	//ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	//ОтборЭлемента.ПравоеЗначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально;

	//Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	//Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<для пропорц. налогового назначения>'"));
	//Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	//
	//Если УстанавливатьВидимость Тогда
	//	
	//	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();				  
	//	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	//	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляВводаСуммаНДСПропорционально);
	//	
	//	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидимостьНалоговыхНазначений");
	//	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	//	ОтборЭлемента.ПравоеЗначение = Ложь;
	//	
	//	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);	
	//	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	//
	//КонецЕсли; 
		
КонецПроцедуры // УстановитьУсловноеОформлениеНалоговыхНазначений

#КонецОбласти

#Область ПрефиксацияИсходящихНалоговыхДокументов

Процедура ПроверитьНомерНалоговогоДокумента(Документ)
	
	// особые опциональные правила нумерации налоговых документов:
	// возможность вести сквозную нумерацию Налоговых накладных и Приложений 2 к ней
	// возможность вести помесячную нумерацию
	Если  НЕ ТипЗнч(Документ) = Тип("ДокументОбъект.НалоговаяНакладная")
		И НЕ ТипЗнч(Документ) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
		Возврат;
	КонецЕсли;
	
	// поддерживаем особый порядок нумерации налоговых накладных
	// при изменении даты документа возможно потребуется изменение=очистка номера
	
	Если НЕ ЗначениеЗаполнено(Документ.Ссылка) Тогда
		// это новый документ, номер еще не присвоен или задан вручную
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Документ.Номер) Тогда
	    Возврат;
	КонецЕсли;
	
	ЗапросСтарыеДанные = Новый Запрос();
	ЗапросСтарыеДанные.Текст = 
	"ВЫБРАТЬ
	|	НалоговыйДокумент.Дата,
	|	НалоговыйДокумент.ВидОперации,
	|	НалоговыйДокумент.СпецРежимНалогообложения,
	|	НалоговыйДокумент.ОбособленноеПодразделение КАК Филиал,
	|	НалоговыйДокумент.Организация
	|ИЗ
	|	Документ.НалоговаяНакладная КАК НалоговыйДокумент
	|ГДЕ
	|	НалоговыйДокумент.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НалоговыйДокумент.Дата,
	|	НалоговыйДокумент.ВидОперации,
	|	НалоговыйДокумент.СпецРежимНалогообложения,
	|	ВЫБОР
	|		КОГДА НалоговыйДокумент.НалоговаяНакладнаяНеЗарегистрированаВИБ
	|	    	ТОГДА НалоговыйДокумент.ОбособленноеПодразделение
	|	    ИНАЧЕ
	|	    	ЕСТЬNULL(НалоговыйДокумент.НалоговаяНакладная.ОбособленноеПодразделение, ЗНАЧЕНИЕ(Справочник.ОбособленныеПодразделенияОрганизаций.ПустаяСсылка))
	|	КОНЕЦ,
	|	НалоговыйДокумент.Организация
	|ИЗ
	|	Документ.Приложение2КНалоговойНакладной КАК НалоговыйДокумент
	|ГДЕ
	|	НалоговыйДокумент.Ссылка = &Ссылка";
	
	ЗапросСтарыеДанные.УстановитьПараметр("Ссылка",    Документ.Ссылка);
	
	СтарыеДанные = ЗапросСтарыеДанные.Выполнить().Выбрать();
	СтарыеДанные.Следующий();
	
	СтарыеНастройкиНумерации = РегистрыСведений.НастройкаНумерацииНалоговыхДокументов.ПолучитьПоследнее(СтарыеДанные.Дата, Новый Структура("Организация", СтарыеДанные.Организация));
	НовыеНастройкиНумерации  = РегистрыСведений.НастройкаНумерацииНалоговыхДокументов.ПолучитьПоследнее(Документ.Дата, 	 Новый Структура("Организация", Документ.Организация));
				   
	Если  СтарыеНастройкиНумерации.Количество() > 0
		И НовыеНастройкиНумерации.Количество()  > 0 Тогда
		
		Если    СтарыеНастройкиНумерации.ВестиМесячнуюНумерациюНалоговыхДокументов   <> НовыеНастройкиНумерации.ВестиМесячнуюНумерациюНалоговыхДокументов
			ИЛИ СтарыеНастройкиНумерации.ВестиДневнуюНумерациюНалоговыхДокументов    <> НовыеНастройкиНумерации.ВестиДневнуюНумерациюНалоговыхДокументов
			Тогда
		
		    Документ.Номер = "";
			
		ИначеЕсли НовыеНастройкиНумерации.ВестиМесячнуюНумерациюНалоговыхДокументов 
			И НачалоМесяца(СтарыеДанные.Дата) <> НачалоМесяца(Документ.Дата) Тогда
			
			Документ.Номер = "";
			
		ИначеЕсли НовыеНастройкиНумерации.ВестиДневнуюНумерациюНалоговыхДокументов 
			И НачалоДня(СтарыеДанные.Дата) <> НачалоДня(Документ.Дата) Тогда
			
			Документ.Номер = "";
			
		КонецЕсли;
		
	КонецЕсли;	
	
	Если НЕ СтарыеДанные.ВидОперации = Документ.ВидОперации
		  И (   (СтарыеДанные.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации)
			 ИЛИ(Документ.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации))
		  И (     НовыеНастройкиНумерации.ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям = Истина
		      ИЛИ СтарыеНастройкиНумерации.ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям = Истина) Тогда
		   
		Документ.Номер = "";		   
			
	КонецЕсли;
	
	Если НЕ СтарыеДанные.СпецРежимНалогообложения = Документ.СпецРежимНалогообложения Тогда
	
		Документ.Номер = "";
	
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.НалоговаяНакладная")Тогда
		ТекФилиал = Документ.ОбособленноеПодразделение;
	ИначеЕсли Документ.НалоговаяНакладнаяНеЗарегистрированаВИБ Тогда
        ТекФилиал = Документ.ОбособленноеПодразделение;
	Иначе
		ТекФилиал = Документ.НалоговаяНакладная.ОбособленноеПодразделение;
	КонецЕсли;

	Если НЕ СтарыеДанные.Филиал = ТекФилиал 
		И Документ.Дата < НДСОбщегоНазначенияПовтИсп.ДатаВступленияВСилуПриказа1307() 
	 	И (НовыеНастройкиНумерации.ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений = Ложь
	 	ИЛИ СтарыеНастройкиНумерации.ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений = Ложь) Тогда
	
		Документ.Номер = "";
	
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьМесячныйПрефиксНалоговыхДокументов(Дата)

	НомерМесяца = Месяц(Дата);
	Индекс = НомерМесяца*3 - 2;
	
	Возврат Сред("Ян|Фв|Мр|Ап|Ма|Ин|Ил|Ав|Сн|Ок|Но|Дк", Индекс, 2);	

КонецФункции

Функция ПолучитьПрефиксНалоговогоДокумента(ДокументОбъект)
	
	// особые опциональные правила нумерации налоговых документов:
	// возможность вести сквозную нумерацию Налоговых накладных и Приложений 2 к ней
	// возможность вести помесячную нумерацию
	Если  НЕ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.НалоговаяНакладная")
		И НЕ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.Приложение2КНалоговойНакладной") Тогда
		Возврат "";
	КонецЕсли;
	
	СписокНастроекНумерации = РегистрыСведений.НастройкаНумерацииНалоговыхДокументов.СрезПоследних(ДокументОбъект.Дата, Новый Структура("Организация", ДокументОбъект.Организация));
	ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям = Ложь;
	ВестиМесячнуюНумерациюНалоговыхДокументов                   = Ложь;
	ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений = Ложь;
	
	Если СписокНастроекНумерации.Количество() > 0 тогда
		НастройкиНумерации = СписокНастроекНумерации[0];
		
		ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям 		= НастройкиНумерации.ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям;
		ВестиМесячнуюНумерациюНалоговыхДокументов                   		= НастройкиНумерации.ВестиМесячнуюНумерациюНалоговыхДокументов;
		ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений 	= НастройкиНумерации.ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений;
		ВестиДневнуюНумерациюНалоговыхДокументов 							= НастройкиНумерации.ВестиДневнуюНумерациюНалоговыхДокументов;
	КонецЕсли;
	
	ПрефиксНалоговых = "";
	
	Если ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям = Истина
		И  ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации Тогда
	   ПрефиксНалоговых = ПрефиксНалоговых + "Т";//технологические (нераспечатываемые) документы
	   
   КонецЕсли;		
	
   Если ВестиМесячнуюНумерациюНалоговыхДокументов = Истина
	   Или (ВестиДневнуюНумерациюНалоговыхДокументов = Истина И ДокументОбъект.Дата >= '2015-01-01') Тогда
	   // разделяем префиксом по месяцам
	   ПрефиксНалоговых = ПрефиксНалоговых + ПолучитьМесячныйПрефиксНалоговыхДокументов(ДокументОбъект.Дата);
   КонецЕсли;
   
   // разделяем префиксом Налоговые от Приложений
   СпецРежим = ДокументОбъект.СпецРежимНалогообложения;
   
   Если СпецРежим = 0 Тогда
	   ПрефиксНалоговых = ПрефиксНалоговых + "";
   ИначеЕсли СпецРежим = 2 Тогда
	   ПрефиксНалоговых = ПрефиксНалоговых + "U";
   ИначеЕсли СпецРежим = 3 Тогда
	   ПрефиксНалоговых = ПрефиксНалоговых + "V";
   ИначеЕсли СпецРежим = 4 Тогда
	   ПрефиксНалоговых = ПрефиксНалоговых + "W";
   КонецЕсли;
   
   Если ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений = Ложь И ДокументОбъект.Дата < НДСОбщегоНазначенияПовтИсп.ДатаВступленияВСилуПриказа1307() Тогда 	   
	   
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.НалоговаяНакладная") Тогда
			ОбособленноеПодразделение = ДокументОбъект.ОбособленноеПодразделение;
		ИначеЕсли ДокументОбъект.НалоговаяНакладнаяНеЗарегистрированаВИБ Тогда
        	ОбособленноеПодразделение = ДокументОбъект.ОбособленноеПодразделение;
		Иначе
			ОбособленноеПодразделение = ДокументОбъект.НалоговаяНакладная.ОбособленноеПодразделение;
		КонецЕсли;
		Если ЗначениеЗаполнено(ОбособленноеПодразделение.Префикс) Тогда
			ПрефиксНалоговых = ПрефиксНалоговых + Прав("0000" + СокрЛП(ОбособленноеПодразделение.Префикс), 4);
		КонецЕсли;
	КонецЕсли;
	
	Если ВестиДневнуюНумерациюНалоговыхДокументов = Истина
		И ДокументОбъект.Дата >= '2015-01-01'Тогда
		
		// добавляем префикс дня - "Д" + порядковый номер дня.
		ПрефиксНалоговых = ПрефиксНалоговых + ПолучитьДневнойПрефиксНалоговыхДокументов(ДокументОбъект.Дата);
		
	КонецЕсли;
	
	ПрефиксНалоговых = Лев(ПрефиксНалоговых + "0000000", 7);
	
	Возврат ПрефиксНалоговых; 
	
КонецФункции

Функция ПолучитьДневнойПрефиксНалоговыхДокументов(Дата)
	
	// номер дня с лидирующим нулем
	Возврат "Д" + Формат(День(Дата), "ЧЦ=2; ЧВН=");	
	
КонецФункции

Процедура УстановитьПрефиксНалоговогоДокумента(ДокументОбъект, Префикс) Экспорт
	
	ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазыИОрганизации(ДокументОбъект,, Префикс);
	
	ПрефиксНалоговогоДокумента = ПолучитьПрефиксНалоговогоДокумента(ДокументОбъект);
	Если ПустаяСтрока(ПрефиксНалоговогоДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	ПозицияРазделителя = СтрНайти(Префикс, "-");
	Если ПозицияРазделителя > 0 Тогда
		Префикс = Лев(Префикс, ПозицияРазделителя - 1) + ПрефиксНалоговогоДокумента + Сред(Префикс, ПозицияРазделителя);	
	Иначе
		Префикс = ПрефиксНалоговогоДокумента + Префикс;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНомерНалоговогоДокументаПередЗаписью(ДокументОбъект) Экспорт
	
	ПрефиксацияОбъектовСобытия.ПроверитьНомерДокументаПоДатеИОрганизации(ДокументОбъект,,,);
	ПроверитьНомерНалоговогоДокумента(ДокументОбъект);
	
КонецПроцедуры

#КонецОбласти
