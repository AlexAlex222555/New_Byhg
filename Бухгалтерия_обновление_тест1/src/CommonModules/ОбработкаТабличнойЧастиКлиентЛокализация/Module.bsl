
#Область СлужебныйПрограммныйИнтерфейс

Функция НеобходимВызовСервера(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Перем ПараметрыДействия;
	
	//++ Локализация
	
	Если СтруктураДействий.Свойство("ЗаполнитьПартиюТМЦВЭксплуатации", ПараметрыДействия)
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура)
		И ЗначениеЗаполнено(ТекущаяСтрока.ФизическоеЛицо) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыДействия.Дата)
			И ЗначениеЗаполнено(ПараметрыДействия.Организация)
			И ЗначениеЗаполнено(ПараметрыДействия.Подразделение)
			И (Не ПараметрыДействия.Свойство("ХозяйственнаяОперация")
				Или (ЗначениеЗаполнено(ПараметрыДействия.ХозяйственнаяОперация)
					И ПараметрыДействия.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратИзЭксплуатации"))) Тогда
			
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
    
	Если СтруктураДействий.Свойство("ЗаполнитьДанныеПоДокументуПриобретенияВОтчетеКомитентуОЗакупках") Тогда
		Возврат Истина;
	КонецЕсли;	
	
    
    //++ НЕ УТ
	Если СтруктураДействий.Свойство("ЗаполнитьСодержаниеПереработки")
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
    КонецЕсли;
    //-- НЕ УТ
    
	Если СтруктураДействий.Свойство("ЗаполнитьНоменклатуруГТД")
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		
		Возврат Истина;
		
	КонецЕсли;	
	
	Если СтруктураДействий.Свойство("ЗаполнитьСтатьюДекларацииПоАкцизномуНалогу")
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		
		Возврат Истина;
		
	КонецЕсли;	
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакУказыватьШтрихкодАкцизнойМаркиПриПечатиЧека")
	  	И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		
	 	Возврат Истина;
		
	КонецЕсли;
	
	//-- Локализация

	Возврат Ложь;
	
КонецФункции

Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	//++ Локализация
	
	ЗаполнитьПризнакиКатегорииЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
    
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ЗаполнитьНалоговоеНазначениеВозвратнойТарыВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ЗаполнитьСтатьюДекларацииПоАкцизномуНалогу(ТекущаяСтрока, СтруктураДействий);
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ПересчитатьСуммуНДСиАкцизногоНалогаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ПересчитатьСуммуНДСПропорциональноВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ЗаполнитьИндексАкцизнойМарки(ТекущаяСтрока, СтруктураДействий);
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ЗаполнитьНоменклатуруГТД(ТекущаяСтрока, СтруктураДействий);
    
	//-- Локализация
	
КонецПроцедуры

Процедура ПолучитьТекущуюСтрокуСтруктурой(СтруктураДействий, СтруктураПолейТЧ) Экспорт

	//++ Локализация
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПартиюТМЦВЭксплуатации") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("Характеристика");
		СтруктураПолейТЧ.Вставить("ФизическоеЛицо");
		СтруктураПолейТЧ.Вставить("ПартияТМЦВЭксплуатации");
		СтруктураПолейТЧ.Вставить("КоличествоУпаковок");
		СтруктураПолейТЧ.Вставить("Количество");
		СтруктураПолейТЧ.Вставить("Упаковка");
	КонецЕсли;
	
	
	
	
	
	
	
	
	
	
    
	Если СтруктураДействий.Свойство("ЗаполнитьНалоговоеНазначениеВозвратнойТары") Тогда
		СтруктураПолейТЧ.Вставить("ТипНоменклатуры");
		СтруктураПолейТЧ.Вставить("НалоговоеНазначение");
	КонецЕсли;
    
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДСПропорционально") Тогда
		СтруктураПолейТЧ.Вставить("НалоговоеНазначение");
		СтруктураПолейТЧ.Вставить("СуммаНДС" , 0);
		СтруктураПолейТЧ.Вставить("СуммаНДСПропорционально" , 0);
	КонецЕсли;
    
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДСиАкцизногоНалога") Тогда
		СтруктураПолейТЧ.Вставить("Сумма"    , 0);
		СтруктураПолейТЧ.Вставить("СтавкаНДС", 0);
		СтруктураПолейТЧ.Вставить("СуммаНДС" , 0);
		СтруктураПолейТЧ.Вставить("СтатьяДекларацииПоАкцизномуНалогу");
		СтруктураПолейТЧ.Вставить("СуммаАкцизногоНалога" , 0);
		СтруктураПолейТЧ.Вставить("ПодакцизныеТоварыДляКоммерческогоИспользования" , Ложь);
	КонецЕсли;
    
    //++ НЕ УТ
	Если СтруктураДействий.Свойство("ЗаполнитьСодержаниеПереработки") Тогда
		СтруктураПолейТЧ.Вставить("Содержание");
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("Характеристика");
    КонецЕсли;
    //-- НЕ УТ
    
	Если СтруктураДействий.Свойство("ЗаполнитьНоменклатуруГТД") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("НомерГТД");
	КонецЕсли;	

	Если СтруктураДействий.Свойство("ЗаполнитьСтатьюДекларацииПоАкцизномуНалогу") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("СтатьяДекларацииПоАкцизномуНалогу");
	КонецЕсли;	

	Если СтруктураДействий.Свойство("ЗаполнитьПризнакУказыватьШтрихкодАкцизнойМаркиПриПечатиЧека", СтруктураПараметровДействия) 
	  	И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
	  	
	  	Для Каждого Поле Из СтруктураПараметровДействия Цикл
	  		СтруктураПолейТЧ.Вставить(Поле.Ключ);
	  		СтруктураПолейТЧ.Вставить(Поле.Значение);
	  	КонецЦикла;
	 КонецЕсли;

	Если СтруктураДействий.Свойство("ЗаполнитьИндексАкцизнойМарки", СтруктураПараметровДействия)  Тогда
	  	
	 	СтруктураПолейТЧ.Вставить("УказыватьШтрихкодАкцизнойМаркиПриПечатиЧека");
	 	СтруктураПолейТЧ.Вставить("ИндексАкцизнойМарки");
	 	СтруктураПолейТЧ.Вставить("КоличествоАкцизныхМарок");
	 	СтруктураПолейТЧ.Вставить("Количество");
	 	
	КонецЕсли;

	//-- Локализация
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	//++ Локализация
	
	
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПодакцизныйТовар") Тогда
		ТекущаяСтрока.ПодакцизныйТовар = Ложь;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьУказыватьШтрихкодАкцизнойМаркиПриПечатиЧека") Тогда
	 	ТекущаяСтрока.УказыватьШтрихкодАкцизнойМаркиПриПечатиЧека = Ложь;
	КонецЕсли;
	
	//-- Локализация
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

Процедура ЗаполнитьПризнакиКатегорииЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Если НЕ СтруктураДействий.Свойство("ЗаполнитьПризнакиКатегорииЭксплуатации") Тогда
		Возврат;
	КонецЕсли;
		
	СтруктураПризнаков = КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Получить(
		ТекущаяСтрока.КатегорияЭксплуатации);
		
	Если СтруктураПризнаков <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураПризнаков);
	КонецЕсли;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти