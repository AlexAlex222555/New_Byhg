
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	//++ НЕ УТ
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УстановитьПараметрыВыборочнойРегистрацииКОтражениюВРеглУчете(Объект, Объект.ДополнительныеСвойства);
	КонецЕсли;
	//-- НЕ УТ
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.<Имя документа> - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	Возврат ТекстОтраженияВРеглУчетеУКР(); 
	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

//++ НЕ УТ
#Область ПроводкиРегУчетаУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	//++ Локализация

	ЯзыкСодержания = МультиязычностьУкр.КодЯзыкаИнформационнойБазы();

#Область РегистрацияПрочихРасходов
	ТекстРегистрацияПрочихРасходов = "
	|ВЫБРАТЬ // Прочие расходы (Дт <разные> :: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА Строки.СуммаРегл + Строки.НДСРегл
	|			ИНАЧЕ Строки.СуммаРегл 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА 
	|				ВЫБОР 
	|					КОГДА Операция.Валюта = &ВалютаУпрУчета 
	|						ТОГДА Строки.Сумма
	|						ИНАЧЕ Строки.Сумма * ЕСТЬNULL(КурсВалюты.Курс, 1)/ЕСТЬNULL(КурсВалютыУпрУчета.Курс, 1)
	|				КОНЕЦ
	|			ИНАЧЕ 
	|				ВЫБОР 
	|					КОГДА Операция.Валюта = &ВалютаУпрУчета 
	|						ТОГДА Строки.СуммаБезНДС
	|						ИНАЧЕ Строки.СуммаБезНДС * ЕСТЬNULL(КурсВалюты.Курс, 1)/ЕСТЬNULL(КурсВалютыУпрУчета.Курс, 1)
	|				КОНЕЦ
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
    |	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2, 
    |	Строки.АналитикаРасходов КАК СубконтоДт3, 
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.Валюта КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение 
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	Строки.СчетУчета КАК СчетКт,
	|	Строки.Субконто1 КАК СубконтоКт1,
	|	Строки.Субконто2 КАК СубконтоКт2,
	|	Строки.Субконто3 КАК СубконтоКт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(
	|			ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|						ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) 
	|				ТОГДА Операция.Дата
	|				ИНАЧЕ Строки.ДатаОтражения
	|			КОНЕЦ, 
	|		ДЕНЬ)
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалюты
	|	ПО
	|		КурсВалюты.Валюта = Операция.Валюта
	|		И КурсВалюты.Дата = НАЧАЛОПЕРИОДА(
	|			ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|						ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) 
	|				ТОГДА Операция.Дата
	|				ИНАЧЕ Строки.ДатаОтражения
	|			КОНЕЦ, 
	|		ДЕНЬ)
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|
	|";    

	ТекстРегистрацияПрочихРасходов = СтрШаблон(ТекстРегистрацияПрочихРасходов, 
		НСтр("ru='Прочие расходы';uk='Інші витрати'",ЯзыкСодержания));

#КонецОбласти

#Область СписаниеПрочихРасходов
	ТекстСписаниеПрочихРасходов = "
	|ВЫБРАТЬ // Списание прочих расходов (Дт <разные> :: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА Строки.СуммаРегл + Строки.НДСРегл
	|			ИНАЧЕ Строки.СуммаРегл 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА Строки.Сумма
	|			ИНАЧЕ Строки.СуммаБезНДС 
	|	КОНЕЦ КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Операция.Валюта КАК ВалютаДт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
    |	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт1,
	|	Строки.СтатьяРасходов КАК СубконтоКт2, 
    |	Строки.АналитикаРасходов КАК СубконтоКт3, 
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
	|	И (Строки.СуммаРегл <> 0)
	|
	|";  

	ТекстСписаниеПрочихРасходов = СтрШаблон(ТекстСписаниеПрочихРасходов, 
		НСтр("ru='Списание прочих расходов';uk='Списання інших витрат'",ЯзыкСодержания));

#КонецОбласти

#Область СторнированиеПрочихРасходов
	ТекстСторнированиеПрочихРасходов = "
	|ВЫБРАТЬ // Прочие расходы (сторно) (Дт <разные> :: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА -(Строки.СуммаРегл + Строки.НДСРегл)
	|			ИНАЧЕ -Строки.СуммаРегл 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА -Строки.Сумма
	|			ИНАЧЕ -Строки.СуммаБезНДС 
	|	КОНЕЦ КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
    |	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2, 
    |	Строки.АналитикаРасходов КАК СубконтоДт3, 
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.Валюта КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	Строки.СчетУчета КАК СчетКт,
	|	Строки.Субконто1 КАК СубконтоКт1,
	|	Строки.Субконто2 КАК СубконтоКт2,
	|	Строки.Субконто3 КАК СубконтоКт3,
	|
	|	-Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов)
	|
	|";     

	ТекстСторнированиеПрочихРасходов = СтрШаблон(ТекстСторнированиеПрочихРасходов, 
		НСтр("ru='Прочие расходы (сторно)';uk='Інші витрати (сторно)'",ЯзыкСодержания));

#КонецОбласти

#Область РегистрацияПрочихДоходов
	ТекстРегистрацияПрочихДоходов = "
	|ВЫБРАТЬ // Прочие доходы (Дт <разные>:: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	ВЫБОР 
	|		КОГДА Операция.Валюта = &ВалютаУпрУчета 
	|			ТОГДА Строки.СуммаУпр
	|			ИНАЧЕ Строки.СуммаУпр * ЕСТЬNULL(КурсВалюты.Курс, 1)/ЕСТЬNULL(КурсВалютыУпрУчета.Курс, 1)
	|	КОНЕЦ КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	Операция.Валюта КАК ВалютаДт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(
	|			ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|						ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) 
	|				ТОГДА Операция.Дата
	|				ИНАЧЕ Строки.ДатаОтражения
	|			КОНЕЦ, 
	|		ДЕНЬ)
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалюты
	|	ПО
	|		КурсВалюты.Валюта = Операция.Валюта
	|		И КурсВалюты.Дата = НАЧАЛОПЕРИОДА(
	|			ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|						ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) 
	|				ТОГДА Операция.Дата
	|				ИНАЧЕ Строки.ДатаОтражения
	|			КОНЕЦ, 
	|		ДЕНЬ)
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|
	|";   

	ТекстРегистрацияПрочихДоходов = СтрШаблон(ТекстРегистрацияПрочихДоходов, 
		НСтр("ru='Прочие доходы';uk='Інші доходи'",ЯзыкСодержания));

#КонецОбласти

#Область СторнированиеПрочихДоходов
	ТекстСторнированиеПрочихДоходов = "
	|ВЫБРАТЬ // Прочие доходы (сторно) (Дт <разные>:: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	-Строки.СуммаРегл КАК Сумма,
	|	-Строки.СуммаУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	Операция.Валюта КАК ВалютаДт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|	
	|	-Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихДоходов)
	|
	|"; 

	ТекстСторнированиеПрочихДоходов = СтрШаблон(ТекстСторнированиеПрочихДоходов, 
		НСтр("ru='Прочие доходы (сторно)';uk='Інші доходи (сторно)'",ЯзыкСодержания));

#КонецОбласти

#Область РеклассификацияРасходов
	ТекстРеклассификацияРасходов = "
	|ВЫБРАТЬ // Реклассификация расходов (Дт <разные>:: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА Строки.СуммаРегл + Строки.НДСРегл
	|			ИНАЧЕ Строки.СуммаРегл 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА Строки.НалоговоеНазначение.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиНДС.Необлагаемая) 
	|			ТОГДА Строки.Сумма
	|			ИНАЧЕ Строки.СуммаБезНДС 
	|	КОНЕЦ КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
    |	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2, 
    |	Строки.АналитикаРасходов КАК СубконтоДт3, 
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
    |	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт1,
	|	Строки.КорСтатьяРасходов КАК СубконтоКт2, 
	|	Строки.КорАналитикаРасходов КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = ВЫБОР КОГДА Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА НАЧАЛОПЕРИОДА(Операция.Дата, День) ИНАЧЕ Строки.ДатаОтражения КОНЕЦ
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительстваКор
	|	ПО 
	|		ОбъектыСтроительстваКор.Ссылка = Строки.КорАналитикаРасходов
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|
	|";   

	ТекстРеклассификацияРасходов = СтрШаблон(ТекстРеклассификацияРасходов, 
		НСтр("ru='Реклассификация расходов';uk='Рекласифікація витрат'",ЯзыкСодержания));

#КонецОбласти

#Область РеклассификацияДоходов
	ТекстРеклассификацияДоходов = "
	|ВЫБРАТЬ // Реклассификация доходов - сторно (Дт <разные>:: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	-Строки.СуммаРегл КАК Сумма,
	|	-Строки.СуммаУпр КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|
	|ВЫБРАТЬ // Реклассификация доходов (Дт <разные>:: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	Строки.СуммаУпр КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяДоходов КАК АналитикаУчетаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов)
	|
	|";   

	ТекстРеклассификацияДоходов = СтрШаблон(ТекстРеклассификацияДоходов, 
		НСтр("ru='Реклассификация доходов';uk='Рекласифікація доходів'",ЯзыкСодержания));

#КонецОбласти


	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстРегистрацияПрочихРасходов);
	ТекстыОтражения.Добавить(ТекстСписаниеПрочихРасходов);
	ТекстыОтражения.Добавить(ТекстСторнированиеПрочихРасходов);
	ТекстыОтражения.Добавить(ТекстРегистрацияПрочихДоходов);
	ТекстыОтражения.Добавить(ТекстСторнированиеПрочихДоходов);
	ТекстыОтражения.Добавить(ТекстРеклассификацияРасходов);
	ТекстыОтражения.Добавить(ТекстРеклассификацияДоходов);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	//++ Локализация
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	//++ НЕ УТ
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ
	//-- Локализация
	
КонецПроцедуры
//++ Локализация
//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДанныеРасходы", ТекстыЗапроса) Тогда
		Документы.ПрочиеДоходыРасходы.ТекстЗапросаВтДанныеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДанныеДоходы", ТекстыЗапроса) Тогда
		Документы.ПрочиеДоходыРасходы.ТекстЗапросаВтДанныеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		Документы.ПрочиеДоходыРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		Документы.ПрочиеДоходыРасходы.ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	Расходы.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(Расходы.Дата, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтДанныеРасходы КАК Расходы
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	&ОрганизацияПолучатель КАК Организация,
	|	НАЧАЛОПЕРИОДА(Расходы.Дата, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтДанныеРасходы КАК Расходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПрочихРасходовМеждуФилиалами)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	Доходы.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(Доходы.Дата, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтДанныеДоходы КАК Доходы
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТ
//-- Локализация

#КонецОбласти

#Область Прочее

//++ Локализация
//++ НЕ УТ

Процедура УстановитьПараметрыВыборочнойРегистрацииКОтражениюВРеглУчете(Объект, ДополнительныеСвойства)
	
	Если НЕ Объект.Проведен Тогда
		Возврат;
	КонецЕсли;
	
	НепроверяемыеРеквизиты = Новый Структура;
	НепроверяемыеРеквизиты.Вставить("Комментарий");
	
	НепроверяемыеТабличныеЧасти = Новый Структура;
	НепроверяемыеТабличныеЧасти.Вставить("ДополнительныеРеквизиты");
	
	ИзмененияДокумента = ОбщегоНазначенияУТ.ИзмененияДокумента(Объект, НепроверяемыеРеквизиты, НепроверяемыеТабличныеЧасти);
	
	Если ИзмененияДокумента.Свойство("Реквизиты") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИзмененияДокумента.Свойство("ТабличныеЧасти") Тогда
		РеглУчетПроведениеСервер.НеРегистрироватьКОтражениюВРеглУчете(ДополнительныеСвойства);
		Возврат;
	КонецЕсли; 
	
	ТабличныеЧастиДоходовРасходов = Новый Структура;
	ТабличныеЧастиДоходовРасходов.Вставить("ПрочиеДоходы");
	ТабличныеЧастиДоходовРасходов.Вставить("ПрочиеРасходы");
	
	Для каждого ТабличнаяЧасть Из ИзмененияДокумента.ТабличныеЧасти Цикл
		Если НЕ ТабличныеЧастиДоходовРасходов.Свойство(ТабличнаяЧасть.Ключ) Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаВыборочнойРегистрации = РеглУчетПроведениеСервер.ТаблицаВыборочнойРегистрацииКОтражению();
	Для каждого ТабличнаяЧасть Из ИзмененияДокумента.ТабличныеЧасти Цикл
		
		Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПрочихРасходовМеждуФилиалами Тогда
			
			ОбщиеКолонки = СтрРазделить("ДатаОтражения,ИдентификаторСтроки,Сумма,СуммаБезНДС,СуммаРегл,ПостояннаяРазница,ВременнаяРазница,СуммаУпр,НомерСтроки", ",");
			КолонкиОтправителя = Новый Массив;
			КолонкиПолучателя = Новый Массив;
			Для каждого Колонка Из ТабличнаяЧасть.Значение.Колонки Цикл
				Если ОбщиеКолонки.Найти(Колонка.Имя) <> Неопределено Тогда
					Продолжить;
				ИначеЕсли СтрНачинаетсяС(Колонка.Имя, "Кор") Тогда
					КолонкиПолучателя.Добавить(Колонка.Имя);
				Иначе
					КолонкиОтправителя.Добавить(Колонка.Имя);
				КонецЕсли;
			КонецЦикла;
			
			ТабличнаяЧасть.Значение.Сортировать("ИдентификаторСтроки");
			ПредыдущаяСтрока = Неопределено;
			
			Для каждого Строка Из ТабличнаяЧасть.Значение Цикл
				
				ОрганизацииДляРегистрации = Новый Массив;
				
				Если ПредыдущаяСтрока = Неопределено Тогда
					
					ПредыдущаяСтрока = Строка;
					
				Иначе
					
					Если Строка.ИдентификаторСтроки = ПредыдущаяСтрока.ИдентификаторСтроки Тогда
						Для каждого Колонка Из ТабличнаяЧасть.Значение.Колонки Цикл
							Если Колонка.Имя = "НомерСтроки" Тогда
								Продолжить;
							КонецЕсли;
							Если Строка[Колонка.Имя] <> ПредыдущаяСтрока[Колонка.Имя] Тогда
								Если ОбщиеКолонки.Найти(Колонка.Имя) <> Неопределено Или КолонкиОтправителя.Найти(Колонка.Имя) <> Неопределено Тогда
									Если ОрганизацииДляРегистрации.Найти(Объект.Организация) = Неопределено Тогда
										ОрганизацииДляРегистрации.Добавить(Объект.Организация);
									КонецЕсли;
								КонецЕсли;
								Если ОбщиеКолонки.Найти(Колонка.Имя) <> Неопределено Или КолонкиПолучателя.Найти(Колонка.Имя) <> Неопределено Тогда
									Если ОрганизацииДляРегистрации.Найти(Объект.ОрганизацияПолучатель) = Неопределено Тогда
										ОрганизацииДляРегистрации.Добавить(Объект.ОрганизацияПолучатель);
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
							Если ОрганизацииДляРегистрации.Количество() = 2 Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
						ПредыдущаяСтрока = Неопределено;
					Иначе
						// Строка единственная, значит она либо удалена, либо добавлена, зарегистрируем обе организации:
						ОрганизацииДляРегистрации.Добавить(Объект.Организация);
						ОрганизацииДляРегистрации.Добавить(Объект.ОрганизацияПолучатель);
						ПредыдущаяСтрока = Строка;
					КонецЕсли;
					
				КонецЕсли;
				
				Для каждого Организация Из ОрганизацииДляРегистрации Цикл
					РеглУчетПроведениеСервер.ДобавитьПараметрыВыборочнойРегистрацииКОтражениюВРеглУчете(
						ТаблицаВыборочнойРегистрации, Организация, Строка.ДатаОтражения);
				КонецЦикла;
				
			КонецЦикла;
			
		Иначе
			
			Для каждого Строка Из ТабличнаяЧасть.Значение Цикл
			
				РеглУчетПроведениеСервер.ДобавитьПараметрыВыборочнойРегистрацииКОтражениюВРеглУчете(
					ТаблицаВыборочнойРегистрации, Объект.Организация, Строка.ДатаОтражения);
			
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТаблицаВыборочнойРегистрации) Тогда
		ПроведениеДокументов.ДобавитьТаблицуДанныхДокумента(Объект,
			"ТаблицаВыборочнойРегистрацииКОтражению", ТаблицаВыборочнойРегистрации);
	КонецЕсли;
	
КонецПроцедуры
//-- НЕ УТ
//-- Локализация
#КонецОбласти

#КонецОбласти
