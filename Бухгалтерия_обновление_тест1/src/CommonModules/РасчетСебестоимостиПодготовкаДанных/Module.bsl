#Область ПрограммныйИнтерфейс

// Этап подготовки к расчету
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета);
	
	// Исправим проблемы в движениях и в самих документах в рассчитываемом периоде.
	ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
	ОчиститьДанныеУпрРегистров(ПараметрыРасчета);
	
	// Проверим корректность исходных данных для расчета.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьКорректностьИсходныхДанныхДоРасчета(ПараметрыРасчета);
	
	// Обновим кэши регистров, данные которых были изменены в этой процедуре.
	РасчетСебестоимостиПрикладныеАлгоритмы.ОбновитьРасчетныеКэшиРегистров(ПараметрыРасчета, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Механизм, аналогичный по своему назначению механизму обновления ИБ.
// Выполняется при расчете конкретного периода и исправляет документы только этого периода.
//
// Исправляет проблемы в движениях документов и в самих документах в рассчитываемом периоде.
//
// Есть ошибки, которые "портят" исходные данные для расчета,
// но для исправления их последствий не пишется обработчик обновления.
// Например, из-за какой-то старой ошибки в каких-то случаях движения документа были некорректны.
// - если для исправления этой ошибки написать обработчик обновления,
// то он изменит данные прошлых, "закрытых" периодов, из-за чего эти периоды перестанут быть "закрытыми".
// Придется повторно закрывать эти периоды, из-за чего в них может измениться себестоимость и, как следствие,
// регламентированная отчетность. Это плохо, т.к. по этим периодам отчетность уже может быть сдана.
// - с другой стороны, в тех периодах, которые пользователь будет пересчитывать, эту ошибку в движениях надо бы исправить,
// чтобы в этих пересчитанных периодах себестоимость основывалась на правильных исходных данных.
//
// Для исправления таких ошибок и предназначена данная процедура.
// По сути, она является аналогом обработчика обновления, исправляющего движения документов в периоде, указанном пользователем.
//
// Также эта процедура используется для корректировки движений периода при его перерасчете в партионном учете версии 2.2.
//
Процедура ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ИсправитьПроблемыВДвиженияхИДокументах");
	
    
 	Запрос = Новый Запрос;
 	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
 	
 	#Область ПоляПартионногоУчетаВерсии22
 	
 	// Проверим в первичных движениях документов корректность заполнения полей:
     // Партия, КорПартия, ТипЗаписи, аналитика учета партий, налоговое назначение.
 	// Если поля заполнены некорректно, то, видимо, документ был проведен до перехода на партионный учет версии 2.2 -
 	// надо перепровести документ по регистру, содержащему некорректные движения.
 	// Если после перепроведения документа поля все равно заполнены некорректно, значит ошибка кроется
 	// в процедурах формирования движений документа или в макете описания движений (для регистра себестоимости товаров).
 	
 	// Подготовим вспомогательные данные.
 	ОписаниеЦепочек 		= РасчетСебестоимостиЗаполнениеПартий.ОписаниеЦепочекПартийТоваров(ПараметрыРасчета);
 	ИспользуемыеТипыЗаписей = РасчетСебестоимостиПрикладныеАлгоритмы.ИспользуемыеТипыЗаписейВЦепочках(ОписаниеЦепочек);
 	
 	Запрос.УстановитьПараметр("ИспользуемыеТипыЗаписей", ИспользуемыеТипыЗаписей);
 	//++ НЕ УТ
 	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции", ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
 	//-- НЕ УТ
 	
 	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
 	Запрос.Текст = 
 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Т.ВидДвижения				 КАК ВидДвижения,
 	|	Т.Регистратор 				 КАК Регистратор,
 	|	Т.Организация 				 КАК Организация,
 	|	Т.КорОрганизация 			 КАК КорОрганизация,
 	|	Т.Партия 	  				 КАК Партия,
 	|	Т.КорПартия 	  			 КАК КорПартия,
 	|	Т.ХозяйственнаяОперация 	 КАК ХозяйственнаяОперация,
 	|	Т.ТипЗаписи					 КАК ТипЗаписи,
 	|	Т.АналитикаУчетаПартий		 КАК АналитикаУчетаПартий,
 	|	Т.КорАналитикаУчетаПартий	 КАК КорАналитикаУчетаПартий,
 	//++ НЕ УТ
 	|	Статьи.Ссылка				 КАК СтатьяКалькуляции, 
 	|	КорСтатьи.Ссылка			 КАК КорСтатьяКалькуляции, 
 	//-- НЕ УТ
 	|	Т.РазделУчета				 КАК РазделУчета,
 	|	Т.ВидЗапасов				 КАК ВидЗапасов,
 	|	Т.КорРазделУчета			 КАК КорРазделУчета,
 	|	Т.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
 	|	Т.КорАналитикаУчетаНоменклатуры	КАК КорАналитикаУчетаНоменклатуры,
 	|	Т.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
 	|	Т.КорАналитикаФинансовогоУчета	КАК КорАналитикаФинансовогоУчета,
     |	Т.НалоговоеНазначение		 КАК НалоговоеНазначение,
     |	Т.КорНалоговоеНазначение	 КАК КорНалоговоеНазначение,
 	|	Т.АналитикаУчетаПоПартнерам	 КАК АналитикаУчетаПоПартнерам,
 	|	Т.ГруппаПродукции			 КАК ГруппаПродукции,
 	|	Т.ДокументИсточник			 КАК ДокументИсточник,
 	|	Т.ПериодПродажи				 КАК ПериодПродажи,
 	|	ВЫБОР КОГДА Т.Количество > 0
 	|		ТОГДА ИСТИНА
 	|		ИНАЧЕ ЛОЖЬ
 	|	КОНЕЦ 						 КАК ПоложительноеКоличество,
 	|	Т.ДокументДвижения			 КАК ДокументДвижения,
 	|	Т.Стоимость					 КАК Стоимость,
 	|	Т.СтоимостьРегл				 КАК СтоимостьРегл,
 	|	Т.СтоимостьЗабалансоваяРегл	 КАК СтоимостьЗабалансоваяРегл
 	|ПОМЕСТИТЬ ВТДвиженияСебестоимостьТоваров
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров КАК Т
 	//++ НЕ УТ
 	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаНоменклатуры
 	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиКалькуляции КАК Статьи
 	|			ПО АналитикаНоменклатуры.СтатьяКалькуляции = Статьи.Ссылка
 	|		ПО Т.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.Ссылка
 	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КорАналитикаНоменклатуры
 	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиКалькуляции КАК КорСтатьи
 	|			ПО КорАналитикаНоменклатуры.СтатьяКалькуляции = КорСтатьи.Ссылка
 	|		ПО Т.КорАналитикаУчетаНоменклатуры = КорАналитикаНоменклатуры.Ссылка
 	//-- НЕ УТ
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости
 	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.РасчетСебестоимостиТоваров),
 	|									ТИП(Документ.КорректировкаРегистров))
 	|;
 	|
 	|////////////////////////////////////////////////////////////////////////////////
 	//++ НЕ УТ
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Т.Регистратор				КАК Регистратор,
 	|	Т.Организация				КАК Организация,
 	|	Т.ПартияПроизводства		КАК ПартияПроизводства,
 	|	Т.УдалитьПартияПроизводства	КАК УдалитьПартияПроизводства
 	|ПОМЕСТИТЬ ВТДвиженияПрочиеРасходыНезавершенногоПроизводства
 	|ИЗ
 	|	РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости
 	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.РасчетСебестоимостиТоваров),
 	|									ТИП(Документ.КорректировкаРегистров))
 	|;
 	|
 	|////////////////////////////////////////////////////////////////////////////////
 	//-- НЕ УТ
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Т.Регистратор 			 КАК Регистратор,
 	|	Т.Организация 			 КАК Организация,
 	|	Т.АналитикаУчетаПартий	 КАК АналитикаУчетаПартий,
     |	Т.НалоговоеНазначение	 КАК НалоговоеНазначение
 	|ПОМЕСТИТЬ ВТДвиженияПартииПрочихРасходов
 	|ИЗ
 	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И НЕ Т.РасчетПартий
 	//++ Локализация
	//++ НЕ УТ	
    |	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций
	//-- НЕ УТ
 	//-- Локализация
 	|;
 	|
 	|////////////////////////////////////////////////////////////////////////////////
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Т.Регистратор КАК Регистратор,
 	|	Т.Организация КАК Организация,
 	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация
 	|ПОМЕСТИТЬ ВозвратыТоваровНаДругойСклад
 	|ИЗ
 	|	ВТДвиженияСебестоимостьТоваров КАК Т
 	|ГДЕ
 	|	ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.ВозвратТоваровОтКлиента), ТИП(Документ.ВозвратТоваровМеждуОрганизациями))
 	|	И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
 	|;
 	|
 	|////////////////////////////////////////////////////////////////////////////////
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Т.ИмяРегистра КАК ИмяРегистра,
 	|	Т.Ссылка 	  КАК Ссылка,
 	|	Т.Организация КАК Организация,
 	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
 	|	Т.КодОшибки   КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"" КАК ИмяРегистра,
 	|		Т.Регистратор КАК Ссылка,
 	|		Т.Организация КАК Организация,
 	|		Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
 	|		ВЫБОР
 	|			КОГДА Правила.ПустоеЗначениеРегистратора ЕСТЬ NULL 
 	|				ТОГДА 1
 	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|				ТОГДА ВЫБОР
 	|						КОГДА Т.ТипЗаписи В
 	|							(ВЫБРАТЬ
 	|								ТипыЗаписей.ТипЗаписиПриход
 	|							 ИЗ
 	|								ВТПравилаЗаполненияПоляТипЗаписи КАК ТипыЗаписей
 	|							 ГДЕ
 	|								ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(ТипыЗаписей.ПустоеЗначениеРегистратора)
 	|								И (ТипыЗаписей.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
 	|									ИЛИ Т.ХозяйственнаяОперация = ТипыЗаписей.ХозяйственнаяОперация)
 	|								И (ТипыЗаписей.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
 	|									ИЛИ Т.ПоложительноеКоличество = ТипыЗаписей.ПоложительноеКоличество))
 	|							ТОГДА 0
 	|						ИНАЧЕ 2
 	|					КОНЕЦ
 	|			ИНАЧЕ ВЫБОР
 	|						КОГДА Т.ТипЗаписи В
 	|							(ВЫБРАТЬ
 	|								ТипыЗаписей.ТипЗаписиРасход
 	|							 ИЗ
 	|								ВТПравилаЗаполненияПоляТипЗаписи КАК ТипыЗаписей
 	|							 ГДЕ
 	|								ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(ТипыЗаписей.ПустоеЗначениеРегистратора)
 	|								И (ТипыЗаписей.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
 	|									ИЛИ Т.ХозяйственнаяОперация = ТипыЗаписей.ХозяйственнаяОперация)
 	|								И (ТипыЗаписей.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
 	|									ИЛИ Т.ПоложительноеКоличество = ТипыЗаписей.ПоложительноеКоличество))
 	|						ТОГДА 0
 	|					ИНАЧЕ 3
 	|				КОНЕЦ
 	|		КОНЕЦ КАК КодОшибки
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
 	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
 	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
 	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
 	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
 	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		ВЫБОР
 	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая) И Т.Партия = НЕОПРЕДЕЛЕНО
 	|				ТОГДА 4
 	|			КОГДА НЕ (Т.Организация В (&ОрганизацииСФИФОСкользящая)) И Т.Партия <> НЕОПРЕДЕЛЕНО
 	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
 	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
 	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
 	//++ НЕ УТ
 	|			  И НЕ (Т.РазделУчета В(ЗНАЧЕНИЕ (Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
 	|											ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение))
 	|					И НЕ Т.СтатьяКалькуляции ЕСТЬ NULL)
 	//-- НЕ УТ
 	|				ТОГДА 5
 	|			ИНАЧЕ 0
 	|		КОНЕЦ
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
 	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения)
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		ВЫБОР
 	// Проверка кор. партии в расходных движениях при заполненной кор. организации (схема Интеркампани).
 	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая)
 	|				И Т.КорОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
 	|				И НЕ Т.КорОрганизация В (&ОрганизацииСФИФОСкользящая)
 	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|				ТОГДА
 	|					ВЫБОР
 	|						КОГДА Т.КорПартия = НЕОПРЕДЕЛЕНО
 	|							ТОГДА 0
 	|						ИНАЧЕ 5
 	|					КОНЕЦ
 	|			КОГДА НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
 	|				И Т.КорОрганизация В (&ОрганизацииСФИФОСкользящая)
 	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|				И Т.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
 	|				ТОГДА
 	|					ВЫБОР
 	|						КОГДА Т.КорПартия = Т.Регистратор
 	|							ТОГДА 0
 	|						ИНАЧЕ 6
 	|					КОНЕЦ
 	// Проверка кор. партии в расходных движениях без кор. организации (изменение документа партии внутри одной организации).
 	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая)
 	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|				И НЕ Т.КорРазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
 	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
 	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка))
 	//++ НЕ УТ
 	|				И НЕ Т.КорСтатьяКалькуляции ЕСТЬ NULL
 	//-- НЕ УТ
 	|				И (
 	// при списании на партии производства кор. партия должна быть заполнена
 	|					(
 	|						(ЕСТЬNULL(Правила.КорПартияВРасходе, ЛОЖЬ)
 	|							И Т.КорПартия = НЕОПРЕДЕЛЕНО
 	|						ИЛИ НЕ ЕСТЬNULL(Правила.КорПартияВРасходе, ЛОЖЬ)
 	|							И НЕ Т.КорПартия = НЕОПРЕДЕЛЕНО)
 	|					)
 	|				)
 	|				ТОГДА 6
 	//++ НЕ УТ
 	|			КОГДА НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
 	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|				И Т.КорПартия = Т.Регистратор
 	|				И НЕ Т.КорРазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
 	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
 	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
 	|				И НЕ Т.КорСтатьяКалькуляции ЕСТЬ NULL
 	|				ТОГДА 5
 	//-- НЕ УТ
 	|			ИНАЧЕ 0
 	|		КОНЕЦ
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
 	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
 	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
 	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
 	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
 	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		7
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		НЕ (Т.ТипЗаписи В (&ИспользуемыеТипыЗаписей))
 	|		И НЕ (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей))
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		8
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
 	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
 	|	ГДЕ
 	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		9
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
 	|			ПО Т.КорАналитикаУчетаПартий = Аналитики.Ссылка
 	|	ГДЕ
 	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""ПартииПрочихРасходов"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		8
 	|	ИЗ
 	|		ВТДвиженияПартииПрочихРасходов КАК Т
 	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
 	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
 	|	ГДЕ
 	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""ПартииПрочихРасходов"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		10
 	|	ИЗ
 	|		ВТДвиженияПартииПрочихРасходов КАК Т
 	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
 	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
 	|	ГДЕ
     |		Т.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
 	|
 	//++ НЕ УТ
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		11
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|
 	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
 	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
 	|
 	|	ГДЕ
 	|		Т.Организация В (&ОрганизацииСФИФОСкользящая)
 	|		И Т.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
 	|									ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
 	|									ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.ПроизводствоБезЗаказа),
 	|										ТИП(Документ.ОтчетПереработчика))
 	|		И ЕСТЬNULL(Аналитики.КодСтроки, 0) = 0
 	//-- НЕ УТ
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		12
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|		И Т.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Возвраты.Регистратор,
 	|		Возвраты.Организация,
 	|		Возвраты.ХозяйственнаяОперация,
 	|		13
 	|	ИЗ
 	|		ВозвратыТоваровНаДругойСклад КАК Возвраты
 	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияСебестоимостьТоваров КАК Т
 	|			ПО Т.Регистратор = Возвраты.Регистратор
 	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратНаДругойСклад)
 	|	ГДЕ
 	|		Т.Регистратор ЕСТЬ NULL
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		14
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
     |		Т.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
 	|		И НЕ Т.РазделУчета В (
 	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
 	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
 	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
 	|
 	//++ НЕ УТ
 	//++ Устарело_Производство21
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		ДД.Регистратор,
 	|		ДД.Организация,
 	|		ДД.ХозяйственнаяОперация,
 	|		15
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК ДД
 	|	ГДЕ
 	|		НЕ ДД.ПоложительноеКоличество
 	|		И ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
 	|		И ДД.Регистратор Ссылка Документ.ВыпускПродукции
 	//-- Устарело_Производство21
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		ДД.Регистратор,
 	|		ДД.Организация,
 	|		ДД.ХозяйственнаяОперация,
 	|		16
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК ДД
 	|
 	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
 	|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
 	|
 	|	ГДЕ
 	|		ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
 	|		И (ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
 	|			И (&АналитическийУчетПоГруппамПродукции
 	|				И НЕ СпрНоменклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
 	|				И ДД.КорАналитикаФинансовогоУчета = НЕОПРЕДЕЛЕНО
 	|				ИЛИ ДД.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
 	|			ИЛИ ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
 	|				И НЕ ДД.КорАналитикаФинансовогоУчета = НЕОПРЕДЕЛЕНО)
 	//-- НЕ УТ
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		17
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыСПартией)
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией)
 	|		И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		ВЫБОР
 	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|					И Правила.ДокументИсточникВПриходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
 	|			  ТОГДА 18
 	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|					И Правила.ДокументИсточникВРасходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
 	|			  ТОГДА 19
 	|			  ИНАЧЕ 0
 	|		КОНЕЦ
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
 	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
 	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
 	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
 	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
 	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
 	|				И (Правила.ДокументИсточникВПриходе И Т.ТипЗаписи = Правила.ТипЗаписиПриход
 	|					ИЛИ Правила.ДокументИсточникВРасходе И Т.ТипЗаписи = Правила.ТипЗаписиРасход)
 	|	ГДЕ
 	|		ВЫБОР
 	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|					И Правила.ДокументИсточникВПриходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
 	|			  ТОГДА 18
 	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|					И Правила.ДокументИсточникВРасходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
 	|			  ТОГДА 19
 	|			  ИНАЧЕ 0
 	|		КОНЕЦ <> 0
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		20
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
 	|		И Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
 	|		И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
 	|		И Т.РазделУчета В (
 	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
 	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		21
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
 	|		И Т.ПериодПродажи = ДАТАВРЕМЯ(1,1,1)
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		22
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
 	|		И Т.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
 	//++ НЕ УТ
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		23
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
 	|		ПО СпрПартииПроизводства.Ссылка = Т.Партия
 	|
 	|	ГДЕ
 	|		СпрПартииПроизводства.ИспользуетсяДляПерехода
 	|		И НЕ ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
 	|			ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)) =
 	|				ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		23
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
 	|		ПО СпрПартииПроизводства.Ссылка = Т.КорПартия
 	|
 	|	ГДЕ
 	|		СпрПартииПроизводства.ИспользуетсяДляПерехода
 	|		И НЕ ЕСТЬNULL(Т.КорАналитикаУчетаНоменклатуры.СтатьяКалькуляции,
 	|			ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)) =
 	|				ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""ПрочиеРасходыНезавершенногоПроизводства"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		23
 	|	ИЗ
 	|		ВТДвиженияПрочиеРасходыНезавершенногоПроизводства КАК Т
 	|
 	|	ГДЕ
 	|		ЕСТЬNULL(Т.ПартияПроизводства.ИспользуетсяДляПерехода, ЛОЖЬ)
 	//-- НЕ УТ
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		24
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
 	|			ПО ВыручкаИСебестоимостьПродаж.Регистратор = Т.Регистратор
 	|	ГДЕ
 	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|		И Т.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
 	|		И Т.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
 	|		И Т.ТипЗаписи <> ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		25
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		(Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
 	|			ИЛИ Т.КорАналитикаУчетаПартий = Т.АналитикаУчетаПартий) // По ошибке перезаполнился реквизит КорАналитикаУчетаПартий
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И Т.ТипЗаписи В (
 	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии),
 	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией))
 	|
 	//++ НЕ УТ
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		26
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПереработчика)
 	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
 	|		И НЕ Т.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
 	|	
 	//++ Устарело_Производство21
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		27
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ВыпускПродукции)
 	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
 	|		И Т.ГруппаПродукции <> Т.АналитикаФинансовогоУчета
 	|		И (
 	|			Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
 	|			И ТИПЗНАЧЕНИЯ(Т.АналитикаФинансовогоУчета) = Тип(Справочник.ГруппыАналитическогоУчетаНоменклатуры)
 	|			И Т.АналитикаФинансовогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
 	|			ИЛИ
 	|			Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
 	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
 	|			И Т.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
 	|			)
 	//-- Устарело_Производство21
 	//-- НЕ УТ
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""ВыручкаИСебестоимостьПродаж"",
 	|		Т.Регистратор,
 	|		Аналитика.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		28
 	|	ИЗ
 	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
 	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
 	|	ГДЕ
 	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|		И Аналитика.Организация В (&МассивОрганизаций)
 	|		И Т.Активность
 	|		И НЕ Т.РасчетПартий
 	|		И НЕ Т.РасчетСебестоимости
 	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями)
 	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		30
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт КАК Декларация
 	|			ПО Декларация.Ссылка = Т.Регистратор
 	|	ГДЕ
     |		Т.КорНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		31
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК СборкаТоваров
 	|			ПО СборкаТоваров.Ссылка = Т.Регистратор
 	|	ГДЕ
 	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
    |		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.НалоговоеНазначение, ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка))
    |			<> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
    |		И Т.НалоговоеНазначение <> Т.АналитикаУчетаНоменклатуры.Назначение.НалоговоеНазначение
 	|	
 	//++ НЕ УТ
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		32
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
 	|			ПО ПроизводствоБезЗаказа.Ссылка = Т.Регистратор
 	|	ГДЕ
 	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
 	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
 	|		И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
 	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
 	|		И Т.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
 	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
 	|
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		34
 	|	ИЗ
 	|		РегистрНакопления.СебестоимостьТоваров КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Реквизиты
 	|			ПО Реквизиты.Ссылка = Т.Регистратор
 	|	ГДЕ
 	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И Т.Активность
 	|		И Т.Организация В(&МассивОрганизаций)
 	|		И НЕ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
 	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
 	|		И Т.Количество < 0
 	//-- НЕ УТ
 	|	
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		36
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
 	|			ПО КорректировкаПриобретения.Ссылка = Т.Регистратор
 	|	ГДЕ
 	|		Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода)
 	|		И Т.Стоимость <> 0
 	|	
 	//++ НЕ УТ
 	// После изменения флажка "Цеховая кладовая" в складе могут остаться движения с неправильным разделом учета.
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		37
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
 	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
 	|		И Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""ВыручкаИСебестоимостьПродаж"",
 	|		Т.Регистратор,
 	|		Аналитика.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		37
 	|	ИЗ
 	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
 	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
 	|	ГДЕ
 	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|		И Аналитика.Организация В (&МассивОрганизаций)
 	|		И Т.Активность
 	|		И НЕ Т.РасчетПартий
 	|		И НЕ Т.РасчетСебестоимости
 	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
 	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
 	|		И Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		38
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
 	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
 	|		И НЕ Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""ВыручкаИСебестоимостьПродаж"",
 	|		Т.Регистратор,
 	|		Аналитика.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		38
 	|	ИЗ
 	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
 	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
 	|	ГДЕ
 	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|		И Аналитика.Организация В (&МассивОрганизаций)
 	|		И Т.Активность
 	|		И НЕ Т.РасчетПартий
 	|		И НЕ Т.РасчетСебестоимости
 	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
 	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
 	|		И НЕ Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
 	//-- НЕ УТ
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""ПартииПрочихРасходов"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		Т.ХозяйственнаяОперация,
 	|		39
 	|	ИЗ
 	|		РегистрНакопления.ПартииПрочихРасходов КАК Т
 	|	ГДЕ
 	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|		И Т.Организация В (&МассивОрганизаций)
 	|		И Т.Активность
 	|		И НЕ Т.РасчетПартий
 	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
 	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПрочиеДоходыРасходы)
 	|		И Т.ДокументПоступленияРасходов = НЕОПРЕДЕЛЕНО
 	|
 	|	ОБЪЕДИНИТЬ ВСЕ
 	|	
 	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|		""СебестоимостьТоваров"",
 	|		Т.Регистратор,
 	|		Т.Организация,
 	|		НЕОПРЕДЕЛЕНО,
 	|		40
 	|	ИЗ
 	|		ВТДвиженияСебестоимостьТоваров КАК Т
 	|	ГДЕ
 	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
 	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
 	|	) КАК Т
 	|
 	|ГДЕ
 	|	Т.КодОшибки <> 0
 	|";
 	
 	// Сформируем описание возможных кодов ошибок в движениях.
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Для движения не найдено соответствующее ему правило заполнения типа записи';uk='Для руху не знайдено відповідне йому правило заповнення типу запису'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Тип записи приходного движения не соответствует правилу';uk='Тип запису прибуткового руху не відповідає правилу'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Тип записи расходного движения не соответствует правилу';uk='Тип запису видаткового руху не відповідає правилу'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(4, НСтр("ru='Для организаций с методом оценки ""ФИФО (скользящая)"" не заполнена первичная партия';uk='Для організацій з методом оцінки ""ФІФО (змінна)"" не заповнена первинна партія'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(5, НСтр("ru='Для организаций с методом оценки, отличным от ""ФИФО (скользящая)"", заполнена первичная партия или кор. партия';uk='Для організацій з методом оцінки, відмінним від ""ФІФО (змінна)"", заповнена первинна партія або кор. партія'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(6, НСтр("ru='Для организаций с методом оценки ""ФИФО (скользящая)"" некорректно заполнена кор. партия в расходном движении';uk='Для організацій з методом оцінки ""ФІФО (змінна)"" некоректно заповнена кор. партія в видатковому русі'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(7, НСтр("ru='Тип записи, указанный в движении, не используется при распределении партий';uk='Тип запису, вказаний в русі, не використовується при розподілі партій'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(8, НСтр("ru='Аналитика учета партий не содержит вида ценности';uk='Аналітика обліку партій не містить виду цінності'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(9, НСтр("ru='Кор. аналитика учета партий не содержит вида ценности';uk='Кор. аналітика обліку партій не містить виду цінності'",ОбщегоНазначения.КодОсновногоЯзыка()));
    РасшифровкаКодовОшибок.Вставить(10, НСтр("ru='Не заполнено налоговое назначение при заполненной аналитике учета партий';uk='Не заповнене податкове призначення при заповненій аналітиці обліку партій'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(11, НСтр("ru='Для аналитики учета партий выпуска изделий не заполнен код строки (продукции)';uk='Для аналітики обліку партій випуску виробів не заповнений код рядка (продукції)'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(12, НСтр("ru='Не заполнена кор. аналитика учета номенклатуры для операции ""Реализация (товары в пути)""';uk='Не заповнена кор. аналітика обліку номенклатури для операції ""Реалізація (товари в дорозі)""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(13, НСтр("ru='Для документов возврата товаров от клиента/организации на склад, отличный от склада реализации, используются неправильные хозяйственные операции и типы записей';uk='Для документів повернення товарів від клієнта/організації на склад, відмінний від складу реалізації, використовуються неправильні господарські операції і типи записів'",ОбщегоНазначения.КодОсновногоЯзыка()));
    РасшифровкаКодовОшибок.Вставить(14, НСтр("ru='Не заполнено налоговое назначение у приходных движений с типом записи ""Партия""';uk='Не заповнене податкове призначення у прибуткових рухів з типом записи ""Партія""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	//++ НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(15, НСтр("ru='Для документов выпуска обнаружены отрицательные движения по возвратным отходам при производстве давальческой продукции';uk='Для документів випуску виявлені від''ємні рухи зі зворотних відходів при виробництві давальницької продукції'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(16, НСтр("ru='Неправильно заполнена аналитика учета выпуска возвратных отходов (кор. аналитика учета номенклатуры, кор. аналитика финансового учета).';uk='Неправильно заповнена аналітика обліку випуску зворотних відходів (кор. аналітика обліку номенклатури, кор. аналітика фінансового обліку).'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	//-- НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(17, НСтр("ru='Не заполнен документ - источник для операции ""Дополнительные расходы с указанием документа партии"".';uk='Не заповнений документ - джерело для операції ""Додаткові витрати з зазначенням документа партії"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(18, НСтр("ru='Заполнение документа-источника в приходном движении не соответствует правилу.';uk='Заповнення документа-джерела в прибутковому русі не відповідає правилу.'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(19, НСтр("ru='Заполнение документа-источника в расходном движении не соответствует правилу.';uk='Заповнення документа-джерела в видатковому русі не відповідає правилу.'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(20, НСтр("ru='Не заполнена кор. аналитика учета партий приходных движений с типом записи ""Партия"" для организаций с методом оценки, отличным от ""ФИФО (скользящая)""';uk='Не заповнена кор. аналітика обліку партій прибуткових рухів з типом запису ""Партія"" для організацій з методом оцінки, відмінним від ""ФІФО (змінна)""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(21, НСтр("ru='Не заполнен период продажи для операции ""Возврат товаров от клиента прошлых периодов""';uk='Не заповнений період продажу для операції ""Повернення товарів від клієнта минулих періодів""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(22, НСтр("ru='Не заполнены кор. реквизиты для операции ""Закупка по регл. учету""';uk='Не заповнені кор. реквізити для операції ""Купівля по регл. обліку""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	//++ НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(23, НСтр("ru='В незавершенном производстве в качестве партии производства используется ссылка на служебный элемент справочника ""Партии производства""';uk='У незавершеному виробництві в якості партії виробництва використовується посилання на службовий елемент довідника ""Партії виробництва""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	//-- НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(24, НСтр("ru='Не заполнена аналитика учета по партнерам в движениях документов продажи';uk='Не заповнена аналітика обліку по партнерах в рухах документів продажу'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(25, НСтр("ru='Не заполнена кор. аналитика учета партий в приходных движениях с типом записей ""Доп. расходы (не указана партия)"" и ""Доп. расходы (указана партия)""';uk='Не заповнена кор. аналітика обліку партій в прибуткових рухах з типом записів ""Дод. витрати (не зазначена партія)"" і ""Дод. витрати (зазначена партія)""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	//++ НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(26, НСтр("ru='В движениях по распределению материалов в отчете переработчика заполнен реквизит ""Группа продукции"".';uk='У рухах з розподілу матеріалів в звіті переробника заповнений реквізит ""Група продукції"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(27, НСтр("ru='В движениях по выпуску продукции отличается заполнение реквизитов ""Аналитика финансового учета"" и ""Группа продукции"".';uk='У рухах з випуску продукції відрізняється заповнення реквізитів ""Аналітика фінансового обліку"" і ""Група продукції"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	//-- НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(28, НСтр("ru='Не корректно заполнен раздел учета для операции ""Отчет по комиссии между организациями""';uk='Не коректно заповнений розділ обліку для операції ""Звіт з комісії між організаціями""'",ОбщегоНазначения.КодОсновногоЯзыка()));
    РасшифровкаКодовОшибок.Вставить(30, НСтр("ru='Не корректно заполнено кор. налоговое назначение для документа ""Таможенная декларация на импорт"".';uk='Не коректно заповнене кор. податкове призначення для документа ""Митна декларація на імпорт"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
    РасшифровкаКодовОшибок.Вставить(31, НСтр("ru='Не корректно заполнено налоговое назначение для документа ""Сборка (разборка) товаров"".';uk='Не коректно заповнене податкове призначення для документа ""Складання (розбирання) товарів"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(32, НСтр("ru='Не корректно заполнен тип записи в приходном движении для документа ""Производство без заказа"".';uk='Не коректно заповнений тип запису в прибутковому русі для документа ""Виробництво без замовлення"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(33, НСтр("ru='Не корректные движения по изменению назначения работ для документа ""Этап производства"".';uk='Не коректні руху зі зміни призначення робіт для документа ""Етап виробництва"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(34, НСтр("ru='Некорректные движения по разделу учета ""Производственные затраты"".';uk='Некоректні рухи за розділом обліку ""Виробничі витрати"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(36, НСтр("ru='Некорректные движения документа ""Корректировка приобретения"", сформированного в предыдущих версиях партионного учета.';uk='Некоректні рухи документа ""Коригування придбання"", сформованого в попередніх версіях партіонного обліку.'"));
 	//++ НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(37, НСтр("ru='Не правильный раздел учета для движений в цеховой кладовой.';uk='Не правильний розділ обліку для рухів в цеховій комори.'"));
 	РасшифровкаКодовОшибок.Вставить(38, НСтр("ru='Не правильный раздел учета для движений по складу, который не является цеховой кладовой.';uk='Не правильний розділ обліку для рухів по складу, який не є цеховою коморою.'"));
 	//-- НЕ УТ
 	РасшифровкаКодовОшибок.Вставить(39, НСтр("ru='Не заполнен документ поступления расходов в движениях документа ""Отражение прочих доходов и расходов"".';uk='Не заповнений документ надходження витрат в рухах документа ""Відображення інших доходів і витрат"".'"));
 	РасшифровкаКодовОшибок.Вставить(40, НСтр("ru='Не правильный раздел учета для товаров на хранении с правом продажи.';uk='Не правильний розділ обліку для товарів на зберіганні з правом продажу.'"));
 	
 	ДополнительныеПоля = Новый Структура("ХозяйственнаяОперация", НСтр("ru='операция';uk='операція'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
        НСтр("ru='переход на партионный учет версии 2.5';uk='перехід на партіонний облік версії 2.5'",ОбщегоНазначения.КодОсновногоЯзыка()),
 		ДополнительныеПоля);
 	
 	#КонецОбласти
 	
 //++ НЕ УТ
 	#Область СебестоимостьТоваровСуммыВОтчетеПереработчика
 	
 	// Исправление некорректных движений в регистре "Себестоимость товаров".
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Упр. стоимость в движениях по регистру ""Себестоимость товаров"" не приведена к валюте упр. учета';uk='Упр. вартість в рухах по регістру ""Собівартість товарів"" не приведена до валюти упр. обліку'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.УстановитьПараметр("ВалютаРегл", Константы.ВалютаРегламентированногоУчета.Получить());
 	Запрос.УстановитьПараметр("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
 	
 	Запрос.Текст =
 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""СебестоимостьТоваров""	КАК ИмяРегистра,
 	|	Т.Регистратор				КАК Ссылка,
 	|	Т.Организация				КАК Организация,
 	|	1							КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров КАК Т
 	|
 	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК ОтчетыПереработчика
 	|	ПО Т.Регистратор = ОтчетыПереработчика.Ссылка
 	|
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПереработчика)
 	|	И НЕ &ВалютаУпр = &ВалютаРегл
 	|	И Т.Стоимость = Т.СтоимостьРегл
 	|	И НЕ Т.Стоимость = 0
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='приведение стоимостей в отчете переработчика к валюте упр. учета';uk='приведення вартостей в звіті переробника до валюти упр. обліку'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти
 //-- НЕ УТ

 	#Область ВыручкаИСебестоимостьПродаж
 	
 	// Исправление некорректных движений в регистре "Выручка и себестоимость продаж".
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Дублирование расчетной себестоимости в первичных движениях';uk='Дублювання розрахункової собівартості в первинних рухах'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
 	
 	Запрос.Текст = ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж();
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='ошибка 00-00071689';uk='помилка 00-00071689'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
 	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
 	
 	#КонецОбласти

 	#Область НекорректнаяАналитикаФинансовогоУчета
 	
 	// Исправление некорректной пустой аналитики финансового учета в регистре "Себестоимость товаров".
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Пустая аналитика финансового учет в первичных движениях имеет значение отличное от НЕОПРЕДЕЛЕНО';uk='Порожня аналітика фінансового облік в первинних рухах має значення відмінне від НЕВИЗНАЧЕНО'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст =
 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
 	|	Т.Регистратор 			 КАК Ссылка,
 	|	Т.Организация 			 КАК Организация,
 	|	1 						 КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости
 	|	И Т.АналитикаФинансовогоУчета В
 	|		(ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
 	|		 ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
 	|		 ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
 	|		 ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='ошибка 00-00098117';uk='помилка 00-00098117'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти

 	#Область СуммыВДвижениях
 	
 	// Проверка соответствия заполненности суммы упр./регл. значениям функциональных опций.
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Заполнение упр. сумм в движениях не соответствует значению функциональной опции ""ВестиУправленческийУчетОрганизаций""';uk='Заповнення упр. сум в рухах не відповідає значенню функціональної опції ""ВестиУправленческийУчетОрганизаций""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Заполнение регл. сумм в движениях не соответствует значению функциональной опции ""ИспользоватьУчетПрочихДоходовРасходовРегл""';uk='Заповнення регл. сум в рухах не відповідає значенню функціональної опції ""Використовувати облік інших доходів витрат регл""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#Область ИсключаемыеТипыДокументов
 	
 	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
 	
 	ТипыРегистраторов = Новый Массив;
 	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеДоходовПоНаправлениямДеятельности"));
 	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеПрочихЗатрат"));
 	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РасчетКурсовыхРазниц"));
 	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РегистраторРасчетов"));
 	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПрочиеДоходыРасходы"));
 	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ДвижениеПрочихАктивовПассивов"));
 	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.КорректировкаПриобретения"));
 	
 	РасчетСебестоимостиПодготовкаДанныхЛокализация.ДополнитьТипыРегистраторовСНестандартнойЛогикойФормированияДвижений(ТипыРегистраторов);
 	
 	Запрос.УстановитьПараметр("ТипыРегламентныхДокументов", ТипыРегистраторов);
 	
 	#КонецОбласти
 	
 	Запрос.Текст =
 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
 	|	Т.Регистратор 			 КАК Ссылка,
 	|	Т.Организация 			 КАК Организация,
 	|	1 						 КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости
 	|	И (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей)
 	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
 	|		  И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
 	|	И (Т.Стоимость <> 0 ИЛИ Т.КорСтоимость <> 0 ИЛИ Т.ДопРасходы <> 0 ИЛИ Т.Трудозатраты <> 0
 	|		ИЛИ Т.ПостатейныеПостоянныеСНДС <> 0 ИЛИ Т.ПостатейныеПеременныеСНДС <> 0
 	|		ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
 	|	И НЕ Т.РазделУчета В (
 	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
 	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
 	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
 	|	И ((&УправленческийУчетОрганизаций И (Т.Стоимость <> 0 ИЛИ Т.КорСтоимость <> 0
 	|			ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
 	|		И Т.СтоимостьУпр = 0 И Т.ДопРасходыУпр = 0 И Т.ТрудозатратыУпр = 0
 	|		И Т.ПостатейныеПостоянныеУпр = 0 И Т.ПостатейныеПеременныеУпр = 0)
 	|	ИЛИ (НЕ &УправленческийУчетОрганизаций
 	|		И (Т.СтоимостьУпр <> 0 ИЛИ Т.ДопРасходыУпр <> 0 ИЛИ Т.ТрудозатратыУпр <> 0
 	|		ИЛИ Т.ПостатейныеПостоянныеУпр <> 0 ИЛИ Т.ПостатейныеПеременныеУпр <> 0)))
 	|	И НЕ (Т.СтоимостьБезНДС = 0	И Т.ДопРасходы <> 0 И Т.ДопРасходыУпр = 0)
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ПрочиеДоходы"" 		 КАК ИмяРегистра,
 	|	Т.Регистратор 			 КАК Ссылка,
 	|	Т.Организация 			 КАК Организация,
 	|	ВЫБОР
 	|		КОГДА Т.Сумма <> 0
 	|			И Т.СуммаРегл = 0
 	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл
 	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
 	|			ТОГДА 2
 	|		ИНАЧЕ 1
 	|	КОНЕЦ					 КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.ПрочиеДоходы КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И НЕ Т.РасчетСебестоимости
 	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
 	|	И ((&УправленческийУчетОрганизаций
 	|			И Т.СуммаУпр = 0
 	|			И Т.Сумма <> 0
 	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
 	|		ИЛИ (НЕ &УправленческийУчетОрганизаций
 	|			И Т.СуммаУпр <> 0)
 	|		ИЛИ (Т.Сумма <> 0
 	|			И Т.СуммаРегл = 0 И ВЫРАЗИТЬ(Т.Сумма * &КоэффициентВалютыРегл КАК ЧИСЛО(31,2)) <> 0
 	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл
 	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)))
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
 	|	Т.Регистратор 			 КАК Ссылка,
 	|	Т.Организация 			 КАК Организация,
 	|	1 						 КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.ПрочиеРасходы КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости
 	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
 	|	И ((&УправленческийУчетОрганизаций И Т.СуммаУпр = 0 И Т.Сумма <> 0
 	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
 	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СуммаУпр <> 0
 	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)))
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
 	|	Т.Регистратор 			 КАК Ссылка,
 	|	Т.Организация 			 КАК Организация,
 	|	1 						 КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
 	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.НДСРегл <> 0 И Т.НДСУпр = 0 И ВЫРАЗИТЬ(Т.НДСРегл * &КоэффициентВалютыУпр КАК ЧИСЛО(31,2)) <> 0
 	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
 	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.НДСУпр <> 0))
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
 	|	Т.Регистратор 			        КАК Ссылка,
 	|	Аналитика.Организация 			КАК Организация,
 	|	1 						        КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
 	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
 	|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Аналитика.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости
 	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
 	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.СтоимостьУпр = 0)
 	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СтоимостьУпр <> 0))
 	|";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='некорректное заполнение сумм упр. учета';uk='некоректне заповнення сум упр. обліку'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти

 //++ НЕ УТ
 	#Область ВыпускПродукции
 	
 	// Исправление движений по регистру Выпуск продукции
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Требуется сформировать движения по регистру ""Выпуск продукции"" для отчетов переработчиков';uk='Потрібно сформувати рухи по регістру ""Випуск продукції"" для звітів переробників'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Требуется удалить движения по регистру ""Выпуск продукции"" для поступлений от переработчиков';uk='Потрібно вилучити руху по регістру ""Випуск продукції"" для надходжень від переробників'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст =
 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ВыпускПродукции"" 	 КАК ИмяРегистра,
 	|	Реквизиты.Ссылка 		 КАК Ссылка,
 	|	Реквизиты.Организация 	 КАК Организация,
 	|	1 						 КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	Документ.ОтчетПереработчика КАК Реквизиты
 	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыпускПродукции КАК Т
 	|	ПО Реквизиты.Ссылка = Т.Регистратор
 	|ГДЕ
 	|	Реквизиты.Проведен
 	|	И Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Реквизиты.Организация В (&МассивОрганизаций)
 	|	И Т.Регистратор ЕСТЬ NULL
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ВыпускПродукции"" 	 КАК ИмяРегистра,
 	|	Т.Регистратор 			 КАК Ссылка,
 	|	Т.Организация 			 КАК Организация,
 	|	2 						 КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.ВыпускПродукции КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И Т.Регистратор Ссылка Документ.ПоступлениеОтПереработчика";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='некорректные движения по регистру ""Выпуск продукции""';uk='некоректні рухи по регістру ""Випуск продукції""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти

 	
 	
 	#Область ТрудозатратыНЗП
 	
 	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
 	// Исключаем документы "ОтражениеЗарплатыВФинансовомУчете", так как они должны быть проведены в самом конце.
 	Запрос.Текст = "
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ТрудозатратыНезавершенногоПроизводства""	КАК ИмяРегистра,
 	|	Т.Регистратор								КАК Ссылка,
 	|	Т.Организация								КАК Организация,
 	|	1											КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И НЕ Т.РасчетПартий
 	|	И ЕСТЬNULL(Т.ПартияПроизводства.ИспользуетсяДляПерехода, ЛОЖЬ)
 	//++ Локализация
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтражениеЗарплатыВФинансовомУчете)
 	//-- Локализация
 	|";
 	
 	// Сформируем описание возможных кодов ошибок в движениях.
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='В незавершенном производстве в качестве партии производства используется ссылка на документ партии, а не на справочник ""Партии производства""';uk='У незавершеному виробництві в якості партії виробництва використовується посилання на документ партії, а не на довідник ""Партії виробництва""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='переход на новую партию производства';uk='перехід на нову партію виробництва'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
 	// Выбираем только документы "ОтражениеЗарплатыВФинансовомУчете", так как все остальные уже перепроведены.
 	Запрос.Текст = " 
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ТрудозатратыНезавершенногоПроизводства""	КАК ИмяРегистра,
 	|	Т.Регистратор								КАК Ссылка,
 	|	Т.Организация								КАК Организация,
 	|	1											КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК Т
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И НЕ Т.РасчетПартий
 	|	И ЕСТЬNULL(Т.ПартияПроизводства.ИспользуетсяДляПерехода, ЛОЖЬ)
 	|";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='некорректные движения ""Трудозатраты НЗП""';uk='некоректні рухи ""Трудовитрати НЗП""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти

 	#Область ДвиженияНоменклатураНоменклатура
 	
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Не сформированы количественные движения по регистру ""Движения Номенклатура - Номенклатура"".';uk='Не сформовані кількісні рухи за регістром ""Рухи Номенклатура - Номенклатура"".'"));
 	
 	Запрос.Текст = "
 	|ВЫБРАТЬ
 	|	""ДвиженияНоменклатураНоменклатура""	КАК ИмяРегистра,
 	|	Движения.Регистратор					КАК Ссылка,
 	|	Движения.Организация					КАК Организация,
 	|	1 										КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК Движения
 	|ГДЕ
 	|	Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Движения.Организация В (&МассивОрганизаций)
 	|	И ТИПЗНАЧЕНИЯ(Движения.Регистратор) В (
 	|		ТИП(Документ.ПроизводствоБезЗаказа))
 	|
 	|СГРУППИРОВАТЬ ПО
 	|	Движения.Регистратор,
 	|	Движения.Организация
 	|
 	|ИМЕЮЩИЕ
 	|	СУММА(Движения.Количество) = 0
 	|	И НЕ (СУММА(Движения.Стоимость)
 	|			+ СУММА(Движения.СтоимостьЗабалансовая)
 	|			+ СУММА(Движения.ДопРасходы)
 	|			+ СУММА(Движения.Трудозатраты)
 	|			+ СУММА(Движения.ПостатейныеПостоянныеСНДС)
 	|			+ СУММА(Движения.ПостатейныеПеременныеСНДС)) = 0
 	|";

 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='Исправление движений документа ""Этап производства""';uk='Виправлення рухів документа ""Етап виробництва""'"));
 	
 	#КонецОбласти
 //-- НЕ УТ

 	#Область ПартнерВКлючеАналитикиУчетаНоменклатуры
 	
 	// Замена ключей аналитики учета номенклатуры с партнером на ключи с договором контрагента.
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Себестоимость товаров"" указан партнер, а не договор контрагента.';uk='У ключі аналітики обліку номенклатури в регістрі ""Собівартість товарів"" вказаний партнер, а не договір контрагента.'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Закупки"" указан партнер, а не договор контрагента.';uk='У ключі аналітики обліку номенклатури в регістрі ""Купівлі"" вказаний партнер, а не договір контрагента.'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Движения Номенклатура - Номенклатура"" указан партнер, а не договор контрагента.';uk='У ключі аналітики обліку номенклатури в регістрі ""Рухи Номенклатура - Номенклатура"" вказаний партнер, а не договір контрагента.'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст =
 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""СебестоимостьТоваров""	КАК ИмяРегистра,
 	|	Т.Регистратор				КАК Ссылка,
 	|	Т.Организация				КАК Организация,
 	|	1							КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров КАК Т
 	|
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И Т.РазделУчета В (
 	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
 	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки))
 	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
 	|	И НЕ Т.РасчетПартий
 	|	И НЕ Т.РасчетСебестоимости
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""Закупки""	  КАК ИмяРегистра,
 	|	Т.Регистратор КАК Ссылка,
 	|	Т.Организация КАК Организация,
 	|	2			  КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.Закупки КАК Т
 	|
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И Т.ТипЗапасов В (
 	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
 	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
 	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
 	|	И НЕ Т.РасчетСебестоимости
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""ДвиженияНоменклатураНоменклатура"" КАК ИмяРегистра,
 	|	Т.Регистратор						 КАК Ссылка,
 	|	Т.Организация						 КАК Организация,
 	|	3									 КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК Т
 	|
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В(&МассивОрганизаций)
 	|	И Т.ТипЗапасов В (
 	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
 	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
 	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
 	|	И НЕ Т.РасчетСебестоимости
 	|";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='исправление ключей аналитики учета номенклатуры в регистрах (замена партнера на договор)';uk='виправлення ключів аналітики обліку номенклатури в регістрах (заміна партнера на договір)'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти
    
 	
 	#Область АналитикаОтгрузкиВВозвратеТоваровМеждуОрганизациями
 	
 	// Заполнение нового реквизита табличной части ВидыЗапасов
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Не заполнена аналитика номенклатуры отгрузки в документе возврата товаров между организациями.';uk='Не заповнена аналітика номенклатури відвантаження в документі повернення товарів між організаціями.'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст =
 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Т.Ссылка				КАК Ссылка,
 	|	Т.Ссылка.Организация	КАК Организация,
 	|	1						КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыДляПерепроведения
 	|ИЗ
 	|	Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасов КАК Т
 	|
 	|ГДЕ
 	|	Т.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Ссылка.Организация В(&МассивОрганизаций)
 	|	И Т.Ссылка.Проведен
 	|	И Т.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимости.ИзДокументаПередачи)
 	|	И Т.АналитикаУчетаНоменклатурыОтгрузки = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
 	|";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументы(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='исправление ключей аналитики учета номенклатуры отгрузки в документе возврата товаров между организациями';uk='виправлення ключів аналітики обліку номенклатури відвантаження в документі повернення товарів між організаціями'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти

 	#Область ВключениеУчетаСебестоимостиПоНазначениям
 	
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Включение учета себестоимости по назначениям';uk='Включення обліку собівартості по призначеннях'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям();
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='включение учета себестоимости по назначениям';uk='включення обліку собівартості по призначеннях'",ОбщегоНазначения.КодОсновногоЯзыка()));
 		
 	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='включение учета выручки и себестоимости продаж по назначениям';uk='включення обліку виторгу і собівартості продажів по призначеннях'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти
 	
 	#Область ВыключениеУчетаСебестоимостиПоНазначениям
 	
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Выключение учета себестоимости по назначениям';uk='Вимкнення обліку собівартості по призначеннях'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст = ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям();
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='выключение учета себестоимости по назначениям';uk='вимкнення обліку собівартості по призначеннях'",ОбщегоНазначения.КодОсновногоЯзыка()));
 		
 	Запрос.Текст = ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='выключение учета выручки и себестоимости продаж по назначениям';uk='вимикання обліку виторгу і собівартості продажів по призначеннях'",ОбщегоНазначения.КодОсновногоЯзыка()));
 		
 	#КонецОбласти
 		
 	#Область ВключениеУчетаСебестоимостиПоВидамЗапасов
 	
 	РасшифровкаКодовОшибок = Новый Соответствие;	
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Включение учета себестоимости по видам запасов';uk='Включення обліку собівартості по видах запасів'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов();
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='включение учета себестоимости по видам запасов';uk='включення обліку собівартості по видах запасів'",ОбщегоНазначения.КодОсновногоЯзыка()));
 		
 	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов();
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='включение учета выручки и себестоимости продаж по видам запасов';uk='включення обліку виторгу і собівартості продажів по видах запасів'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти
 	
 	
 	#Область КорректировкаПриобретения
 	
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Не правильный вид движения для документа ""Корректировка приобретения"".';uk='Не правильний вид руху для документа ""Коригування придбання"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	// Условия проверок соответствуют функции ТекстЗапросаВтВозвратыПоставщикам() в модуле менеджера документа "КорректировкаПриобретения".
 	Запрос.Текст = "
 	|ВЫБРАТЬ
 	|	ТаблицаРасхождения.Ссылка КАК Ссылка,
 	|	ТаблицаРасхождения.Ссылка.Организация КАК Организация,
 	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
 	|	(ВЫБОР
 	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
 	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ) КАК АналитикаУчетаСебестоимости,
 	|	ТаблицаРасхождения.ВидЗапасов,
 	|	(ВЫБОР
 	|		КОГДА ТаблицаРасхождения.Ссылка.Организация В (&ОрганизацииСФИФОСкользящая)
 	|			ТОГДА Приобретение.Ссылка
 	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК Партия,
 	|	СУММА(ТаблицаРасхождения.Количество) КАК Количество
 	|ПОМЕСТИТЬ ВтКорректировкиПриобретения
 	|ИЗ
 	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
 	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Приобретение
 	|		ПО Приобретение.Ссылка = ТаблицаРасхождения.Ссылка.ДокументОснование
 	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
 	|		ПО Аналитика.Ссылка = ТаблицаРасхождения.АналитикаУчетаНоменклатуры
 	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
 	|		ПО АналитикаБезНазначения.Номенклатура = Аналитика.Номенклатура
 	|		И АналитикаБезНазначения.Характеристика = Аналитика.Характеристика
 	|		И АналитикаБезНазначения.Серия = Аналитика.Серия
 	|		И АналитикаБезНазначения.МестоХранения = Аналитика.МестоХранения
 	|		И АналитикаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
 	//++ НЕ УТ
 	|		И АналитикаБезНазначения.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
 	//-- НЕ УТ
 	|ГДЕ
 	|	ТаблицаРасхождения.Ссылка.Проведен
 	|	И ТаблицаРасхождения.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И ТаблицаРасхождения.Ссылка.Организация В (&МассивОрганизаций)
 	|	И Приобретение.Дата < &НачалоПериода
 	|	И ТаблицаРасхождения.ВариантОтражения В (
 	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки),
 	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации))
 	|СГРУППИРОВАТЬ ПО
 	|	ТаблицаРасхождения.Ссылка,
 	|	ТаблицаРасхождения.Ссылка.Организация,
 	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
 	|	(ВЫБОР
 	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
 	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ),
 	|	(ВЫБОР
 	|		КОГДА ТаблицаРасхождения.Ссылка.Организация В (&ОрганизацииСФИФОСкользящая)
 	|			ТОГДА Приобретение.Ссылка
 	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
 	|	ТаблицаРасхождения.ВидЗапасов
 	|;
 	|ВЫБРАТЬ
 	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
 	|	ОстаткиТоваров.ВидЗапасов,
 	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
 	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиТоваров
 	|ИЗ
 	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаНачалоПериода,
 	|		НЕ Организация В (&ОрганизацииСФИФОСкользящая)
 	|		И (АналитикаУчетаНоменклатуры, Организация, ВидЗапасов) В (
 	|			ВЫБРАТЬ
 	|				Корректировки.АналитикаУчетаНоменклатуры,
 	|				Корректировки.Организация,
 	|               Корректировки.ВидЗапасов
 	|			ИЗ
 	|				ВтКорректировкиПриобретения КАК Корректировки
 	|		)
 	|	) КАК ОстаткиТоваров
 	|;
 	|ВЫБРАТЬ
 	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
 	|	ОстаткиТоваров.ВидЗапасов,
 	|	ОстаткиТоваров.Партия,
 	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
 	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиСебестоимости
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаНачалоПериода,
 	|		Организация В (&ОрганизацииСФИФОСкользящая)
 	|		И (АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, Партия) В (
 	|			ВЫБРАТЬ
 	|				Корректировки.АналитикаУчетаСебестоимости,
 	|               Корректировки.ВидЗапасов,
 	|				Корректировки.Организация,
 	|				Корректировки.Партия
 	|			ИЗ
 	|				ВтКорректировкиПриобретения КАК Корректировки
 	|		)
 	|	) КАК ОстаткиТоваров
 	|;
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Себестоимость.Регистратор,
 	|	Корректировки.АналитикаУчетаНоменклатуры,
 	|	Себестоимость.ВидЗапасов
 	|ПОМЕСТИТЬ ВтВозвратыПоставщикам
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
 	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКорректировкиПриобретения КАК Корректировки
 	|		ПО Корректировки.Ссылка = Себестоимость.Регистратор
 	|		И Корректировки.ВидЗапасов = Себестоимость.ВидЗапасов
 	|		И Корректировки.АналитикаУчетаСебестоимости = Себестоимость.АналитикаУчетаНоменклатуры
 	|ГДЕ
 	|	Себестоимость.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Себестоимость.Активность
 	|	И Себестоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
 	|	И Себестоимость.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику)
 	|;
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	""СебестоимостьТоваров""  КАК ИмяРегистра,
 	|	Корректировки.Ссылка 	  КАК Ссылка,
 	|	Корректировки.Организация КАК Организация,
 	|	1 						  КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	ВтКорректировкиПриобретения КАК Корректировки
 	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВозвратыПоставщикам КАК Возвраты
 	|		ПО Возвраты.Регистратор = Корректировки.Ссылка
 	|		И Возвраты.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
 	|		И Возвраты.ВидЗапасов = Корректировки.ВидЗапасов
 	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиТоваров КАК ОстаткиТоваров
 	|		ПО ОстаткиТоваров.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
 	|		И ОстаткиТоваров.ВидЗапасов = Корректировки.ВидЗапасов
 	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиСебестоимости КАК ОстаткиСебестоимости
 	|		ПО ОстаткиСебестоимости.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
 	|		И ОстаткиСебестоимости.ВидЗапасов = Корректировки.ВидЗапасов
 	|		И ОстаткиСебестоимости.Партия = Корректировки.Партия
 	|ГДЕ
 	|	Возвраты.Регистратор ЕСТЬ NULL
 	|	И (НЕ Корректировки.Организация В (&ОрганизацииСФИФОСкользящая) И ЕСТЬNULL(ОстаткиТоваров.Количество, 0) <= -Корректировки.Количество
 	|	ИЛИ Корректировки.Организация В (&ОрганизацииСФИФОСкользящая) И ЕСТЬNULL(ОстаткиСебестоимости.Количество, 0) <= -Корректировки.Количество)
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|ВЫБРАТЬ ПЕРВЫЕ 0
 	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
 	|	Движения.Регистратор	 КАК Ссылка,
 	|	Движения.Организация 	 КАК Организация,
 	|	1 						 КАК КодОшибки
 	|ИЗ
 	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
 	|";

 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='исправление движений документа ""Корректировка приобретения""';uk='виправлення рухів документа ""Коригування придбання""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти
 	
 	#Область ПрочаяВыручка
 	
 	// Документы, у которых ранее были движения по регистру "Прочие расходы", теперь должны формировать движения по регистру "Прочая выручка":
 	// - "Реализация услуг и прочих активов"
 	// - "Корректировка реализации", у которой документ - основание "Реализация услуг и прочих активов"
 	// - "Начисления по кредитам и депозитам"
 	// - "Отражение зарплаты в финансовом учете"  
 	
 	РасшифровкаКодовОшибок = Новый Соответствие;
 	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Восстановление движений по регистру ""Прочая выручка"".';uk='Відновлення рухів за регістром ""Інший виторг"".'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	Запрос.Текст = "
 	|ВЫБРАТЬ РАЗЛИЧНЫЕ
 	|	Т.Регистратор     КАК Регистратор,
 	|	Т.Организация     КАК Организация
 	|ПОМЕСТИТЬ ВТРегистраторыСПрочимиДоходами
 	|ИЗ
 	|	РегистрНакопления.ПрочиеДоходы КАК Т
 	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
 	|		ПО Корректировка.Ссылка = Т.Регистратор
 	|ГДЕ
 	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
 	|	И Т.Организация В (&МассивОрганизаций)
 	|	И Т.Активность
 	|	И НЕ Т.РасчетСебестоимости
 	|	И (ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.РеализацияУслугПрочихАктивов)
 	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.КорректировкаРеализации)
 	|			И ТИПЗНАЧЕНИЯ(Корректировка.ДокументОснование) = ТИП(Документ.РеализацияУслугПрочихАктивов)
 	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.НачисленияКредитовИДепозитов)
 	//++ Локализация
	//++ НЕ УТ
	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтражениеЗарплатыВФинансовомУчете)
	//-- НЕ УТ
 	//-- Локализация
 	|		)
 	|;
 	|ВЫБРАТЬ
 	|	""ПрочаяВыручка"" КАК ИмяРегистра,
 	|	Т.Регистратор     КАК Ссылка,
 	|	Т.Организация     КАК Организация,
 	|	1                 КАК КодОшибки
 	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
 	|ИЗ
 	|	ВТРегистраторыСПрочимиДоходами КАК Т
 	|
 	|ОБЪЕДИНИТЬ ВСЕ
 	|
 	|ВЫБРАТЬ
 	|	""ПрочиеДоходы""  КАК ИмяРегистра,
 	|	Т.Регистратор     КАК Ссылка,
 	|	Т.Организация     КАК Организация,
 	|	1                 КАК КодОшибки
 	|ИЗ
 	|	ВТРегистраторыСПрочимиДоходами КАК Т
 	|;
 	|УНИЧТОЖИТЬ ВТРегистраторыСПрочимиДоходами
 	|";
 	
 	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
 		ПараметрыРасчета,
 		Запрос,
 		РасшифровкаКодовОшибок,
 		НСтр("ru='восстановление движений по регистру ""Прочая выручка""';uk='відновлення рухів за регістром ""Інший виторг""'",ОбщегоНазначения.КодОсновногоЯзыка()));
 	
 	#КонецОбласти
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
КонецПроцедуры

// Возвращает текст запроса, позволяющего получить список документов
// с некорректными (задублированными) движениями по выручке и себестоимости продаж,
// появившимися в результате ошибки 00-00071689.
//
Функция ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Регистратор 					КАК Ссылка,
		|	Аналитики.Организация			КАК Организация,
		|	1 								КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО Т.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И Т.Количество = 0
		|	И Т.Стоимость <> 0
		|	И НЕ Т.РасчетСебестоимости
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И НЕ Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРеализации";
КонецФункции

Процедура ОчиститьДанныеУпрРегистров(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ОчиститьДанныеУправленческихРегистров");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор
		|ИЗ
		|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Организация В(&МассивОрганизаций)
		|	И Т.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		ПараметрыЗаписи = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыЗаписи(
			ПараметрыРасчета.РасчетныйПериод.НачалоПериода,
			ПараметрыРасчета.ОграниченияВыборки.КоличествоЗаписейВНЗ,
			ПараметрыРасчета.НомерЗаданияДоРасчета);
	
		Попытка
			РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
				Выборка,
				РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы,
				ПараметрыЗаписи);
		Исключение
			
			// Информацию об ошибке добавим в протокол расчета, в механизме закрытия месяца ошибки уже должны быть зарегистрированы.
			ТекстДляПротокола = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
				ПараметрыРасчета,
				Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаЗаписиДвиженийПоРегистрам,
				ТекстДляПротокола);
				
			ВызватьИсключение ТекстДляПротокола;
			
		КонецПопытки;
		
		РасчетСебестоимостиПротоколРасчета.ДополнительнаяИнформация(
			ПараметрыРасчета,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Очищено движений по регистру %1: %2';uk='Очищено рухів по регістру %1: %2'",ОбщегоНазначения.КодОсновногоЯзыка()),
				Метаданные.РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.ПолноеИмя(),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Количество())));
		
		
	КонецЕсли;
	
КонецПроцедуры

#Область ВключениеУчетаСебестоимостиПоВидамЗапасов

// Выбираем документы, в движениях которых не заполнен вид запасов, кроме исключений.
// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
//
// Движения, для которых не заполняется вид запасов в учете себестоимости товаров:
// - в аналитике учета номенклатуры указана номенклатура с типом номенклатуры "Работа"
// - товары, выкупленные у комитентов
//		раздел учета "Товары на складах", в аналитике учета номенклатуры тип места хранения "Партнер" или "Организация"
// - продукция, отраженная отчетами переработчика
//		раздел учета "Производственные затраты", в аналитике учета номенклатуры тип места хранения "Партнер"
// - работы к оформлению отчетов давальцу
//		движения "Приход" по разделу учета "Производственные затраты" с заполненным реквизитом "Кор. аналитика учета номенклатуры", 
//		хозяйственной операцией "Выпуск продукции", и типом записи "Партия"
// - продукция, реализованная документом "Отчет давальцу"
//		движения с хозяйственной операцией "Отчет давальцу"
//
// Дополнительные исключения для выручки и себестоимости продаж:
// - в типе запасов указано значение "Агентская услуга"
//	(в аналитике учета номенклатуры указана номенклатура с типом номенклатуры "Услуга. Выполняется партнером")
//
// Исключения для документов производства 2.1:	
// - вид запасов в движениях заполняется оффлайн: раздел учета "Производственные затраты" и тип записи "Потребление (производство)"
// - для документа "Перемещение материалов в производстве" вид запасов в движениях заполняется оффлайн
//
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		// Товары, выкупленные у комитентов, или отраженные отчетами переработчика
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета В (
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
		|		И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.ТипМестаХранения В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))
		//++ НЕ УТ
		// Возвратные отходы, отраженные отчетом переработчика.
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
		|		И СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
		|		И СебестоимостьТоваров.ХозяйственнаяОперация В
		|			(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
		|		И СебестоимостьТоваров.Количество < 0
		|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) = ТИП(Документ.ОтчетПереработчика))
		//-- НЕ УТ
		//++ Устарело_Производство21
		//++ НЕ УТ
		// Для документов производства 2.1 вид запасов в движениях заполняется оффлайн:
		// раздел учета "Производственные затраты" и тип записи "Потребление (производство)"
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|		И СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПотреблениеПроизводство))
		// Для документа "Перемещение материалов в производстве" вид запасов в движениях заполняется оффлайн.
		|	И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) <> ТИП(Документ.ПеремещениеМатериаловВПроизводстве)
		//-- НЕ УТ
		//-- Устарело_Производство21
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
		// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""		КАК ИмяРегистра,
		|	Исправления.Ссылка				КАК Ссылка,
		|	Исправления.Ссылка.Организация	КАК Организация,
		|	1								КАК КодОшибки
		|ИЗ
		|	Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций.Исправления КАК Исправления
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Период = Исправления.Ссылка.Дата
		|		И СебестоимостьТоваров.Регистратор = Исправления.Ссылка
		|ГДЕ
		|	Исправления.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Исправления.Ссылка.Организация В(&МассивОрганизаций)
		|	И Исправления.ВидЗапасовСписания <> Исправления.ВидЗапасовОприходования
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|";
КонецФункции

Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И ВыручкаИСебестоимостьПродаж.Количество <> 0
		|	И НЕ ВыручкаИСебестоимостьПродаж.ТипЗапасов В (
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга))
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

#КонецОбласти

#Область ВыключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры указано назначение.
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
Функция ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

Функция ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

#КонецОбласти

#Область ВключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
// а в движении учета себестоимости не указано назначение.
// Используются данные следующих оперативных регистров:
//	- Товары организаций
//		кроме таможенных деклараций с нулевыми суммами пошлины, акциза и НДС
//	- Товары организаций к передаче
//	- Товары переданные переработчику
//	- Товары полученные от переработчика
//	- Товары к оформлению таможенных деклараций (движения "Приход)
//	- Материалы и работы в производстве (учитываются работы при партионном учете версии 2.2,
//		при партионном учете 2.1 не используется одновременно с онлайн движениями по регистру "Себестоимость товаров")
//		исключаются движения у документов "Маршрутный лист производства", "Списание затрат на выпуск", "Распределение материалов и работ",
//		используемых для производства 2.1, т.к. эти документы не делают онлайн движений по регистру "Себестоимость товаров" 
//		одновременно с регистром "Материалы и работы в производстве".
// Дополнительно к оперативным регистрам выбираем данные непосредственно из документов (у этих документов нет оперативных регистров):
//	- Отчет давальцу
//	- Ввод остатков с выключенным флажком "ОУ"
// Исключения:
//	- По товарам, переданным на комиссию, учет по назначениям не ведется
//	- По товаром к оформлению отчетов комитенту учет по назначениям не ведется
//	- По товарам, отгруженным без перехода права собственности, учет по назначениям не ведется
//	- Для движений себестоимости по разделу учета "Незавершенное производство" нет соответствующих оперативных регистров.
//		Но этот раздел учета всегда корреспондирует с другими разделами, которые имеют соответствующие оперативные регистры.
//		Поэтому движения по разделу "Незавершенное производстве" не проверяем. Они будут переформированы одновременно с другими движениями.
//	- Движения документов "Распределение материалов и работ" при распределении материалов по правилу
//	- Движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
// 		У таких документов не будет движений по регистру "Себестоимость товаров".
//	- Документы корректировки регистров. У таких документов могут быть движения только по оперативным регистрам.
//	- Документы ввода начальных остатков, у которых снят флажок "Себестоимость" (у таких документов нет движений по учету себестоимости)
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		//++ НЕ УТ
		|	И Ключи.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		//-- НЕ УТ
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
        // Выбираем таможенные декларации на импорт, в которых указаны нулевые суммы таможенной пошлины, акциза и НДС.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Ссылка,
		|	Строки.АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ ДекларацииСНулевымиСуммами
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = Строки.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И Строки.СуммаПошлины = 0
        |	И Строки.СуммаАкциза = 0
		|	И Строки.СуммаНДС = 0
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
        // Исключаем таможенные декларации с нулевыми суммами пошлины, акциза и НДС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДекларацииСНулевымиСуммами КАК Декларации
		|			ПО Декларации.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Декларации.АналитикаУчетаНоменклатуры = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем движения документов производства 2.2, для которых нет онлайн движений по регистру "Себестоимость товаров"
		|		И ТоварыОрганизаций.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства)
		// Исключаем движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
		// У таких документов не будет движений по регистру "Себестоимость товаров".
		|		И НЕ (
		|			ТоварыОрганизаций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбособленногоУчета)
		|			И ТоварыОрганизаций.ВидЗапасов = ТоварыОрганизаций.КорВидЗапасов)
        // Исключаем таможенные декларации с нулевыми суммами пошлины, акциза и НДС
		|		И Декларации.Ссылка ЕСТЬ NULL
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизацийКПередаче.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|
		//++ НЕ УТ
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыПереданныеПереработчику.Регистратор КАК Регистратор,
		|		ТоварыПереданныеПереработчику.ВидЗапасов.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыПереданныеПереработчику КАК ТоварыПереданныеПереработчику
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыПереданныеПереработчику.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыПереданныеПереработчику.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыПереданныеПереработчику.ВидЗапасов.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыПереданныеПереработчику.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыПереданныеПереработчику.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыПереданныеПереработчику.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыПолученныеОтПереработчика.Регистратор КАК Регистратор,
		|		ТоварыПолученныеОтПереработчика.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыПолученныеОтПереработчика КАК ТоварыПолученныеОтПереработчика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыПолученныеОтПереработчика.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыПолученныеОтПереработчика.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыПолученныеОтПереработчика.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыПолученныеОтПереработчика.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		//-- НЕ УТ
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыКОформлениюТаможенныхДеклараций.Регистратор КАК Регистратор,
		|		ТоварыКОформлениюТаможенныхДеклараций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыКОформлениюТаможенныхДеклараций КАК ТоварыКОформлениюТаможенныхДеклараций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыКОформлениюТаможенныхДеклараций.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыКОформлениюТаможенныхДеклараций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыКОформлениюТаможенныхДеклараций.Организация В(&МассивОрганизаций)
		|		И ТоварыКОформлениюТаможенныхДеклараций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ТИПЗНАЧЕНИЯ(ТоварыКОформлениюТаможенныхДеклараций.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И МатериалыИРаботыВПроизводстве.Количество <> 0
		|		И ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		//++ НЕ УТ
		// Кроме документов "Распределение материалов и работ". Они делают движения по регистру "Материалы и работы в производстве",
		// но не всегда имеют движения по регистру "Себестоимость товаров".
		|		И ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) <> ТИП(Документ.РаспределениеПроизводственныхЗатрат)
		//++ Устарело_Производство21
		// При использовании производства 2.1 документы "Маршрутный лист производства", "Списание затрат на выпуск" и "Распределение материалов и работ"
		// не имеют онлайн движений по регистру "Себестоимость товаров".
		|		И ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) <> ТИП(Документ.СписаниеЗатратНаВыпуск)
		//-- Устарело_Производство21
		//-- НЕ УТ
		|
		|
		//++ Локализация
		//-- Локализация
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатков.Товары КАК ВводОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ВводОстатков.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатковТоваров.Товары КАК ВводОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ВводОстатков.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по себестоимости указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСНазначениями.Организация = СебестоимостьТоваров.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движениях учета себестоимости не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		//++ НЕ УТ
		|	И Ключи.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		//-- НЕ УТ
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыПродаж
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В (&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|;
		// Выбираем документы продаж, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизаций.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизаций.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизацийКПередаче.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = МатериалыИРаботыВПроизводстве.Регистратор
		|			И ДокументыПродаж.Организация = МатериалыИРаботыВПроизводстве.Организация
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по выручке и себестоимости продаж указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = ВыручкаИСебестоимостьПродаж.Регистратор
		|		И ДокументыСНазначениями.Организация = Аналитики.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движении выручки и себестоимости продаж не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""		КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДокументыПродаж;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

#КонецОбласти

#КонецОбласти