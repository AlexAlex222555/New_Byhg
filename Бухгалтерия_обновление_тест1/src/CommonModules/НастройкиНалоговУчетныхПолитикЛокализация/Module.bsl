#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СистемаНалогообложенияОрганизации(Организация, ДатаДействия = Неопределено, ПоУмолчанию = Истина) Экспорт
    
    СистемаНалогообложения = Перечисления.СистемыНалогообложения.НалогНаПрибыльИНДС;
	Если ТипЗнч(Организация) = Тип("Массив") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	//++ Локализация
	Если НЕ ЗначениеЗаполнено(ДатаДействия) Тогда
		ДатаДействия = ТекущаяДатаСеанса();
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период,
    |		Организация = &Организация) КАК ТаблицаСрезПоследних
	|";
	Запрос.УстановитьПараметр("Период", ДатаДействия);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СистемаНалогообложения = Выборка.СистемаНалогообложения;
	ИначеЕсли Не ПоУмолчанию Тогда
		СистемаНалогообложения = Неопределено;
	КонецЕсли;
	//-- Локализация
	Возврат СистемаНалогообложения;
	
КонецФункции

Процедура ДополнитьДоступныеПараметрыНалоговУчетныхПолитик(Список, Знач Организации, Дата = Неопределено) Экспорт
	//++ Локализация
	ПоВсемОрганизациям = Ложь;
	СистемаНалогообложенияОрганизации = Неопределено;
    ПлательщикНалогаНаПрибыль  = Ложь;
    ПлательщикНДС              = Ложь;
    ПлательщикЕН               = Ложь;

	Если ТипЗнч(Организации) = Тип("Массив")
		ИЛИ ТипЗнч(Организации) = Тип("СписокЗначений") Тогда
		ПоВсемОрганизациям = Истина;
		Если Организации.Количество() = 1 Тогда
			Организации = Организации[0];
		КонецЕсли;
	КонецЕсли;
		
	Если ТипЗнч(Организации) = Тип("СправочникСсылка.Организации") Тогда
		СистемаНалогообложенияОрганизации = НастройкиНалоговУчетныхПолитикПовтИсп.СистемаНалогообложенияОрганизации(Организации, Дата, Ложь);
        ПлательщикНалогаНаПрибыль  = (СистемаНалогообложенияОрганизации = Перечисления.СистемыНалогообложения.НалогНаПрибыль) ИЛИ (СистемаНалогообложенияОрганизации = Перечисления.СистемыНалогообложения.НалогНаПрибыльИНДС);
        ПлательщикНДС              = (СистемаНалогообложенияОрганизации = Перечисления.СистемыНалогообложения.НалогНаПрибыльИНДС) ИЛИ (СистемаНалогообложенияОрганизации = Перечисления.СистемыНалогообложения.ЕдиныйНалогИНДС);
        ПлательщикЕН               = (СистемаНалогообложенияОрганизации = Перечисления.СистемыНалогообложения.ЕдиныйНалог) ИЛИ (СистемаНалогообложенияОрганизации = Перечисления.СистемыНалогообложения.ЕдиныйНалогИНДС);
	КонецЕсли;
	
	Список.Добавить("НастройкиСистемыНалогообложения");
	Если ЗначениеЗаполнено(СистемаНалогообложенияОрганизации)
		Или ПоВсемОрганизациям Тогда
        
        //++ НЕ УТ
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
			Список.Добавить("УчетнаяПолитикаБухУчета");
		КонецЕсли;
		//-- НЕ УТ
		Если ПлательщикЕН Или ПоВсемОрганизациям Тогда
			Список.Добавить("НастройкиУчетаЕН");
		КонецЕсли;
		Если ПлательщикНалогаНаПрибыль Или ПоВсемОрганизациям Тогда
			//++ НЕ УТ
			Список.Добавить("НастройкиУчетаНалогаНаПрибыль");
			//-- НЕ УТ
        КонецЕсли;
		Если ПлательщикНДС Или ПоВсемОрганизациям Тогда
			Список.Добавить("НастройкиУчетаНДС");
		КонецЕсли;
		Если НЕ ПлательщикНДС И НЕ ПоВсемОрганизациям Тогда
			
			Индекс = Список.Найти("НастройкиУчетаНДС");
			Если Индекс <> Неопределено Тогда
				Список.Удалить(Индекс);
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
	//-- Локализация
КонецПроцедуры

Процедура ТекстыЗапросовПолученияСтатистикиУчетнойПолитики(ТекстыЗапросов) Экспорт
	
	//++ Локализация
	//Регистр НастройкиСистемыНалогообложения
    ПоказателиСтатистики = "СистемаНалогообложения";
	НастройкиНалоговУчетныхПолитик.ДополнитьТекстыЗапросаПоказателейСтатистики("НастройкиСистемыНалогообложения", ПоказателиСтатистики, ТекстыЗапросов);
	//НастройкиУчетаНДС
    ПоказателиСтатистики = "РаздельныйУчетТоваровПоНалогообложениюНДС";
	НастройкиНалоговУчетныхПолитик.ДополнитьТекстыЗапросаПоказателейСтатистики("НастройкиСистемыНалогообложения", ПоказателиСтатистики, ТекстыЗапросов);
	//++ НЕ УТ
	//УчетнаяПолитикаБухУчета

	ПоказателиСтатистики = "ИспользуемыеКлассыСчетовРасходов,
							|СборкаТоваровЧерезСчет23,
							|ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли,
							|ПроводкиПоРаботникам";

	НастройкиНалоговУчетныхПолитик.ДополнитьТекстыЗапросаПоказателейСтатистики("УчетнаяПолитикаБухУчета", ПоказателиСтатистики, ТекстыЗапросов);
	//-- НЕ УТ
	
	//НастройкиУчетаНалогаНаПрибыль

	ПоказателиСтатистики = "НалогНаПрибыльБезКорректировокФинансовогоРезультата";

	НастройкиНалоговУчетныхПолитик.ДополнитьТекстыЗапросаПоказателейСтатистики("НастройкиУчетаНалогаНаПрибыль", ПоказателиСтатистики, ТекстыЗапросов);
	
	//НастройкиУчетаЕН
	ПоказателиСтатистики = "ГруппаПлательщикаЕдиногоНалога";
	НастройкиНалоговУчетныхПолитик.ДополнитьТекстыЗапросаПоказателейСтатистики("НастройкиУчетаЕН", ПоказателиСтатистики, ТекстыЗапросов);

	//-- Локализация
	
КонецПроцедуры

// Функция - агрегатор сведений о настройках налогового учета на определенную дату
//
// Параметры:
//  Период       - Дата - дата чтения настроек.
//  Организация  - СправочникСсылка.Организации - организация,
//                 для которой требуется получить настройки
// 
// Возвращаемые значения:
//  Неопределено, ТаблицаЗначений - если нет ни одной записи настроек учета неопределено,
//                                  иначе таблица
Функция УчетнаяПолитикаНалоговыйУчет(Период, Организация) Экспорт
	//++ НЕ УТ
	//++ Локализация
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Организации", Организация);
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
    |	СпрОрганизации.Ссылка КАК Организация
	|ПОМЕСТИТЬ
	|	ВтГоловныеОрганизации
	|ИЗ
	|	Справочник.Организации КАК СпрОрганизации
	|ГДЕ
	|	СпрОрганизации.Ссылка В (&Организации)
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация;
	|
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложенияСрезПоследних.Период КАК Период,
    |	ГоловныеОрганизации.Организация КАК Организация,
	|	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
    |	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикНДС КАК ПлательщикНДС,
    |	НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикЕН КАК ПлательщикЕН
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК НастройкиСистемыНалогообложенияСрезПоследних
	|		ПО НастройкиСистемыНалогообложенияСрезПоследних.Организация = ГоловныеОрганизации.Организация
    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаЕН.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК НастройкиУчетаЕНСрезПоследних
    |		ПО НастройкиСистемыНалогообложенияСрезПоследних.Организация = ГоловныеОрганизации.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК УчетнаяПолитикаБухУчетаСрезПоследних
	|		ПО УчетнаяПолитикаБухУчетаСрезПоследних.Организация = ГоловныеОрганизации.Организация";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат Результат.Выгрузить();
	КонецЕсли;
	//-- Локализация
	//-- НЕ УТ
	Возврат Неопределено;
КонецФункции

// Возвращает признак использования раздельного учета товаров по налогообложению НДС. 
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить признак учетной политики
// 	Период 		- Дата, Неопределено - Дата действия учетной политики, если Неопределено, то на текущую дату
//
// Возвращаемое значение:
// 	Результат - Булево - Значение признака
// 
Функция РаздельныйУчетТоваровПоНалогообложениюНДС(Организация, Период = Неопределено) Экспорт
	
	РаздельныйУчетТоваровПоНалогообложениюНДС = Ложь;
	
	//++ Локализация
	ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик("НастройкиСистемыНалогообложения",
		Организация,
		Период);
		
	РаздельныйУчетТоваровПоНалогообложениюНДС = ?(ПараметрыПолитики <> Неопределено, ПараметрыПолитики.РаздельныйУчетТоваровПоНалогообложениюНДС, Ложь);
	//-- Локализация
	Возврат РаздельныйУчетТоваровПоНалогообложениюНДС;
	
КонецФункции

Процедура ПрочитатьЗаписьРегистра_УчетнаяПолитикаФинУчета(Форма) Экспорт
	
	//++ Локализация
	//-- Локализация

КонецПроцедуры

Процедура НастройкиУчетаНДСПередЗаписью(СтрокаНабора) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры
 
#КонецОбласти
