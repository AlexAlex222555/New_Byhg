////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Амортизация ОС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.<Имя документа> - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	Возврат ТекстОтраженияВРеглУчетеУКР();
	//-- Локализация
	
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	Возврат ТекстЗапросаВТОтраженияВРеглУчетеУКР();
	//-- Локализация
	
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ
//++ НЕ УТ
#Область ПроводкиРегУчетаУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеУКР()
	
	ТекстЗапроса = "";
	//++ Локализация

	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаНачислениеАмортизацииОС(СписокЗапросов);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	//-- Локализация
	
	Возврат ТекстЗапроса;

	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР()
	
	ТекстЗапроса = "";

	//++ Локализация

	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаВТНачислениеАмортизацииОС(СписокЗапросов);
	
	СписокЗапросов.Добавить(Символы.ПС);

	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;

	
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры);

	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете_Амортизация(Запрос, ТекстыЗапроса, Регистры);

	//-- Локализация
	
КонецПроцедуры

Процедура СформироватьЗаданияКЗакрытиюМесяца(Объект) Экспорт

	//++ Локализация
	//-- Локализация
	
КонецПроцедуры
 
#КонецОбласти

#Область ФормаДокумента

Процедура ПриСозданииНаСервере(Форма) Экспорт

	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ФормаРасчетАмортизации

Процедура ДополнитьСлужебныеПараметрыФормыРасчетАмортизации(Знач СлужебныеПараметры) Экспорт

	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область РасчетАмортизации

Процедура ПослеРасчетаАмортизации(ИсходныеПараметрыРасчета, МенеджерВременныхТаблиц) Экспорт

	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы) Экспорт

	//++ Локализация
	#Область ВТ_СписокОбъектовУчета_БУ

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                   КАК НачалоМесяца,
	|	ПорядокУчетаОС.Организация      КАК Организация,
	|	ПорядокУчетаОС.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПорядокУчетаОС.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ПорядокУчетаОС.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|
	|	ПорядокУчетаОС.СпособОтраженияРасходовБУ КАК СпособОтраженияРасходовБУ,
	|	ПорядокУчетаОС.СпособОтраженияРасходовНУ КАК СпособОтраженияРасходовНУ,
	|
	|	ПорядокУчетаОС.СтатьяРасходовБУ    КАК СтатьяРасходовБУ,
	|	ПорядокУчетаОС.АналитикаРасходовБУ КАК АналитикаРасходовБУ,
	|	ПорядокУчетаОС.СтатьяРасходовНУ    КАК СтатьяРасходовНУ,
	|	ПорядокУчетаОС.АналитикаРасходовНУ КАК АналитикаРасходовНУ
	|
	|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_БУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&КонецПредыдущегоМесяца,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.Организация,
	|					ПакетыАмортизации.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаОС
	|ГДЕ
	|	(ПорядокУчетаОС.НачислятьАмортизациюБУ
	|			ИЛИ ПорядокУчетаОС.НачислятьАмортизациюНУ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ГоловнаяОрганизация,
	|	Организация";

	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_БУ", "ВТ_СписокОбъектовУчета_БУ" + ПрефиксТаблицы);
	КонецЕсли; 

	СписокЗапросов.Добавить(ТекстЗапроса);

	#КонецОбласти

	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаПараметрыРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов) Экспорт

	//++ Локализация
	
	ОбъектУчета = "ОсновноеСредство";
		
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПараметрыАмортизацииОСБУ", "", Ложь, ОбъектУчета, СписокЗапросов);
		
	
	#Область ГрафикиАмортизацииОС

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ГрафикиАмортизацииОС.Организация       КАК Организация,
	|	ГрафикиАмортизацииОС.ОсновноеСредство  КАК ОсновноеСредство,
	|	ГрафикиАмортизацииОС.ГрафикАмортизации КАК ГрафикАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА ГодовыеГрафикиАмортизацииОС.Коэффициент1 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент2 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент3 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент4 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент5 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент6 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент7 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент8 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент9 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент10 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент11 
	|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент12 <> 0
	|		ТОГДА 
	|			ВЫБОР МЕСЯЦ(&НачалоМесяца)
	|				КОГДА 1 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент1, 0)
	|				КОГДА 2 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент2, 0) 
	|				КОГДА 3 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент3, 0)
	|				КОГДА 4 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент4, 0) 
	|				КОГДА 5 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент5, 0)
	|				КОГДА 6 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент6, 0)
	|				КОГДА 7 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент7, 0)
	|				КОГДА 8 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент8, 0)
	|				КОГДА 9 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент9, 0)
	|				КОГДА 10 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент10, 0)
	|				КОГДА 11 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент11, 0)
	|				КОГДА 12 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент12, 0)
	|			КОНЕЦ / (ГодовыеГрафикиАмортизацииОС.Коэффициент1 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент2 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент3 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент4 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент5 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент6 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент7 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент8 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент9 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент10 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент11 
	|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент12)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентПериода
	|
	|ПОМЕСТИТЬ ВТ_ГрафикиАмортизацииОС
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|			&КонецПредыдущегоМесяца,
	|			(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						СписокОС.Организация,
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						ВТ_СписокОбъектовУчета_БУ КАК СписокОС)) КАК ГрафикиАмортизацииОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГодовыеГрафикиАмортизацииОС КАК ГодовыеГрафикиАмортизацииОС
	|		ПО ГодовыеГрафикиАмортизацииОС.Ссылка = ГрафикиАмортизацииОС.ГрафикАмортизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация";

	СписокЗапросов.Добавить(ТекстЗапроса);

	#КонецОбласти

	#Область СписокОССПараметрами_БУ

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.Организация,
	|
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки,
	|	ПорядокУчетаОС.ОбъемНаработки,
	|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	СписокОС.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	СписокОС.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|
	|	СписокОС.СтатьяРасходовБУ,
	|	СписокОС.АналитикаРасходовБУ,
	|	СписокОС.СтатьяРасходовНУ,
	|	СписокОС.АналитикаРасходовНУ,
	|
	|	СписокОС.СпособОтраженияРасходовБУ,
	|	СписокОС.СпособОтраженияРасходовНУ,
	|
	|	ЕСТЬNULL(ГрафикиАмортизацииОС.ГрафикАмортизации, ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка)) КАК ГрафикАмортизации,
	|	ЕСТЬNULL(ГрафикиАмортизацииОС.КоэффициентПериода, 0) КАК КоэффициентПериода,
	|
	// ЧислоЛетПользования
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4,0))
	|	КОНЕЦ
	|		+ ВЫБОР 
	|			КОГДА (ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 
	|					- ВЫБОР
	|							КОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
	|								ТОГДА 0
	|							ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4,0))
	|						КОНЕЦ) > 0 
	|				ТОГДА 1 
	|			ИНАЧЕ 0 
	|		КОНЕЦ КАК ЧислоЛетПользования,
	|
	// ЧислоПрошедшихЛет
	|	ВЫБОР
	|		КОГДА (&ЧислоПрошедшихЛет / 12) < 1
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(&ЧислоПрошедшихЛет / 12 - 0.5 КАК ЧИСЛО(4,0))
	|	КОНЕЦ КАК ЧислоПрошедшихЛет,
	|
	|
	|	ЕСТЬNULL(СтоимостьОС.Стоимость, 0)     КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) КАК СтоимостьРегл,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)   КАК СтоимостьНУ,
	|	0   КАК СтоимостьПР,
	|	0   КАК СтоимостьВР,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)   КАК СтоимостьЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|	ЕСТЬNULL(СтоимостьОСНаНачалоГода.СтоимостьРегл, 0) 
	|		+ ЕСТЬNULL(СтоимостьОСНаНачалоГода.СтоимостьЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.АмортизацияРегл, 0) 
	|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.АмортизацияЦФ, 0) КАК СтоимостьНаНачалоГода,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)
	|		+ ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) КАК СтоимостьНУНачальныйОстаток,
	|
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)     КАК ОстаточнаяСтоимостьРегл,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)   - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)       КАК ОстаточнаяСтоимостьНУ,
	|	0       КАК ОстаточнаяСтоимостьПР,
	
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)   - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)       КАК ОстаточнаяСтоимостьЦФ,
	|	0     КАК ОстаточнаяСтоимостьНУЦФ,
	|	0     КАК ОстаточнаяСтоимостьПРЦФ,
	|	ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)
	|		+ ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) КАК АмортизацияНУНачальныйОстаток,
	|
	|	МестонахождениеОС.Местонахождение,
	|
	|	ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиБУ, 0) КАК ОбъемВыработки,
	|	ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца, 0) КАК ОбъемВыработкиНаНачалоМесяца,
	|
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ,
	|
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ,
	|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость
	
	|
	|ПОМЕСТИТЬ ВТ_СписокОССПараметрами_БУ
	|ИЗ
	|	ВТ_СписокОбъектовУчета_БУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (СтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоГода КАК СтоимостьОСНаНачалоГода
	|		ПО (СтоимостьОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
	|		ПО (АмортизацияОС.Организация = СписокОС.Организация)
	|			И (АмортизацияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (АмортизацияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоГода КАК АмортизацияОСНаНачалоГода
	|		ПО (АмортизацияОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (МестонахождениеОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|		ПО (ПараметрыАмортизацииОСБУ.Организация = СписокОС.Организация)
	|			И (ПараметрыАмортизацииОСБУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ГрафикиАмортизацииОС КАК ГрафикиАмортизацииОС
	|		ПО (ГрафикиАмортизацииОС.Организация = СписокОС.Организация)
	|			И (ГрафикиАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС КАК ВыработкаОС
	|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
	|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
	|			И (ВыработкаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&КонецМесяца) КАК РегУчетнаяПолитика
	|		ПО РегУчетнаяПолитика.Организация = СписокОС.Организация
	|ГДЕ
	|	(СписокОС.НачислятьАмортизациюБУ
	|		ИЛИ СписокОС.НачислятьАмортизациюНУ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.Организация";
	
	
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, 
		"&ЧислоПрошедшихЛет",
		ВнеоборотныеАктивыСлужебный.ТекстПоляФактическийСрокИспользования("ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ"));
		
		

	СписокЗапросов.Добавить(ТекстЗапроса);
		
	#КонецОбласти

	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаВтСписокОСУменьшаемогоОстатка(ПараметрыРасчета) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСтоимостьИАмортизацияНаНачалоГода(ПараметрыРасчета) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции
 
Процедура ДополнитьТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов) Экспорт

	//++ Локализация
	
		ТекстЗапросаРасчетАмортизации_БУ(СписокЗапросов, ПараметрыРасчета);
	
	//-- Локализация
	
КонецПроцедуры
 
Процедура ДополнитьТекстЗапросаПроверкаПараметровРасчета(ПараметрыРасчета, СписокЗапросов) Экспорт

	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|
	// НеУказанСрокИспользованияБУ
	|	СписокОС.НачислятьАмортизациюБУ
	|		И СписокОС.МетодНачисленияАмортизацииБУ В (
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Кумулятивный))
	|		И СписокОС.СтоимостьДляВычисленияАмортизации <> 0 
	|		И СписокОС.СрокИспользованияДляВычисленияАмортизации = 0 КАК НеУказанСрокИспользованияБУ,
	|
	// НеУказанСрокИспользованияНУ
	|	СписокОС.НачислятьАмортизациюНУ
	|		И СписокОС.МетодНачисленияАмортизацииНУ В (
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Кумулятивный))
	|		И СписокОС.СрокПолезногоИспользованияНУ = 0 КАК НеУказанСрокИспользованияНУ,
	|
	// НеУказанОбъемНаработки
	|	СписокОС.НачислятьАмортизациюБУ
	|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И СписокОС.ОбъемНаработки = 0 КАК НеУказанОбъемНаработки,
	|
	// ОбъемНаработкиВыработан
	|	СписокОС.НачислятьАмортизациюБУ
	|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И (СписокОС.ОбъемНаработки - СписокОС.ОбъемВыработкиНаНачалоМесяца) <= 0 
	|		И СписокОС.ОбъемНаработки <> 0 КАК ОбъемНаработкиВыработан
	|
	|
	|ПОМЕСТИТЬ ВТ_ПроверкаПараметровРасчета_БУ
	|ИЗ
	|	ВТ_СписокОССПараметрами_БУ КАК СписокОС
	|ГДЕ
	|	СписокОС.НачислятьАмортизациюБУ
	|		И СписокОС.МетодНачисленияАмортизацииБУ В (
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Кумулятивный))
	
	|		И СписокОС.СтоимостьДляВычисленияАмортизации <> 0 
	|		И СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
	|
	|	ИЛИ СписокОС.НачислятьАмортизациюНУ
	|		И СписокОС.МетодНачисленияАмортизацииНУ В (
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка),
	|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Кумулятивный))
	|		И СписокОС.СрокПолезногоИспользованияНУ = 0
	
	|
	|	ИЛИ СписокОС.НачислятьАмортизациюБУ
	|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И СписокОС.ОбъемНаработки = 0
	|
	|	ИЛИ СписокОС.НачислятьАмортизациюБУ
	|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И (СписокОС.ОбъемНаработки - СписокОС.ОбъемВыработкиНаНачалоМесяца) <= 0 
	|		И СписокОС.ОбъемНаработки <> 0
	|
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";

	СписокЗапросов.Добавить(ТекстЗапроса);

	//-- Локализация
	
КонецПроцедуры
 
Процедура ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц) Экспорт

	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|	ПроверкаПараметровРасчета.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).Наименование КАК ОбъектУчетаПредставление,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанСрокИспользованияБУ) КАК НеУказанСрокИспользованияБУ,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанСрокИспользованияНУ) КАК НеУказанСрокИспользованияНУ,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанОбъемНаработки) КАК НеУказанОбъемНаработки,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.ОбъемНаработкиВыработан) КАК ОбъемНаработкиВыработан
	
	|ИЗ
	|	ВТ_ПроверкаПараметровРасчета_БУ КАК ПроверкаПараметровРасчета
	|ГДЕ
	|	ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверкаПараметровРасчета.ОбъектУчета,
	|	ПроверкаПараметровРасчета.Организация";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	ШаблонОбъемНаработки         = НСтр("ru='Для основного средства ""%1"" (инв. №%2) предполагаемый объем наработки полностью выработан.""
|	""Если была произведена модернизация после полной выработки ресурса, то необходимо оформить документ ""Изменение параметров ОС"" и увеличить предполагаемый объем наработки или приостановить начисление амортизации в управленческом учете.'
|;uk='Для основного засобу ""%1"" (інв. №%2) передбачуваний обсяг напрацювання повністю вироблений.""
|  ""Якщо була проведена модернізація після повного вироблення ресурсу, то необхідно оформити документ ""Зміна параметрів ОЗ"" і збільшити передбачуваний обсяг напрацювання або призупинити нарахування амортизації в управлінському обліку.'");

	
	ШаблонСрокИспользованияБУ    = НСтр("ru='Для основного средства ""%1"" (инв. №%2) в бухгалтерском учете не определен срок использования, необходимый для вычисления амортизации.""
|	""Если была произведена модернизация после окончания срока использования, то следует оформить документ ""Изменение параметров ОС"" и указать новый срок (первоначальный + срок, на который произошло увеличение после модернизации) или приостановить начисление амортизации.'
|;uk='Для основного засобу "" %1"" (інв. №%2) в бухгалтерському обліку не визначено строк використання, необхідний для обчислення амортизації.""
|  ""Якщо була проведена модернізація після закінчення строку використання, то слід оформити документ ""Зміна параметрів ОЗ"" і вказати новий строк (початковий + строк, на який відбулося збільшення після модернізації) або призупинити нарахування амортизації.'");
										
	ШаблонСрокИспользованияНУ    = НСтр("ru='Для основного средства ""%1"" (инв. №%2) не указан срок использования в налоговом учете.';uk='Для основного засобу ""%1"" (інв. № %2) не зазначено строк використання в податковому обліку.'");

	ШаблонОбъемНаработкиВыработан = НСтр("ru='Для основного средства ""%1"" (инв. №%2) предполагаемый объем наработки полностью выработан.""
|	""Если была произведена модернизация после полной выработки ресурса, то необходимо оформить документ ""Изменение параметров ОС"" и увеличить предполагаемый объем наработки или приостановить начисление амортизации в управленческом учете.'
|;uk='Для основного засобу ""%1"" (інв. №%2) передбачуваний обсяг напрацювання повністю вироблений.""
|  ""Якщо була проведена модернізація після повного вироблення ресурсу, то необхідно оформити документ ""Зміна параметрів ОЗ"" і збільшити передбачуваний обсяг напрацювання або призупинити нарахування амортизації в управлінському обліку.'");
												
	Пока Выборка.Следующий() Цикл
		
		
		Если Выборка.НеУказанОбъемНаработки Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработки, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.ОбъемНаработкиВыработан Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработкиВыработан, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		 
		
		Если Выборка.НеУказанСрокИспользованияНУ Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСрокИспользованияНУ, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
	КонецЦикла;

	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаСписокОбъектовУчета(ПараметрыРасчета) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.НачалоМесяца                        КАК НачалоМесяца,
	|	СписокОС.КонецМесяца                         КАК КонецМесяца,
	|	СписокОС.Организация                         КАК Организация,
	|	СписокОС.ОсновноеСредство                    КАК ОсновноеСредство,
	|	СписокОС.СпособОтраженияРасходовУУ           КАК СпособОтраженияРасходов,
	|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета
	|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета
	|ИЗ
	|	ВТ_СписокОбъектовУчета_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|		ПО ПакетыАмортизацииОС.Организация = СписокОС.Организация
	|			И ПакетыАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&НачалоМесяца                                КАК НачалоМесяца,
	|	&КонецМесяца                                 КАК КонецМесяца,
	|	СписокОС.Организация                         КАК Организация,
	|	СписокОС.ОсновноеСредство                    КАК ОсновноеСредство,
	|	СписокОС.СпособОтраженияРасходовБУ           КАК СпособОтраженияРасходов,
	|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета
	|ИЗ
	|	ВТ_СписокОбъектовУчета_БУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|		ПО ПакетыАмортизацииОС.Организация = СписокОС.Организация
	|			И ПакетыАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&НачалоМесяца                                КАК НачалоМесяца,
	|	&КонецМесяца                                 КАК КонецМесяца,
	|	СписокОС.Организация                         КАК Организация,
	|	СписокОС.ОсновноеСредство                    КАК ОсновноеСредство,
	|	СписокОС.СпособОтраженияРасходовНУ           КАК СпособОтраженияРасходов,
	|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета
	|ИЗ
	|	ВТ_СписокОбъектовУчета_БУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|		ПО ПакетыАмортизацииОС.Организация = СписокОС.Организация
	|			И ПакетыАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство
	|ГДЕ
	|	СписокОС.СпособОтраженияРасходовНУ <> СписокОС.СпособОтраженияРасходовБУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация";

	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДополнитьТекстПодзапросаНачисленнаяАмортизация(СписокПодзапросов, ПараметрыРасчета) Экспорт

	//++ Локализация

	ТекстПодзапроса =
	"	ВЫБРАТЬ
	|		РасчетАмортизации.ПериодРасчета КАК ПериодРасчета,
	|		РасчетАмортизации.Организация КАК Организация,
	|		РасчетАмортизации.Подразделение КАК Подразделение,
	|		РасчетАмортизации.КорПодразделение КАК КорПодразделение,
	|		РасчетАмортизации.ОсновноеСредство КАК ОсновноеСредство,
	|		РасчетАмортизации.ГруппаФинансовогоУчета,
	|		РасчетАмортизации.НаправлениеДеятельности,
	|		РасчетАмортизации.КорНаправлениеДеятельности,
	|		РасчетАмортизации.СтатьяРасходов КАК СтатьяРасходов,
	|		РасчетАмортизации.АналитикаРасходов КАК АналитикаРасходов,
	|		0 КАК СуммаУУ,
	|		РасчетАмортизации.СуммаБУ КАК СуммаБУ,
	|		РасчетАмортизации.СуммаНУ КАК СуммаНУ,
	|
	|
	|		РасчетАмортизации.СуммаЦФ КАК СуммаЦФ,
	|		РасчетАмортизации.СуммаНУЦФ КАК СуммаНУЦФ,
	|
	|
	|		0 КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		0 КАК РесурсВыработанныйЗаТекущийПериод,
	|		0 КАК СтоимостьДляВычисленияАмортизации,
	|		0 КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_РасчетАмортизации_БУ КАК РасчетАмортизации";

	СписокПодзапросов.Добавить(ТекстПодзапроса);

	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаКоличествоОбработанныхДанных(ПараметрыРасчета) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	//-- Локализация

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтНачисленнаяАмортизация() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	&НомерПакета КАК НомерПакета,
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	СУММА(НачисленнаяАмортизация.СуммаУУ) КАК СуммаУУ,
	|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ,
	|	СУММА(НачисленнаяАмортизация.СуммаЦФ) КАК СуммаЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУЦФ) КАК СуммаНУЦФ,
	|	НачисленнаяАмортизация.ЭтоДоначисление,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС) КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод) КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод) КАК РесурсВыработанныйЗаТекущийПериод,
	|	МАКСИМУМ(НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации) КАК СтоимостьДляВычисленияАмортизации,
	|	МАКСИМУМ(НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл) КАК СтоимостьДляВычисленияАмортизацииРегл
	|ПОМЕСТИТЬ ВТ_НачисленнаяАмортизация
	|ИЗ
	|	(&ТекстПодзапроса) КАК НачисленнаяАмортизация
	|	//ТекстСоединения//
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	&НомерПакета,
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.ОсновноеСредство,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	НачисленнаяАмортизация.ЭтоДоначисление,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС)
	|
	|ИМЕЮЩИЕ
	|	СУММА(НачисленнаяАмортизация.СуммаУУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаБУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаНУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаЦФ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаНУЦФ) <> 0";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции
 
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

//++ Локализация
Функция ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат "";
	
	

КонецФункции

Функция ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокОтраженияПрочихОпераций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	&Период КАК Дата,
	|	&Организация КАК Организация,
	|	&Ссылка КАК Документ,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиПрочихОпераций
	|ГДЕ
	|	СтатьиПрочихОпераций.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И СтатьиПрочихОпераций.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.СтатьяРасходов
	|			ИЗ
	|				ВТ_НачисленнаяАмортизация КАК Т
	|			ГДЕ
	|				Т.Организация = &Организация
	|				И Т.НомерПакета = &НомерПакета)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- Локализация

#КонецОбласти

//++ Локализация

#Область РасчетАмортизации

Процедура ТекстЗапросаРасчетАмортизации_БУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	Амортизация.Подразделение,
	|	Амортизация.ОсновноеСредство,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	Амортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК КорНаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК КорПодразделение,
	|
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов)          КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов)    КАК АналитикаРасходов,
	|
	|	ВЫБОР 
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов) ТОГДА
	|			&СуммаБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|
	|	ВЫБОР 
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов) ТОГДА
	|			&СуммаНУ_регл
	|		ИНАЧЕ Амортизация.СуммаНУ
	|	КОНЕЦ КАК СуммаНУ,
	|
	|
	|	ВЫБОР 
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов) ТОГДА
	|			&СуммаЦФ
	|		ИНАЧЕ Амортизация.СуммаЦФ
	|	КОНЕЦ КАК СуммаЦФ,
	|
	|	ВЫБОР 
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов) ТОГДА
	|			&СуммаНУЦФ_регл
	|		ИНАЧЕ Амортизация.СуммаНУЦФ
	|	КОНЕЦ КАК СуммаНУЦФ
	|
	|
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_БУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.ПериодРасчета              КАК ПериодРасчета,
	|		СписокОС.Организация                КАК Организация,
	|		СписокОС.Подразделение              КАК Подразделение,
	|		СписокОС.ОсновноеСредство           КАК ОсновноеСредство,
	|		СписокОС.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		СписокОС.СтатьяРасходов             КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов          КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов    КАК СпособОтраженияРасходов,
	|
			// СуммаБУ
	|		&РасчетСуммыБУ_БезЦФ КАК СуммаБУ,
	|
			// СуммаНУ
	|		&РасчетСуммыНУ_БезЦФ КАК СуммаНУ,
	|
			// СуммаПР
	|
			// СуммаЦФ
	|		&РасчетСуммыЦФ КАК СуммаЦФ,
	|
			// СуммаНУЦФ
	|		&РасчетСуммыНУЦФ КАК СуммаНУЦФ
	|
	
		// СуммаПРЦФ
	|
	|	ИЗ
	|		(ВЫБРАТЬ
	|			&КонецМесяца                          КАК ПериодРасчета,
	|			СписокОС.Организация                  КАК Организация,
	|			СписокОС.Местонахождение              КАК Подразделение,
	|			СписокОС.ОсновноеСредство             КАК ОсновноеСредство,
	|			СписокОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|			СписокОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|			СписокОС.СтатьяРасходовБУ             КАК СтатьяРасходов,
	|			СписокОС.АналитикаРасходовБУ          КАК АналитикаРасходов,
	|			СписокОС.СпособОтраженияРасходовБУ    КАК СпособОтраженияРасходов,
	|
	|			&СуммаАмортизацииБУ КАК СуммаБУ,
	|			ВЫБОР
	|				КОГДА СписокОС.СтатьяРасходовНУ = СписокОС.СтатьяРасходовБУ
	|						И СписокОС.АналитикаРасходовНУ = СписокОС.АналитикаРасходовБУ
	|						И СписокОС.СпособОтраженияРасходовНУ = СписокОС.СпособОтраженияРасходовБУ
	|					ТОГДА &СуммаАмортизацииНУ 
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК СуммаНУ,
	|
	|			СписокОС.ОстаточнаяСтоимостьРегл     КАК ОстаточнаяСтоимостьРегл,
	|			СписокОС.ОстаточнаяСтоимостьНУ       КАК ОстаточнаяСтоимостьНУ,
	|			СписокОС.ОстаточнаяСтоимостьЦФ       КАК ОстаточнаяСтоимостьЦФ,
	|			СписокОС.ОстаточнаяСтоимостьНУЦФ     КАК ОстаточнаяСтоимостьНУЦФ
	|		ИЗ
	|			ВТ_СписокОССПараметрами_БУ КАК СписокОС
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_БУ КАК ПроверкаПараметровРасчета
	|				ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|					И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|		ГДЕ
	|			ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			&КонецМесяца                          КАК ПериодРасчета,
	|			СписокОС.Организация                  КАК Организация,
	|			СписокОС.Местонахождение              КАК Подразделение,
	|			СписокОС.ОсновноеСредство             КАК ОсновноеСредство,
	|			СписокОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|			СписокОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|			СписокОС.СтатьяРасходовНУ             КАК СтатьяРасходов,
	|			СписокОС.АналитикаРасходовНУ          КАК АналитикаРасходов,
	|			СписокОС.СпособОтраженияРасходовНУ    КАК СпособОтраженияРасходов,
	|
	|			0 КАК СуммаБУ,
	|			&СуммаАмортизацииНУ КАК СуммаНУ,
	|
	|			СписокОС.ОстаточнаяСтоимостьРегл     КАК ОстаточнаяСтоимостьРегл,
	|			СписокОС.ОстаточнаяСтоимостьНУ       КАК ОстаточнаяСтоимостьНУ,
	|			СписокОС.ОстаточнаяСтоимостьЦФ       КАК ОстаточнаяСтоимостьЦФ,
	|			СписокОС.ОстаточнаяСтоимостьНУЦФ     КАК ОстаточнаяСтоимостьНУЦФ
	|		ИЗ
	|			ВТ_СписокОССПараметрами_БУ КАК СписокОС
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_БУ КАК ПроверкаПараметровРасчета
	|				ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|					И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|		ГДЕ
	|			ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL
				// Отдельным запросом с условием, чтобы не увеличивать количество записей, если статьи для БУ и НУ одинаковые
	|			И (СписокОС.СтатьяРасходовНУ <> СписокОС.СтатьяРасходовБУ
	|				ИЛИ СписокОС.АналитикаРасходовНУ <> СписокОС.АналитикаРасходовБУ
	|				ИЛИ СписокОС.СпособОтраженияРасходовНУ <> СписокОС.СпособОтраженияРасходовБУ)
	|
	|		) КАК СписокОС
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписокОС.ПериодРасчета,
	|		СписокОС.Организация,
	|		СписокОС.Подразделение,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.ОстаточнаяСтоимостьРегл,
	|		СписокОС.ОстаточнаяСтоимостьНУ,
	|		СписокОС.ОстаточнаяСтоимостьЦФ,
	|		СписокОС.ОстаточнаяСтоимостьНУЦФ
	|
	|	) КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	 
	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстЗапросаСуммаАмортизацииБУ());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииНУ", ТекстЗапросаСуммаАмортизацииНУ());
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаБУ",        ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНУ_регл",   ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаНУ"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаЦФ",        ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаЦФ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНУЦФ_регл", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаНУЦФ"));
	
	ТекстПоля =
	"		ВЫБОР 
	|			КОГДА СписокОС.ОстаточнаяСтоимостьРегл <= 0
	|				ТОГДА 0
	|			КОГДА &СуммаБУ > СписокОС.ОстаточнаяСтоимостьРегл
	|					ИЛИ СписокОС.ОстаточнаяСтоимостьРегл - &СуммаБУ < &МинимальнаяОстаточнаяСтоимость
	|				ТОГДА СписокОС.ОстаточнаяСтоимостьРегл
	|			ИНАЧЕ &СуммаБУ
	|		КОНЕЦ";
	ТекстПоля = СтрЗаменить(ТекстПоля, "&СуммаБУ", "СУММА(ВЫРАЗИТЬ(СписокОС.СуммаБУ КАК ЧИСЛО(31,2)))");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РасчетСуммыБУ_БезЦФ", ТекстПоля);
	
	ТекстПоля =
	"		ВЫБОР 
	|			КОГДА СписокОС.ОстаточнаяСтоимостьНУ <= 0
	|				ТОГДА 0
	|			КОГДА &СуммаНУ > СписокОС.ОстаточнаяСтоимостьНУ
	|					ИЛИ СписокОС.ОстаточнаяСтоимостьНУ - &СуммаНУ < &МинимальнаяОстаточнаяСтоимость
	|				ТОГДА СписокОС.ОстаточнаяСтоимостьНУ
	|			ИНАЧЕ &СуммаНУ
	|		КОНЕЦ";
	ТекстПоля = СтрЗаменить(ТекстПоля, "&СуммаНУ", "СУММА(ВЫРАЗИТЬ(СписокОС.СуммаНУ КАК ЧИСЛО(31,2)))");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РасчетСуммыНУ_БезЦФ", ТекстПоля);
	
	
	
	
	ТекстПоля =
	"		ВЫБОР
	|			КОГДА СписокОС.ОстаточнаяСтоимостьЦФ = 0
	|				ТОГДА 0
	|			КОГДА &СуммаЦФ > СписокОС.ОстаточнаяСтоимостьЦФ
	|					ИЛИ СписокОС.ОстаточнаяСтоимостьЦФ - &СуммаЦФ < &МинимальнаяОстаточнаяСтоимость
	|				ТОГДА СписокОС.ОстаточнаяСтоимостьЦФ
	|			ИНАЧЕ &СуммаЦФ
	|		КОНЕЦ";
	ТекстПоля = СтрЗаменить(ТекстПоля, "&СуммаЦФ", "СУММА(ВЫРАЗИТЬ(СписокОС.СуммаБУ КАК ЧИСЛО(31,2)))");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РасчетСуммыЦФ", ТекстПоля);
	
	ТекстПоля =
	"		ВЫБОР 
	|			КОГДА СписокОС.ОстаточнаяСтоимостьНУЦФ = 0
	|				ТОГДА 0
	|			КОГДА &СуммаНУЦФ > СписокОС.ОстаточнаяСтоимостьНУЦФ
	|					ИЛИ СписокОС.ОстаточнаяСтоимостьНУЦФ - &СуммаНУЦФ < &МинимальнаяОстаточнаяСтоимость
	|				ТОГДА СписокОС.ОстаточнаяСтоимостьНУЦФ
	|			ИНАЧЕ &СуммаНУЦФ
	|		КОНЕЦ";
	ТекстПоля = СтрЗаменить(ТекстПоля, "&СуммаНУЦФ", "СУММА(ВЫРАЗИТЬ(СписокОС.СуммаНУ КАК ЧИСЛО(31,2)))");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РасчетСуммыНУЦФ", ТекстПоля);
	
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ВернутьПараметрыРасчета(ИсходныеПараметрыРасчета, МенеджерВременныхТаблиц)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&КонецМесяца                                                      КАК Дата,
	|	ПараметрыАмортизации.Организация                                  КАК Организация,
	|	ПараметрыАмортизации.ОсновноеСредство                             КАК ВнеоборотныйАктив,
	
	// БУ
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюБУ 
	|			ТОГДА ПараметрыАмортизации.МетодНачисленияАмортизацииБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК МетодНачисленияБУ,
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюБУ 
	|			ТОГДА ПараметрыАмортизации.СрокИспользованияДляВычисленияАмортизации
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СрокИспользованияБУ,
	|	ПараметрыАмортизации.КоэффициентПериода                           КАК КоэффициентПериода,
	|	ПараметрыАмортизации.СтоимостьДляВычисленияАмортизации            КАК СтоимостьБУ,
	|	ПараметрыАмортизации.ЛиквидационнаяСтоимость			          КАК ЛиквидационнаяСтоимостьБУ,
	|	ПараметрыАмортизации.ОбъемВыработки                               КАК ВыработкаЗаМесяцБУ,
	|	ПараметрыАмортизации.ОбъемПродукцииРаботДляВычисленияАмортизации  КАК ОбъемПродукцииБУ,
	|	ПараметрыАмортизации.СтоимостьНаНачалоГода                        КАК СтоимостьНаНачалоГодаБУ,
	|	(ПараметрыАмортизации.ЧислоЛетПользования
	|		- ПараметрыАмортизации.ЧислоПрошедшихЛет) * 12                КАК ОставшийсяСрокИспользования,
	|	ПараметрыАмортизации.ЧислоПрошедшихЛет * 12                       КАК ФактическийСрокИспользования,
	
	// НУ
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА ПараметрыАмортизации.МетодНачисленияАмортизацииНУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК МетодНачисленияНУ,
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА ПараметрыАмортизации.СрокПолезногоИспользованияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокИспользованияНУ,
	|	ВЫБОР
	|		КОГДА НЕ ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА 0
	|		КОГДА ПараметрыАмортизации.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) 
	|			ТОГДА ПараметрыАмортизации.СтоимостьНУНачальныйОстаток 
	|	КОНЕЦ                                                             КАК СтоимостьНУ
	|ИЗ
	|	ВТ_СписокОССПараметрами_БУ КАК ПараметрыАмортизации
	|ГДЕ
	|	ПараметрыАмортизации.НачислятьАмортизациюБУ
	|		ИЛИ ПараметрыАмортизации.НачислятьАмортизациюНУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецМесяца                                                      КАК Дата,
	|	ПараметрыАмортизации.Организация                                  КАК Организация,
	|	ПараметрыАмортизации.ОсновноеСредство                             КАК ВнеоборотныйАктив,
	
	// УУ
	|	ПараметрыАмортизации.МетодНачисленияАмортизации                   КАК МетодНачисленияУУ,
	|	ПараметрыАмортизации.ОстаточнаяСтоимость                          КАК СтоимостьУУ,
	|	ПараметрыАмортизации.ЛиквидационнаяСтоимость                      КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизации.РесурсНазначенный                            КАК СрокИспользованияУУ,
	|	ПараметрыАмортизации.РесурсВыработанныйЗаТекущийПериод            КАК РесурсВыработанныйЗаТекущийПериод,
	|	ПараметрыАмортизации.РесурсВыработанныйЗаПрошлыйПериод            КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	ПараметрыАмортизации.ОтработанныхМесяцевПолныхЛет                 КАК ОтработанныхМесяцевПолныхЛет,
	|	ПараметрыАмортизации.КоэффициентУскоренияУУ                       КАК КоэффициентУскоренияУУ
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК ПараметрыАмортизации
	|ГДЕ
	|	ПараметрыАмортизации.НачислятьАмортизациюУУ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ИсходныеПараметрыРасчета.Период));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ПараметрыАмортизации_Регл = Результат[0].Выгрузить();
	ПараметрыАмортизации_Упр = Результат[1].Выгрузить();

	СписокПараметровАмортизации = ПолучитьИзВременногоХранилища(ИсходныеПараметрыРасчета.АдресПараметровРасчета);
	
	Если СписокПараметровАмортизации = Неопределено Тогда
		СписокПараметровАмортизации = Новый Структура;
		СписокПараметровАмортизации.Вставить("ПараметрыАмортизации_Регл", ПараметрыАмортизации_Регл);
		СписокПараметровАмортизации.Вставить("ПараметрыАмортизации_Упр", ПараметрыАмортизации_Упр);
	Иначе
		ОбщегоНазначенияУТКлиентСервер.ДополнитьТаблицу(СписокПараметровАмортизации.ПараметрыАмортизации_Регл, ПараметрыАмортизации_Регл);
		ОбщегоНазначенияУТКлиентСервер.ДополнитьТаблицу(СписокПараметровАмортизации.ПараметрыАмортизации_Упр, ПараметрыАмортизации_Упр);
	КонецЕсли; 
	
	ПоместитьВоВременноеХранилище(СписокПараметровАмортизации, ИсходныеПараметрыРасчета.АдресПараметровРасчета);
	
КонецПроцедуры

Функция ТекстЗапросаСуммаАмортизацииБУ()
	
	ТекстЗапроса =
	"ВЫБОР 
	|	КОГДА НЕ СписокОС.НачислятьАмортизациюБУ ТОГДА
	|		0
	// Линейный
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) 
	|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации / СписокОС.СрокИспользованияДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * 12 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации 
	|							* СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|   
	// ПропорциональноОбъемуПродукции
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.ОбъемВыработки <> 0 И СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации <> 0 
	|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|									* СписокОС.ОбъемВыработки 
	|									/ СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|
	// УменьшаемогоОстатка
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) ТОГДА
	|				ВЫБОР
	|					КОГДА ГОД(СписокОС.ДатаВводаВЭксплуатациюБУ) = ГОД(&КонецМесяца)
	 
	|						ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|										/ СписокОС.СрокИспользованияДляВычисленияАмортизации  КАК ЧИСЛО(31,2))
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьНаНачалоГода 
	
	|									/ СписокОС.СрокИспользованияДляВычисленияАмортизации  КАК ЧИСЛО(31,2))
	|				КОНЕЦ 
	|			ИНАЧЕ
	|				ВЫБОР
	|					КОГДА ГОД(СписокОС.ДатаВводаВЭксплуатациюБУ) = ГОД(&КонецМесяца) ТОГДА
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации * 12 
	|							* СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьНаНачалоГода 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации * 12 
	|							* СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|		КОНЕЦ 
	|
	// ПоСуммеЧиселЛетИспользования
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Кумулятивный) ТОГДА
	
	|		ВЫБОР 
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) ТОГДА
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * &ЧислоЛетОставшихся / &СуммаЧиселЛет / 12 КАК ЧИСЛО(31,2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * &ЧислоЛетОставшихся / &СуммаЧиселЛет * СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|КОНЕЦ";				
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЧислоЛетОставшихся", "(СписокОС.ЧислоЛетПользования - СписокОС.ЧислоПрошедшихЛет)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаЧиселЛет", "((СписокОС.ЧислоЛетПользования + 1) * СписокОС.ЧислоЛетПользования / 2)");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСуммаАмортизацииНУ()

	ТекстЗапроса =
	"ВЫБОР 
	|	КОГДА НЕ СписокОС.НачислятьАмортизациюНУ ТОГДА
	|		0
	// Линейный
	|	КОГДА СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) 
	|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации / СписокОС.СрокИспользованияДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * 12 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации 
	|							* СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|   
	// ПропорциональноОбъемуПродукции
	|	КОГДА СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.ОбъемВыработки <> 0 И СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации <> 0 
	|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|									* СписокОС.ОбъемВыработки 
	|									/ СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|
	// УменьшаемогоОстатка
	|	КОГДА СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) ТОГДА
	|				ВЫБОР
	|					КОГДА ГОД(СписокОС.ДатаВводаВЭксплуатациюБУ) = ГОД(&КонецМесяца)
	|						ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|										/ СписокОС.СрокИспользованияДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьНаНачалоГода 
	|									/ СписокОС.СрокИспользованияДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|				КОНЕЦ 
	|			ИНАЧЕ
	|				ВЫБОР
	|					КОГДА ГОД(СписокОС.ДатаВводаВЭксплуатациюБУ) = ГОД(&КонецМесяца) ТОГДА
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации * 12 
	|							* СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьНаНачалоГода 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации * 12 
	|							* СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|		КОНЕЦ 
	|
	// ПоСуммеЧиселЛетИспользования
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Кумулятивный) ТОГДА
	
	|		ВЫБОР 
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) ТОГДА
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * &ЧислоЛетОставшихся / &СуммаЧиселЛет / 12 КАК ЧИСЛО(31,2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * &ЧислоЛетОставшихся / &СуммаЧиселЛет * СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|КОНЕЦ";				

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЧислоЛетОставшихся", "(СписокОС.ЧислоЛетПользования - СписокОС.ЧислоПрошедшихЛет)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаЧиселЛет", "((СписокОС.ЧислоЛетПользования + 1) * СписокОС.ЧислоЛетПользования / 2)");
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

//-- Локализация

#КонецОбласти
