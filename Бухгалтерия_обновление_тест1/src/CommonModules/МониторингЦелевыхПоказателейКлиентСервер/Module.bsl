#Область ПрограммныйИнтерфейс

// Возвращает стандартные настройки формирования монитора целевых показателей
//
// Возвращаемое значение:
//	Структура - стандартные настройки:
//		* ВариантГруппировкиПоказателей - Строка - "ПоКатегориямЦелей", группировка показателей по категориям целей
//		* КоличествоКолонокМонитораПоказателей - Число - 3 колонки, количество колонок
//		* РазмерДиаграмм - Число - крупный (2), высота примерно равна ширине
//		* ПроизвольныйМасштаб - Число - 100 %, стандартное значение масштабирования
//		* ВариантОтображенияДеталей - Строка - "ПоказыватьТолькоТекущееЗначение" - только текущее значение
//		* ПериодАвтообновления - Число - 15 мин. Число минут, через которые следует автоматически обновлять данные на форме.
//
Функция СтандартныеНастройки() Экспорт
	
	НастройкиПоУмолчанию = Новый Структура;
	
	// порядок вывода показателей
	НастройкиПоУмолчанию.Вставить("ВариантГруппировкиПоказателей", "ПоКатегориямЦелей");
	НастройкиПоУмолчанию.Вставить("КоличествоКолонокМонитораПоказателей", 3);
	
	// Внешний вид показателей
	НастройкиПоУмолчанию.Вставить("РазмерДиаграмм", 1);
	НастройкиПоУмолчанию.Вставить("ПроизвольныйМасштаб", 100);
	НастройкиПоУмолчанию.Вставить("ВариантОтображенияДеталей", "ПоказыватьТолькоТекущееЗначение");
	
	// Обновление данных
	НастройкиПоУмолчанию.Вставить("ПериодАвтообновления", 15);
	
	Возврат НастройкиПоУмолчанию;
	
КонецФункции

// Возвращает названия перечисления Периодичность в разных склонениях
//
// Параметры
//	ЧислоПериодов - Число - количество периодов, определяет склонение
//	Периодичность - ссылка на значение перечисления "Периодичность".
//
// Возвращаемое значение:
//  Строка - название периода в правильном склонении.
//
Функция ПериодПрописью(ЧислоПериодов, Периодичность) Экспорт
	ПериодПрописью = "";
	ФорматнаяСтрока = "";
	ПараметрыПредметаИсчисления = "";
	
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
		ПараметрыПредметаИсчисления = НСтр("ru='день, дня, дней, м';uk='день, дні, днів, м'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='день';uk='день'")) Тогда
			ПериодПрописью = НСтр("ru='день';uk='день'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='дня';uk='дні'")) Тогда
			ПериодПрописью = НСтр("ru='дня';uk='дні'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='дней';uk='днів'")) Тогда
			ПериодПрописью = НСтр("ru='дней';uk='днів'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		ПараметрыПредметаИсчисления = НСтр("ru='неделя, недели, недель, ж';uk='тиждень, тижні, тижнів, м'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='неделя';uk='тиждень'")) Тогда
			ПериодПрописью = НСтр("ru='неделя';uk='тиждень'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='недели';uk='тижні'")) Тогда
			ПериодПрописью = НСтр("ru='недели';uk='тижні'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='недель';uk='тижнів'")) Тогда
			ПериодПрописью = НСтр("ru='недель';uk='тижнів'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		ПараметрыПредметаИсчисления = НСтр("ru='декада, декады, декад, ж';uk='декада, декади, декад, ж'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='декада';uk='декада'")) Тогда
			ПериодПрописью = НСтр("ru='декада';uk='декада'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='декады';uk='декади'")) Тогда
			ПериодПрописью = НСтр("ru='декады';uk='декади'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='декад';uk='декад'")) Тогда
			ПериодПрописью = НСтр("ru='декад';uk='декад'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		ПараметрыПредметаИсчисления = НСтр("ru='месяц, месяца, месяцев, м';uk='місяць, місяці, місяців, м'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='месяцев';uk='місяців'")) Тогда
			ПериодПрописью = НСтр("ru='месяцев';uk='місяців'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='месяца';uk='місяці'")) Тогда
			ПериодПрописью = НСтр("ru='месяца';uk='місяці'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='месяц';uk='місяць'")) Тогда
			ПериодПрописью = НСтр("ru='месяц';uk='місяць'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		ПараметрыПредметаИсчисления = НСтр("ru='квартал, квартала, кварталов, м';uk='квартал, квартали, кварталів, м'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='кварталов';uk='кварталів'")) Тогда
			ПериодПрописью = НСтр("ru='кварталов';uk='кварталів'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='квартала';uk='квартали'")) Тогда
			ПериодПрописью = НСтр("ru='квартала';uk='квартали'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='квартал';uk='квартал'")) Тогда
			ПериодПрописью = НСтр("ru='квартал';uk='квартал'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		ПараметрыПредметаИсчисления = НСтр("ru='полугодие, полугодия, полугодий, с';uk='півріччя, півріччя, півріч, з'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='полугодие';uk='півріччя'")) Тогда
			ПериодПрописью = НСтр("ru='полугодие';uk='півріччя'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='полугодия';uk='півріччя'")) Тогда
			ПериодПрописью = НСтр("ru='полугодия';uk='півріччя'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='полугодий';uk='піврічь'")) Тогда
			ПериодПрописью = НСтр("ru='полугодий';uk='піврічь'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		ПараметрыПредметаИсчисления = НСтр("ru='год, года, лет, м';uk='рік, роки, років, м'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='год';uk='рік'")) Тогда
			ПериодПрописью = НСтр("ru='год';uk='рік'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='года';uk='роки'")) Тогда
			ПериодПрописью = НСтр("ru='года';uk='роки'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='лет';uk='років'")) Тогда
			ПериодПрописью = НСтр("ru='лет';uk='років'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПериодПрописью;
КонецФункции

// Возвращает структуру настроек для формирования состава монитора
//
// Возвращаемое значение:
//	Структура - незаполненная структура с требуемыми свойствами:
//		* АдресаДинамическихПараметров - Строка - содержит указание на временные настройки вывода вариантов анализа в монитор целевых показателей.
//		* ВидПоиска - ПеречислениеСсылка.ВидыПоискаСвязанныхПоказателей, Строка - значения зависимые, влияющие, взаимосвязанные, все.
//		* ДемонстрационныйРежим - Булево - указание на необходимость отображения демонстрационных данных вариантов анализа.
//		* ОтборВариантовАнализа - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей, Массив - отбор данных на основании ссылки на вариант анализа.
//		* ОтборЗонаВнимания - Число - параметр устарел, оставлен для совместимости.
//		* ОтборСтатус - Строка - отбор вариантов анализа по их статусу. 
//			Принимает значения: "" (без учета состояния), "ЦельДостигнута", "ПриемлемоеСостояние", "НеприемлемоеСостояние", "СостояниеНеОпределено", "РассчитаноСОшибками".
//		* ПериодичностьКонтроля - ПеречислениеСсылка.Периодичность - отбор вариантов анализа по соответствующему реквизиту
//		* ПринудительноОбновитьДанные - Булево - указание на обновление кэша ранее запланированного времени
//		* РежимАнализаВзаимосвязанных - Булево - признак, что производится анализ связанных показателей
//		* СвойстваВариантаАнализа - Структура - свойства:
//			** Ссылка - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант анализа, для которого будут получаться данные
//			** ПериодАнализа - ХранилищеЗначения - содержит упакованный СтандартныйПериод, описывающий период выборки данных
//		* СтруктураНастроек - Структура - см. функцию МониторингЦелевыхПоказателей.ПолучитьНастройкиМонитораЦелевыхПоказателей
//		* УчитыватьВариантыОтображения - Булево - Истина, подготовка данных с учетом отображения в виде таблицы, диаграммы или текста.
//
Функция ИнициализацияПараметровСоставаМонитора() Экспорт
	ПараметрыСоставаМонитора = Новый Структура;
	ПараметрыСоставаМонитора.Вставить("АдресаДинамическихПараметров",);
	ПараметрыСоставаМонитора.Вставить("ВидПоиска",);
	ПараметрыСоставаМонитора.Вставить("ДемонстрационныйРежим",);
	ПараметрыСоставаМонитора.Вставить("ОтборВариантовАнализа",);
	ПараметрыСоставаМонитора.Вставить("ОтборЗонаВнимания",);
	ПараметрыСоставаМонитора.Вставить("ОтборСтатус",);
	ПараметрыСоставаМонитора.Вставить("ПериодичностьКонтроля",);
	ПараметрыСоставаМонитора.Вставить("ПринудительноОбновитьДанные",);
	ПараметрыСоставаМонитора.Вставить("РежимАнализаВзаимосвязанных",);
	ПараметрыСоставаМонитора.Вставить("СвойстваВариантаАнализа",);
	ПараметрыСоставаМонитора.Вставить("СтруктураНастроек",);
	ПараметрыСоставаМонитора.Вставить("УчитыватьВариантыОтображения",);
	
	Возврат ПараметрыСоставаМонитора;
КонецФункции

#КонецОбласти
