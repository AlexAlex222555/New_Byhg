//////////////////////////////////////////////////////////////////////////////////////////////
// УчетнаяПолитикаПереопределяемый: переопределяемый механизм отвечающий за получение данных учетной политики.
//  
//////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщиеПараметрыУчетнойПолитики

Функция Существует(Организация, Период, ВыводитьСообщениеОбОтсутствииУчетнойПолитики = Ложь, ДокументСсылка = Неопределено) Экспорт

	Возврат Истина;

КонецФункции

//++ НЕ УТ

// Возвращает признак, является ли организация плательщиком налога на прибыль.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется необходимость оплаты налога на прибыль;
//	Период - Дата - дата, на начало месяца которой определяется необходимость оплаты налога на прибыль.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату является плательщиком налога на прибыль. В противном случае - ложь.
//
Функция ПлательщикНалогаНаПрибыль(Организация, Период) Экспорт
	
	Результат = ПолучитьФункциональнуюОпцию("ПлательщикНалогаНаПрибыль", 
		Новый Структура("Организация, Период", Организация, НачалоМесяца(Период)));
	
	Возврат Результат;
	
КонецФункции 




//-- НЕ УТ

// Возвращает систему налогообложения применяемую организацией.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется система налогообложения;
//	Период - Дата - дата, на начало месяца которой определяется система налогообложения.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.СистемыНалогообложения - система налогообложения, применяемая для данной организации на указанную дату.
//	Значение по умолчанию: НалогНаПрибыльИНДС
//
Функция СистемаНалогообложения(Организация, Период) Экспорт

	ПараметрыСистемыНалогообложения = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
        "НастройкиСистемыНалогообложения",
		Организация,
		Период
    );
    Если ПараметрыСистемыНалогообложения <> Неопределено Тогда
        СистемаНалогообложения = ПараметрыСистемыНалогообложения.СистемаНалогообложения;
    Иначе
        СистемаНалогообложения = Перечисления.СистемыНалогообложения.НалогНаПрибыльИНДС; 
    КонецЕсли;    
	Возврат СистемаНалогообложения;

КонецФункции

#КонецОбласти

#Область ПараметрыУчетнойПолитикиПоНДС

// Параметры учетной политики по НДС

// Возвращает признак, является ли организация плательщиком налога на добавленную стоимость (НДС).
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак оплаты НДС;
//	Период - Дата - дата, на начало месяца которой определяется необходимость оплаты НДС.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату является плательщиком налога на добавленную стоимость. В противном случае - ложь.
//
Функция ПлательщикНДС(Организация, Период) Экспорт

	ПараметрыСистемыНалогообложения = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
        "НастройкиСистемыНалогообложения",
		Организация,
		Период
    );
    
    ПлательщикНДС = Не ПараметрыСистемыНалогообложения = Неопределено И ПараметрыСистемыНалогообложения.ПлательщикНДС;
    
    Возврат ПлательщикНДС;

КонецФункции 


//++ НЕ УТ
//-- НЕ УТ
#КонецОбласти


//++ НЕ УТ
#Область ПараметрыУчетнойПолитикиБухучет

Функция ИспользуемыеКлассыСчетовРасходов(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
		"УчетнаяПолитикаБухУчета",
		ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация),
		НачалоМесяца(Период),
		Ложь);
		
	Если ЗначениеЗаполнено(ПараметрыУчетнойПолитики) Тогда
		Результат = ПараметрыУчетнойПолитики.ИспользуемыеКлассыСчетовРасходов;
	Иначе 
		Результат = Перечисления.КлассыСчетовРасходов.Класс9;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции 

Функция НалогНаПрибыльБезКорректировокФинансовогоРезультата(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
		"НастройкиУчетаНалогаНаПрибыль",
		ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация),
		НачалоМесяца(Период),
		Ложь);
		
	Если ЗначениеЗаполнено(ПараметрыУчетнойПолитики) Тогда
		Результат = ПараметрыУчетнойПолитики.НалогНаПрибыльБезКорректировокФинансовогоРезультата;
	Иначе 
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

Функция ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
		"УчетнаяПолитикаБухУчета",
		ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация),
		НачалоМесяца(Период),
		Ложь);
		
	Если ЗначениеЗаполнено(ПараметрыУчетнойПолитики) Тогда
		Результат = ПараметрыУчетнойПолитики.ВключатьСуммуДооценокОСВСоставНераспределеннойПрибыли;
	Иначе 
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

#КонецОбласти
//-- НЕ УТ

//++ НЕ УТ 
//-- НЕ УТ


#Область ПараметрыУчетнойПолитикиПоЕН

// Возвращает признак, является ли организация плательщиком единого налога.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак оплаты НДС;
//	Период - Дата - дата, на начало месяца которой определяется необходимость оплаты НДС.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату является плательщиком единого налога. В противном случае - Ложь.
//
Функция ПлательщикЕН(Организация, Период) Экспорт

	ПараметрыСистемыНалогообложения = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
        "НастройкиСистемыНалогообложения",
		Организация,
		Период
    );
    
    ПлательщикЕН = Не ПараметрыСистемыНалогообложения = Неопределено И ПараметрыСистемыНалогообложения.ПлательщикЕН;
	
    Возврат ПлательщикЕН;

КонецФункции 

#КонецОбласти


//++ НЕ УТ
//-- НЕ УТ

// Определяет, применялся ли для организации признак, указанный в первом параметре, в течении указанного периода.
//
// Параметры:
//	ИмяРесурса - Строка - имя ресурса регистра сведений "УчетныеПолитикиОрганизаций" с типом "Булево";
//	Организация - СправочникСсылка.Организация - организация, для которой определяется значение ресурса;
//	НачалоПериода - Дата - начало периода, для которого определяется значение ресурса;
//	КонецПериода - Дата - конец периода, для которого определяется значение ресурса.
//
// Возвращаемое значение:
//	Булево - Истина, если для организации в течении указанного периода, значение ресурса хотя бы раз принимало значение "Истина". В противном случае - ложь.
//
Функция ЗначенияРесурсаУчетнойПолитикиЗаПериод(ИмяРесурса, Организация, НачалоПериода, ОкончаниеПериода) Экспорт
	
	СледующийМесяц = НачалоМесяца(ДобавитьМесяц(НачалоПериода, 1));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("НачалоПериода",    НачалоМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("СледующийМесяц",   СледующийМесяц);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(ОкончаниеПериода));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения КАК Значение
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&НачалоПериода, Организация = ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).ГоловнаяОрганизация) КАК НастройкиСистемыНалогообложенияСрезПоследних";
	
	Если СледующийМесяц < КонецМесяца(ОкончаниеПериода) Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|"+
		"ВЫБРАТЬ
		|	НастройкиСистемыНалогообложения.СистемаНалогообложения
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
		|ГДЕ
        |	НастройкиСистемыНалогообложения.Организация = &Организация
		|	И НастройкиСистемыНалогообложения.Период МЕЖДУ &СледующийМесяц И &ОкончаниеПериода";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СистемаНалогообложения", ИмяРесурса);
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Значение");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти