///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает минимальную версию информационной базы среди всех областей данных.
//
// Возвращаемое значение:
//  Строка - например, "2.3.1.4".
//
Функция МинимальнаяВерсияИБ() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		МодульОбновлениеИнформационнойБазыСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"ОбновлениеИнформационнойБазыСлужебныйВМоделиСервиса");
		
		МинимальнаяВерсияОбластейДанных = МодульОбновлениеИнформационнойБазыСлужебныйВМоделиСервиса.МинимальнаяВерсияОбластейДанных();
	Иначе
		МинимальнаяВерсияОбластейДанных = Неопределено;
	КонецЕсли;
	
	ВерсияИБ = ОбновлениеИнформационнойБазыСлужебный.ВерсияИБ(Метаданные.Имя);
	
	Если МинимальнаяВерсияОбластейДанных = Неопределено Тогда
		МинимальнаяВерсияИБ = ВерсияИБ;
	Иначе
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияИБ, МинимальнаяВерсияОбластейДанных) > 0 Тогда
			МинимальнаяВерсияИБ = МинимальнаяВерсияОбластейДанных;
		Иначе
			МинимальнаяВерсияИБ = ВерсияИБ;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МинимальнаяВерсияИБ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверить необходимость обновления информационной базы при смене версии конфигурации.
//
Функция НеобходимоОбновлениеИнформационнойБазы() Экспорт
	
	Если ОбновлениеИнформационнойБазыСлужебный.НеобходимоВыполнитьОбновление(
			Метаданные.Версия, ОбновлениеИнформационнойБазыСлужебный.ВерсияИБ(Метаданные.Имя)) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ОбновлениеИнформационнойБазыСлужебный.ВыполненаРегистрацияОтложенныхОбработчиковОбновления() Тогда
		Возврат Истина;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запустить = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ЗапуститьОбновлениеИнформационнойБазы");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Запустить <> Неопределено И ОбновлениеИнформационнойБазыСлужебный.ЕстьПраваНаОбновлениеИнформационнойБазы() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает соответствие имен и идентификаторов отложенных обработчиков
// и их очередей.
Функция ОчередьОтложенногоОбработчикаОбновления() Экспорт
	
	ИменаПодсистемРежимПараллельно = Новый Массив;
	ОписанияПодсистем  = СтандартныеПодсистемыПовтИсп.ОписанияПодсистем();
	Для Каждого ИмяПодсистемы Из ОписанияПодсистем.Порядок Цикл
		ОписаниеПодсистемы = ОписанияПодсистем.ПоИменам.Получить(ИмяПодсистемы);
		Если ОписаниеПодсистемы.РежимВыполненияОтложенныхОбработчиков = "Параллельно" Тогда
			ИменаПодсистемРежимПараллельно.Добавить(ИмяПодсистемы);
		КонецЕсли;
	КонецЦикла;
	
	ОчередьПоИмени          = Новый Соответствие;
	ОчередьПоИдентификатору = Новый Соответствие;
	
	СведенияОбОбновлении = ОбновлениеИнформационнойБазыСлужебный.СведенияОбОбновленииИнформационнойБазы();
	
	Для Каждого Библиотека Из СведенияОбОбновлении.ДеревоОбработчиков.Строки Цикл
		Если ИменаПодсистемРежимПараллельно.Найти(Библиотека.ИмяБиблиотеки) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Версия Из Библиотека.Строки Цикл
			Для Каждого ОтложенныйОбработчик Из Версия.Строки Цикл
				Если ОтложенныйОбработчик.ОчередьОтложеннойОбработки = 0 Тогда
					Продолжить;
				КонецЕсли;       
				ОчередьПоИмени.Вставить(ОтложенныйОбработчик.ИмяОбработчика, ОтложенныйОбработчик.ОчередьОтложеннойОбработки);
				Если ЗначениеЗаполнено(ОтложенныйОбработчик.Идентификатор) Тогда
					ОчередьПоИдентификатору.Вставить(ОтложенныйОбработчик.Идентификатор, ОтложенныйОбработчик.ОчередьОтложеннойОбработки);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;           
	
	Результат = Новый Соответствие;
	Результат.Вставить("ПоИмени", ОчередьПоИмени);
	Результат.Вставить("ПоИдентификатору", ОчередьПоИдентификатору);
	
	Возврат Новый ФиксированноеСоответствие(Результат);
	
КонецФункции

#КонецОбласти
