////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С СТАВКАМИ НДС

// Возвращает строковое значение ставки НДС по элементу справочника СтавкиНДС
//
// Параметры:
// 		СтавкаНДС - СправочникСсылка.СтавкиНДС - элемент справочника СтавкиНДС
//
// Возвращаемое значение:
// 		Строка - Значение ставки НДС строкой
//
Функция ПолучитьСтавкуНДССтрокой(СтавкаНДС) Экспорт
    
    ПеречислениеСтавкаНДС = НДСКлиентСерверПовтИсп.ПеречислениеСтавкаНДСПоСтавкеНДС(СтавкаНДС);
    
	Если ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20") Тогда
		ЗначениеНалога = "20";
    ИначеЕсли ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС14") Тогда
    	ЗначениеНалога = "14";
	ИначеЕсли ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС7") Тогда
		ЗначениеНалога = "7";
	ИначеЕсли ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0") Тогда
		ЗначениеНалога = "0";
	ИначеЕсли ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС") Тогда
		ЗначениеНалога = "Без НДС";
	ИначеЕсли ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НеНДС") Тогда
		ЗначениеНалога = "Не НДС";
	Иначе
		ЗначениеНалога = Строка(ПеречислениеСтавкаНДС);
	КонецЕсли;
	
	Возврат ЗначениеНалога;
	
КонецФункции // ПолучитьСтавкуНДССтрокой()

// Возвращает элемент справочника СтавкиНДС по строковому значению ставки НДС 
//
// Параметры:
// 		ЗначениеНалога - Строка - Значение ставки НДС строкой
//
// Возвращаемое значение:
// 		СправочникСсылка.СтавкиНДС - элемент справочника СтавкиНДС
//
Функция ПолучитьПоСтрокеСтавкуНДС(ЗначениеНалога) Экспорт
	
	Если ЗначениеНалога = "20" Тогда
		ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");
    ИначеЕсли ЗначениеНалога = "14" Тогда
    	ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС14");
	ИначеЕсли ЗначениеНалога = "7" Тогда
		ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС7");
	ИначеЕсли ЗначениеНалога = "0" Тогда
		ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0");
	ИначеЕсли ЗначениеНалога = "Без НДС" Тогда
		ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
	Иначе
		ПеречислениеСтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НеНДС");
	КонецЕсли;
    
    СтавкаНДС = НДСКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(ПеречислениеСтавкаНДС);
    
	Возврат СтавкаНДС;
		
КонецФункции // ПолучитьПоСтрокеСтавкуНДС()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ФОРМАМИ НАЛОГОВЫХ ДОКУМЕНТОВ

// Пересчитывает сумму из валюты ВалютаНач по курсу ПоКурсуНач 
// в валюту ВалютаКон по курсу ПоКурсуКон без округления до 2-х знаков после запятой
Функция ПересчитатьИзВалютыВВалюту(Сумма, ВалютаНач, ВалютаКон, ПоКурсуНач, ПоКурсуКон, 
	ПоКратностьНач = 1, ПоКратностьКон = 1) Экспорт
	
	Если ВалютаНач = ВалютаКон Тогда
		Возврат Сумма;
	КонецЕсли;
	
	Если ПоКурсуНач = 0
		ИЛИ ПоКратностьНач = 0
		ИЛИ ПоКурсуКон = 0
		ИЛИ ПоКратностьКон = 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='При пересчете в валюту %1 сумма %2 установлена в нулевое значение, т.к. курс валюты не задан.';uk='При перерахуванні у валюту %1 сума %2 установлена в нульове значення, тому що курс валюти не заданий.'"), 
				ВалютаКон, 
				Формат(Сумма, "ЧДЦ=2; ЧН=0")
			)
		);
		
		Возврат 0;
		
	КонецЕсли;
	
	Возврат (Сумма * ПоКурсуНач * ПоКратностьКон) / (ПоКурсуКон * ПоКратностьНач);
	
КонецФункции

Функция КодЕдиницыИзмеренияДляПечатиНалоговых(Код) Экспорт

	Код = СтрЗаменить(Строка(Код), " ", "");
	
	Если НЕ СтрДлина(Код) = 4 Тогда
	
		Возврат "";	
		
	Иначе
		
		Для НомСимв = 1 По 4 Цикл
			КодСимвола = КодСимвола(Код, НомСимв);
			Если НЕ (КодСимвола >= 48 И КодСимвола <= 57) Тогда
				Возврат "";
			КонецЕсли; 
		КонецЦикла; 		
		
		Возврат Код;
		
	КонецЕсли;	

КонецФункции


