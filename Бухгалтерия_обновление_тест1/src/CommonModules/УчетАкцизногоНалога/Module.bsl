#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - ФиксированнаяСтруктура - свойства документа (См. ПроведениеДокументов.СвойстваДокумента).
//
// Возвращаемое значение:
//  Структура - параметры учетного механизма (См. ПроведениеДокументов.ПараметрыУчетногоМеханизма()).
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РозничныеПродажиПодакцизныхТоваров);
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Процедура формирования движений по регистру.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РозничныеПродажиПодакцизныхТоваров");
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСоСтавками

// Функция возвращает ставку акцизного налога 
//
Функция ПолучитьСтавкуАкцизногоНалога(СтатьяДекларацииПоАкцизномуНалогу) Экспорт

	Если ЗначениеЗаполнено(СтатьяДекларацииПоАкцизномуНалогу) Тогда
		Возврат 0.05;
	Иначе
		Возврат 0;	
	КонецЕсли; 
	

КонецФункции // ПолучитьСтавкуАкцизногоНалога()

// Рассчитывает сумму акцизного налога исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            	- число, сумма от которой надо рассчитывать налоги, 
//  СуммаВключаетНалоги - булево, признак учета НДС и акциза в сумме, 
//  СтатьяДекларацииПоАкцизномуНалогу, 
//  ПодакцизныеТоварыДляКоммерческогоИспользования - параметры, влияющие на расчет суммы
//
// Возвращаемое значение:
//  Число, полученная сумма акцизного налога
//
Функция РассчитатьСуммуАкцизногоНалога(Сумма, СуммаВключаетАкциз, СтатьяДекларацииПоАкцизномуНалогу, ПодакцизныеТоварыДляКоммерческогоИспользования) Экспорт

	Если ПодакцизныеТоварыДляКоммерческогоИспользования Тогда
		СуммаАкцизногоНалога = 0;
	Иначе 
		СтавкаАкцизногоНалога = ПолучитьСтавкуАкцизногоНалога(СтатьяДекларацииПоАкцизномуНалогу);
		Если СуммаВключаетАкциз Тогда
			СуммаАкцизногоНалога = Сумма * СтавкаАкцизногоНалога/(1+СтавкаАкцизногоНалога);
		Иначе
			СуммаАкцизногоНалога = Сумма * СтавкаАкцизногоНалога;
		КонецЕсли;
	КонецЕсли; 
	
	Возврат СуммаАкцизногоНалога;

КонецФункции // РассчитатьСуммуАкцизногоНалога()

// Процедура устанавливает видимость полей акцизного налога.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - Текущий документ
//	Элемент - ПолеВвода - Элемент формы
//

Процедура УстановитьВидимостьПолейАкцизногоНалога(Объект, Элемент) Экспорт
	
	Элемент.Видимость = ДействуетАкцизныйНалог(Объект.Дата);
	
КонецПроцедуры // УстановитьВидимостьСтатьиДекларацииПоЕдиномуНалогу()

// Функция проверяет, действует ли акцизный налог на дату.
//
// Параметры:
//	ДатаДокумента - Дата - Дата,на которую проверить действие акцизного налога
//
Функция ДействуетАкцизныйНалог(ДатаДокумента) Экспорт

	Возврат НЕ ЗначениеЗаполнено(ДатаДокумента) ИЛИ ДатаДокумента > '20150101';

КонецФункции // ДействуетАкцизныйНалог()

#КонецОбласти

#КонецОбласти

#КонецЕсли
