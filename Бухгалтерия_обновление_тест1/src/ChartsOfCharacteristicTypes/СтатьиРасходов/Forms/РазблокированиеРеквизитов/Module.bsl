
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РазрешитьРедактированиеВидаДеятельностиИВариантаРаспределенияРасходов = Истина;
	РазрешитьРедактированиеАналитикаРасходов = Истина;
	РазрешитьРедактированиеНастроекРегламентированногоУчета = Истина;
	
	Если Параметры.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность
        И (Параметры.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы ИЛИ Параметры.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.КурсовыеРазницыНеоперационные) Тогда
		ЗапретРазблокировкиВидаДеятельностиРасходов = Истина;
		Элементы.ОписаниеВариантРаспределенияРасходов.Заголовок = 
			НСтр("ru='Вариант распределения расходов не рекомендуется изменять, если есть проведенные документы отражения расходов, в которых указана данная статья.';uk='Варіант розподілу витрат не рекомендується змінювати, якщо є проведені документи відображення витрат, в яких вказана ця стаття.'");
		Элементы.РазрешитьРедактированиеВидаДеятельностиИВариантаРаспределенияРасходов.Заголовок = 
			НСтр("ru='Разрешить редактирование типа расходов и варианта распределения расходов';uk='Дозволити редагування типу витрат і варіанта розподілу витрат'");
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)

	Результат = РедактируемыеРеквизиты();
	Закрыть(Результат);

КонецПроцедуры

&НаСервере
Функция РедактируемыеРеквизиты()
	
	Результат = Новый Массив;
	Если РазрешитьРедактированиеВидаДеятельностиИВариантаРаспределенияРасходов Тогда
		Результат.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Реквизиты.ТипРасходов.Имя);
		Если НЕ ЗапретРазблокировкиВидаДеятельностиРасходов Тогда
			Результат.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Реквизиты.ВидДеятельностиРасходов.Имя);
		КонецЕсли;
		Результат.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Реквизиты.ВариантРаспределенияРасходовРегл.Имя);
		Результат.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Реквизиты.ВариантРаспределенияРасходовУпр.Имя);
	КонецЕсли;
	Если РазрешитьРедактированиеАналитикаРасходов Тогда
		Результат.Добавить("ТипЗначения");
	КонецЕсли;
	
	СтатьиРасходовЛокализация.ДополнитьБлокируемыеРеквизитыОбъекта(Результат, Истина);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
