#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список реквизитов, которые разрешается редактировать
//  с помощью обработки группового изменения объектов.
// 
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Возврат РаботаСФайлами.РеквизитыРедактируемыеВГрупповойОбработке();
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(ВладелецФайла)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ИзменениеОбъектаРазрешено(ВладелецФайла)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Справочники.ПередачаМатериаловВПроизводствоПрисоединенныеФайлы.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ПередачаМатериаловВПроизводствоПрисоединенныеФайлы.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("bccd56bd-dd40-4a62-8735-68e62b405807");
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты = "РегистрСведений.ДвоичныеДанныеФайлов,"
		+ "Документ.ВнутреннееПотреблениеТоваров,"
		+ "Документ.ПередачаМатериаловВПроизводство,"
		+ "Справочник.ВнутреннееПотреблениеТоваровПрисоединенныеФайлы";
	Обработчик.ИзменяемыеОбъекты = "РегистрСведений.ДвоичныеДанныеФайлов";
	Обработчик.БлокируемыеОбъекты = "РегистрСведений.ДвоичныеДанныеФайлов";
	Обработчик.Комментарий = НСтр("ru='Обновляет данные присоединенных файлов документов ""Передача материалов в производство"".
    |Пока работа обработчика не завершена присоединенные файлы могут быть недоступны.'
    |;uk='Оновлює дані приєднаних файлів документів ""Передача матеріалів у виробництво"".
    |Поки робота обробника не завершена, приєднані файли можуть бути недоступні.'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПередачаМатериаловВПроизводство.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВнутреннееПотреблениеТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После"; 
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РаботаСФайлами.ПеренестиЭлектронныеПодписиИСертификатыШифрованияВРегистрыСведений";
	НоваяСтрока.Порядок = "После";          
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ДвоичныеДанныеФайлов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";          

КонецПроцедуры	

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "Справочник.ПередачаМатериаловВПроизводствоПрисоединенныеФайлы";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТаблицаСсылокДляОбработки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(
		Параметры.Очередь, 
		ПолноеИмяОбъекта, 
		МенеджерВременныхТаблиц
	);
	
	Параметры.ОбработкаЗавершена = НЕ ТаблицаСсылокДляОбработки.ЕстьДанныеДляОбработки;
	Если НЕ ТаблицаСсылокДляОбработки.ЕстьДанныеДляОбработки Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ТаблицаСсылокДляОбработки.ЕстьЗаписиВоВременнойТаблице Тогда
		Возврат;
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НовыйФайл.Ссылка КАК НовыйФайлСсылка,
	|	МАКСИМУМ(ЕСТЬNULL(СтарыйФайл.Ссылка, НЕОПРЕДЕЛЕНО)) КАК СтарыйФайлСсылка
	|ПОМЕСТИТЬ ТаблицаФайлов
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ВТОбъектыДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПередачаМатериаловВПроизводствоПрисоединенныеФайлы КАК НовыйФайл
	|		ПО НовыйФайл.Ссылка = ВТОбъектыДляОбработки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДвоичныеДанныеФайлов КАК ДанныеНовогоФайла
	|		ПО НовыйФайл.Ссылка = ДанныеНовогоФайла.Файл
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВПроизводство КАК НовыйДокумент
	|		ПО (НовыйДокумент.Ссылка = НовыйФайл.ВладелецФайла)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров КАК СтарыйДокумент
	|		ПО (СтарыйДокумент.Номер = НовыйДокумент.Номер)
	|			И (СтарыйДокумент.Дата = НовыйДокумент.Дата)
	|			И (СтарыйДокумент.ХозяйственнаяОперация = &ХозяйственнаяОперация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнутреннееПотреблениеТоваровПрисоединенныеФайлы КАК СтарыйФайл
	|		ПО (СтарыйФайл.ВладелецФайла = СтарыйДокумент.Ссылка)
	|			И (СтарыйФайл.Наименование = НовыйФайл.Наименование)
	|			И (СтарыйФайл.Размер = НовыйФайл.Размер)
	|			И (СтарыйФайл.ДатаСоздания = НовыйФайл.ДатаСоздания)
	|			И (СтарыйФайл.ДатаМодификацииУниверсальная = НовыйФайл.ДатаМодификацииУниверсальная)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДвоичныеДанныеФайлов КАК ДанныеСтарогоФайла
	|		ПО (СтарыйФайл.Ссылка = ДанныеСтарогоФайла.Файл)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоСтарыйФайл КАК ВТЗаблокированоСтарыйФайл
	|		ПО (ВТЗаблокированоСтарыйФайл.Ссылка = СтарыйФайл.Ссылка)
	|ГДЕ
	|	ВТЗаблокированоСтарыйФайл.Ссылка ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	НовыйФайл.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаФайлов.НовыйФайлСсылка,
	|	ТаблицаФайлов.СтарыйФайлСсылка,
	|	НовыйФайл.ВерсияДанных КАК НовыйФайлВерсияДанных,
	|	СтарыйФайл.ВерсияДанных КАК СтарыйФайлВерсияДанных
	|ИЗ
	|	ТаблицаФайлов КАК ТаблицаФайлов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПередачаМатериаловВПроизводствоПрисоединенныеФайлы КАК НовыйФайл
	|		ПО (НовыйФайл.Ссылка = ТаблицаФайлов.НовыйФайлСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнутреннееПотреблениеТоваровПрисоединенныеФайлы КАК СтарыйФайл
	|		ПО (СтарыйФайл.Ссылка = ТаблицаФайлов.СтарыйФайлСсылка)";
	
	// Нужно получить заблокированные объекты, чтобы начать обновление только после их разблокировки
	ТаблицыДляЧтения = Новый СписокЗначений;
	ТаблицыДляЧтения.Добавить("Справочник.ВнутреннееПотреблениеТоваровПрисоединенныеФайлы", "ВТЗаблокированоСтарыйФайл");
	Для каждого ПолноеИмяОбъекта Из ТаблицыДляЧтения Цикл
		Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
			Параметры.Очередь, 
			ПолноеИмяОбъекта.Значение,
			МенеджерВременныхТаблиц
		);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПолноеИмяОбъекта.Представление, Результат.ИмяВременнойТаблицы);
	КонецЦикла; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТОбъектыДляОбработки", ТаблицаСсылокДляОбработки.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПередачаВПроизводство);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
 	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 			ЭлементБлокировки = Блокировка.Добавить("Справочник.ПередачаМатериаловВПроизводствоПрисоединенныеФайлы");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.НовыйФайлСсылка);
 			ЭлементБлокировки = Блокировка.Добавить("Справочник.ВнутреннееПотреблениеТоваровПрисоединенныеФайлы");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.СтарыйФайлСсылка);
 			Блокировка.Заблокировать();
 			
 			НовыйФайлОбъект = Выборка.НовыйФайлСсылка.ПолучитьОбъект();
 			СтарыйФайлОбъект = Выборка.СтарыйФайлСсылка.ПолучитьОбъект();

			Если НовыйФайлОбъект = Неопределено 
				ИЛИ СтарыйФайлОбъект = Неопределено 
				ИЛИ НЕ ЗначениеЗаполнено(Выборка.СтарыйФайлСсылка) Тогда
 				ОтменитьТранзакцию();
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.НовыйФайлСсылка);
 				Продолжить;
 			КонецЕсли;
			
			Если НовыйФайлОбъект.ВерсияДанных <> Выборка.НовыйФайлВерсияДанных
				ИЛИ СтарыйФайлОбъект.ВерсияДанных <> Выборка.СтарыйФайлВерсияДанных Тогда
 				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли; 
			
			НаборЗаписей = РегистрыСведений.ДвоичныеДанныеФайлов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Файл.Установить(Выборка.СтарыйФайлСсылка);
			НаборЗаписей.Прочитать();
			НаборЗаписей.Отбор.Файл.Установить(Выборка.НовыйФайлСсылка);
			Для каждого ЗаписьНабора Из НаборЗаписей Цикл
				ЗаписьНабора.Файл = Выборка.НовыйФайлСсылка;
			КонецЦикла; 
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей, Истина);
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.НовыйФайлСсылка);
	
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось обработать объект: %Ссылка% по причине: %Причина%';uk='Не вдалося обробити об''єкт: %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.НовыйФайлСсылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.НовыйФайлСсылка.Метаданные(),
				Выборка.НовыйФайлСсылка,
				ТекстСообщения
			);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НовыйФайл.Ссылка КАК НовыйФайлСсылка
	|ИЗ
	|	Справочник.ПередачаМатериаловВПроизводствоПрисоединенныеФайлы КАК НовыйФайл
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДвоичныеДанныеФайлов КАК ДанныеНовогоФайла
	|		ПО НовыйФайл.Ссылка = ДанныеНовогоФайла.Файл
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВПроизводство КАК НовыйДокумент
	|		ПО (НовыйДокумент.Ссылка = НовыйФайл.ВладелецФайла)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров КАК СтарыйДокумент
	|		ПО (СтарыйДокумент.Номер = НовыйДокумент.Номер)
	|			И (СтарыйДокумент.Дата = НовыйДокумент.Дата)
	|			И (СтарыйДокумент.ХозяйственнаяОперация = &ХозяйственнаяОперация)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВнутреннееПотреблениеТоваровПрисоединенныеФайлы КАК СтарыйФайл
	|		ПО (СтарыйФайл.ВладелецФайла = СтарыйДокумент.Ссылка)
	|			И (СтарыйФайл.Наименование = НовыйФайл.Наименование)
	|			И (СтарыйФайл.Размер = НовыйФайл.Размер)
	|			И (СтарыйФайл.ДатаСоздания = НовыйФайл.ДатаСоздания)
	|			И (СтарыйФайл.ДатаМодификацииУниверсальная = НовыйФайл.ДатаМодификацииУниверсальная)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДвоичныеДанныеФайлов КАК ДанныеСтарогоФайла
	|		ПО (СтарыйФайл.Ссылка = ДанныеСтарогоФайла.Файл)
	|ГДЕ
	|	ДанныеНовогоФайла.Файл ЕСТЬ NULL 
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПередачаВПроизводство);
	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("НовыйФайлСсылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли
