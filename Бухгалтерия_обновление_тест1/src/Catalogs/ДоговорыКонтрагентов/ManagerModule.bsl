#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Добавляет команду создания объекта.
//
// Параметры:
//   КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Справочники.ДоговорыКонтрагентов) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Справочники.ДоговорыКонтрагентов);
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Устанавливает статус договоров.
//
// Параметры:
//	Договоры - Массив - Массив ссылок на договоры;
//	Статус - ПеречислениеСсылка.СтатусыДоговоровКонтрагентов - Статус, который будет установлен у договоров.
//
// Возвращаемое значение:
//	Число - Количество обработанных объектов.
//
Функция УстановитьСтатус(Договоры, Статус) Экспорт
	
	МассивСсылок = Новый Массив();
	КоличествоОбработанных = 0;
	
	Для Каждого Договор Из Договоры Цикл
		
		Если ТипЗнч(Договор) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСсылок.Добавить(Договор);
		
	КонецЦикла;
	
	Если МассивСсылок = 0 Тогда
		Возврат КоличествоОбработанных;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.ПометкаУдаления КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА ДоговорыКонтрагентов.Статус = &Статус
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатусСовпадает
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка В(&МассивСсылок)");
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.УстановитьПараметр("Статус", Статус);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПометкаУдаления Тогда
			
			ТекстОшибки = НСтр("ru='Договор %Договор% помечен на удаление. Невозможно изменить статус';uk='Договір %Договор% позначений на вилучення. Неможливо змінити статус'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Договор%", Выборка.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
			Продолжить;
			
		КонецЕсли;
		
		Если Выборка.СтатусСовпадает Тогда
			
			ТекстОшибки = НСтр("ru='Договору %Договор% уже присвоен статус ""%Статус%""';uk='Договору %Договор% вже присвоєно статус ""%Статус%""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Договор%", Выборка.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%", Статус);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
			Продолжить;
			
		КонецЕсли;
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Договор%. %ОписаниеОшибки%';uk='Не вдалося заблокувати %Договор%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Договор%", Выборка.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
			
		КонецПопытки;
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.Статус = Статус;
		
		Если Статус = Перечисления.СтатусыДоговоровКонтрагентов.НеСогласован Тогда
			Если Объект.Согласован Тогда
				Объект.Согласован = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если Не Объект.ПроверитьЗаполнение() Тогда
			Продолжить;
		КонецЕсли;
			
		Попытка
			
			Объект.Записать();
			КоличествоОбработанных = КоличествоОбработанных + 1;
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось записать %Договор%. %ОписаниеОшибки%';uk='Не вдалося записати %Договор%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Договор%", Выборка.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
			
		КонецПопытки
		
	КонецЦикла;
	
	Возврат КоличествоОбработанных;
	
КонецФункции

// Процедура заполняет банковские счета документа по договору.
//
// Параметры:
//	Договор - СправочникСсылка.ДоговорыКонтрагентов - Договор, указанный в документе;
//	БанковскийСчетОрганизации - СправочникСсылка.БанковскиеСчетаОрганизаций - Реквизит документа "Банковский счет организации";
//	БанковскийСчетКонтрагента - СправочникСсылка.БанковскиеСчетаКонтрагентов - Реквизит документа "Банковский счет контрагента".
//
Процедура ЗаполнитьБанковскиеСчетаПоДоговору(Договор, БанковскийСчетОрганизации, БанковскийСчетКонтрагента) Экспорт
	
	ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "БанковскийСчет, БанковскийСчетКонтрагента"); 
		
	Если БанковскийСчетОрганизации <> Неопределено И ЗначениеЗаполнено(ДанныеДоговора.БанковскийСчет) Тогда
		БанковскийСчетОрганизации = ДанныеДоговора.БанковскийСчет;
	КонецЕсли;
	Если БанковскийСчетКонтрагента <> Неопределено И ЗначениеЗаполнено(ДанныеДоговора.БанковскийСчетКонтрагента) Тогда
		БанковскийСчетКонтрагента = ДанныеДоговора.БанковскийСчетКонтрагента;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает учетную информацию по договору
//
// Параметры:
//	Договор - СправочникСсылка.ДоговорыКонтрагентов - Договор, указанный в документе
//
// Возвращаемое значение:
//	Структура - Реквизиты договора с учетной информацией
//
Функция УчетнаяИнформацияПоДоговору(Договор) Экспорт
	
	Если ЗначениеЗаполнено(Договор) Тогда
		Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор,
			"СтатьяДвиженияДенежныхСредств, НаправлениеДеятельности, ГруппаФинансовогоУчета");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает хозяйственную операцию по умолчанию
//
// Параметры:
//  ТипДоговора - ПеречислениеСсылка.ТипыДоговоров - Тип договора, для которого определяется хозяйственная операция.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция.
//
Функция ХозяйственнаяОперация(ТипДоговора, ВариантОформленияЗакупок) Экспорт
	Типы = Перечисления.ТипыДоговоров;
	Операции = Перечисления.ХозяйственныеОперации;
	
	//++ НЕ УТ
	Если ТипДоговора = Типы.СДавальцем Тогда
		Возврат Операции.ПроизводствоИзДавальческогоСырья;
	ИначеЕсли ТипДоговора = Типы.СПереработчиком Тогда
		Возврат Операции.ПроизводствоУПереработчика;
	КонецЕсли;
	//-- НЕ УТ            
	Если ТипДоговора = Типы.СПоклажедателем Тогда
		Возврат Операции.ПриемНаХранениеСПравомПродажи;
	ИначеЕсли ТипДоговора = Типы.СХранителем Тогда
		Возврат Операции.ПередачаНаХранениеСПравомПродажи;
	КонецЕсли;
	
	ТоварыВПути = (ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.ТоварыВПути);
	НеотфактурованныеПоставки = (ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеотфактурованныеПоставки);
	
	ОперацияЗакупкаУПоставщика = Операции.ЗакупкаУПоставщика;
	ОперацияЗакупкаУПоставщика = 
		?(ТоварыВПути, Операции.ЗакупкаУПоставщикаТоварыВПути, ОперацияЗакупкаУПоставщика);
	ОперацияЗакупкаУПоставщика = 
		?(НеотфактурованныеПоставки, Операции.ЗакупкаУПоставщикаФактуровкаПоставки, ОперацияЗакупкаУПоставщика);
		
	ОперацияЗакупкаПоИмпорту = Операции.ЗакупкаПоИмпорту;
	ОперацияЗакупкаПоИмпорту = 
		?(ТоварыВПути, Операции.ЗакупкаПоИмпортуТоварыВПути, ОперацияЗакупкаПоИмпорту);
		
	
	ХозяйственнаяОперация = 
		?(ТипДоговора = Типы.СПоставщиком,    ОперацияЗакупкаУПоставщика,
		?(ТипДоговора = Типы.Импорт,          ОперацияЗакупкаПоИмпорту,
        ?(ТипДоговора = Типы.ИмпортКомиссия,  Операции.ПриемНаКомиссиюИмпорт,
		?(ТипДоговора = Типы.СПокупателем,    Операции.РеализацияКлиенту,
		?(ТипДоговора = Типы.СКомиссионером,  Операции.ПередачаНаКомиссию,
		?(ТипДоговора = Типы.СКомитентом,     Операции.ПриемНаКомиссию,
		?(ТипДоговора = Типы.СКомитентомНаЗакупку,     Операции.ПоставкаПодПринципала,
		Неопределено)))))));
	
	Возврат ХозяйственнаяОперация;
	
КонецФункции

// Возвращает тип договора по умолчанию
//
// Параметры:
// ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция, для которой определяется
//                                                                    тип договора.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыДоговоров - тип договора.
//
Функция ТипДоговора(ХозяйственнаяОперация) Экспорт
	
	Типы = Перечисления.ТипыДоговоров;
	Операции = Перечисления.ХозяйственныеОперации;
	
	//++ НЕ УТ
	Если ХозяйственнаяОперация = Операции.ПроизводствоИзДавальческогоСырья Тогда
		Возврат Типы.СДавальцем;
	ИначеЕсли ХозяйственнаяОперация = Операции.ПроизводствоУПереработчика Тогда
		Возврат Типы.СПереработчиком;
	КонецЕсли;
	//-- НЕ УТ            
	Если ХозяйственнаяОперация = Операции.ПередачаНаХранениеСПравомПродажи Тогда
		Возврат Типы.СХранителем;
	ИначеЕсли ХозяйственнаяОперация = Операции.ПриемНаХранениеСПравомПродажи Тогда
		Возврат Типы.СПоклажедателем;
	КонецЕсли;
	
	ОперацииЗакупки = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ОперацииИмпорта = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	
	Возврат
		?(ХозяйственнаяОперация = Операции.РеализацияКлиенту 
			И ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами"), Типы.СПокупателем,
		?(ХозяйственнаяОперация = Операции.ПередачаНаКомиссию, Типы.СКомиссионером,
		?(ОперацииЗакупки.Найти(ХозяйственнаяОперация) <> Неопределено
			И ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками"), Типы.СПоставщиком,
		?(ХозяйственнаяОперация = Операции.ПриемНаКомиссию, Типы.СКомитентом,
		?(ОперацииИмпорта.Найти(ХозяйственнаяОперация) <> Неопределено, Типы.Импорт,
        ?(ХозяйственнаяОперация = Операции.ПриемНаКомиссиюИмпорт, Типы.ИмпортКомиссия, 
		?(ХозяйственнаяОперация = Операции.ПоставкаПодПринципала, Типы.СКомитентомНаЗакупку,
		Неопределено)))))));
	
КонецФункции

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
//
// Возвращаемое значение:
//	Массив - имена блокируемых реквизитов.
//
// Возвращаемое значение:
//  Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	Результат = Новый Массив;
	Результат.Добавить("ТипДоговора");
	Результат.Добавить("Партнер");
	Результат.Добавить("Контрагент");
	Результат.Добавить("Организация");
	Результат.Добавить("ОплатаВВалюте");
	Результат.Добавить("ВалютаВзаиморасчетов");
	Результат.Добавить("ПорядокРасчетов");
	Результат.Добавить("НаправлениеДеятельности");
	Результат.Добавить("Подразделение");
	Результат.Добавить("ГруппаФинансовогоУчета");
	Результат.Добавить("ВариантОформленияЗакупок; ВариантОформленияЗакупок, ВариантОформленияЗакупокДвумяДокументами");
	Результат.Добавить("ВариантПриемкиТоваров; ОформлениеОрдера, ПриемкаТоваров");
	Результат.Добавить("МоментОпределенияБазыНДС");
	
	Возврат Результат;
КонецФункции

// Определяет реквизиты выбранного элемента справочника
//
// Параметры:
//	Ссылка - СправочникСсылка.ДоговорыКонтрагентов - Ссылка на элемент справочника.
//
// Возвращаемое значение:
//	Структура - реквизиты выбранного элемента справочника.
//
Функция РеквизитыОбъекта(Ссылка) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Данные.Дата КАК Дата,
	|	Данные.Организация КАК Организация,
	|	Данные.Партнер КАК Партнер,
	|	Данные.Контрагент КАК Контрагент,
	|	Данные.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Данные.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Данные.Сумма КАК СуммаДокумента,
	|	Данные.Сумма КАК СуммаВзаиморасчетов,
	|	&Ссылка КАК Договор,
	|	Данные.ПорядокРасчетов КАК ПорядокРасчетов,
	|	НЕ Данные.ПометкаУдаления КАК Проведен
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Данные
	|ГДЕ
	|	Данные.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Дата = Выборка.Дата;
		Организация = Выборка.Организация;
		Партнер = Выборка.Партнер;
		Контрагент = Выборка.Контрагент;
		Договор = Выборка.Договор;
		ПорядокРасчетов = Выборка.ПорядокРасчетов;
		ВалютаВзаиморасчетов = Выборка.ВалютаВзаиморасчетов;
		ХозяйственнаяОперация = ?(ЗначениеЗаполнено(Выборка.ХозяйственнаяОперация), Выборка.ХозяйственнаяОперация, Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
		СуммаДокумента = Выборка.СуммаДокумента;
		СуммаВзаиморасчетов = ?(Выборка.Проведен, Выборка.СуммаВзаиморасчетов, 0);
	Иначе
		Дата = Дата(1,1,1);
		Организация = Справочники.Организации.ПустаяСсылка();
		Партнер = Справочники.Партнеры.ПустаяСсылка();
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПустаяСсылка();
		ВалютаВзаиморасчетов = Справочники.Валюты.ПустаяСсылка();
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
		СуммаДокумента = 0;
		СуммаВзаиморасчетов = 0;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура();
	СтруктураРеквизитов.Вставить("Дата", Дата);
	СтруктураРеквизитов.Вставить("Организация", Организация);
	СтруктураРеквизитов.Вставить("Партнер", Партнер);
	СтруктураРеквизитов.Вставить("Контрагент", Контрагент);
	СтруктураРеквизитов.Вставить("Договор", Договор);
	СтруктураРеквизитов.Вставить("ПорядокРасчетов", ПорядокРасчетов);
	СтруктураРеквизитов.Вставить("ВалютаВзаиморасчетов", ВалютаВзаиморасчетов);
	СтруктураРеквизитов.Вставить("ХозяйственнаяОперация", ХозяйственнаяОперация);
	СтруктураРеквизитов.Вставить("СуммаДокумента", СуммаДокумента);
	СтруктураРеквизитов.Вставить("СуммаВзаиморасчетов", СуммаВзаиморасчетов);
	
	Возврат СтруктураРеквизитов;

КонецФункции

// Возвращает признак использования как агрегирующей сущности в товарах к поступлению.
//
// Параметры:
//  ВариантПриемкиТоваров - ПеречислениеСсылка.ВариантыПриемкиТоваров - ссылка на вариант приемки.
//
// Возвращаемое значение:
//  Булево - используется или нет договор при приемке.
//
Функция ДоговорИспользуетсяПриПриемке(Знач ВариантПриемкиТоваров) Экспорт
	
	Если Не ЗначениеЗаполнено(ВариантПриемкиТоваров) Тогда
		ВариантПриемкиТоваров = Константы.ВариантПриемкиТоваров.Получить();
	КонецЕсли;
	
	Результат = Ложь;
	Если ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных
		Или ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных 
		Или ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных Тогда

		Результат = Истина;

	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает структуру параметров для заполнения налогообложения НДС продажи.
//
// Параметры:
//  Объект - СправочникОбъект.ДоговорыКонтрагентов - договор, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  Структура - Параметры заполнения, описание параметров см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи();
//
Функция ПараметрыЗаполненияНалогообложенияНДСПродажи(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи();
	
	ПараметрыЗаполнения.Организация = Объект.Организация;
	ПараметрыЗаполнения.НаправлениеДеятельности = Объект.НаправлениеДеятельности;

	Если Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем Тогда
		
		ПараметрыЗаполнения.РеализацияТоваров = Истина;
		ПараметрыЗаполнения.РеализацияРаботУслуг = Истина;
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СКомиссионером Тогда
		
		ПараметрыЗаполнения.ПередачаНаКомиссию = Истина;
		ПараметрыЗаполнения.ОтчетКомиссионера = Истина;
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем Тогда
		
		ПараметрыЗаполнения.ОтчетДавальцу = Истина;
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СХранителем Тогда
		
		ПараметрыЗаполнения.ВыкупТоваровХранителем = Истина;
		
	КонецЕсли;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Возвращает структуру параметров для заполнения налогообложения НДС закупки.
//
// Параметры:
//  Объект - СправочникОбъект.ДоговорыКонтрагентов - договор, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  Структура - Параметры заполнения, описание параметров см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСЗакупки();
//
Функция ПараметрыЗаполненияНалогообложенияНДСЗакупки(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСЗакупки();
	
	ПараметрыЗаполнения.Контрагент = Объект.Контрагент;

	Если Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
		
		ПараметрыЗаполнения.ПриобретениеТоваров = Истина;
		ПараметрыЗаполнения.ПриобретениеРабот = Истина;
		ПараметрыЗаполнения.ПриобретениеНаСтатьи = Истина;
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоклажедателем Тогда
		
		ПараметрыЗаполнения.ПриобретениеТоваров = Истина;
		 
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком Тогда
		
		ПараметрыЗаполнения.ПриобретениеРабот = Истина;
	
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СКомитентом Тогда
		
		ПараметрыЗаполнения.ПриемНаКомиссию = Истина;
		
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.Импорт Тогда
		
		ПараметрыЗаполнения.ИмпортТоваров = Истина;
		
	КонецЕсли;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Возвращает структуру параметров для заполнения вида деятельности НДС.
//
// Параметры:
//  Объект - СправочникОбъект.ДоговорыКонтрагентов - 
//
// Возвращаемое значение:
//  см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС
//
Функция ПараметрыЗаполненияВидаДеятельностиНДС(Объект) Экспорт

	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС();
	ПараметрыЗаполнения.Организация = Объект.Организация;
	ПараметрыЗаполнения.НаправлениеДеятельности = Объект.НаправлениеДеятельности;
	
	Если Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
		
		ПараметрыЗаполнения.ПриобретениеТоваров = Истина;
		ПараметрыЗаполнения.ПриобретениеРабот = Истина;
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоклажедателем Тогда
		
		ПараметрыЗаполнения.ПриобретениеТоваров = Истина;
		 
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком Тогда
		
		ПараметрыЗаполнения.ПриобретениеРабот = Истина;
		
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.Импорт Тогда
		
		ПараметрыЗаполнения.ПриобретениеТоваров = Истина;
		
	КонецЕсли;
	
	Возврат ПараметрыЗаполнения;

КонецФункции

// Возвращает параметры механизма взаиморасчетов.
// 
// Возвращаемое значение:
// 	Массив из см. ВзаиморасчетыСервер.ПараметрыМеханизма
//
Функция ПараметрыВзаиморасчеты(ТипДоговора = Неопределено, Сумма = 0) Экспорт
	
	МассивПараметров = Новый Массив();
	
	#Область ОсновнаяСтруктура
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	СтруктураПараметров.ЭтоДоговор                       = Истина;
	
	//Определяет какой регистр двигают параметры, какие общие формы, перечисления и справочники использовать.
	Если ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем 
		ИЛИ ТипДоговора = Перечисления.ТипыДоговоров.СКомиссионером
		ИЛИ ТипДоговора = Перечисления.ТипыДоговоров.СХранителем
		ИЛИ ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем
		ИЛИ ТипДоговора = Перечисления.ТипыДоговоров.СКомитентомНаЗакупку Тогда
		СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
	Иначе
		СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
	КонецЕсли;

	СтруктураПараметров.ИзменяетПланОплаты = Сумма <> 0;
	СтруктураПараметров.ИзменяетПланОтгрузкиПоставки = СтруктураПараметров.ИзменяетПланОплаты;
	
	СтруктураПараметров.Дата                             = "Объект.Дата"; 
	
	//Валюта и сумма операции. Обязательно путь к реквизитам объекта.
	СтруктураПараметров.ВалютаДокумента                  = "Объект.ВалютаВзаиморасчетов";
	СтруктураПараметров.СуммаДокумента                   = "Объект.Сумма";
	СтруктураПараметров.Партнер                          = "Объект.Партнер";
	СтруктураПараметров.Договор                          = "Объект.Ссылка";
	СтруктураПараметров.ПорядокРасчетов                  = "Объект.ПорядокРасчетов";
	СтруктураПараметров.Соглашение                       = "";
	
	//Используются для определения значения ОплатаВВалюте и в форме редактирования правил оплаты.
	СтруктураПараметров.БанковскийСчетОрганизации        = "Объект.БанковскийСчет";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.ФормаОплаты                      = "";
	
	СтруктураПараметров.Менеджер                         = "Объект.Менеджер";
	СтруктураПараметров.НомерВходящегоДокумента          = "Объект.Номер";
	СтруктураПараметров.ДатаВходящегоДокумента           = "Объект.Дата";
	
	//Имя кнопки, открывающей помощник зачета оплат для текущего набора параметров.
	СтруктураПараметров.ЭлементыФормы.ЗачетОплаты            = "ЗачетОплатыФорма";
	//Имя элемента формы содержащего группу финансового учета для отражения текущего набора параметров по БУ.
	СтруктураПараметров.ЭлементыФормы.ГруппаФинансовогоУчета = "ГруппаФинансовогоУчета";
	
	#Область СостояниеВзаиморасчетов
	
	//Имя гиперссылки, отображающей состояние расчетов и открывающей соответствующий отчет,
	СтруктураПараметров.ЭлементыФормы.НадписьРасчеты                   = "ДекорацияСостояниеРасчетов";
	
	#КонецОбласти
	
	#Область ОграниченияЗадолженностиПоДоговору
	
	//Гиперссылка отображающая состояние ограничения задолженности
	СтруктураПараметров.ЭлементыФормы.ОграничениеЗадолженностиТекст    = "ДекорацияОграничениеЗадолженности";
	//Картинка отображающая запрет отгрузки
	СтруктураПараметров.ЭлементыФормы.ОграничениеЗадолженностиКартинка = "КартинкаОтгрузкаЗапрещена"; 
	
	#КонецОбласти
	
	МассивПараметров.Добавить(СтруктураПараметров);
	
	#КонецОбласти
	
	#Область ВознаграждениеПоКомиссии
	
	Если ТипДоговора = Перечисления.ТипыДоговоров.СКомиссионером 
		ИЛИ ТипДоговора = Перечисления.ТипыДоговоров.СКомитентом Тогда 
		
		СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
		
		СтруктураПараметров.ЭтоДоговор                       = Истина;
		
		//Определяет какой регистр двигают параметры, какие общие формы, перечисления и справочники использовать.
		Если ТипДоговора = Перечисления.ТипыДоговоров.СКомиссионером Тогда
			СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
		ИначеЕсли ТипДоговора = Перечисления.ТипыДоговоров.СКомитентом Тогда
			СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
		КонецЕсли;
	
		СтруктураПараметров.ИзменяетПланОплаты = Ложь;
		СтруктураПараметров.ИзменяетПланОтгрузкиПоставки = Ложь;
		
		СтруктураПараметров.Дата                             = "Объект.Дата"; 
		
		//Валюта и сумма операции. Обязательно путь к реквизитам объекта.
		СтруктураПараметров.ВалютаДокумента                  = "Объект.ВалютаВзаиморасчетов";
		СтруктураПараметров.СуммаДокумента                   = "Объект.Сумма";
		СтруктураПараметров.Партнер                          = "Объект.Партнер";
		СтруктураПараметров.Договор                          = "Объект.Ссылка";
		СтруктураПараметров.ПорядокРасчетов                  = "Объект.ПорядокРасчетов";
		СтруктураПараметров.Соглашение                       = "";
		
		//Используются для определения значения ОплатаВВалюте и в форме редактирования правил оплаты.
		СтруктураПараметров.БанковскийСчетОрганизации        = "Объект.БанковскийСчет";
		СтруктураПараметров.Касса                            = "";
		СтруктураПараметров.ФормаОплаты                      = "";
		
		СтруктураПараметров.Менеджер                         = "Объект.Менеджер";
		СтруктураПараметров.НомерВходящегоДокумента          = "Объект.Номер";
		СтруктураПараметров.ДатаВходящегоДокумента           = "Объект.Дата";
		
		МассивПараметров.Добавить(СтруктураПараметров);
	
	КонецЕсли;
	
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

// Функция возвращает текст запроса получения товаров к доставке по распоряжению
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаТоварыКДоставке() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварыКПоступлениюОстатки.ДокументПоступления КАК Ссылка,
	|	ТоварыКПоступлениюОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлениюОстатки.Характеристика КАК Характеристика,
	|	ТоварыКПоступлениюОстатки.Назначение КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТоварыКПоступлениюОстатки.Склад КАК Склад,
	|	ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(, ДокументПоступления В (&Ссылки)) КАК ТоварыКПоступлениюОстатки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЭтоВнешнеэкономическийДоговор(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
		
	Возврат ((Объект.ВалютаВзаиморасчетов <> Константы.ВалютаРегламентированногоУчета.Получить()) ИЛИ
	         (Объект.ТипДоговора = Перечисления.ТипыДоговоров.Импорт) ИЛИ
	         (Объект.ТипДоговора = Перечисления.ТипыДоговоров.ИмпортКомиссия));
						   
КонецФункции // ЭтоВнешнеэкономическийДоговор
		 
// Определяет, необходимо ли указание номенклатуры на аванс для данного договора
Функция ИспользоватьНоменклатуруЗаполненияНалоговыхНаАванс(Объект) Экспорт
	
	// Доступно только тогда, когда есть настраиваемые моменты определения базы НДС
	Возврат ((Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем) ИЛИ 
	         (Объект.ТипДоговора = Перечисления.ТипыДоговоров.СКомитентом)) И 
			(Объект.МоментОпределенияБазыНДС <> Перечисления.МоментыОпределенияНалоговойБазы.ПоОтгрузке);
						   
КонецФункции // ИспользоватьНоменклатуруЗаполненияНалоговыхНаАванс

// Функция возвращает значение по умолчанию для реквизита МоментОпределенияБазыНДСПоТипуДоговора на основании данных
// организации. Если для данного договора МоментОпределенияБазыНДС не настраивается, то возвращается Неопределено
//
Функция ПолучитьМоментОпределенияБазыНДСПоУмолчанию(Объект) Экспорт
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Внешнеэкономический = ЭтоВнешнеэкономическийДоговор(Объект);
	
	Если ЗначениеЗаполнено(Объект.ТипДоговора) Тогда
		
		Если Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем Тогда
			Если Не Внешнеэкономический Тогда
				Возврат Объект.Организация.МоментОпределенияБазыНДСПоПродажам;
			Иначе
				Возврат Объект.Организация.МоментОпределенияБазыНДСПоЭкспорту;
			КонецЕсли;
			
		ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
			Если Не Внешнеэкономический Тогда
				Возврат Объект.Организация.МоментОпределенияБазыНДСПоЗакупкам;
			Иначе
				Возврат Неопределено;
			КонецЕсли;
			
		ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СКомиссионером Тогда
			Если Не Внешнеэкономический Тогда
				Возврат Объект.Организация.МоментОпределенияБазыНДСПоЗакупкам;
			Иначе
				Возврат Неопределено;				
			КонецЕсли;
			
		ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СКомитентом Тогда
			Если Не Внешнеэкономический Тогда
			    Возврат Объект.Организация.МоментОпределенияБазыНДСПоПродажам;
			Иначе
				Возврат Объект.Организация.МоментОпределенияБазыНДСПоПродажам;				
			КонецЕсли;
			
		ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.Импорт Тогда
			Возврат Неопределено;				
			
		ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.ИмпортКомиссия Тогда
			Возврат Объект.Организация.МоментОпределенияБазыНДСПоПродажам;
			
		ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем Тогда
			Возврат Объект.Организация.МоментОпределенияБазыНДСПоПродажам;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
		
КонецФункции // ПолучитьМоментОпределенияБазыНДСПоУмолчанию



#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК Т2 
	|	ПО Т2.Родитель = Т.Партнер
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т2.Партнер)
	|	И ЗначениеРазрешено(Т.Организация)
	|	И ЗначениеРазрешено(Т.ХозяйственнаяОперация)";
	
	Ограничение.ТекстДляВнешнихПользователей =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПользователи КАК ВнешниеПользователиПартнеры
	|	ПО ВнешниеПользователиПартнеры.ОбъектАвторизации = ЭтотСписок.Партнер
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|	ПО КонтактныеЛицаПартнеров.Владелец = ЭтотСписок.Партнер
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПользователи КАК ВнешниеПользователиКонтактныеЛица
	|	ПО ВнешниеПользователиКонтактныеЛица.ОбъектАвторизации = КонтактныеЛицаПартнеров.Ссылка
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ВнешниеПользователиПартнеры.Ссылка)
	|	ИЛИ ЗначениеРазрешено(ВнешниеПользователиКонтактныеЛица.Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	
	ТекстСоединений = "";
	ТекстУсловий    = "";
	НомерОтбора     = 0;
	
	Если Параметры.Свойство("Партнер") Тогда
		
		Запрос.УстановитьПараметр("Партнер", Параметры.Партнер);
		
		ТекстСоединений = ТекстСоединений + "
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|	ПО Договоры.Партнер = ИерархияПартнеров.Родитель
		|		И ИерархияПартнеров.Партнер = &Партнер";
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
	Если Параметры.Свойство("Соглашение") Тогда
		
		ПоСоглашениюИспользуютсяДоговорыКонтрагентов = Ложь;
		Если ЗначениеЗаполнено(Параметры.Соглашение) Тогда
			УстановитьПривилегированныйРежим(Истина);
			ПоСоглашениюИспользуютсяДоговорыКонтрагентов = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(Параметры.Соглашение, "ИспользуютсяДоговорыКонтрагентов");
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;	
		
		Если ПоСоглашениюИспользуютсяДоговорыКонтрагентов = Ложь Тогда
			Запрос.УстановитьПараметр("Соглашение", Параметры.Соглашение);
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами")
				И Тип("СправочникСсылка.СоглашенияСКлиентами") = ТипЗнч(Параметры.Соглашение) Тогда
				
				ТекстСоединений = ТекстСоединений + "
				|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
				|	ПО Договоры.ОплатаВВалюте = СоглашенияСКлиентами.ОплатаВВалюте
				|		И СоглашенияСКлиентами.Ссылка = &Соглашение";
				
				СтандартнаяОбработка = Ложь;
				
			ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками")
				И Тип("СправочникСсылка.СоглашенияСПоставщиками") = ТипЗнч(Параметры.Соглашение)
				И ЗначениеЗаполнено(Параметры.Соглашение) Тогда
			
				ТекстСоединений = ТекстСоединений + "
				|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками КАК СоглашенияСПоставщиками
				|	ПО Договоры.ОплатаВВалюте = СоглашенияСПоставщиками.ОплатаВВалюте
				|		И СоглашенияСПоставщиками.Ссылка = &Соглашение";
				
				СтандартнаяОбработка = Ложь;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если Параметры.Свойство("Отбор") Тогда
		
		Для Каждого ТекущийОтбор Из Параметры.Отбор Цикл
			
			Если ТекущийОтбор.Ключ = "Контрагент" И ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
				
				Если Не Запрос.Параметры.Свойство("Партнер") Тогда
					
					Запрос.УстановитьПараметр("Контрагент", ТекущийОтбор.Значение);
					
					ТекстСоединений = ТекстСоединений + "
					|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
					|	ПО Договоры.Партнер = Контрагенты.Партнер
					|		И Контрагенты.Ссылка = &Контрагент";
					
					СтандартнаяОбработка = Ложь;
					
				КонецЕсли;
				
				Продолжить;
				
			КонецЕсли;
			
			Если ТекущийОтбор.Ключ = "ХозяйственнаяОперация" 
				И ТекущийОтбор.Значение = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности Тогда
				
				ТекстУсловий = ТекстУсловий + "
				|	И Договоры.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)";
				
				СтандартнаяОбработка = Ложь;
				
				Продолжить;
				
			КонецЕсли;
			
			Если ТекущийОтбор.Ключ = "ХозяйственнаяОперация" И
				(ТекущийОтбор.Значение = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика
					Или ТекущийОтбор.Значение = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути
					Или ТекущийОтбор.Значение = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки)
				И Параметры.Свойство("ПоказыватьЗакупкуПоИмпорту") Тогда
				
				ТекстУсловий = ТекстУсловий + "
				|	И Договоры.ХозяйственнаяОперация В (
				|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту),
				|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
				|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
				|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
				|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
				|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПроизводствоУПереработчика)
				|		)";
				
				СтандартнаяОбработка = Ложь;
				
				Продолжить;
				
			КонецЕсли;
			
			НомерОтбора = НомерОтбора + 1;
			
			ТекстУсловий = ТекстУсловий + "
			|	И Договоры." + ТекущийОтбор.Ключ + " В (&ЗначениеОтбора" + СокрЛП(НомерОтбора) + ")";
			
			Запрос.УстановитьПараметр("ЗначениеОтбора" + СокрЛП(НомерОтбора), ТекущийОтбор.Значение);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не СтандартнаяОбработка Тогда
		
		//Текст запроса содержит литералы для переопределения/подстановки //ТекстСоединений //ТекстУсловий
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	Договоры.Ссылка КАК Ссылка,
		               |	Договоры.Наименование КАК Наименование,
		               |	Договоры.Наименование КАК Совпадение,
		               |	1 КАК Порядок
		               |ПОМЕСТИТЬ ДоговорыПоиск 
					   |ИЗ
		               |	Справочник.ДоговорыКонтрагентов КАК Договоры
					   |
					   |//ТекстСоединений
					   |
		               |ГДЕ
		               |	Договоры.Наименование ПОДОБНО &СтрокаПоиска
					   |	//ТекстУсловий
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	Договоры.Ссылка,
		               |	Договоры.Наименование,
		               |	Договоры.Номер,
		               |	2
		               |ИЗ
		               |	Справочник.ДоговорыКонтрагентов КАК Договоры
					   |
					   |//ТекстСоединений
					   |
		               |ГДЕ
		               |	Договоры.Номер ПОДОБНО &СтрокаПоиска
					   |	//ТекстУсловий
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ДоговорыПоиск.Ссылка КАК Ссылка,
		               |	МИНИМУМ(ДоговорыПоиск.Порядок) КАК Порядок
		               |ПОМЕСТИТЬ ДоговорыПоПорядку
		               |ИЗ
		               |	ДоговорыПоиск КАК ДоговорыПоиск
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ДоговорыПоиск.Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЕСТЬNULL(ДоговорыПоиск.Ссылка, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК Ссылка,
		               |	ЕСТЬNULL(ДоговорыПоиск.Наименование, """") КАК Наименование,
		               |	ЕСТЬNULL(ДоговорыПоиск.Совпадение, """") КАК Совпадение,
		               |	ЕСТЬNULL(ДоговорыПоиск.Порядок, 0) КАК Порядок
		               |ИЗ
		               |	ДоговорыПоПорядку КАК ДоговорыПоПорядку
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыПоиск КАК ДоговорыПоиск
		               |		ПО ДоговорыПоПорядку.Ссылка = ДоговорыПоиск.Ссылка
		               |			И ДоговорыПоПорядку.Порядок = ДоговорыПоиск.Порядок
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ЕСТЬNULL(ДоговорыПоиск.Порядок, 0),
		               |	ЕСТЬNULL(ДоговорыПоиск.Совпадение, """"),
		               |	ЕСТЬNULL(ДоговорыПоиск.Наименование, """")";
		
		Если Не ПустаяСтрока(ТекстСоединений) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ТекстСоединений", ТекстСоединений);
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстУсловий) Тогда
			
			ТекстУсловий = Сред(ТекстУсловий, 3);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ТекстУсловий", ТекстУсловий);
			
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ТекстЗначение = ?(
				Выборка.Порядок = 1,
				Выборка.Наименование,
				СокрП(Выборка.Совпадение) + " (" + Выборка.Наименование + ")");
				
			ДанныеВыбора.Добавить(Выборка.Ссылка, ТекстЗначение);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Назначения

// Возвращает шаблон для генерации назначения
//
// Параметры:
// 		Объект - СправочникОбъект.ДоговорыКонтрагентов, ДанныеФормыСтруктура - объект по которому необходимо получить шаблон назначения.
//
// Возвращаемое значение:
// 		Структура - (см. функцию Справочники.Назначения.ШаблонНового).
//
Функция ШаблонНазначения(Объект) Экспорт
	
	ШаблонНазначения = Справочники.Назначения.ШаблонНового();
	
	Если Объект.ТипДоговора = Перечисления.ТипыДоговоров.СКомитентомНаЗакупку Тогда
		
		Если НаправленияДеятельностиСервер.ЭтоНаправлениеДеятельностиСОбособлениемТоваровИРабот(Объект.НаправлениеДеятельности) Тогда
			ШаблонНазначения.НаправлениеДеятельности = Объект.НаправлениеДеятельности;
		КонецЕсли;
		
		ШаблонНазначения.Партнер = Объект.Партнер;
		ШаблонНазначения.Договор = Объект.Ссылка;
		ШаблонНазначения.ТипНазначения = Перечисления.ТипыНазначений.ПоставкаПодПринципала;
		
	//++ НЕ УТ
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком Тогда
		
		Если НаправленияДеятельностиСервер.ЭтоНаправлениеДеятельностиСОбособлениемТоваровИРабот(Объект.НаправлениеДеятельности) Тогда
			ШаблонНазначения.НаправлениеДеятельности = Объект.НаправлениеДеятельности;
		КонецЕсли;
		
		ШаблонНазначения.Партнер = Объект.Партнер;
		ШаблонНазначения.Договор = Объект.Ссылка;
		
	ИначеЕсли Объект.ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем Тогда
		
		Если НаправленияДеятельностиСервер.ЭтоНаправлениеДеятельностиСОбособлениемТоваровИРабот(Объект.НаправлениеДеятельности) Тогда
			ШаблонНазначения.НаправлениеДеятельности = Объект.НаправлениеДеятельности;
		КонецЕсли;
		
		ШаблонНазначения.Партнер       = Объект.Партнер;
		ШаблонНазначения.Договор       = Объект.Ссылка;
		ШаблонНазначения.ТипНазначения = Перечисления.ТипыНазначений.ДавальческоеМатериалы22;
	//-- НЕ УТ
		
	КонецЕсли;
	
	Возврат ШаблонНазначения;
	
КонецФункции

#КонецОбласти

#Область ТекущиеДела

// Заполняет список текущих дел пользователя.
// Параметры: 
//     ТекущиеДела - см. ТекущиеДелаСлужебный.ТекущиеДела
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	ОбщиеПараметрыЗапросов = ТекущиеДелаСлужебный.ОбщиеПараметрыЗапросов();
	
	ЗаполнитьДелоДоговорыСКлиентами(ТекущиеДела, ОбщиеПараметрыЗапросов);
	ЗаполнитьДелоДоговорыСПоставщиками(ТекущиеДела, ОбщиеПараметрыЗапросов);
	
КонецПроцедуры

// Параметры: 
//     ТекущиеДела - см. ТекущиеДелаСлужебный.ТекущиеДела
// 
Процедура ЗаполнитьДелоДоговорыСКлиентами(ТекущиеДела, ОбщиеПараметрыЗапросов)
	
	ИмяФормы = "Справочник.ДоговорыКонтрагентов.Форма.ФормаСпискаПродажи";
	
	// Определим доступны ли текущему пользователю показатели группы
	Доступность =
		(ОбщиеПараметрыЗапросов.ЭтоПолноправныйПользователь
			Или ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКонтрагентов))
		И ДоступныДоговорыСКлиентами();
	
	Если НЕ Доступность Тогда
		Возврат;
	КонецЕсли;
	
	// Расчет показателей
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ДоговорСКлиентом.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.НеСогласован)
	|				ТОГДА ДоговорСКлиентом.Ссылка
	|		КОНЕЦ) КАК ДоговорыСКлиентамиНаСогласовании,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ДоговорСКлиентом.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.НеСогласован)
	|					И (ДоговорСКлиентом.ДатаНачалаДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|							И ДоговорСКлиентом.ДатаНачалаДействия < &ДатаАктуальности
	|						ИЛИ ДоговорСКлиентом.ДатаОкончанияДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|							И ДоговорСКлиентом.ДатаОкончанияДействия < &ДатаАктуальности)
	|				ТОГДА ДоговорСКлиентом.Ссылка
	|			КОГДА ДоговорСКлиентом.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|					И ДоговорСКлиентом.ДатаОкончанияДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|					И ДоговорСКлиентом.ДатаОкончанияДействия < &ДатаАктуальности
	|				ТОГДА ДоговорСКлиентом.Ссылка
	|		КОНЕЦ) КАК ДоговорыСКлиентамиПросроченные
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорСКлиентом
	|ГДЕ
	|	ДоговорСКлиентом.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Закрыт)
	|	И (ДоговорСКлиентом.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|			ИЛИ ДоговорСКлиентом.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|			ИЛИ ДоговорСКлиентом.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи))
	|	И ДоговорСКлиентом.Менеджер = &Пользователь
	|	И (НЕ ДоговорСКлиентом.ПометкаУдаления)";
	
	Результат = ТекущиеДелаСлужебный.ЧисловыеПоказателиТекущихДел(Запрос, ОбщиеПараметрыЗапросов);
	
	// Заполнение дел.
	// ДоговорыСКлиентами
	ДелоРодитель = ТекущиеДела.Добавить();
	ДелоРодитель.Идентификатор  = "ДоговорыСКлиентами";
	ДелоРодитель.Представление  = НСтр("ru='Договоры с клиентами';uk='Договори з клієнтами'");
	ДелоРодитель.Владелец       = Метаданные.Подсистемы.Продажи;
	
	// ДоговорыСКлиентамиНаСогласовании
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние", Перечисления.СостоянияДоговоровКонтрагентов.ОжидаетсяСогласование);
	ПараметрыОтбора.Вставить("Актуальность", "");
	ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Менеджер", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("Организация", Неопределено);
	ПараметрыОтбора.Вставить("Контрагент", Неопределено);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ДоговорыСКлиентамиНаСогласовании";
	Дело.ЕстьДела       = Результат.ДоговорыСКлиентамиНаСогласовании > 0;
	Дело.Представление  = НСтр("ru='Договоры на согласовании';uk='Договори на погодженні'");
	Дело.Количество     = Результат.ДоговорыСКлиентамиНаСогласовании;
	Дело.Важное         = Ложь;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	Дело.Владелец       = "ДоговорыСКлиентами";
	
	// ДоговорыСКлиентамиПросроченные
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние", Неопределено);
	ПараметрыОтбора.Вставить("Актуальность", НСтр("ru='Просроченные';uk='Прострочені'"));
	ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Менеджер", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("Организация", Неопределено);
	ПараметрыОтбора.Вставить("Контрагент", Неопределено);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ДоговорыСКлиентамиПросроченные";
	Дело.ЕстьДела       = Результат.ДоговорыСКлиентамиПросроченные > 0;
	Дело.Представление  = НСтр("ru='Просроченные договоры';uk='Прострочені договори'");
	Дело.Количество     = Результат.ДоговорыСКлиентамиПросроченные;
	Дело.Важное         = Истина;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	Дело.Владелец       = "ДоговорыСКлиентами";
	
	Если Результат.ДоговорыСКлиентамиНаСогласовании > 0
		Или Результат.ДоговорыСКлиентамиПросроченные > 0 Тогда
		ДелоРодитель.ЕстьДела = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДелоДоговорыСПоставщиками(ТекущиеДела, ОбщиеПараметрыЗапросов)
	
	ИмяФормы = "Справочник.ДоговорыКонтрагентов.Форма.ФормаСпискаЗакупки";
	
	// Определим доступны ли текущему пользователю показатели группы
	Доступность =
		(ОбщиеПараметрыЗапросов.ЭтоПолноправныйПользователь
			Или ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКонтрагентов))
		И ДоступныДоговорыСПоставщиками();
	
	Если НЕ Доступность Тогда
		Возврат;
	КонецЕсли;
	
	// Расчет показателей
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ДоговорСПоставщиком.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.НеСогласован)
	|				ТОГДА ДоговорСПоставщиком.Ссылка
	|		КОНЕЦ) КАК ДоговорыСПоставщикамиНаСогласовании,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ДоговорСПоставщиком.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.НеСогласован)
	|					И (ДоговорСПоставщиком.ДатаНачалаДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|							И ДоговорСПоставщиком.ДатаНачалаДействия < &ДатаАктуальности
	|						ИЛИ ДоговорСПоставщиком.ДатаОкончанияДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|							И ДоговорСПоставщиком.ДатаОкончанияДействия < &ДатаАктуальности)
	|				ТОГДА ДоговорСПоставщиком.Ссылка
	|			КОГДА ДоговорСПоставщиком.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|					И ДоговорСПоставщиком.ДатаОкончанияДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|					И ДоговорСПоставщиком.ДатаОкончанияДействия < &ДатаАктуальности
	|				ТОГДА ДоговорСПоставщиком.Ссылка
	|		КОНЕЦ) КАК ДоговорыСПоставщикамиПросроченные
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорСПоставщиком
	|ГДЕ
	|	ДоговорСПоставщиком.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Закрыт)
	|	И (ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	ИЛИ ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути)
	|	ИЛИ ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки)
	|	ИЛИ ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|	ИЛИ ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути)
    |	ИЛИ ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссиюИмпорт)
	|	ИЛИ ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
	|	ИЛИ ДоговорСПоставщиком.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи))
	|	И ДоговорСПоставщиком.Менеджер = &Пользователь
	|	И (НЕ ДоговорСПоставщиком.ПометкаУдаления)";
	
	Результат = ТекущиеДелаСлужебный.ЧисловыеПоказателиТекущихДел(Запрос, ОбщиеПараметрыЗапросов);
	
	// Заполнение дел.
	// ДоговорыСПоставщиками
	ДелоРодитель = ТекущиеДела.Добавить();
	ДелоРодитель.Идентификатор  = "ДоговорыСПоставщиками";
	ДелоРодитель.Представление  = НСтр("ru='Договоры с поставщиками';uk='Договори з постачальниками'");
	ДелоРодитель.Владелец       = Метаданные.Подсистемы.Закупки;
	
	// ДоговорыСПоставщикамиНаСогласовании
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние", Перечисления.СостоянияДоговоровКонтрагентов.ОжидаетсяСогласование);
	ПараметрыОтбора.Вставить("Актуальность", "");
	ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Менеджер", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("Организация", Неопределено);
	ПараметрыОтбора.Вставить("Контрагент", Неопределено);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ДоговорыСПоставщикамиНаСогласовании";
	Дело.ЕстьДела       = Результат.ДоговорыСПоставщикамиНаСогласовании > 0;
	Дело.Представление  = НСтр("ru='Договоры на согласовании';uk='Договори на погодженні'");
	Дело.Количество     = Результат.ДоговорыСПоставщикамиНаСогласовании;
	Дело.Важное         = Ложь;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	Дело.Владелец       = "ДоговорыСПоставщиками";
	
	// ДоговорыСПоставщикамиПросроченные
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Состояние", Неопределено);
	ПараметрыОтбора.Вставить("Актуальность", НСтр("ru='Просроченные';uk='Прострочені'"));
	ПараметрыОтбора.Вставить("ДатаСобытия", ОбщиеПараметрыЗапросов.ПустаяДата);
	ПараметрыОтбора.Вставить("Менеджер", ОбщиеПараметрыЗапросов.Пользователь);
	ПараметрыОтбора.Вставить("Организация", Неопределено);
	ПараметрыОтбора.Вставить("Контрагент", Неопределено);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ДоговорыСПоставщикамиПросроченные";
	Дело.ЕстьДела       = Результат.ДоговорыСПоставщикамиПросроченные > 0;
	Дело.Представление  = НСтр("ru='Просроченные договоры';uk='Прострочені договори'");
	Дело.Количество     = Результат.ДоговорыСПоставщикамиПросроченные;
	Дело.Важное         = Истина;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбора);
	Дело.Владелец       = "ДоговорыСПоставщиками";
	
	Если Результат.ДоговорыСПоставщикамиНаСогласовании > 0
		Или Результат.ДоговорыСПоставщикамиПросроченные > 0 Тогда
		ДелоРодитель.ЕстьДела = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ДоступныДоговорыСКлиентами()
	
	МассивХозяйственныхОпераций = Новый Массив;
	МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию);
	МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи);
	
	Возврат ЕстьПравоНаИзменениеДоговоров(МассивХозяйственныхОпераций);
	
КонецФункции

Функция ДоступныДоговорыСПоставщиками()
	
	МассивХозяйственныхОпераций = Новый Массив;
	МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПриемНаКомиссию);
    МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПриемНаКомиссиюИмпорт);
	МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи);
	
	Возврат ЕстьПравоНаИзменениеДоговоров(МассивХозяйственныхОпераций);
	
КонецФункции

Функция ЕстьПравоНаИзменениеДоговоров(МассивХозяйственныхОпераций)
	
	ТаблицаНаборы = УправлениеДоступом.ТаблицаНаборыЗначенийДоступа();
	
	// Заполним наборы значений доступа к договорам
	Сч = 1;
	Для Каждого ХозяйственнаяОперация Из МассивХозяйственныхОпераций Цикл
		СтрокаТаб = ТаблицаНаборы.Добавить();
		СтрокаТаб.НомерНабора     = Сч;
		СтрокаТаб.ЗначениеДоступа = ХозяйственнаяОперация;
		Сч = Сч+1;
	КонецЦикла;
	
	Возврат УправлениеДоступом.ЕстьРоль("ДобавлениеИзменениеДоговоровКонтрагентов", ТаблицаНаборы);
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	ДоговорыКонтрагентовЛокализация.ДобавитьКомандыПечати(КомандыПечати);
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	КомандаОтчет = ВзаиморасчетыСервер.ВедомостьРасчетовСПартнерами_ДобавитьКомандуОтчета(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.ВидимостьВФормах = "ФормаЭлемента,ФормаСпискаПродажи";
	КонецЕсли;

	ДоговорыКонтрагентовЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
КонецПроцедуры

#КонецОбласти

#Область ШаблоныСообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона:
//         * Имя            - Строка - Уникальное имя общего реквизита.
//         * Представление  - Строка - Представление общего реквизита.
//         * Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         * Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения:
//         * Имя            - Строка - Уникальное имя вложения.
//         * Представление  - Строка - Представление варианта.
//         * ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl"
//                                      и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие - список используемых в шаблоне реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие - список используемых в шаблоне общих реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие - значения реквизитов:
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS:
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - СправочникСсылка - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма.
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * ВариантОтправки - Строка - Варианты отправки письма: "Кому", "Копия", "СкрытаяКопия", "ОбратныйАдреса";
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

#Область ЗарегистрироватьДоговорыДляСозданияРегистраторовГрафикаДвиженияТоваров

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ДоговорыКонтрагентов.СоздатьРегистраторыГрафикаДвиженияТоваровПоДоговорам";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("10a5f1f0-db96-4090-b38f-1d646a9df042");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ДоговорыКонтрагентов.ЗарегистрироватьДоговорыДляСозданияРегистраторовГрафикаДвиженияТоваров";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Создает служебные документы ""Регистратор графика движения товаров""';uk='Створює службові документи ""Реєстратор графіка руху товарів""'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РегистраторГрафикаДвиженияТоваров.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.РегистраторГрафикаДвиженияТоваров.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.РегистраторГрафикаДвиженияТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляГенерацииНазначений";
	НоваяСтрока.Порядок = "После";
	
#КонецОбласти

#Область ОбработатьДанныеДляПереходаНаНовуюВерсию

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "3.5.4.400";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2f964a16-2d86-43da-a6a2-1cf4239e9453");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ДоговорыКонтрагентов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "Справочники.Назначения.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru='Заполняет 
                                   |- реквизиты ""Назначение"", ""Тип договора"", ""Вариант приемки товаров"", ""Хозяйственная операция"", ""Вариант оформления закупок"" и ""Способ доставки"";
                                   |- реквизит ""Порядок оформления списания недостач принятых на хранение товаров"" значением ""Оформлять выкуп принятых на хранение товаров"";
                                   |- признак оплаты в иностранной валюте;
                                   |- реквизит Подразделение в давальческих договорах.
                                   |Пока обработчик не выполнен, не возможно обособление сырья и материалов в переработке на стороне по договору, а так же оформление документов закупок (при использовании договоров).
                                   |'
                                   |;uk='Заповнює
                                   |- реквізити ""Призначення"", ""Тип договору"", ""Варіант приймання товарів"", ""Господарська операція"", ""Варіант оформлення закупівель"" та ""Спосіб доставки"";
                                   |- реквізит ""Порядок оформлення списання нестач прийнятих на зберігання товарів"" значенням ""Оформляти викуп прийнятих зберігання товарів"";
                                   |- ознаку оплати в іноземній валюті; 
                                   |- реквізит Підрозділ у давальницьких договорах. 
                                   |Поки обробник не виконаний, не можливе відокремлення сировини та матеріалів у переробці на стороні за договором, а також оформлення документів закупівель (при використанні договорів).'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтруктураПредприятия.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.Назначения.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.НаправленияДеятельности.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Справочники.НаправленияДеятельности.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказПоставщику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПриходныйОрдерНаТовары.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ГруппыФинансовогоУчетаРасчетов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ЗарегистрироватьДоговорыДляСозданияРегистраторовГрафикаДвиженияТоваров";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Назначения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.НаправленияДеятельности.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Партнеры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.СуммыДокументовВВалютахУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизаций.СгенерироватьДокументыДляПереброскиОстатковСПустогоНазначенияПоДавальческойСхеме";
	НоваяСтрока.Порядок = "Любой";
	
	

	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Справочники.ДоговорыКонтрагентов";
	НоваяСтрока.Порядок    = "После"; 
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Справочники.НаправленияДеятельности";
	НоваяСтрока.Порядок    = "После"; 
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыОбработатьДанныеДляГенерацииНазначений(
		Обработчик.ПриоритетыВыполнения,
		"Любой",
		Исключения	
	);
	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Документы.ПервичныйДокумент";
	НоваяСтрока.Порядок    = "После"; 
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыСгенерироватьОбъектыРасчетов(
		Обработчик.ПриоритетыВыполнения,
		"Любой",
		Исключения
	);

#КонецОбласти

//++ НЕ УТ

#Область ОбработатьДанныеДляГенерацииНазначений

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.5.4.400"; 
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляГенерацииНазначений";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("223a25d2-ec6b-4335-b320-c73a6f0fd5b2");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ДоговорыКонтрагентов.ЗарегистрироватьДанныеКОбработкеДляГенерацииНазначений";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.ЧитаемыеОбъекты = "Справочник.Назначения,"
		+ "Справочник.ДоговорыКонтрагентов,"
		+ "Справочник.НаправленияДеятельности";
	Обработчик.ИзменяемыеОбъекты = "Справочник.Назначения";
	Обработчик.БлокируемыеОбъекты = "Справочник.Назначения";
	Обработчик.Комментарий = НСтр("ru='Создает новые назначения связанные со справочником ""Договоры контрагентов""';uk='Створює нові призначення, пов''язані з довідником ""Договори контрагентів""'");
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.СебестоимостьТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ТоварыОрганизаций.СгенерироватьДокументыДляПереброскиОстатковСПустогоНазначенияПоДавальческойСхеме";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СборкаТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказПереработчику.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Партнеры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Назначения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.НаправленияДеятельности.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказНаСборку.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаявкаНаВозвратТоваровОтКлиента.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказНаПеремещение.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказНаВнутреннееПотребление.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказМатериаловВПроизводство.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ЗаказКлиента.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

	
	Исключения = ОбновлениеИнформационнойБазыПереопределяемый.ИсключенияПриДобавленииПриоритетов();
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Справочники.ДоговорыКонтрагентов";
	НоваяСтрока.Порядок    = "Удалить"; 
	НоваяСтрока = Исключения.Добавить();
	НоваяСтрока.ИмяОбъекта = "Справочники.НаправленияДеятельности";
	НоваяСтрока.Порядок    = "После"; 
	ОбновлениеИнформационнойБазыПереопределяемый.ДобавитьПриоритетыОбработатьДанныеДляГенерацииНазначений(
		Обработчик.ПриоритетыВыполнения,
		"Любой",
		Исключения	
	);

#КонецОбласти

//-- НЕ УТ

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Справочник.ДоговорыКонтрагентов";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	//Заполнение порядка оплаты
	|	(ДоговорыКонтрагентов.УдалитьПорядокОплаты <> ЗНАЧЕНИЕ(Перечисление.УдалитьПорядокОплатыПоСоглашениям.ПустаяСсылка)
	|	//Генерация объектов расчетов корректных
	|			ИЛИ ДоговорыКонтрагентов.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
	|				И НЕ ИСТИНА В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ
	|							Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|						ГДЕ
	|							ОбъектыРасчетов.Объект = ДоговорыКонтрагентов.Ссылка)
	|	//Генерация объектов расчетов исключений
	|			ИЛИ ДоговорыКонтрагентов.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
	|				И (ИСТИНА В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ
	|							РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|						ГДЕ
	|							РасчетыСПоставщиками.УдалитьЗаказПоставщику = ДоговорыКонтрагентов.Ссылка)
	|					ИЛИ ИСТИНА В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ
	|							РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|						ГДЕ
	|							РасчетыСКлиентами.УдалитьЗаказКлиента = ДоговорыКонтрагентов.Ссылка))
	|				И НЕ ИСТИНА В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ
	|							Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|						ГДЕ
	|							ОбъектыРасчетов.Объект = ДоговорыКонтрагентов.Ссылка))";
	
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Запрос = Новый Запрос;
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВариантОформленияЗакупок = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.ПустаяСсылка)";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПустаяСсылка)";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);	
	
	//++ НЕ УТ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Таблица
	|ГДЕ
	|	Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И Таблица.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПереработчиком)";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);	
	//-- НЕ УТ
	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ХозяйственнаяОперация В (&ОперацииОдноходовки)
	|	И ДоговорыКонтрагентов.ВариантОформленияЗакупок В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.ТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.НеотфактурованныеПоставки))";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ПустаяСсылка)";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);	
	
	//++ НЕ УТ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Таблица
	|ГДЕ
	|	Таблица.ПорядокОформленияСписанияНедостачПринятыхНаХранениеТоваров = ЗНАЧЕНИЕ(Перечисление.ПорядокОформленияСписанияНедостачПринятыхНаХранениеТоваров.ПустаяСсылка)
	|	И Таблица.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПоклажедателем)";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);	
	//-- НЕ УТ
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));

	ОперацииОдноходовки = Новый Массив();
	ОперацииОдноходовки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ОперацииОдноходовки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	
	Запрос.УстановитьПараметр("ОперацииОдноходовки", ОперацииОдноходовки);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка                     КАК Ссылка,
	|	ЕСТЬNULL(НаправленияДеятельности.Ссылка, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ОбъектыРасчетов.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                           КАК ТребуетсяСоздатьОбъектРасчетов
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.Объект = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК НаправленияДеятельности
	|		ПО ДоговорыКонтрагентов.НаправлениеДеятельности = НаправленияДеятельности.Ссылка
	|			И НаправленияДеятельности.УчетЗатрат
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка В (&ОбновляемыеДанные)
	|
	|" + ОбъектыРасчетовСервер.ТекстПроверкаИспользованияВРасчетныхРегистрах();

	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);

	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); 
	ЭлементСправочника = РезультатЗапроса[0].Выбрать();
	
	ИспользованиеВРасчетныхРегистрах = РезультатЗапроса[1].Выгрузить();
	ИспользованиеВРасчетныхРегистрах.Индексы.Добавить("ОбъектРасчетов, Организация, ТипРасчетов");
	
	ВариантПриемкиКонстанта = Константы.ВариантПриемкиТоваров.Получить();
	
	ОперацииОдноходовки = Новый Массив();
	ОперацииОдноходовки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ОперацииОдноходовки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);

	Пока ЭлементСправочника.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементСправочника.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			СправочникОбъект = ЭлементСправочника.Ссылка.ПолучитьОбъект();

			Если СправочникОбъект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ЭлементСправочника.Ссылка);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			
			Если ЗначениеЗаполнено(СправочникОбъект.УдалитьПорядокОплаты) Тогда
 					СправочникОбъект.ОплатаВВалюте = (СправочникОбъект.УдалитьПорядокОплаты = Перечисления.УдалитьПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВВалюте);
			КонецЕсли;
			
			//++ НЕ УТ
			Если СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком И СправочникОбъект.Назначение.Пустая() Тогда
				
				ШаблонНазначения = Справочники.Назначения.ШаблонНового();
				ШаблонНазначения.НаправлениеДеятельности = ЭлементСправочника.НаправлениеДеятельности;
				ШаблонНазначения.Партнер                 = СправочникОбъект.Партнер;
				ШаблонНазначения.Договор                 = СправочникОбъект.Ссылка;
				
				Назначение = Справочники.Назначения.НайтиПоШаблону(ШаблонНазначения);
				
				Если ЗначениеЗаполнено(Назначение) Тогда
					СправочникОбъект.Назначение = Назначение;
				Иначе
					ВызватьИсключение НСтр("ru='В информационной базе не обнаружено нужное назначение';uk='В інформаційній базі не виявлено потрібне призначення'");
				КонецЕсли;
				
			КонецЕсли;
			//-- НЕ УТ
			
			
			Если СправочникОбъект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.ПустаяСсылка() Тогда
				СправочникОбъект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеРазделять;
			КонецЕсли;
			
			Если СправочникОбъект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПустаяСсылка() Тогда
				СправочникОбъект.ВариантПриемкиТоваров = ВариантПриемкиКонстанта;
			КонецЕсли;
			
			Если СправочникОбъект.СпособДоставки = Перечисления.СпособыДоставки.ПустаяСсылка() Тогда
				СправочникОбъект.СпособДоставки = Перечисления.СпособыДоставки.ОпределяетсяВРаспоряжении;
			КонецЕсли;
			
			Если СправочникОбъект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.ТоварыВПути
				И ОперацииОдноходовки.Найти(СправочникОбъект.ХозяйственнаяОперация) <> Неопределено
				И СправочникОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика Тогда
				СправочникОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути;
			ИначеЕсли СправочникОбъект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.ТоварыВПути
				И ОперацииОдноходовки.Найти(СправочникОбъект.ХозяйственнаяОперация) <> Неопределено
				И СправочникОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
				СправочникОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути;
			ИначеЕсли СправочникОбъект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеотфактурованныеПоставки
				И ОперацииОдноходовки.Найти(СправочникОбъект.ХозяйственнаяОперация) <> Неопределено
				И СправочникОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика Тогда
				СправочникОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки;
			КонецЕсли;
			
			//++ НЕ УТ
			Если СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПоклажедателем
				И Не ЗначениеЗаполнено(СправочникОбъект.ПорядокОформленияСписанияНедостачПринятыхНаХранениеТоваров) Тогда
				
				СправочникОбъект.ПорядокОформленияСписанияНедостачПринятыхНаХранениеТоваров = Перечисления.ПорядокОформленияСписанияНедостачПринятыхНаХранениеТоваров.ОформлятьВыкупПринятыхНаХранениеТоваров;
				
			КонецЕсли;
			//-- НЕ УТ
			
			Если ЭлементСправочника.ТребуетсяСоздатьОбъектРасчетов Тогда
				
				ПараметрыГенерации = ПараметрыВзаиморасчеты(СправочникОбъект.ТипДоговора, СправочникОбъект.Сумма);
				
				Если Не ТипЗнч(ПараметрыГенерации) = Тип("Массив") Тогда
					ПараметрыГенерации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыГенерации);
				КонецЕсли;
				
				ДополненныеПараметрыМеханизма = ВзаиморасчетыСервер.ДополненныеПараметрыМеханизма(СправочникОбъект, ПараметрыГенерации);
				ОбъектыРасчетовСервер.ДополнитьПараметрыГенерацииИсключениями(ПараметрыГенерации, ИспользованиеВРасчетныхРегистрах, СправочникОбъект);
				
				Для Каждого ПараметрГенерации Из ДополненныеПараметрыМеханизма.МассивПараметров Цикл
					НовыйОбъектРасчетов = ОбъектыРасчетовСервер.ПроверитьЗаполнитьОбъектРасчетовПоСтруктуре(СправочникОбъект, ПараметрГенерации);
					Если Не ЗначениеЗаполнено(НовыйОбъектРасчетов) Тогда
						ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Не удалось создать объект расчетов для договора: %1.';uk='Не вдалося створити об''єкт розрахунків для договору: %1.'"),
							ЭлементСправочника.Ссылка));
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			Если СправочникОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ЭлементСправочника.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ЭлементСправочника.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДоговорыДляСозданияРегистраторовГрафикаДвиженияТоваров(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистраторГрафикаДвиженияТоваров КАК РегистраторГрафикаДвиженияТоваров
	|		ПО РегистраторГрафикаДвиженияТоваров.Распоряжение = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	РегистраторГрафикаДвиженияТоваров.Ссылка ЕСТЬ NULL";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура СоздатьРегистраторыГрафикаДвиженияТоваровПоДоговорам(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Справочник.ДоговорыКонтрагентов";
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
	ПараметрыСинхронизацииКлючей = Новый Соответствие;
	ПараметрыСинхронизацииКлючей.Вставить("Документ.РегистраторГрафикаДвиженияТоваров", "ПометкаУдаления");
	
	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("Документ.РегистраторГрафикаДвиженияТоваров");
 			ЭлементБлокировки.УстановитьЗначение("Распоряжение", Выборка.Ссылка);
			
 			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Блокировка.Заблокировать();
			
			ОбъектДоговор = Выборка.Ссылка.ПолучитьОбъект();
			
			ОбщегоНазначенияУТ.ПодготовитьДанныеДляСинхронизацииКлючей(ОбъектДоговор, ПараметрыСинхронизацииКлючей);
			ОбъектДоговор.ДополнительныеСвойства.ЭтоНовый = Истина;
			ОбщегоНазначенияУТ.СинхронизироватьКлючи(ОбъектДоговор);
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбъектДоговор.Ссылка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);		
	
КонецПроцедуры

//++ НЕ УТ

Процедура ЗарегистрироватьДанныеКОбработкеДляГенерацииНазначений(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|		ПО Таблица.Ссылка = Назначения.Договор
	|			И Таблица.Партнер = Назначения.Партнер
	|			И (ВЫБОР
	|				КОГДА ЕСТЬNULL(Таблица.НаправлениеДеятельности.УчетЗатрат, ИСТИНА)
	|					ТОГДА Таблица.НаправлениеДеятельности
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			КОНЕЦ = Назначения.НаправлениеДеятельности)
	|			И Назначения.Заказ = НЕОПРЕДЕЛЕНО
	|			И Назначения.ТипНазначения В (ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Собственное),
	|										  ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.УдалитьПереработчик))
	|ГДЕ
	|	Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И Назначения.Ссылка ЕСТЬ NULL
	|	И Таблица.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПереработчиком)
	|
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляГенерацииНазначений(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Справочник.ДоговорыКонтрагентов";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДопПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДопПараметры.ИмяВременнойТаблицы = "ВТСсылкиДляОбработки";
	ДопПараметры.ДополнительныеИсточникиДанных.Вставить("НаправлениеДеятельности");
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(
		Параметры.Очередь, 
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц, 
		ДопПараметры
	);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	Если Не ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, "Справочник.Назначения") Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КОбработке.Ссылка КАК Ссылка,
	|	КОбработке.Ссылка.ВерсияДанных КАК ВерсияДанных,
	|	КОбработке.Ссылка.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА КОбработке.Ссылка.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПереработчиком) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Собственное)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПустаяСсылка)
	|	КОНЕЦ КАК ТипНазначения,
	|	ЕСТЬNULL(НаправленияДеятельности.Ссылка, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности
	|ИЗ
	|	ВТСсылкиДляОбработки КАК КОбработке
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК НаправленияДеятельности
	|		ПО КОбработке.Ссылка.НаправлениеДеятельности = НаправленияДеятельности.Ссылка
	|			И (НаправленияДеятельности.УчетЗатрат)");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			ЭлементБлокировки = Блокировка.Добавить("Справочник.Назначения");
			ЭлементБлокировки.УстановитьЗначение("НаправлениеДеятельности", Выборка.НаправлениеДеятельности);
			ЭлементБлокировки.УстановитьЗначение("Заказ",                   Неопределено);
			ЭлементБлокировки.УстановитьЗначение("Партнер",                 Выборка.Партнер);
			ЭлементБлокировки.УстановитьЗначение("Договор",                 Выборка.Ссылка);
			ЭлементБлокировки.УстановитьЗначение("ТипНазначения",           Выборка.ТипНазначения);
			
			Блокировка.Заблокировать();
			
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если Объект <> Неопределено Тогда
				
				Если Не ЗначениеЗаполнено(Объект.Назначение) И Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком Тогда
					
					ШаблонНазначения = Справочники.Назначения.ШаблонНового();
					ШаблонНазначения.НаправлениеДеятельности = Выборка.НаправлениеДеятельности;
					ШаблонНазначения.Партнер                 = Объект.Партнер;
					ШаблонНазначения.Договор                 = Объект.Ссылка;
					
					Назначение = Справочники.Назначения.НайтиПоШаблону(ШаблонНазначения);
					
					Если Не ЗначениеЗаполнено(Назначение) Тогда
						
						НазначениеОбъект = Справочники.Назначения.СоздатьЭлемент();
						ЗаполнитьЗначенияСвойств(НазначениеОбъект, ШаблонНазначения);
						
						НазначениеОбъект.Наименование                = Справочники.Назначения.ПредставлениеНазначенияДляЗаписиВИнформационнуюБазу(ШаблонНазначения, Неопределено);
						НазначениеОбъект.ПометкаУдаления             = Объект.ПометкаУдаления;
						НазначениеОбъект.КонтролироватьТолькоНаличие = Истина;
						НазначениеОбъект.ДвиженияПоСкладскимРегистрам = Истина;
						
						ОбновлениеИнформационнойБазы.ЗаписатьДанные(НазначениеОбъект, Истина)
						
					КонецЕсли;
					
				КонецЕсли;
				
				
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#КонецЕсли
