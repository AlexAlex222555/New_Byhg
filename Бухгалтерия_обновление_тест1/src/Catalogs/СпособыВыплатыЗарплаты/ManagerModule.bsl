#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область МетодыПолученияСпособовВыплаты

// Возвращает способ выплаты зарплаты по умолчанию
//
// Возвращаемое значение:
//  СправочникСсылка.СпособыВыплатыЗарплаты - способ выплаты зарплаты по умолчанию
//
Функция ПоУмолчанию() Экспорт
	Возврат ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СпособыВыплатыЗарплаты.Зарплата");
КонецФункции	

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	ВзаиморасчетыССотрудникамиВызовСервера.СпособыВыплатыЗарплатыОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

Процедура НачальноеЗаполнение() Экспорт
	ВзаиморасчетыССотрудникамиВнутренний.СпособыВыплатыЗарплатыНачальноеЗаполнение();
КонецПроцедуры

Процедура ОбновитьУдаленныеПредопределенные() Экспорт
	
	СпособВыплатыСсылка = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СпособыВыплатыЗарплаты.Зарплата");
	
	Если СпособВыплатыСсылка <> Неопределено И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СпособВыплатыСсылка, "ПометкаУдаления") Тогда
		
		СпособВыплаты = СпособВыплатыСсылка.ПолучитьОбъект();
		СпособВыплаты.ПометкаУдаления = Ложь;
		СпособВыплаты.Записать();
		
	КонецЕсли;
	
	ЗапросПоискаОбъекта = Новый Запрос;
	
	ЗапросПоискаОбъекта.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	                            |	СпособыВыплатыЗарплаты.Ссылка КАК Ссылка
	                            |ИЗ
	                            |	Справочник.СпособыВыплатыЗарплаты КАК СпособыВыплатыЗарплаты
	                            |ГДЕ
	                            |	СпособыВыплатыЗарплаты.ПометкаУдаления
	                            |	И СпособыВыплатыЗарплаты.Наименование = &Наименование";
	
	ЗапросПоискаОбъекта.УстановитьПараметр("Наименование", "Аванс");
	
	Выборка = ЗапросПоискаОбъекта.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		СпособВыплаты = Выборка.Ссылка.ПолучитьОбъект();
		СпособВыплаты.ПометкаУдаления = Ложь;
		СпособВыплаты.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПризнакПоставляемый() Экспорт
	ВзаиморасчетыССотрудникамиВнутренний.СпособыВыплатыЗарплатыЗаполнитьПризнакПоставляемый()	
КонецПроцедуры

Процедура ЗаполнитьОкругление() Экспорт
	
	СпособОкругленияПоУмолчанию = Справочники.СпособыОкругленияПриРасчетеЗарплаты.ПоУмолчанию();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыВыплатыЗарплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыВыплатыЗарплаты КАК СпособыВыплатыЗарплаты
	|ГДЕ
	|	СпособыВыплатыЗарплаты.Округление = ЗНАЧЕНИЕ(Справочник.СпособыОкругленияПриРасчетеЗарплаты.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СпособВыплаты = Выборка.Ссылка.ПолучитьОбъект();
		СпособВыплаты.Округление = СпособОкругленияПоУмолчанию;
		СпособВыплаты.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьХарактерВыплаты() Экспорт
	
	СпособВыплатыЗарплата = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СпособыВыплатыЗарплаты.Зарплата");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыВыплатыЗарплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыВыплатыЗарплаты КАК СпособыВыплатыЗарплаты
	|ГДЕ
	|	СпособыВыплатыЗарплаты.ХарактерВыплаты = ЗНАЧЕНИЕ(Перечисление.ХарактерВыплатыЗарплаты.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СпособВыплаты = Выборка.Ссылка.ПолучитьОбъект();
		
		Если Выборка.Ссылка = СпособВыплатыЗарплата Тогда
			СпособВыплаты.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
		Иначе
			СпособВыплаты.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс;
		КонецЕсли;	
		
		СпособВыплаты.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСпособПолученияЗарплатыКВыплате() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыВыплатыЗарплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыВыплатыЗарплаты КАК СпособыВыплатыЗарплаты
	|ГДЕ
	|	СпособыВыплатыЗарплаты.СпособПолучения = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияЗарплатыКВыплате.ПустаяСсылка)";
	
	ОбновляемыеДанные = Запрос.Выполнить();

	ВыборкаОбновляемыхДанных = ОбновляемыеДанные.Выбрать();
	Пока ВыборкаОбновляемыхДанных.Следующий() Цикл
		
		СпособВыплаты = ВыборкаОбновляемыхДанных.Ссылка.ПолучитьОбъект();
		Если СпособВыплаты.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс Тогда
			СпособВыплаты.СпособПолучения = Перечисления.СпособыПолученияЗарплатыКВыплате.Аванс;
		ИначеЕсли СпособВыплаты.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата Тогда
			СпособВыплаты.СпособПолучения = Перечисления.СпособыПолученияЗарплатыКВыплате.ОкончательныйРасчет;
		ИначеЕсли СпособВыплаты.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Межрасчет Тогда
			СпособВыплаты.СпособПолучения = Перечисления.СпособыПолученияЗарплатыКВыплате.ОтдельныйРасчет;
		Иначе
			СпособВыплаты.СпособПолучения = Перечисления.СпособыПолученияЗарплатыКВыплате.ОкончательныйРасчет;
		КонецЕсли;	
		
		СпособВыплаты.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсправитьВыплатуУвольнения() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыВыплатыЗарплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыВыплатыЗарплаты КАК СпособыВыплатыЗарплаты
	|ГДЕ
	|	СпособыВыплатыЗарплаты.СпособПолучения = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияЗарплатыКВыплате.ПустаяСсылка)
	|	И СпособыВыплатыЗарплаты.ХарактерВыплаты = ЗНАЧЕНИЕ(Перечисление.ХарактерВыплатыЗарплаты.Зарплата)
	|	И СпособыВыплатыЗарплаты.ВидДокументаОснования  = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовМежрасчетныхНачислений.Увольнение)";
	
	ОбновляемыеДанные = Запрос.Выполнить();

	ВыборкаОбновляемыхДанных = ОбновляемыеДанные.Выбрать();
	Пока ВыборкаОбновляемыхДанных.Следующий() Цикл
		
		СпособВыплаты = ВыборкаОбновляемыхДанных.Ссылка.ПолучитьОбъект();
		СпособВыплаты.СпособПолучения = Перечисления.СпособыПолученияЗарплатыКВыплате.ОкончательныйРасчет;
		СпособВыплаты.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Межрасчет;
		
		СпособВыплаты.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВыплатуОтпускаПодРасчет() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыВыплатыЗарплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыВыплатыЗарплаты КАК СпособыВыплатыЗарплаты
	|ГДЕ
	|	СпособыВыплатыЗарплаты.СпособПолучения = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияЗарплатыКВыплате.ОкончательныйРасчет)
	|	И СпособыВыплатыЗарплаты.ВидДокументаОснования  = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовМежрасчетныхНачислений.Отпуск)";
	
	ОбновляемыеДанные = Запрос.Выполнить();
	
	Если ОбновляемыеДанные.Пустой() Тогда
		
		СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.СоздатьЭлемент();
		
		СпособВыплаты.Наименование = НСтр("ru='Отпуска (под расчет)';uk='Відпустки (під розрахунок)'",МультиязычностьУкр.КодЯзыкаИнформационнойБазы());;
		СпособВыплаты.ВидДокументаОснования	= Перечисления.ВидыДокументовМежрасчетныхНачислений.Отпуск;
		СпособВыплаты.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Межрасчет;
		СпособВыплаты.СпособПолучения = Перечисления.СпособыПолученияЗарплатыКВыплате.ОкончательныйРасчет;
		СпособВыплаты.Округление = Справочники.СпособыОкругленияПриРасчетеЗарплаты.ПоУмолчанию();
		СпособВыплаты.ПроцентВыплаты = 100;
		СпособВыплаты.Поставляемый = Истина;
		
		СпособВыплаты.Записать();
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
	
#КонецОбласти

#КонецЕсли