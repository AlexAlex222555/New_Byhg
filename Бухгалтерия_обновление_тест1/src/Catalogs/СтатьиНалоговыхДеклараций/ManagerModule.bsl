#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
Функция ПолучитьСписокСтатейПоНаборуСтатей(НаборСтатейНалоговыхДеклараций, ВключаяРодителей = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НаборыСтатейНалоговыхДеклараций.Статья КАК Статья
	|ИЗ
	|	РегистрСведений.НаборыСтатейНалоговыхДеклараций КАК НаборыСтатейНалоговыхДеклараций
	|ГДЕ
	|	НаборыСтатейНалоговыхДеклараций.Набор = &Набор";
	
	Если ВключаяРодителей Тогда
		Запрос.Текст = Запрос.Текст + "
		|ИТОГИ ПО
		|	Статья ИЕРАРХИЯ";		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Набор", НаборСтатейНалоговыхДеклараций);
	
	СписокСтатейСРодителями = Новый СписокЗначений;
	СписокСтатейСРодителями.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Статья"));
	
	Возврат СписокСтатейСРодителями;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ВидНалоговойДекларации = Неопределено;
	Если Параметры.Свойство("ВидНалоговойДекларации", ВидНалоговойДекларации) Тогда
		Параметры.Отбор.Вставить("ВидНалоговойДекларации", ВидНалоговойДекларации);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ОписаниеОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.СтатьиНалоговыхДеклараций.ОбновитьПредопределенныеСтатьи";
	Обработчик.Версия = "*";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("f77f15e9-a232-4c75-9256-f1187ee05694");
	Обработчик.Комментарий = НСтр("ru='Обновляет предопределенные элементы справочника ""Статьи налоговых деклараций"".';uk='Оновлює напередвизначені елементи довідника ""Статті податкових декларацій"".'");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.СтатьиНалоговыхДеклараций.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры

Процедура ОбновитьПредопределенныеСтатьи(Параметры = Неопределено) Экспорт
	
	Обработки.НачальноеЗаполнениеИОбновлениеОбъектов.ЗаполнитьОбъект("Справочник", "СтатьиНалоговыхДеклараций");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли