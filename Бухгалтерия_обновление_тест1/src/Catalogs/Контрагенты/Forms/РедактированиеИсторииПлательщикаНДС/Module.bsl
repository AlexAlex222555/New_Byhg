
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();

	Если НЕ Параметры.Свойство("ИсторияПлательщикаНДС")
		ИЛИ НЕ Параметры.Свойство("ТекущийПлательщикНДС") ИЛИ НЕ Параметры.Свойство("ТекущийИННПлательщикаНДС") ИЛИ НЕ Параметры.Свойство("ТекущийКодФилиала") Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
    
	ИсторияПлательщикаНДС.Загрузить(Параметры.ИсторияПлательщикаНДС.Выгрузить());
    Если ИсторияПлательщикаНДС.Количество() = 0 Тогда
        СтрокаИсторияПлательщикаНДС = ИсторияПлательщикаНДС.Добавить();
        СтрокаИсторияПлательщикаНДС.ПлательщикНДС     = Параметры.ТекущийПлательщикНДС;
        СтрокаИсторияПлательщикаНДС.ИННПлательщикаНДС = Параметры.ТекущийИННПлательщикаНДС;
        СтрокаИсторияПлательщикаНДС.КодФилиала        = Параметры.ТекущийКодФилиала;
    КонецЕсли;
	
	Если НЕ ПравоДоступа("Редактирование", Метаданные.Справочники.Контрагенты) Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Если ТолькоПросмотр Тогда
		
		Элементы.ФормаОтмена.КнопкаПоУмолчанию = Истина;
		
	КонецЕсли;
    
    УстановитьПризнакПервоначальногоЗначения(ИсторияПлательщикаНДС);
    
	Если ИсторияПлательщикаНДС.Количество() > 0 Тогда
		Элементы.ИсторияПлательщикаНДС.ТекущаяСтрока = ИсторияПлательщикаНДС[ИсторияПлательщикаНДС.Количество()-1].ПолучитьИдентификатор();
	КонецЕсли;
	
	КоличествоСтрокИстории = ИсторияПлательщикаНДС.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не ВыполняетсяЗакрытие Тогда
		
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		
		ТекстВопроса = НСтр("ru='Данные были изменены, перенести изменения?';uk='Дані були змінені, перенести зміни?'");
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВопросОПереносеИзмененийПослеОтвета", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыИсторияПлательщикаНДС

&НаКлиенте
Процедура ИсторияПлательщикаНДСПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)    
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			
			НовыйПериод = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
			ТекущиеДанные.НачальноеЗначение = Ложь;
			
			// Получим максимальный период в таблице
			ПоследнийПериод = '00010101000000';
            Для Каждого ЗаписьИстории Из ИсторияПлательщикаНДС Цикл    
				Если ЗаписьИстории.Период > ПоследнийПериод Тогда
					ПоследнийПериод = ЗаписьИстории.Период;
				КонецЕсли;
			КонецЦикла;
			
			Если НовыйПериод <= ПоследнийПериод Тогда
				НовыйПериод = НачалоДня(ПоследнийПериод + 60*60*24);
			КонецЕсли;
			
			ТекущиеДанные.Период = НовыйПериод;
            
            ЭтотОбъект.ТекущийЭлемент = Элементы.ИсторияПлательщикаНДСИННПлательщикаНДС;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПлательщикаНДСПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)    
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
    УстановитьПризнакПервоначальногоЗначения(ИсторияПлательщикаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПлательщикаНДСПередУдалением(Элемент, Отказ)    
	
    Если ИсторияПлательщикаНДС.Индекс(Элемент.ТекущиеДанные) = 0 Тогда    
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияПлательщикаНДСПриИзменении(Элемент)    
    КоличествоСтрокИстории = ИсторияПлательщикаНДС.Количество();
	ОтключитьОтметкуНезаполненного();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиИзменения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПризнакПервоначальногоЗначения(ТаблицаИстории)
	
	ТаблицаИстории.Сортировать("Период");
	Если ТаблицаИстории.Количество() > 0 Тогда
		ПерваяСтрока = ТаблицаИстории[0];
		ПерваяСтрока.НачальноеЗначение = Истина;
		ПерваяСтрока.Период = '00010101';
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиИзменения()
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеИстории(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыполняетсяЗакрытие = Истина;
	РезультатВыбора = Новый Структура;
    РезультатВыбора.Вставить("ИсторияПлательщикаНДС", ИсторияПлательщикаНДС);
	
	Закрыть(РезультатВыбора);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеИстории(Отказ)
	
	ОчиститьСообщения();
	
	ПериодыСтрок = Новый Массив;
    
	Если ИсторияПлательщикаНДС.Количество() = 1
		И ПустаяСтрока(ИсторияПлательщикаНДС[0].ИННПлательщикаНДС) И ПустаяСтрока(ИсторияПлательщикаНДС[0].КодФилиала) Тогда
		
		Возврат;
		
	КонецЕсли;
	
    Для Каждого Запись Из ИсторияПлательщикаНДС Цикл    
        
		ИндексСтроки = ИсторияПлательщикаНДС.Индекс(Запись);
		ПрефиксСообщенияСтроки = "ИсторияПлательщикаНДС["+Формат(ИндексСтроки, "ЧЦ=; ЧДЦ=; ЧГ=")+"].";
		
		Если НЕ ЗначениеЗаполнено(Запись.Период) И ИндексСтроки > 0 Тогда
			СообщениеОбОшибке = НСтр("ru='Нужно указать дату начала действия';uk='Потрібно вказати дату початку дії'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки+"Период", , Отказ);
		КонецЕсли;
		
		Если ПериодыСтрок.Найти(Запись.Период) = Неопределено Тогда
			Если ЗначениеЗаполнено(Запись.Период) Тогда
				ПериодыСтрок.Добавить(Запись.Период);
			КонецЕсли;
		Иначе
			СообщениеОбОшибке = НСтр("ru='Уже есть запись с указанной датой сведений';uk='Вже є запис з вказаною датою дані'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки+"Период", , Отказ);
		КонецЕсли;
        
		Если Запись.ПлательщикНДС И НЕ ЗначениеЗаполнено(Запись.ИННПлательщикаНДС) Тогда
			СообщениеОбОшибке = НСтр("ru='Поле ""ИНН плательщика НДС"" не заполнено';uk='Поле ""ІПН платника ПДВ"" не заповнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , ПрефиксСообщенияСтроки+"ИННПлательщикаНДС",  , Отказ);
        КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОПереносеИзмененийПослеОтвета(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПеренестиИзменения();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		ВыполняетсяЗакрытие = Истина;
		Закрыть(Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
    ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсторияПлательщикаНДСИННПлательщикаНДС.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсторияПлательщикаНДС.ПлательщикНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсторияПлательщикаНДС.ИННПлательщикаНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
    ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсторияПлательщикаНДСПериод.Имя);
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсторияПлательщикаНДС.НачальноеЗначение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеактуальногоСписка);

КонецПроцедуры

#КонецОбласти