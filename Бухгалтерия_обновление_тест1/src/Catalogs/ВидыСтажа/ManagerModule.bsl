#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает массив видов стажа с категорией, переданной в параметре.
//
// Параметры:
//		КатегорияСтажа - ПеречислениеСсылка.КатегорииСтажа
//
// Возвращаемое значение:
//		Массив ссылок на элементы видов стажей.
//
Функция ВидыСтажаПоКатегории(КатегорияСтажа) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("КатегорияСтажа", КатегорияСтажа);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыСтажа.Ссылка КАК ВидСтажа
	|ИЗ
	|	Справочник.ВидыСтажа КАК ВидыСтажа
	|ГДЕ
	|	ВидыСтажа.КатегорияСтажа = &КатегорияСтажа";
	
	ВидыСтажа = Запрос.Выполнить().Выгрузить();
	
	Возврат ВидыСтажа.ВыгрузитьКолонку("ВидСтажа");
	
КонецФункции

// Добавляет новый вид стажа с категорией "ВыслугаЛет", 
// если вид стажа с такой категорией есть, то возвращается первый существующий вид стажа.
//
// Возвращаемое значение:
//		СправочникСсылка.ВидыСтажа
//
Функция СоздатьВидСтажаЗаВыслугуЛет() Экспорт
	
	// Стаж на надбавку за выслугу лет.
	Описание = ОписаниеВидаСтажа();
	Описание.Наименование = НСтр("ru='Стаж на надбавку за выслугу лет';uk='Стаж на надбавку за вислугу років'");
	Описание.КатегорияСтажа = Перечисления.КатегорииСтажа.ВыслугаЛет;
	Возврат ЗаписатьВидСтажа(Описание);
	
КонецФункции

// Добавляет новый вид стажа с категорией "НаГосударственнойСлужбе", 
// если вид стажа с такой категорией есть, то возвращается первый существующий вид стажа.
//
// Параметры:
//		ИспользоватьГосударственнуюСлужбу - Булево
//
// Возвращаемое значение:
//		СправочникСсылка.ВидыСтажа
//
Функция СоздатьВидСтажаЗаВыслугуЛетНаГосударственнойСлужбе(ИспользоватьГосударственнуюСлужбу) Экспорт
	
	// Стаж на надбавку за выслугу лет.
	Описание = ОписаниеВидаСтажа();
	Если ИспользоватьГосударственнуюСлужбу Тогда
		Описание.Наименование = НСтр("ru='Стаж государственной службы';uk='Стаж державної служби'");
	Иначе
		Описание.Наименование = НСтр("ru='Стаж муниципальной службы';uk='Стаж муніципальної служби'");
	КонецЕсли;
	Описание.КатегорияСтажа = Перечисления.КатегорииСтажа.ВыслугаЛетНаГосударственнойСлужбе;
	Возврат ЗаписатьВидСтажа(Описание);
	
КонецФункции 

// Добавляет новый вид стажа с категорией "НаГосударственнойСлужбе", 
// если вид стажа с такой категорией есть, то возвращается первый существующий вид стажа.
//
// Параметры:
//		ИспользоватьРасчетДенежногоДовольствияВоеннослужащих - Булево
//
// Возвращаемое значение:
//		СправочникСсылка.ВидыСтажа
//
Функция СоздатьВидСтажаЗаВыслугуЛетНаВоеннойСлужбе(ИспользоватьРасчетДенежногоДовольствияВоеннослужащих) Экспорт
	
	// Стаж на надбавку за выслугу лет.
	Описание = ОписаниеВидаСтажа();
	Если ИспользоватьРасчетДенежногоДовольствияВоеннослужащих Тогда
		Описание.Наименование = НСтр("ru='Стаж военной службы';uk='Стаж військової служби'");
	Иначе
		Описание.Наименование = НСтр("ru='Стаж службы (выслуги лет)';uk='Стаж служби (вислуги років)'");
	КонецЕсли;
	Описание.КатегорияСтажа = Перечисления.КатегорииСтажа.ВыслугаЛетНаВоеннойСлужбе;
	Возврат ЗаписатьВидСтажа(Описание);
	
КонецФункции

// Добавляет новый вид стажа с категорией "Северный", 
// если вид стажа с такой категорией есть, то возвращается первый существующий вид стажа.
//
// Возвращаемое значение:
//		СправочникСсылка.ВидыСтажа
//
Функция СоздатьСтажРаботыНаСевере() Экспорт
	
	// Стаж на надбавку за выслугу лет.
	Описание = ОписаниеВидаСтажа();
	Описание.Наименование = НСтр("ru='Стаж работы на Севере';uk='Стаж роботи на Півночі'");
	Описание.КатегорияСтажа = Перечисления.КатегорииСтажа.Северный;
	Возврат ЗаписатьВидСтажа(Описание);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтажРаботыНаСевере() Экспорт
	
	ВидыСеверногоСтажа = ВидыСтажаПоКатегории(Перечисления.КатегорииСтажа.Северный);
	Если ВидыСеверногоСтажа.Количество() > 0 Тогда
		Возврат ВидыСеверногоСтажа[0];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ЗаписатьВидСтажа(Описание, ПроверятьНаличиеВидаСтажа = Истина)
	
	Если ПроверятьНаличиеВидаСтажа Тогда
		ВидыСтажаПоОписанию = ВидыСтажаПоКатегории(Описание.КатегорияСтажа);
		Если ВидыСтажаПоОписанию.Количество() > 0 Тогда
			Возврат ВидыСтажаПоОписанию[0];
		КонецЕсли;
	КонецЕсли;
	
	ВидСтажаОбъект = Справочники.ВидыСтажа.СоздатьЭлемент();
	ВидСтажаОбъект.Наименование = Описание.Наименование;
	ВидСтажаОбъект.КатегорияСтажа = Описание.КатегорияСтажа;
	ВидСтажаОбъект.Записать();
	
	Возврат ВидСтажаОбъект.Ссылка;
	
КонецФункции

Функция ОписаниеВидаСтажа()
	
	Описание = Новый Структура(
	"Наименование,
	|КатегорияСтажа");
	
	Возврат Описание;
	
КонецФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Добавляет новый вид стажа с категорией "Медицинский", 
// если вид стажа с такой категорией есть, то возвращается первый существующий вид стажа.
//
Функция СоздатьВидСтажаМедицинский() Экспорт
	
	// Медицинский стаж
	Описание = ОписаниеВидаСтажа();
	Описание.Наименование = НСтр("ru='Медицинский стаж';uk='Медичний стаж'");
	Описание.КатегорияСтажа = Перечисления.КатегорииСтажа.Медицинский;
	ЗаписатьВидСтажа(Описание);
	
КонецФункции

Процедура СоздатьВидыСтажаПоНастройкам() Экспорт
	//
	// Общий стаж
	Описание = ОписаниеВидаСтажа();
	Описание.Наименование = НСтр("ru='Общий стаж';uk='Загальний стаж'");
	Описание.КатегорияСтажа = Перечисления.КатегорииСтажа.Общий;
	ЗаписатьВидСтажа(Описание);
	
	
	// Страховой стаж для оплаты больничных листов.
	Описание = ОписаниеВидаСтажа();
	Описание.Наименование = НСтр("ru='Страховой стаж для оплаты больничных листов';uk='Страховий стаж для оплати лікарняних листів'");
	Описание.КатегорияСтажа = Перечисления.КатегорииСтажа.Страховой;
	ЗаписатьВидСтажа(Описание);
	
КонецПроцедуры

Процедура ЗаполнитьКатегориюСтажа() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыСтажа.Ссылка КАК ВидСтажа,
	|	ВидыСтажа.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ВидыСтажа.Код = ""00001""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Непрерывный)
	|		КОГДА ВидыСтажа.Код = ""00002""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Общий)
	|		КОГДА ВидыСтажа.Код = ""00003""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.ВыслугаЛет)
	|		КОГДА ВидыСтажа.Код = ""00004""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Страховой)
	|		КОГДА ВидыСтажа.Код = ""00005""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.РасширенныйСтраховой)
	|		КОГДА ВидыСтажа.Код = ""00006""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.ОбщийНаучноПедагогический)
	|		КОГДА ВидыСтажа.Код = ""00007""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Педагогический)
	|		КОГДА ВидыСтажа.Код = ""00008""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Северный)
	|		КОГДА ВидыСтажа.Код = ""00009""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Медицинский)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.Прочее)
	|	КОНЕЦ КАК КатегорияСтажа
	|ИЗ
	|	Справочник.ВидыСтажа КАК ВидыСтажа
	|ГДЕ
	|	ВидыСтажа.КатегорияСтажа = ЗНАЧЕНИЕ(Перечисление.КатегорииСтажа.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ВидСтажаОбъект = Выборка.ВидСтажа.ПолучитьОбъект();
		ВидСтажаОбъект.КатегорияСтажа = Выборка.КатегорияСтажа;
		ВидСтажаОбъект.ПометкаУдаления = Ложь;
		ВидСтажаОбъект.ОбменДанными.Загрузка = Истина;
		ВидСтажаОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли