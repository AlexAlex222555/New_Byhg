
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Элементы.КнопкаОК.Видимость = Не Параметры.ТолькоПросмотр; 
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеНастройки);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.КонтекстНастройки);
	ИсточникиПодбораСубконто.ЗагрузитьЗначения(Параметры.ИсточникиПодбораСубконто.ВыгрузитьЗначения());
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	СчетУчетаПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто1ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто2ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто3ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	НастройкаОтражения = Новый Структура;
	НастройкаОтражения.Вставить("СчетУчета", СчетУчета);
	НастройкаОтражения.Вставить("Субконто1", Субконто1);
	НастройкаОтражения.Вставить("Субконто2", Субконто2);
	НастройкаОтражения.Вставить("Субконто3", Субконто3);
	
	Закрыть(НастройкаОтражения);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура СчетУчетаПриИзмененииСервер()
	
	УправлениеЭлементамиФормы();
	
	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "Субконто1", "Субконто2", "Субконто3");
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(СчетУчета, ЭтаФорма, ПоляОбъекта, Ложь);
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетУчета);
	Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
		ОписаниеТиповСубконто = СвойстваСчета["ВидСубконто" + НомерСубконто + "ТипЗначения"];
		Если ОписаниеТиповСубконто.СодержитТип(ТипЗнч(АналитикаАктивовПассивов)) И ЗначениеЗаполнено(АналитикаАктивовПассивов) Тогда
			ЭтаФорма["Субконто" + НомерСубконто] = АналитикаАктивовПассивов;
		КонецЕсли;
	КонецЦикла;
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	ПоляФормы = Новый Структура(
		"Субконто1, Субконто2, Субконто3",
		"Субконто1", "Субконто2", "Субконто3");
	ЗаголовкиПолей = Новый Структура(
		"Субконто1, Субконто2, Субконто3",
		"ЗаголовокСубконто1", "ЗаголовокСубконто2", "ЗаголовокСубконто3");
	
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(СчетУчета, ЭтаФорма, ПоляФормы, ЗаголовкиПолей);
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("Субконто1");
	МассивЭлементов.Добавить("Субконто2");
	МассивЭлементов.Добавить("Субконто3");
	
	Для Каждого ЭлементФормы Из МассивЭлементов Цикл
		Элементы[ЭлементФормы].Видимость = Элементы[ЭлементФормы].Видимость;
		Элементы["Заголовок" + ЭлементФормы].Видимость = Элементы[ЭлементФормы].Видимость;
	КонецЦикла;
	
	Если ИсточникиПодбораСубконто.Количество() > 0 Тогда
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетУчета);
		Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
			
			Элементы["Субконто" + НомерСубконто].ПодсказкаВвода = "";
			
			ОписаниеТиповСубконто = СвойстваСчета["ВидСубконто" + НомерСубконто + "ТипЗначения"];
			
			Для Каждого Аналитика Из ИсточникиПодбораСубконто Цикл
				Если ОписаниеТиповСубконто.СодержитТип(Аналитика.Значение) Тогда
					Элементы["Субконто" + НомерСубконто].ПодсказкаВвода = НСтр("ru='<подбирается автоматически>';uk='<підбирається автоматично>'");
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма)
	
	ПараметрыЗаписи = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСубконто(ЭтаФорма.Организация, ЭтаФорма, "Субконто%Индекс%", "СчетУчета");
	НастройкаСчетовУчетаКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, ЭтаФорма, "Субконто%Индекс%", "Субконто%Индекс%", ПараметрыЗаписи); 
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыЗаписи = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСубконто(ЭтаФорма.Организация, ЭтаФорма, "Субконто%Индекс%", "СчетУчета");;
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ПараметрыЗаписи);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
